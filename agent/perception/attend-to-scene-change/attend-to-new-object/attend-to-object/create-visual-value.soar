sp {attend-to-object*propose*create-visual-value
   (state <s> ^name attend-to-object
              ^object-pair <op>
              ^property-info.property.name <prop-name>)
   (<op> ^svs.<prop-name> {<label> <> unknown}
         ^ts <ts-obj>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
             -^value.label <label>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-visual-value
        ^category perception
        ^level value
        ^property <ts-prop>
        ^value <label>)
}

sp {attend-to-object*reject*create-visual-value*not-specified
   (state <s> ^name attend-to-object
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name create-visual-value
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-object*apply*create-visual-value
   (state <s> ^name attend-to-object
              ^operator <o>)
   (<o> ^name create-visual-value
        ^property <ts-prop>
        ^value <label>)
-->
   (<ts-prop> ^value.label <label>)
}