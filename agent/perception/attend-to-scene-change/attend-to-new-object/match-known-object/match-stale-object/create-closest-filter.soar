sp {match-stale-object*propose*create-closest-filter
   (state <s> ^name match-stale-object
              ^svs-object.id <id>
             -^closest-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-closest-filter
        ^category perception
        ^object-id <id>)
}

sp {match-stale-object*apply*create-closest-filter
   (state <s> ^name match-stale-object
              ^operator <o>
              ^svs <svs>)
   (<o> ^name create-closest-filter
        ^object-id <id>)
   (<svs> ^command <cmd>)
-->
   (<cmd> ^extract_once <closest>)
   (<closest> ^type closest
              ^a <new-node>
              ^b <stale-nodes>)
   (<new-node> ^type node
               ^id <id>)
   (<stale-nodes> ^type has-property
                  ^a <all-nodes>
                  ^property-name stale
                  ^property-value true)
   (<all-nodes> ^type all_nodes)
   (<s> ^closest-filter <closest>)
}

sp {match-stale-object*apply*create-closest-filter*failure
   (state <s> ^name match-stale-object
              ^operator.name create-closest-filter
              ^closest-filter <closest>)
   (<closest> ^result <result>)
   (<result> -^record)
-->
   (<s> ^closest-object none)
}

sp {match-stale-object*apply*create-closest-filter*success
   (state <s> ^name match-stale-object
              ^operator.name create-closest-filter
              ^closest-filter <closest>
              ^svs.spatial-scene.child <svs-obj>
              ^topstate.object <wm-obj>)
   (<closest> ^result.record.params <params>)
   (<params> ^b <closest-id>)
   (<svs-obj> ^id <closest-id>
              ^id-int <closest-id-int>)
   (<wm-obj> ^id <closest-id-int>)
-->
   (<s> ^closest-object <wm-obj>)
}