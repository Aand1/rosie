sp {interpret-failure*propose*interpret-no-action-selection-knowledge
   (state <s> ^name interpret-failure
              ^failure.type no-action-selection-knowledge
             -^handled <failure>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-no-action-selection-knowledge)
}

sp {interpret-failure*apply*interpret-no-action-selection-knowledge*mark*handled
   (state <s> ^name interpret-failure
              ^operator.name interpret-no-action-selection-knowledge
              ^failure <failure>)
-->
   (<s> ^handled <failure>)
}

sp {interpret-failure*apply*interpret-no-action-selection-knowledge*create*message
   (state <s> ^name interpret-failure
              ^operator.name interpret-no-action-selection-knowledge)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type simple-message
          ^message-type get-next-subaction)
}

sp {interpret-failure*apply*interpret-no-action-selection-knowledge*create*new*segment
   (state <s> ^name interpret-failure
              ^operator.name interpret-no-action-selection-knowledge
              ^top-state.interaction.status <status>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-next-subaction
   	^satisfaction.dialog-event <de>)
   (<de> ^type command
         ^type object-description
         ^originator instructor)
}