# When simulating go-to-next-area, update the current-location

# on the top-state
sp {evaluate-operator*apply*go-to-next-area*update*current-location*top-state
   (state <s> ^name evaluate-operator
              ^operator <o>
              ^top-state <ts>)
   (<o> ^name go-to-next-area
        ^area <new-loc>)
   (<ts> ^current-location <cur-loc>)
   (<cur-loc> ^id <cid>)
-->
   (<ts> ^current-location <cur-loc> -
         ^current-location <new-loc>)
}

# on the evaluation structure
sp {evaluate-operator*apply*go-to-next-area*update*current-location*evaluation
   (state <s> ^name evaluate-operator
              ^operator <o>
              ^superstate.operator.evaluation <eval>)
   (<o> ^name go-to-next-area
        ^area <new-loc>)
   (<eval> ^current-location <cur-loc>)
   (<cur-loc> ^id <cid>)
-->
   (<eval> ^current-location <cur-loc> -
           ^current-location <new-loc>)
}

# When simulating go-to-next-area, update the path cost
sp {evaluate-operator*elaborate*go-to-next-area*dx*dy
   (state <s> ^name evaluate-operator
              ^operator <o> +
              ^top-state.current-location <cur-loc>)
   (<o> ^name go-to-next-area
        ^area <new-loc>)
   (<cur-loc> ^id <cid> ^x <cx> ^y <cy>)
   (<new-loc> ^id <did> ^x <dx> ^y <dy>)
-->
   (<o> ^dx (- <dx> <cx>)
        ^dy (- <dy> <cy>) )
}

sp {evaluate-operator*apply*go-to-next-area*update*path-cost
   (state <s> ^name evaluate-operator
              ^operator <o>
              ^superstate.operator.evaluation <eval>)
   (<o> ^name go-to-next-area
        ^dx <dx>
        ^dy <dy>
        ^area.id <new-loc>)
   (<eval> ^current-location.id {<cur-loc> <> <new-loc>}
           ^path-cost <old-cost>)
-->
   (<eval> ^path-cost <old-cost> -
           ^path-cost (+ <old-cost> (sqrt (+ (* <dx> <dx>) (* <dy> <dy>)))))
}

# Cleanup estimated-cost and updated-estimate
sp {evaluate-operator*apply*go-to-next-area*cleanup*evaluation
   (state <s> ^name evaluate-operator
              ^operator <o>
              ^superstate.operator.evaluation <eval>)
   (<o> ^name go-to-next-area
        ^area <new-loc>)
   (<new-loc> ^id <new-id>)
   (<eval> ^path-cost { <pc> <> 0 }
           ^estimated-cost <ec>
           ^current-location.id <> <new-id>
           ^updated-estimate true)
-->
   (<eval> ^estimated-cost <ec> -
           ^updated-estimate true -)
}
