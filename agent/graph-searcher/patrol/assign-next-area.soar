# When we have reached the area we are trying to get to, 
#   Assign the next area in the patrol
#   Either go to the next in the list, or restart at the beginning

sp {patrol*propose*assign-next-area*starting*patrol
   (state <s> ^name patrol
              ^top-state.current-mission <mission>)
   (<mission> -^next-area)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name assign-next-area
        ^mission <mission>)
}

sp {patrol*propose*assign-next-area*reached*area
   (state <s> ^name patrol
              ^achieved-area true
              ^top-state.current-mission <mission>)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name assign-next-area
        ^mission <mission>)
}

sp {patrol*apply*assign-next-area*starting*patrol
   (state <s> ^name patrol
              ^operator <o>)
   (<o> ^name assign-next-area
        ^mission <mission>)
   (<mission> -^next-area
               ^first-area <first>)
-->
   (<mission> ^next-area <first>)
}

sp {patrol*apply*assign-next-area*change*next
   (state <s> ^name patrol
              ^operator <o>)
   (<o> ^name assign-next-area
        ^mission <mission>)
   (<mission> ^next-area <old-next>)
   (<old-next> ^next { <next> <> nil })
-->
   (<mission> ^next-area <old-next> -
              ^next-area <next>)
}

sp {patrol*apply*assign-next-area*restart*patrol
   (state <s> ^name patrol
              ^operator <o>)
   (<o> ^name assign-next-area
        ^mission <mission>)
   (<mission> ^next-area.next nil
              ^first-area <first>
              ^next-area <old-next>)
-->
   (<mission> ^next-area <old-next> -
              ^next-area <first>)
}
              