sp {go-to-area*propose*go-to-next-area*all*adjacent
   (state <s> ^type go-to-area
              ^top-state.current-location.neighbor-area <next>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-next-area
        ^area <next>)
}

sp {go-to-area*prefer*go-to-next-area*desired*area
   (state <s> ^type go-to-area
              ^operator <o> +
              ^desired.location.id <id>)
   (<o> ^name go-to-next-area
         ^area.id <id>)
-->
   (<s> ^operator <o> >)
}

sp {go-to-area*propose*go-to-next-area*destination-area
   (state <s> ^type go-to-area
              ^name evaluate-operator
              ^destination-area.id <dest-id>
              ^current-location <cur-loc>)
   (<cur-loc> ^neighbor-area <neighbor>)
   (<neighbor> ^id <dest-id>)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name go-to-next-area
        ^area <neighbor>)
}
