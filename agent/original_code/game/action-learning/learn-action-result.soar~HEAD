sp {find-parameters-results-impasse*propose*learn-action-result
   (state <s> ^superstate.name select-param
              ^superstate.legal-action <item>
             -^superstate.action-result.item <item>
              ^impasse no-change)
    (<item> ^{<arg> 1})
-->
#   (force-learn <s>)
#   (interrupt)
   (<s> ^operator <o> + =)
   (<o> ^name learn-action-result
        ^category svs
        ^superitem <item>)
}

sp {learn-action-result*elaborate*item
   (state <s> ^name learn-action-result
              ^superstate.operator.superitem <item>)
-->
   (<s> ^superitem <item>)
}
sp {learn-action-result*elaborate*found-preps
   (state <s> ^name learn-action-result
              ^superstate.superstate.found-prep <fprep>)
-->
   (<s> ^found-prep <fprep>)
}
sp {learn-action-result*elaborate*svs
   (state <s> ^name learn-action-result
              ^superstate.superstate.svs <svs>)
-->
   (<s> ^supersvs <svs>)
}

sp {learn-action-result*propose*find-projection
   (state <s> ^name learn-action-result
              ^supersvs <svs>
              ^superstate.superstate.<arg0>.verb-prep <name>
             -^projection-result
              ^superitem <item>
              ^found-prep <fprep>)
    (<fprep> ^type <name>)
   (<item> ^arg1 <arg1> ^arg2 <arg2> ^<arg1> <a> ^<arg2> <b> ^action <arg0>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-projection
        ^category svs
        ^interaction false
        ^svs-link <svs>
        ^a.id <a>
        ^b.id <b>
        ^found-prep <fprep>)
}

sp {learn-action-result*property-adjust-x
   (state <s> ^name learn-action-result
              ^projection-result <res>
              ^superitem <item>
              ^supersvs.spatial-scene <sp> 
              ^supersvs.command <cmd>)
   (<item> ^arg1 <arg1> ^<arg1> <id>)
   (<sp> ^child <c1>)
   (<c1> ^id-int <id> ^id <obja>)
   (<res> ^x <x>)
-->
   (<cmd> ^property <P11>)
   (<P11> ^id <obja> ^value <x> ^property px)
}
sp {learn-action-result*property-adjust-y
   (state <s> ^name learn-action-result
              ^projection-result <res>
              ^superitem <item>
              ^supersvs.spatial-scene <sp> 
              ^supersvs.command <cmd>)
   (<item> ^arg1 <arg1> ^<arg1> <id>)
   (<sp> ^child <c1>)
   (<c1> ^id-int <id> ^id <obja>)
   (<res> ^y <y>)
-->
   (<cmd> ^property <P11>)
   (<P11> ^id <obja> ^value <y> ^property py)
}

###dont do z because movement doesn't actually use z
#sp {learn-action-result*property-adjust-z
#   (state <s> ^name learn-action-result
#              ^projection-result <res>
#              ^item.obj1.id <id>
#              ^svs.spatial-scene <sp> 
#              ^svs.command <cmd>)
#   (<sp> ^child <c1>)
#   (<c1> ^id-int <id> ^id <obja>)
#   (<res> ^z <z>)
#-->
#   (<cmd> ^property <P11>)
#   (<P11> ^id <obja> ^value <z> ^property pz)
#}

####now find result again

sp {learn-action-result*propose*find-relations
   (state <s> ^name learn-action-result
              ^superstate.superstate.<arg0>.verb-prep <name>
              ^superitem <item>
              ^projection-result <res>
              ^found-prep <fprep>
              ^supersvs <svs>)
    (<fprep> ^type <name>)
    (<item> ^<< arg1  arg2 >> <arg> ^<arg> <id> ^action <arg0>)
 -{(<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <name>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-relations
        ^category svs
        ^svs-link <svs>
        ^obj <id>
        ^ind 1
        ^found-prep <fprep>
        ^prep <name>) 
}
sp {learn-action-result*propose*find-relations*related #TODO improve heuristic at what to look for
   (state <s> ^name learn-action-result
              ^superstate.superstate.<arg0>.verb-prep <name>
              ^superitem.<< 1 2 3 4 5 6 7 8 9 10 >> <id>
              ^superitem <item>
              ^superstate.superstate.superstate.superstate.relationships <rels2>
              ^projection-result <res>
              ^found-prep <fprep>
              ^supersvs <svs>)
    (<item> ^action <arg0> ^arg1 <arg1> ^<arg1> <id2>)
    (<rels2> ^with <id2> ^preposition <name> ^relations.obj <id>)
    (<fprep> ^type <name>)
 -{(<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <name>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-relations
        ^category svs
        ^svs-link <svs>
        ^obj <id>
        ^ind 1
        ^found-prep <fprep>
        ^prep <name>) 
}


sp {learn-action-result*propose*create-result
   (state <s> ^name learn-action-result
             -^action-result
              ^superitem <item>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-result
        ^item <item>
        ^category svs)
}
sp {learn-action-result*apply*create-result
   (state <s> ^name learn-action-result
              ^operator <o>)
   (<o> ^name create-result
        ^item <item>)
-->
   (<s> ^action-result <ar>)
}

sp {learn-action-result*propose*copy-relation
   (state <s> ^name learn-action-result
              ^action-result <ar>
              ^relationships <rels>
              ^superitem <item>)
   (<rels> -^copied true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relation
        ^category svs
        ^action-result <ar>
        ^relationships <rels>)
}


sp {learn-action-result*apply*copy-relation
   (state <s> ^name learn-action-result
              ^superitem.{<arg> << 1 2 3 4 5 6 7 8 9 10 >>} <id>
              ^superitem.action <arg0>
              ^superstate.superstate.<arg0>.verb-prep <prep>
              ^operator <o>)
   (<o> ^name copy-relation
        ^action-result <ar>
        ^relationships <rels>)
   (<rels> ^with <id> ^preposition <prep> ^relations <r>)
-->
   (<rels> ^copied true)
   (<ar> ^relationships <rels>)
}

sp {learn-action-result*apply*copy-relation*nowith
   (state <s> ^name learn-action-result
              ^superitem.{<arg> << 1 2 3 4 5 6 7 8 9 10 >>} <id>
              ^superitem.action <arg0>
              ^superstate.superstate.<arg0>.verb-prep <prep>
              ^operator <o>)
   (<o> ^name copy-relation
        ^action-result <ar>
        ^relationships <rels>)
    (<rels> ^with <id> ^preposition <prep> ^failure none-found)
-->
   (<rels> ^copied true)
   (<ar> ^relationships <rels>)
}

sp {learn-action-result*propose*complete-learn-action-result
   (state <s> ^name learn-action-result
              ^projection-result
              ^action-result <ar>
              ^superitem <item>)
--> 
   (<s> ^operator <o> + =)
   (<o> ^name complete-learn-action-result
        ^category svs
        ^action-result <ar>
        ^item <item>)
}
sp {learn-action-result*apply*complete-learn-action-result
   (state <s> ^name learn-action-result
              ^superitem <item>
              ^superstate.superstate <sss>
              ^operator <o>)
   (<o> ^name complete-learn-action-result
        ^action-result <ar>
        ^item <item>)
-->
   (<sss> ^action-result <ar>)
   (<ar> ^item <item>)
}
