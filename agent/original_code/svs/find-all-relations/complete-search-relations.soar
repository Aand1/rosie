sp {find-all-relations*propose*copy-relation-to-relationship
   (state <s> ^name find-all-relations
			  ^relation <r>)
   (<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type> -^relations <r>)
   (<r> ^0 true
        ^1 true
        ^with <id>
        ^name <type>
        ^matched true
        ^2 true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relation-to-relationship
        ^category svs
        ^rel <rels>
        ^r <r>)
}
sp {find-all-relations*apply*copy-relation-to-relationship
   (state <s> ^name find-all-relations
              ^relationships <rels>
			  ^operator <o>)
   (<o> ^name copy-relation-to-relationship
        ^rel <rels>
        ^r <r>)
-->
   (<rels> ^relations <r>)
}

sp {find-all-relations*propose*complete-relations-search*true
   (state <s> ^name find-all-relations
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type> ^relations)
  -{(<s> ^all-relationships.relationships <re>)
    (<re> ^with <id> ^preposition <type>)}
-->   
   (<s> ^operator <o> + =)
   (<o> ^name complete-one-relation-search
        ^category svs
        ^rel <rels>
        ^type true)
}


sp {find-all-relations*propose*found-possible-match
   (state <s> ^name find-all-relations
              ^predicate <p1>
			  ^relation <r>)
   (<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<p1> ^all-aligned true)
   (<r> ^0 true
        ^1 true
        ^with <id>
        ^name <type>
        ^2 true
       -^matched)
-->
   (<s> ^operator <o> + =)
   (<o> ^name found-possible-match
        ^category svs
        ^relation <r>)
}

sp {find-all-relations*propose*found-possible-match*not-all-aligned
   (state <s> ^name find-all-relations
              ^predicate <p1>
			  ^relation <r>)
   (<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<p1> ^all-aligned false)
   (<r> ^0 true
        ^1 true
        ^with <id>
        ^name <type>
        ^2 true
       -^matched)
  -{(<r> ^0 x-aligned ^1 y-aligned ^2 z-aligned ^with <id> ^name <type>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name found-possible-match
        ^category svs
        ^relation <r>)
}

sp {find-all-relations*apply*found-possible-match*extract_once-distance
   (state <s> ^name find-all-relations
              ^operator <o>
              ^svs-link <svs>
              ^predicate <p1>
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^with <id>)
   (<p1> ^name <type>
         ^relation.axis <axis>)
   (<svs> ^command <cmd>)
   (<svs> ^spatial-scene <sc>)
   (<sc> ^child <C2> ^child <C>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<cmd> ^extract_once <P11>)
   (<P11> ^a <N7> ^b <N8> ^type distance_xyz ^axis <axis>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}

sp {find-all-relations*apply*found-possible-match*matched-true
   (state <s> ^name find-all-relations
              ^operator <o>
              ^predicate <p1>
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^with <id>
        ^dist <obj0>
        ^dist <obj1>
        ^dist <obj2>)
   (<obj0> ^0 true)
   (<obj1> ^1 true)
   (<obj2> ^2 true)
-->
   (<r> ^matched true)
}

sp {find-all-relations*apply*found-possible-match*matched-false
   (state <s> ^name find-all-relations
              ^operator <o>
              ^predicate <p1>
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^with <id>
        ^dist.<< 0 1 2 >> false)
-->
   (<r> ^matched false)
} 

sp {find-all-relations*apply*found-possible-match*check-distance
   (state <s> ^name find-all-relations
              ^operator <o>
              ^svs-link <svs>
              ^predicate <p1>
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^with <id> -^<axis> << x-aligned y-aligned z-aligned >>)
   (<p1> ^name <type>
         ^relation <rel>)
   (<rel> ^axis <axis> ^avg <avg> ^top <top> ^bottom <bot> -^count 1)
   (<svs> ^command <cmd>)
   (<cmd> ^extract_once <ED1>)
   (<ED1> ^type distance_xyz ^axis <axis> ^result.record <rec>)# ^b <C2>)
   (<rec> ^value { >= <bot> <= <top> } ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)
   (<svs> ^spatial-scene <sc>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<r> ^dist.<axis> true)
}

sp {find-all-relations*apply*found-possible-match*check-distance*false
   (state <s> ^name find-all-relations
              ^operator <o>
              ^svs-link <svs>
              ^predicate <p1>
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^with <id> -^<axis> << x-aligned y-aligned z-aligned >>)
   (<p1> ^name <type>
         ^relation <rel>)
   (<rel> ^axis <axis> ^avg <avg> ^top <top> ^bottom <bot> -^count 1)
   (<svs> ^command <cmd>)
   (<cmd> ^extract_once <ED1>)
   (<ED1> ^type distance_xyz ^axis <axis> ^result.record <rec>)
   (<rec> -^value { >= <bot> <= <top> } ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)
   (<svs> ^spatial-scene <sc>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<r> ^dist.<axis> false)
}

sp {find-all-relations*apply*found-possible-match*check-distance*true-for-one
   (state <s> ^name find-all-relations
              ^operator <o>
              ^svs-link <svs>
              ^predicate <p1>
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^with <id> -^<axis> << x-aligned y-aligned z-aligned >>)
   (<p1> ^name <type>
         ^relation <rel>)
   (<rel> ^axis <axis> ^avg <avg> ^top <top> ^bottom <bot> ^count 1)
   (<svs> ^command <cmd>)
   (<cmd> ^extract_once <ED1>)
   (<ED1> ^type distance_xyz ^axis <axis> ^result.record <rec>)
   (<rec> ^value <val> ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)
   (<svs> ^spatial-scene <sc>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<r> ^dist.<axis> true)
}

sp {find-all-relations*apply*found-possible-match*check-distance*true-aligned
   (state <s> ^name find-all-relations
              ^operator <o>
              ^predicate <p1>
              ^relationships <rels>)
   (<rels> ^with <id> ^preposition <type>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^with <id> ^<axis> << x-aligned y-aligned z-aligned >>)
-->
   (<r> ^dist.<axis> true)
}

#sp {find-all-relations*complete-relations-search*false
#   (state <s> ^name find-all-relations
#              ^obj <id>
#             #-^superstate.relationships
#			  ^relation <r>)
#   (<r> ^result false)
#-->
#   (<s> ^operator <o> + =)
#   (<o> ^name complete-relations-search
#        ^type false
#        ^category svs)
#}


sp {find-all-relations*propose*complete-relations-search*nonefound
   (state <s> ^name find-all-relations
              ^relationships <rels>)
   (<rels> -^relations
            ^with <id> ^preposition <type>
           -^failure prep-not-found)
 -{(<s> ^all-relationships.relationships <re>)
    (<re> ^with <id> ^preposition <type>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-one-relation-search
        ^rel <rels>
        ^type none
        ^category svs)
}

sp {find-all-relations*propose*complete-relations-search*prep-not-found
   (state <s> ^name find-all-relations
              ^relationships <rels>)
   (<rels> ^failure prep-not-found ^with <id> ^preposition <type>)
 -{(<s> ^all-relationships.relationships <re>)
    (<re> ^with <id> ^preposition <type>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-one-relation-search
        ^type none
        ^rel <rels>
        ^category svs)
}


sp {find-all-relations*apply*complete-one-relation-search
   (state <s> ^name find-all-relations
              ^operator <o>
              ^all-relationships <ar>
              ^superstate <ss>)
   (<o> ^name complete-one-relation-search
       -^type none
        ^rel <rels>)
-->
#   (<rels> ^relations <r>)
   (<ar> ^relationships <rels>)
}
sp {find-all-relations*apply*complete-one-relation-search*none
   (state <s> ^name find-all-relations
              ^operator <o>
              ^all-relationships <ar>
              ^superstate <ss>)
   (<o> ^name complete-one-relation-search
        ^type none
        ^rel <rels>)
-->
   (<rels> ^failure none-found)
   (<ar> ^relationships <rels>)
}

sp {find-all-relations*propose*complete-all-relations-search
   (state <s> ^name find-all-relations
              ^all-relationships <ar>)
  -{(<s> ^relationships <rels>)
    (<ar> -^relationships <rels>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-all-relations-search
        ^all <ar>
        ^category svs)
}

sp {find-all-relations*apply*complete-all-relations-search
   (state <s> ^name find-all-relations
              ^superstate <ss>
              ^operator.name complete-all-relations-search
              ^all-relationships <ar>)
-->
   (<ss> ^all-relationships <ar>)
}
