# Operator: smem-retrieve
#   Used to lookup the structure for a Long-Term Identifier (LTI)

#(<s> ^operator <o> +)
#(<o> ^name smem-retrieve
#     ^identifier <lti>) <- long-term identifier (required)

# Will put a retrieve for the given identifier on the smem command link
# When finished, it will put a smem-retrieve-result on the state, 
#   Which will either be ^smem-retrieve-result <lti> or ^smem-retrieve-result failure

sp {anystate*propose*smem-retrieve*create-cue
   (state <s> ^retrieve-lti <lti>
              ^smem.command <cmd>
             -^retrieved-lti <lti>)
   (<cmd> -^retrieve <lti>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name smem-retrieve
        ^type create-cue
        ^category smem
        ^identifier <lti>)
}
   
sp {anystate*apply*smem-retrieve*create-cue
   (state <s> ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name smem-retrieve
        ^type create-cue
        ^identifier <lti>)
-->
   (<cmd> ^retrieve <lti>)
}

sp {anystate*propose*smem-retrieve*copy-result
   (state <s> ^retrieve-lti <lti>
              ^smem.result.{<< success failure >>} <lti>
             -^retrieved-lti <lti>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name smem-retrieve
        ^type copy-result
        ^category smem
        ^identifier <lti>)
}


sp {anystate*apply*smem-retrieve-copy-result*remove*command
   (state <s> ^operator <o>
              ^smem <smem>)
   (<o> ^name smem-retrieve
        ^type copy-result
        ^identifier <lti>)
   (<smem> ^command <cmd>)
   (<cmd> ^retrieve <lti>)
-->
   (<cmd> ^retrieve <lti> -)
}

sp {anystate*apply*smem-retrieve*copy-result*copy
   (state <s> ^operator <o>
              ^smem.result.{ << success failure >>}  <lti>)
   (<o> ^name smem-retrieve
        ^type copy-result
        ^identifier <lti>)
-->
   (<s> ^retrieved-lti <lti>)
}

sp {prefer*copy-result*over*create-cue
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name smem-retrieve
         ^type copy-result)
   (<o2> ^name smem-retrieve
         ^type create-cue)
-->
   (<s> ^operator <o1> > <o2>)
}