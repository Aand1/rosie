sp {attend-to-scene*propose*delete-visual-value
   (state <s> ^name attend-to-scene
              ^object-pair <op>
              ^parameters.create-delete-only false)
   (<op> ^il <il-obj>
         ^ts <ts-obj>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
              ^value <val>)
   (<val> ^label <label>)
   (<il-obj> ^visual-prop <il-prop>)
   (<il-prop> ^category <prop-name>
              -^<label>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-visual-value
        ^category object-processing
        ^level value
        ^property <ts-prop>
        ^value <val>)
}

sp {attend-to-scene*reject*delete-visual-value*not-specified
   (state <s> ^name attend-to-scene
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name delete-visual-value
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-scene*apply*delete-visual-value
   (state <s> ^name attend-to-scene
              ^operator <o>)
   (<o> ^name delete-visual-value
        ^property <ts-prop>
        ^value <val>)
-->
   (<ts-prop> ^value <val> -)
}