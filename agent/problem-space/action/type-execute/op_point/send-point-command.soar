###############################################################################
#
# Inside the op_point1 substate, this will put a grab command on the output link
# If the point fails and needs to be re-issued, this will cause that to occur
#
###############################################################################

sp {op_point1*tabletop*propose*send-point-command
   (state <s> ^name op_point1
              ^problem-space.action.type execute
              ^target-object <obj>
              ^world.robot.arm.moving-status wait
             -^current-action.point-command)
   (<obj> ^predicates.visible true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-point-command
        ^object <obj>)
}

sp {op_point1*tabletop*apply*send-point-command*send
   (state <s> ^name op_point1
              ^operator <o>
              ^io.output-link <ol>
              ^current-action <a>)
   (<o> ^name send-point-command
		^object.handle <obj-handle>)
-->
   (<ol> ^point <cmd>)
   (<cmd> ^object-handle <obj-handle>)
   (<a> ^point-command <cmd>)
}

