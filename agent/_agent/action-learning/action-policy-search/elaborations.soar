### elaborate segment
sp {elaborate*action-policy*segment
   (state <s> ^name action-policy
   	      ^superstate.operator <op>)
   (<op> ^segment <seg>)
-->
   (<s> ^segment <seg>)
}

### elaborate indexed-action
sp {action-policy-learning*elaborate*indexed-action
   (state <s> ^name action-policy
   	      ^segment.purpose.parameters.indexable <pi>)
   (<pi> ^result <ia>
   	 ^lexical <lex>)
   (<ia> ^_metadata.map-id <map-id>)
-->
   (<s> ^indexed-action <ia>
   	^lexical-information <lex>
	^relevant-map-id <map-id>)
}

### elaborate copy-set
sp {elaborate*action-policy*copy-set
   (state <s> ^name action-policy)
-->
   (<s> ^relevant-attributes <cs>)
   (<cs> ^attribute object
   	 ^attribute open
	 ^attribute closed
	 ^attribute on
	 ^attribute off
	 ^attribute self
	 ^attribute grabbed)
}

### 
sp {elaborate*action-policy*copy-set*substate
   (state <s> ^superstate <ss>)
   (<ss> ^relevant-attributes <cs>)
-->
   (<s> ^relevant-attributes <cs>)
}

sp {action-policy*superstate*retrieved*relevant-attributes
   (state <s> ^superstate <ss>
   	      ^relevant-attributes.attribute <attr>
	      ^action-marker composite)
   (<ss> ^<attr> <val>)
-->
   (<s> ^<attr> <val>)
}


###
sp {elaborate*action-policy*problemspace
   (state <s> ^name action-policy)
-->
   (<s> ^problemspace action
   	^problemspace learning)
}
   	      
sp {learning*copy*problem-space
   (state <s> ^superstate <ss>)
   (<ss> ^problemspace learning)
-->
   (<s> ^problemspace learning)
}

