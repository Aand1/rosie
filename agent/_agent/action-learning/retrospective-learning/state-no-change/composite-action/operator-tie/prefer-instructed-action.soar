sp {elaborate*attribute*match*epmem*name
   (state <s> ^name selection
   	      ^retrieved-action <map-op>
   	      ^operator <o> +)
   (<o> ^superoperator <sop>)
   (<sop> ^name <n>)
   (<map-op> ^name <n>) 
-->
   (<o> ^matched name)
}

sp {selection*mark*operators*with*same*arguments*relation
   (state <s> ^name selection
   	      ^operator <op> +
	      ^retrieved-action <ia>)
   (<op> ^superoperator <sop>)
   (<ia> ^<arg> <val>)
   (<val> ^<arg2> <obj>)
   (<sop> ^<arg> <nval>)
   (<nval> ^<arg2> <nobj>)
   (<obj> ^id <id>) 
   (<nobj> ^id <id>)
-->
   (<op> ^matched <arg>)
}

sp {selection*mark*operators*with*same*argument
   (state <s> ^name selection
   	      ^operator <op> +
	      ^retrieved-action <ia>)
   (<op> ^superoperator <sop>)
   (<ia> ^<arg> <val>)
   (<val> -^relation <any>)
   (<sop> ^<arg> <nval>)
   (<val> ^id <id>) 
   (<nval> ^id <id>)
-->
   (<op> ^matched <arg>)
}

sp {selection*elaborate*operator*match
   (state <s> ^name selection
   	      ^operator <op> + 
   	      ^retrieved-action <map-op>
	      -^ignore-set.attribute <attr>)
   (<op> ^superoperator <sop>)
   (<map-op> ^<attr> <val>)
   (<op> -^matched <attr>)
-->
   (<op> ^op-matched no
   	 ^not-matched <attr>)
}

sp {selection*prefer*match*epmem
   (state <s> ^name selection
              ^retrieved-action <map-op>
   	      ^operator <o> +)
   (<o> -^op-matched no
   	^name evaluate-operator)
   -->
   (<s> ^operator <o> >)
}

sp {prefer*match*epmem*elaborate*spatial-rel
   (state <s> ^name selection
	      ^superstate <ss>
   	      ^operator <o>)
   (<ss> ^desired <d>
   	 ^in-focus.<f> <val2>)
   (<o> -^not-matched <any>
   	^superoperator <sop>)
   (<d> ^track-relation <tr>
   	  ^in-focus.<f> <trval>)
   (<tr> ^relation <prep>
   	 ^p2 <trval>)
   (<sop> ^<arg2> <sop-arg>
   	  ^name op_put-down-object-location)
   (<sop-arg> ^<arg> <val2>)
   -->
   (<sop-arg> ^relation <prep>)
   (<s> ^copied-relation-from-desired <sop-arg>)
}

# special handling for the operator op_wait 
sp {mark*operators*match*op_wait*learning
   (state <s> ^name selection
   	      ^operator <op> +
	      ^retrieved-action <ia>
	      ^superstate.object <obj>)
   (<op> ^superoperator <sop>)
   (<ia> ^argument1 <val>
   	 ^name op_wait)
   (<sop> ^name op_wait)
-->
   (<op> ^matched argument1)
}


### copy state if matched
sp {prefer*match*epmem*elaborate*state*copy
   (state <s> ^name selection
	      ^superstate <ss>
   	      ^operator <o>)
   (<ss> ^desired <des>
   	 ^object <obj>
	 ^in-focus.<f> <obj>)
   (<o> -^not-matched <any>
   	^superoperator <sop>)
   (<des> ^achieve-state <astate>
   	  ^in-focus.<f> <val1>)
   (<astate> ^p1 <val1>
      	     ^state cooked)
   (<sop> ^name op_wait)
-->
   (<sop> ^argument1 <new-arg-struct>)
   (<new-arg-struct> ^argument <obj>
   		     ^state cooked)
}

