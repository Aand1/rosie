sp {generate-new-operator*reject*unmatched*name
   (state <s> ^name generate-new-operator
   	      ^operator <op1> +
	      ^retrieved-action <ra>)
   (<ra> ^name <name>)
   (<op1> ^definition <aop>)
   (<aop> -^op_name <name>)
-->
   (<s> ^operator <op1> -)
}
   

sp {generate-new-operator*prefer*generate-argument*single
   (state <s> ^name generate-new-operator
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^type single)
   (<op2> ^type two)
-->
   (<s> ^operator <op1> > <op2>)
} 

sp {generate-new-operator*prefer*goal
   (state <s> ^name generate-new-operator
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^name get-goal-definition)
   (<op2> ^name complete-generate-new-operator)
-->
   (<s> ^operator <op1> > <op2>)
} 

sp {generate-new-operator*prefer*generate-argument*two
   (state <s> ^name generate-new-operator
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^type two)
   (<op2> ^type relation)
-->
   (<s> ^operator <op1> > <op2>)
} 


sp {generate-new-operator*reject*unmatched*one-level
   (state <s> ^name generate-new-operator
   	      ^operator <op> +
	      ^retrieved-action <ra>)
   (<op> ^argument <attr>
   	  ^candidate <cand>
	  ^type single)
   -{(<ra> ^<attr> <obj>)
    (<obj> ^smem-rep <id>)}
   (<cand> ^smem-rep <id>)
-->
   (<s> ^operator <op> -)
} 

sp {generate-new-operator*reject*unmatched*two-level
   (state <s> ^name generate-new-operator
   	      ^operator <op> +
	      ^retrieved-action <ra>)
   (<op> ^argument <attr>
   	  ^candidate <cand>
	  ^type two)
   -{(<ra> ^<attr>.argument <obj>)
    (<obj> ^smem-rep <id>)}
   (<cand> ^smem-rep <id>)
-->
   (<s> ^operator <op> -)
} 

sp {generate-new-operator*reject*unmatched*relation
   (state <s> ^name generate-new-operator
   	      ^operator <op> +
	      ^retrieved-action <ra>)
   (<op> ^argument <attr>
   	  ^candidate <cand>
	  ^type relation)
   -{(<ra> ^<attr>.relation <obj>)
    (<obj> ^smem-rep <id>)}
   (<cand> ^smem-rep <id>)
-->
   (<s> ^operator <op> -)
} 


sp {elaborate*generate-new-operator*metadata
   (state <s> ^name generate-new-operator
   	      ^new-operator <nop>
	      ^map-id <map>)
-->
   (<nop> ^_metadata.map-id <map>)
}