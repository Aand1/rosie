sp {generate-new-operator*propose*select-operator-definiton
   (state <s> ^name generate-new-operator
   	      ^superstate.action-space-id <aid>
	      -^new-operator)
   (<aid> ^_operator <aop>)
-->
   (<s> ^operator <op> +)
   (<op> ^name select-operator-definition
   	  ^definition <aop>)
}


## for every important operator argument, propose generate argument
sp {generate-new-operator*propose*generate*argument*single-level
   (state <s> ^name generate-new-operator
   	      ^operator-definition <aid>
	      -^ignore-set.attribute <attr>
	      ^candidate <cand>
	      ^map-id <map>)
   (<aid> ^<attr> <val>)
   (<val> -^argument)
-->
   (<s> ^operator <op> +)
   (<op> ^name generate-argument
   	 ^argument <attr>
	 ^candidate <cand>
	 ^type single)
}

sp {generate-new-operator*propose*generate*argument*two-level
   (state <s> ^name generate-new-operator
   	      ^operator-definition <aid>
	      -^ignore-set.attribute <attr>
	      ^candidate <cand>
	      ^map-id <map>)
   (<aid> ^<attr> <val>)
   (<val> ^argument <sobj>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name generate-argument
   	 ^argument <attr>
	 ^candidate <cand>
	 ^type two)
}

sp {generate-new-operator*propose*generate*relation*two-level
   (state <s> ^name generate-new-operator
   	      ^operator-definition <aid>
	      -^ignore-set.attribute <attr>
	      ^candidate-relation <cand>
	      ^map-id <map>)
   (<aid> ^<attr> <val>)
   (<val> ^relation <sobj>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name generate-argument
   	 ^argument <attr>
	 ^candidate <cand>
	 ^type relation)
}

sp {propose*get-goal-defintion*generate-new-operator
   (state <s> ^name generate-new-operator
   	      ^map-id.goal <gid>)
-->
   (<s> ^operator <op> + <)
   (<op> ^name get-goal-definition
   	 ^goal-id <gid>)
}


sp {generate-new-operator*propose*complete*generate-new-operator
   (state <s> ^name generate-new-operator
   	      ^new-operator <nop>)
-->
   (<s> ^operator <op> + <)
   (<op> ^name complete-generate-new-operator)
}

sp {generate-new-operator*apply*complete*generate-new-operator
   (state <s> ^name generate-new-operator
   	      ^new-operator <nop>
	      ^superstate <ss>
	      ^operator.name complete-generate-new-operator)
-->
   (<ss> ^new-operator <nop>)
}
	      
