#### elaborate state
## elaborate predicate
sp {generate-grounded-relation-predicate*elaborate*predicate
   (state <s> ^name generate-grounded-predicate
   	      ^superstate.operator <sop>
	      ^smem.command <scomm>)
   (<sop> ^predicate-definition <pred>
	  ^superoperator <super>
	  ^desired <d>
	  ^procedural-id <pid>
	  ^type <type>)
-->
   (<s> ^predicate-definition <pred>
   	^type <type>
	^indexed-action <super>
	^desired <d>
	^grounded-predicate <gp>
	^procedural-id <pid>
	^type <type>
	^desired-grounded-predicate <dgp>)
   (<gp> ^_instantiation-of <pred>)
}

### retrieve pred
sp {generate-grounded-relation-predicate*retrieve-predicate
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      -^retrieved-lti <pred>)
-->
   (<s> ^nretrieve-lti <pred>)
}

### elaborate arguments that can be filled in from the action-command
sp {generate-grounded-predicate*elaborate*arguments*action-command*level1
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<ia> ^<parg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level1*equal
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pobj>)
   (<pred> ^<pred-arg> <nobj>)
   (<nobj> ^equals <pobj>)
   (<ia> ^<parg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level2
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pval>)
   (<pval> ^<pval-arg> <pobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<ia> ^<parg> <ival>)
   (<ival> ^<pval-arg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level2*equals
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pval>)
   (<pval> ^<pval-arg> <nobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<pobj> ^equals <nobj>)
   (<ia> ^<parg> <ival>)
   (<ival> ^<pval-arg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

### test if smem-object and wmem-object have the same attributes
sp {generate-grounded-predicate*test-attributes*smem*wmem*pairings*attribute*1
   (state <s> ^name generate-grounded-predicate
   	      ^pairing <pair>)
   (<pair> ^smem-object <sobj>
   	   ^wmem-object <wobj>)
   (<sobj> ^attribute <label1>
   	   -^attribute {<label2> <> <label1>})
   (<wobj> ^property.label <label1>)
-->
   (<pair> ^matched yes)
}

sp {generate-grounded-predicate*test-attributes*smem*wmem*pairings*attribute*3
   (state <s> ^name generate-grounded-predicate
   	      ^pairing <pair>)
   (<pair> ^smem-object <sobj>
   	   ^wmem-object <wobj>)
   (<sobj> ^attribute <label1>
   	   ^attribute {<label2> <> <label1>}
	   ^attribute {<label3> <> <label1> <> <label2>}
	   -^attribute {<label4> <> <label3> <> <label1> <> <label2>})
   (<wobj> ^property.label <label1>
   	   ^property.label <label2>
	   ^property.label <label3>)
-->
   (<pair> ^matched yes)
}

sp {generate-grounded-predicate*test-attributes*smem*wmem*pairings*attribute*2
   (state <s> ^name generate-grounded-predicate
   	      ^pairing <pair>)
   (<pair> ^smem-object <sobj>
   	   ^wmem-object <wobj>)
   (<sobj> ^attribute <label1>
   	   -^attribute {<label2> <> <label1>}
	   -^attribute {<label3> <> <label1> <> <label2>})
   (<wobj> ^property.label <label1>
   	   ^property.label <label2>)
-->
   (<pair> ^matched yes)
}

### elaborate objects that do not have pairings
sp {generate-grounded-predicate*elaborate*arguments*not*in*action-command
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>)
   (<pred> ^{<parg> <> state <> identifier} <pobj>)
  -{(<s> ^pairing <pair>)
    (<pair> ^smem-object <pobj>)}
-->
   (<s> ^not-matched <pobj>)
}

### elaborate retrievals for objects that do-not have pairings
sp {generate-grounded-predicate*retrieve*not-matched
   (state <s> ^name generate-grounded-predicate
   	      ^not-matched <pobj>
	      -^retrieved-lti <pobj>)
#   (<pobj> -^<arg>)
-->
   (<s> ^nretrieve-lti <pobj>)
}