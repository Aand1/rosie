#################### learn goal-elaboration operator #######################
sp {generate-goal*propose*complete-generate-goal*elaborate-goal
   (state <s> ^name generate-goal
   	      ^desired <d>
	      ^superstate <ss> 
	      ^operator.name complete-generate-goal)
   (<ss> ^action <ia>
   	 ^superstate <sss>)
   (<ia> ^name <name>
   	 ^_metadata.argument-count <arg-count>)
-->
    (<ss> ^desired-state <d>)
}

sp {generate-goal*propose*elaborate-goal
   (state <s> ^desired-state <d>
   	      ^superstate <ss>)
   (<ss> ^indexed-action <ia>)
-->
   (<ss> ^operator <op> + >)
   (<op> ^name elaborate-goal
   	 ^desired <d>
	 ^indexed-action <ia>)
}

##############################################################################

sp {generate-goal*apply*complete-generate-goal*single
   (state <s> ^operator <op>
   	      ^indexed-action <ia>)
   (<ia> -^desired)
   (<op> ^name elaborate-goal
   	 ^desired <d>
	 ^indexed-action <ia>)
-->
   (<ia> ^desired <d>)
   (<s> ^operator <op> -)
}

sp {generate-goal*apply*complete-generate-goal*single*already-elaborated
   (state <s> ^operator <op>
   	      ^indexed-action <ia>)
   (<ia> ^desired)
   (<op> ^name elaborate-goal
   	 ^desired <d>
	 ^indexed-action <ia>)
-->
   (<s> ^operator <op> -)
}

sp {generate-goal*prefer*any*elaborate-goal*same-indexed-action
   (state <s> ^operator <op1> +
   	      ^operator {<op2> > <op1>} +)
   (<op1> ^name elaborate-goal
   	  ^indexed-action <ia>)
   (<op2> ^name elaborate-goal
   	  ^indexed-action <ia>)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {generate-goal*prefer*any*elaborate-goal*different-indexed-action
   (state <s> ^operator <op1> +
   	      ^operator {<op2> > <op1>} +)
   (<op1> ^name elaborate-goal
   	  ^indexed-action <ia>)
   (<op2> ^name elaborate-goal
   	  ^indexed-action {<ia3> <> <ia1>})
-->
   (<s> ^operator <op1> = <op2>)
}

sp {generate-goal*apply*complete-generate-goal*multiple*same-indexed-action
   (state <s> ^operator <op>
	      ^operator {<op2> <> <op>} +)
   (<op> ^name elaborate-goal
   	 ^desired <d>
	 ^indexed-action <ia>)
   (<op2> ^name elaborate-goal
   	  ^indexed-action <ia>)
-->
   (<ia> ^desired <d>)
   (<s> ^operator <op> -
   	^operator <op2> -)
}


sp {elaborate-goal*lookahead*required
   (state <s> ^look-ahead-operator <lop>
   	      ^operator <op> +)
   (<op> ^name elaborate-goal
   	 ^indexed-action <lop>)
-->
   (<s> ^operator <op> !)
}

sp {elaborate-goal*primitive*better
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op2> ^name elaborate-goal)
   (<op1> ^action-type << primitive composite >>)
-->
   (<s> ^operator <op2> > <op1>)
}

sp {elaborate-goal*smem-retrieve*better
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name elaborate-goal)
   (<op2> ^name smem-retrieve-new)
-->
   (<s> ^operator <op2> > <op1>)
}
   	  