### ground goal arguments using pairs
sp {generate-ground-predicate*paired-arugments
   (state <s> ^name generate-grounded-predicate
   	      ^default-assignment <dva>
	      ^definition <def>
	      ^pair <pair>)
   (<dva> ^grounded-predicate <gp>
   	  ^matched <pair>)
   (<pair> ^wmem-object <wobj>
	   ^argument <arg>)
   (<def> ^<arg> <sobj>)
-->
   (<gp> ^<arg> <wobj>)
}

#### elaborate if grounding is complete
sp {generate-grounded-predicate*grounding*relation*success
   (state <s> ^name generate-grounded-predicate
	      ^default-assignment <dva>)
   (<dva> ^grounded-predicate <gp>)
   (<gp> ^relation <any-rel>
   	 ^p1 <any-p1>
	 ^p2 <any-p2>)
-->
   (<dva> ^evaluation <eval>)
   (<eval> ^grounding-successful <gp>)
}

sp {generate-grounded-predicate*grounding*state*success
   (state <s> ^name generate-grounded-predicate
	      ^default-assignment <dva>)
   (<dva> ^grounded-predicate <gp>)
   (<gp> ^state <any-rel>
   	 ^p1 <any-p1>)
-->
   (<dva> ^evaluation <eval>)
   (<eval> ^grounding-successful <gp>)
}
   

### grounding failure
sp {generate-grounded-predicate*grounding*failure
   (state <s> ^name generate-grounded-predicate
   	      ^default-assignment <dva>
	      ^quiescence t)
   (<dva> ^grounded-predicate <gp>
	 -^grounding-successful <gp>)  
   (<gp> ^{<arg> <> _instantiation-of <> relation})
-->
   (<dva> ^evaluation <eval>)
   (<eval> ^grounding-failure <gp>)
}