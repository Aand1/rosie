### object attribute match
sp {generate-grounded-predicate*elaborate*filters*no-defaults
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<pair> -^default)
-->
   (<dva> ^<pair> <f>)
}

sp {generate-grounded-predicate*elaborate*filters*same-defaults
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<pair> ^default <def>
   	   ^argument <garg>)
   (<dva> ^<garg> <def>)
-->
   (<dva> ^<pair> <f>)
}

#### for defaults also match the constraint
sp {generate-grounded-predicapte*elaborate*extra-constraints
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^pair {<pair2> <> <pair>}
	      ^operator.default-assignment <dva>)
   (<dva> ^<pair2> <f>
   	  ^<goal-arg> <def>)
   (<pair> ^default <def>)
   (<def> ^<arg> <sobj>)
   (<pair2> ^argument <arg>
   	    ^wmem-object <wobj>
	    ^smem-object {<sobj2> <> <sobj>})
   (<sobj> ^<attribute> {<label> <> generic})
-->
   (<f> ^<attribute> <label>)
}


sp {generate-grounded-predicate*elaborate*filters*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<dva> ^<pair> <filter>)
   (<pair> ^smem-object <sobj>)
   (<sobj> ^<attribute> {<label> <> generic})
-->
   (<filter> ^<attribute> <label>)
}

sp {generate-grounded-predicate*perform*feature*match*1*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<pair> ^wmem-object <wobj>)
   (<dva> ^<pair> <sobj>)
   (<sobj> ^<attribute1> <label1>
   	   -^<attribute2> {<label2> <> <label1>})
   (<wobj> ^property <prop>)
   (<prop> ^label <label1>
   	   ^name <attribute1>)
-->
   (<dva> ^matched <pair>)
}

sp {generate-grounded-predicate*perform*feature*match*2*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<pair> ^wmem-object <wobj>)
   (<dva> ^<pair> <sobj>)
   (<sobj> ^<attribute1> <label1>
   	   ^<attribute2> {<label2> <> <label1>}
	   -^<attribute> {<label3> <> <label1> <> <label2>})
   (<wobj> ^property <prop1>
   	   ^property <prop2>)
   (<prop1> ^label <label1>
   	    ^name <attribute1>)
   (<prop2> ^label <label2>
   	    ^name <attribute2>)
-->
   (<dva> ^matched <pair>)
}

sp {generate-grounded-predicate*perform*feature*match*3*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<pair> ^wmem-object <wobj>)
   (<dva> ^<pair> <sobj>)
   (<sobj> ^<attribute1> <label1>
   	   ^<attribute2> {<label2> <> <label1>}
	   ^<attribute3> {<label3> <> <label1> <> <label2>}
	   -^attribute {<label4> <> <label3> <> <label1> <> <label2>})
   (<wobj> ^property <prop1>
   	   ^property <prop2>
	   ^property <prop3>)
   (<prop1> ^label <label1>
   	    ^name <attribute1>)
   (<prop2> ^label <label2>
   	    ^name <attribute2>)
   (<prop3> ^label <label3>
   	    ^name <attribute3>)
-->
   (<dva> ^matched <pair>)
}

### elaborate matches for relation and state
sp {generate-grounded-predicate*perform*relation*match
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<pair> ^argument relation)
-->
   (<dva> ^matched <pair>)
}

sp {generate-grounded-predicate*perform*state*match
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>
	      ^operator.default-assignment <dva>)
   (<pair> ^argument state)
-->
   (<dva> ^matched <pair>)
}

