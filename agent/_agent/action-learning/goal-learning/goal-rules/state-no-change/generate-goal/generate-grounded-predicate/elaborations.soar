#### elaborate state ##################################################
sp {generate-grounded-predicate*elaborate*state
   (state <s> ^name generate-grounded-predicate
   	      ^superstate.operator <sop>)
   (<sop> ^definition <val>
   	  ^grounded-action <ia>
	  ^desired <d>
	  ^procedural-id <pid>
	  ^type <predicate>)
-->
   (<s> ^definition <val>
   	^grounded-action <ia>
	^desired <d>
	^procedural-id <pid>
	^predicate-type <predicate>)
}

sp {generate-grounded-predicate*elaborate*grounded-predicate
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      ^default-assignment <dva>)
-->
   (<dva> ^grounded-predicate <gp>)
   (<gp> ^_instantiation-of <def>)
}

sp {generate-grounded-predicate*copy*failed*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^superstate.failure <f>)
-->
   (<s> ^failure <f>)
}

#### retrieve defintion ##################################################
sp {generate-grounded-predicate*retrieve*definition
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <def>)
   (<def> -^<attr>)
-->
   (<s> ^nretrieve-lti <def>)
}

#### elaborate arguments that are common with the action definition #########
sp {generate-grounded-predicate*elaborate*object-pairs*level1
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      ^procedural-id <pid>
	      ^grounded-action <ia>)
   (<def> ^<arg> <val>)
   (<pid> ^<p-arg> <val>)
   (<ia> ^<p-arg> <obj>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <val>
   	   ^wmem-object <obj>
	   ^paired-with action
	   ^argument <arg>)
}

sp {generate-grounded-predicate*elaborate*object-pairs*level2
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      ^procedural-id <pid>
	      ^grounded-action <ia>)
   (<def> ^<arg> <val>)
   (<pid> ^<p-arg> <pval>)
   (<pval> ^<pval-arg> <val>)
   (<ia> ^<p-arg> <iaval>)
   (<iaval> ^<pval-arg> <obj>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <val>
   	   ^wmem-object <obj>
	   ^paired-with action
	   ^argument <arg>)
}

##### elaborate objects that have not been paired with grounded actions ###
sp {generate-grounded-predicate*retrieve*predicate*arguments
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <val>)
   (<def> ^{<arg> <> identifier} <val>)
   (<val> -^<attr>)
-->
   (<s> ^nretrieve-lti <val>)
}


sp {generate-grounded-predicate*retrieve*defaults*default
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <val>)
   (<def> ^{<arg> <> identifier}.default <val>)
   (<val> -^<attr>)
-->
   (<s> ^nretrieve-lti <val>)
}

sp {generate-grounded-predicate*retrieve*defaults*default*attr
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <val>)
   (<def> ^{<arg> <> identifier}.default.<narg> <val>)
   (<val> -^<attr>)
-->
   (<s> ^nretrieve-lti <val>)
}

sp {generate-grounded-predicate*elaborate*default*objects
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^paired-with-action <val>
	      ^object <obj>)
   (<def> ^{<arg> <> relation} <val>)
   (<val> ^default <default>)
   (<default> ^value <def-val>)
  -{(<s> ^pair <p>)
    (<p> ^paired-with action
    	 ^wmem-object <obj>)}
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <def-val>
   	   ^wmem-object <obj>
	   ^default <default>
	   ^argument <arg>)
}
   
sp {generate-grounded-predicate*elaborate*default*relation
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^paired-with-action <val>
	      ^topstate.found-prep <fp>)
   (<fp> ^smem-rep <dval>)
   (<def> ^relation <val>)
   (<val> ^default <default>)
   (<default> ^value <dval>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <dval>
   	   ^wmem-object <fp>
	   ^argument relation
	   ^default <default>)
}

sp {generate-grounded-predicate*elaborate*default*state
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^paired-with-action <val>)
   (<def> ^state <val>)
   (<val> ^default <def>)
   (<def> ^value <dval>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <dval>
   	   ^wmem-object <dval>
	   ^argument state
	   ^default <def>)
}

############## elaborate default-value-assignment
sp {generate-grounded-predicate*elaborate*assignment
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>)
-->
   (<s> ^assignment <asg>)
}

sp {generate-grounded-predicate*elaborate*default-value-assignment*one
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>)
   (<def> ^<arg1> <val1>)
   (<val1> ^default <def1>)
 -{(<def> ^{<arg2> <> <arg1>} <val2>)
   (<val2> ^default <def2>)}
-->
   (<s> ^assignment <dva>)
   (<dva> ^<arg1> <def1>)
}

sp {generate-grounded-predicate*elaborate*default-value-assignment*two
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>)
   (<def> ^<arg1> <val1>)
   (<def> ^{<arg2> > <arg1>} <val2>)
   (<val1> ^default <def1>)
   (<val2> ^default <def2>)
 -{(<def> ^{<arg3> <> <arg1> <> <arg2>} <val3>)
   (<val3> ^default <def3>)}
-->
   (<s> ^assignment <dva>)
   (<dva> ^<arg1> <def1>
   	  ^<arg2> <def2>)
}

sp {generate-grounded-predicate*elaborate*default-value-assignment*three
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>)
   (<def> ^<arg1> <val1>)
   (<def> ^{<arg2> > <arg1>} <val2>)
   (<def> ^{<arg3> > <arg2> > <arg1>} <val3>)
   (<val1> ^default <def1>)
   (<val2> ^default <def2>)
   (<val3> ^default <def3>)
-->
   (<s> ^assignment <dva>)
   (<dva> ^<arg1> <def1>
   	  ^<arg2> <def2>
	  ^<arg3> <def3>)
}

#### if no assignment has a successful evaluation, grounding is a failure
sp {generate-grounded-predicate*elaborate*grounding*success
   (state <s> ^name generate-grounded-predicate
   	      ^grounded-action <ga>
   	      ^assignment.evaluation.evaluation successful)
   
-->
   (<s> ^grounding successful)
}

sp {generate-grounded-predicate*elaborate*grounding*failure
   (state <s> ^name generate-grounded-predicate
   	      -^grounding successful)
-->
   (<s> ^grounding failure)
}

     