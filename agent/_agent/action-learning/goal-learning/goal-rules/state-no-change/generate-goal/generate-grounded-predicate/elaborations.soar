#### elaborate state ##################################################
sp {generate-grounded-predicate*elaborate*state
   (state <s> ^name generate-grounded-predicate
   	      ^superstate.operator <sop>)
   (<sop> ^definition <val>
   	  ^grounded-action <ia>
	  ^desired <d>
	  ^procedural-id <pid>
	  ^type <predicate>)
-->
   (<s> ^definition <val>
   	^grounded-action <ia>
	^desired <d>
	^procedural-id <pid>
	^type <predicate>)
}

sp {generate-grounded-predicate*elaborate*grounded-predicate
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>)
-->
   (<s> ^grounded-predicate <gp>)
   (<gp> ^_instantiation-of <def>)
}

#### retrieve defintion ##################################################
sp {generate-grounded-predicate*retrieve*definition
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <def>)
   (<def> -^<attr>)
-->
   (<s> ^nretrieve-lti <def>)
}

#### elaborate arguments that are common with the action definition #########
sp {generate-grounded-predicate*elaborate*object-pairs*level1
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      ^procedural-id <pid>
	      ^grounded-action <ia>)
   (<def> ^<arg> <val>)
   (<pid> ^<p-arg> <val>)
   (<ia> ^<p-arg> <obj>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <val>
   	   ^wmem-object <obj>
	   ^paired-with action
	   ^argument <arg>)
}

sp {generate-grounded-predicate*elaborate*object-pairs*level2
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      ^procedural-id <pid>
	      ^grounded-action <ia>)
   (<def> ^<arg> <val>)
   (<pid> ^<p-arg> <pval>)
   (<pval> ^<pval-arg> <val>)
   (<ia> ^<p-arg> <iaval>)
   (<iaval> ^<pval-arg> <obj>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <val>
   	   ^wmem-object <obj>
	   ^paired-with action
	   ^argument <arg>)
}

##### elaborate objects that have not been paired with grounded actions ###
sp {generate-grounded-predicate*retrieve*predicate*arguments
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <val>)
   (<def> ^{<arg> <> identifier} <val>)
   (<val> -^<attr>)
-->
   (<s> ^nretrieve-lti <val>)
}


sp {generate-grounded-predicate*retrieve*defaults*default
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <val>)
   (<def> ^{<arg> <> identifier}.default <val>)
   (<val> -^<attr>)
-->
   (<s> ^nretrieve-lti <val>)
}

sp {generate-grounded-predicate*retrieve*defaults*default*attr
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^retrieved-lti <val>)
   (<def> ^{<arg> <> identifier}.default.<narg> <val>)
   (<val> -^<attr>)
-->
   (<s> ^nretrieve-lti <val>)
}

sp {generate-grounded-predicate*elaborate*default*objects
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^paired-with-action <val>
	      ^object <obj>)
   (<def> ^{<arg> <> relation} <val>)
   (<val> ^default <default>)
   (<default> ^value <def-val>)
  -{(<s> ^pair <p>)
    (<p> ^paired-with action
    	 ^wmem-object <obj>)}
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <def-val>
   	   ^wmem-object <obj>
	   ^default <default>
	   ^argument <arg>)
}
   
sp {generate-grounded-predicate*elaborate*default*relation
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^paired-with-action <val>
	      ^topstate.found-prep <fp>)
   (<fp> ^smem-rep <dval>)
   (<def> ^relation <val>)
   (<val> ^default <default>)
   (<defaul> ^value <dval>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <val>
   	   ^wmem-object <fp>
	   ^argument relation
	   ^default <default>
	   ^matched yes)
}

sp {generate-grounded-predicate*elaborate*default*state
   (state <s> ^name generate-grounded-predicate
   	      ^definition <def>
	      -^paired-with-action <val>)
   (<def> ^state <val>)
   (<val> ^default <def>)
   (<def> ^value <dval>)
-->
   (<s> ^pair <pair>)
   (<pair> ^smem-object <dval>
   	   ^wmem-object <dval>
	   ^argument state
	   ^default <def>
	   ^matched yes)
}
#### for defaults also match the constraint
sp {generate-grounded-predicate*elaborate*extra-constraints
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>)
   (<pair> ^default <def>)
   (<def> ^<arg> <sobj>)
   (<pair2> ^argument <arg>
   	    ^wmem-object <wobj>
	    ^smem-object {<sobj2> <> <sobj>}
	    ^filters <f>)
   (<sobj> ^<attribute> <label>)
-->
   (<f> ^<attribute> <label>)
}

### object attribute match
sp {generate-grounded-predicate*elaborate*filters
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>)
-->
   (<pair> ^filters <filter>)
}
sp {generate-grounded-predicate*elaborate*filters*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>)
   (<pair> ^filters <filter>
   	   ^smem-object <sobj>)
   (<sobj> ^<attribute> <label>)
-->
   (<filter> ^<attribute> <label>)
}

sp {generate-grounded-predicate*perform*feature*0*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>)
   (<pair> ^filters <sobj>)
   (<sobj> -^<attribute> <label1>)
-->
   (<sobj> ^no attributes)
}

sp {generate-grounded-predicate*perform*feature*match*0*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>)
   (<pair> ^filters <sobj>)
   (<sobj> ^no attributes)
-->
   (<pair> ^matched true)
}

sp {generate-grounded-predicate*perform*feature*match*1*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pair <pair>)
   (<pair> ^filters <sobj>
   	   ^wmem-object <wobj>)
   (<sobj> ^<attribute1> <label1>
   	   -^<attribute2> {<label2> <> <label1>})
   (<wobj> ^property <prop>)
   (<prop> ^label <label1>
   	   ^name <attribute1>)
-->
   (<pair> ^matched yes)
}

sp {generate-grounded-predicate*perform*feature*match*2*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pairing <pair>)
   (<pair> ^filters <sobj>
   	   ^wmem-object <wobj>)
   (<sobj> ^<attribute1> <label1>
   	   ^<attribute2> {<label2> <> <label1>}
	   -^<attribute> {<label3> <> <label1> <> <label2>})
   (<wobj> ^property <prop1>
   	   ^property <prop2>)
   (<prop1> ^label <label1>
   	    ^name <attribute1>)
   (<prop2> ^label <label2>
   	    ^name <attribute2>)
-->
   (<pair> ^matched yes)
}

sp {generate-grounded-predicate*perform*feature*match*3*attribute
   (state <s> ^name generate-grounded-predicate
   	      ^pairing <pair>)
   (<pair> ^filters <sobj>
   	   ^wmem-object <wobj>)
   (<sobj> ^<attribute1> <label1>
   	   ^<attribute2> {<label2> <> <label1>}
	   ^<attribute3> {<label3> <> <label1> <> <label2>}
	   -^attribute {<label4> <> <label3> <> <label1> <> <label2>})
   (<wobj> ^property <prop1>
   	   ^property <prop2>
	   ^property <prop3>)
   (<prop1> ^label <label1>
   	    ^name <attribute1>)
   (<prop2> ^label <label2>
   	    ^name <attribute2>)
   (<prop3> ^label <label3>
   	    ^name <attribute3>)
-->
   (<pair> ^matched yes)
}

