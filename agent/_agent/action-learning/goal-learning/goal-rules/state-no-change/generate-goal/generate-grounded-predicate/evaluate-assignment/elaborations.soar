sp {evaluate-assigment*copy*assignment
   (state <s> ^name evaluate-assignment	
   	      ^superstate.operator.assignment <asg>)
-->
   (<s> ^assignment <asg>
   	^evaluation <eval>)
}

sp {evaluate-assigment*copy*definition
   (state <s> ^name evaluate-assignment	
   	      ^superstate.definition <def>)
-->
   (<s> ^grounded-predicate <gp>)
   (<gp> ^_instantiation-of <def>)
   (<gp> ^default-values <dv>)
}

sp {evaluate-assignment*elaborate*defaults*grounded-predicate
   (state <s> ^name evaluate-assignment
   	      ^grounded-predicate.default-values <def>
	      ^assignment <asg>)
   (<asg> ^{<arg> << p1 p2 relation state >>} <val>)
-->
   (<def> ^<arg> <val>)
}

sp {evaluate-assignment*copy*pairs
   (state <s> ^name evaluate-assignment	
   	      ^superstate.pair <pair>)
-->
   (<s> ^pair <pair>)
}

sp {evaluate-assignment*create*filter*no-defaults
   (state <s> ^name evaluate-assignment	
   	      ^pair <pair>)
   (<pair> -^default)  
-->
   (<s> ^<pair> <f>)
}

sp {evaluate-assignment*create*filter*same-defaults
   (state <s> ^name evaluate-assignment	
   	      ^pair <pair>
	      ^assignment <asg>)
   (<pair> ^default <def>
   	   ^argument <garg>)
   (<asg> ^<garg> <def>)
-->
   (<s> ^<pair> <f>)
}

######### elaborate filters 
sp {evaluate-assignment*elaborate*filters
   (state <s> ^name evaluate-assignment	
   	      ^pair <pair>
	      ^<pair> <filter>)
   (<pair> ^smem-object <sobj>
   	   -^argument relation
	   -^argument state)
   (<sobj> ^{<attr> <> default} {<label> <> generic})
-->
   (<filter> ^<attr> <label>)
}

sp {evaluate-assignment*elaborate*filters*constraint*from*defaults
   (state <s> ^name evaluate-assignment
	      ^pair <pair2>
	      ^assignment <asg>
	      ^<pair2> <f>)
   (<asg> ^<garg> <def>)
   (<def> ^<arg> <sobj>)
   (<pair2> ^argument <arg>
   	    -^default
   	    ^smem-object {<sobj2> <> <sobj>}
     	    ^wmem-object <wobj>)
   (<sobj> ^<attr> {<label> <> generic})
-->
   (<f> ^<attr> <label>)
}


############# ground goal arguments using pairs
sp {evaluate-assignment*paired-argument*relation
   (state <s> ^name evaluate-assignment	
   	      ^superstate.definition <def>
	      ^pair <pair> 
	      ^matched <pair>
	      ^grounded-predicate <gp>)
   (<def> ^<arg> <sobj>)
   (<pair> ^wmem-object <wobj>
   	   ^argument <arg>)
-->
   (<gp> ^<arg> <wobj>)
}

#### all grounded action objects should be grounded

sp {evaluate-assignment*superstate*grounded-action*objects*level1
   (state <s> ^name evaluate-assignment
   	      ^superstate.grounded-action.{<arg> <> name <> _metadata} <obj>
	      -^grounded-predicate.<garg> <obj>)
   (<obj> -^argument)
-->
   (<s> ^not-grounded-action-argument <obj>)
}

sp {evaluate-assignment*superstate*grounded-action*objects*level2
   (state <s> ^name evaluate-assignment
   	      ^superstate.grounded-action.{<arg> <> name <> _metadata}.<< argument relation >> <obj>
	      -^grounded-predicate.<garg> <obj>)
-->
   (<s> ^not-grounded-action-argument <obj>)
}

############ ground predicate
sp {evaluate-assignment*grounding*relation*success
   (state <s> ^name evaluate-assignment
   	      ^grounded-predicate <gp>
	      ^evaluation <eval>
	      ^superstate <ss>
	      -^not-grounded-action-argument <any>)
   (<gp> ^relation <any-rel>
   	 ^p1 <any-p1>
	 ^p2 <any-p2>)
-->
   (<eval> ^evaluation successful
   	   ^grounded-predicate <gp>)
}

sp {evaluate-assignment*grounding*relation*failure*not-complete
   (state <s> ^name evaluate-assignment
   	      ^grounded-predicate <gp>
	      ^evaluation <eval>
	      ^superstate.definition.relation)
   -{(<gp> ^relation <any-rel>
   	 ^p1 <any-p1>
	 ^p2 <any-p2>)}
-->
   (<eval> ^evaluation failure
   	   ^grounded-predicate <gp>)
}

sp {evaluate-assignment*grounding*failure*action-not-grounded
   (state <s> ^name evaluate-assignment
   	      ^grounded-predicate <gp>
	      ^evaluation <eval>
	      ^not-grounded-action-argument <arg>)
-->
   (<eval> ^evaluation failure
   	   ^grounded-predicate <gp>)
}

sp {evaluate-assignment*grounding*state*success
   (state <s> ^name evaluate-assignment
   	      ^grounded-predicate <gp>
	      ^evaluation <eval>
	      ^superstate.definition.state
	      -^not-grounded-action-argument <any>)
   (<gp> ^state <any-state>
   	 ^p1 <any-p1>)
-->
   (<eval> ^evaluation successful
   	   ^grounded-predicate <gp>)
}


sp {evaluate-assignment*grounding*state*failure
   (state <s> ^name evaluate-assignment
   	      ^grounded-predicate <gp>
	      ^evaluation <eval>
	      ^superstate.definition.state)
   -{(<gp> ^state <any-state>
   	 ^p1 <any-p1>)}
-->
   (<eval> ^evaluation failure
   	   ^grounded-predicate <gp>)
}
