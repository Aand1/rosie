sp {generate-goal*propose*initialize-desired
   (state <s> ^name generate-goal
   	      ^goal-id.<attr>
	      -^desired)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name initialize-desired)
}


### for every predicate definition in the goal definition, propose generate-grounded-predicate

sp {generate-goal*propose*generate-grounded-predicate
   (state <s> ^name generate-goal
   	      ^goal-id <gid>
	      ^desired <d>
	      ^procedural-id <pid>
	      ^grounded-action <ia>
	      ^failure <det>)
   (<gid> ^{<predicate> << relation-predicate state-predicate >>} <val>)
### no grounded predicate in desired
  -{(<d> ^<dpred> <dval>)
    (<dval> ^_instantiation-of <val>
    	    ^next <n>)}
### grounding predicate did not fail
  -{(<det> ^predicate-grounding-failure <pgf>)
    (<pgf> ^predicate-definition <val>)}
-->
    (<s> ^operator <op> +)
    (<op> ^name generate-grounded-predicate
    	  ^definition <val>
	  ^grounded-action <ia>
	  ^desired <d>
	  ^procedural-id <pid>
	  ^type <predicate>)
}

## if there is a failure message on the state, propose process failure
sp {generate-goal*propose*failure*failure-message
   (state <s> ^name generate-goal
   	      ^failure <f>)
  -{(<f> -^predicate-grounding-failure)}
-->
   (<s> ^operator <op> + <, =)
   (<op> ^name process-failure)
}

sp {generate-goal*propose*failure*failure-message*unknown-goal
   (state <s> ^name generate-goal
   	      ^failure.type unknown-goal)
 -->
   (<s> ^operator <op> + <, =)
   (<op> ^name process-failure)
}

sp {generate-goal*propose*process-failure
   (state <s> ^name generate-goal
   	      ^assigned <a1>
	      ^assigned {<a2> <> <a1>}
	      ^failure <f>)
   (<a1> ^smem <sobj>
   	 ^wmem <wobj>)
   (<a2> ^smem <sobj>
   	 ^wmem {<wobj2> <> <wobj>})
-->
   (<s> ^operator <op> + <, =)
   (<op> ^name process-failure
   	 ^type unmatched-constraints
	 ^wmem-object <wobj> <wobj2>)
}



sp {generate-goal*propose*complete-generate-goal
   (state <s> ^name generate-goal
   	      ^desired <d>
	      ^goal-id <gid>
	      ^desired <des>)
 # -{(<gid> ^<arg> <pred-def>)
 #   (<des> -^<garg>._instantiation-of <pred-def>)}
   (<d> -^list.next null)
-->
   (<s> ^operator <op> + <)
   (<op> ^name complete-generate-goal)
}
