sp {generate-goal*propose*initialize-desired
   (state <s> ^name generate-goal
   	      ^goal-id.<attr>
	      -^desired)
-->
   (<s> ^operator <op> +)
   (<op> ^name initialize-desired)
}


### for every predicate definition in the goal definition, propose generate-grounded-predicate

sp {generate-goal*propose*generate-grounded-predicate
   (state <s> ^name generate-goal
   	      ^goal-id <gid>
	      ^desired <d>
	      ^procedural-id <pid>
	      ^grounded-action <ia>)
   (<gid> ^{<predicate> << relation-predicate state-predicate >>} <val>)
### no grounded predicate in desired
  -{(<d> ^<dpred> <dval>)
    (<dval> ^_instantiation-of <val>
    	    ^next <n>)}
### grounding predicate failed
  -{(<s> ^failure <f>)
    (<f> ^_instantiation-of <val>)}
-->
    (<s> ^operator <op> +, =)
    (<op> ^name generate-grounded-predicate
    	  ^definition <val>
	  ^grounded-action <ia>
	  ^desired <d>
	  ^procedural-id <pid>
	  ^type <predicate>)
}

## if there is a failure message on the state, propose process failure
sp {generate-goal*propose*failure*failure-message
   (state <s> ^name generate-goal
   	      ^failure <f>)
-->
   (<s> ^operator <op> + <, =)
   (<op> ^name process-failure)
}


sp {generate-goal*propose*complete-generate-goal
   (state <s> ^name generate-goal
   	      ^desired <d>)
-->
   (<s> ^operator <op> + <)
   (<op> ^name complete-generate-goal)
}
