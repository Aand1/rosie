sp {generate-revisions*elaborate*state
   (state <s> ^name generate-revisions
   	      ^superstate.operator <sop>)
   (<sop> ^name generate-revisions
   	  ^grounded-predicate <rel>
	  ^goal-definition <gdef>
	  ^grounded-action <ga>)
   (<ga> ^_metadata.map-id.procedural <pid>)
-->
   (<s> ^grounded-predicate <rel>
   	^goal-definition <gdef>
	^grounded-action <ga>
	^action-definition <pid>)
}

sp {generate-revisions*elaborate*common*action*level1
   (state <s> ^name generate-revisions
   	      ^grounded-predicate <pred>
	      ^grounded-action <ga>)
   (<pred> ^<arg> <val>)
   (<ga> ^<aarg> <val>)
-->
   (<s> ^common <cw>)
   (<cw> ^goal-argument <arg>
   	 ^action-argument <aarg>
	 ^level 1)
}

sp {generate-revisions*elaborate*common*action*level2
   (state <s> ^name generate-revisions
   	      ^grounded-predicate <pred>
	      ^grounded-action <ga>)
   (<pred> ^<arg> <val>)
   (<ga> ^<aarg>.{<val-arg> << argument relation >>} <val>)
-->
   (<s> ^common <cw>)
   (<cw> ^goal-argument <arg>
   	 ^action-argument <aarg>
	 ^level 2
	 ^type <val-arg>)
}

sp {generate-revisions*elaborate*not-common
   (state <s> ^name generate-revisions
   	      ^grounded-predicate <pred>)
   (<pred> ^<arg> <val>)
 -{(<s> ^common <cw>)
   (<cw> ^goal-argument <arg>
   	 ^action-argument {<aarg> <> none})}
-->
   (<s> ^common <cw>)
   (<cw> ^goal-argument <arg>
   	 ^action-argument none
	 ^level none)
}

sp {generate-revisions*verify*goal*action*level1
   (state <s> ^name generate-revisions
   	      ^action-definition <pid>
	      ^goal-definition.<pred-arg> <def>
	      ^common <cw>)
   (<cw> ^goal-argument <arg>
   	 ^action-argument {<aarg> <> none}
	 ^level 1)
   (<pid> ^<aarg> <sobj>)
   (<def> ^<arg> <sobj>)
-->
   (<cw> ^matched <def>)
}

sp {generate-revisions*verify*goal*action*level2
   (state <s> ^name generate-revisions
   	      ^action-definition <pid>
	      ^goal-definition.<pred-def> <def>
	      ^common <cw>)
   (<cw> ^goal-argument <arg>
   	 ^action-argument {<aarg> <> none}
	 ^level 2
	 ^type <val-arg>)
   (<pid> ^<aarg>.<val-arg> <sobj>)
   (<def> ^<arg> <sobj>)
-->
   (<cw> ^matched <def>)
}

sp {generate-revisions*verify*goal*action*none
   (state <s> ^name generate-revisions
   	      ^action-definition <pid>
	      ^goal-definition.<pred-def> <def>
	      ^common <cw>)
   (<cw> ^goal-argument <arg>
   	 ^action-argument none
	 ^level none)
   (<def> ^<arg> <sobj>)
   (<pid> -^<p1arg> <sobj>)
   (<pid> -^<p2arg>.<val-arg> <sobj>)
-->
   (<cw> ^matched <def>)
}
  


sp {generate-revisions*match*relation-predicate
   (state <s> ^name generate-revisions
   	      ^goal-definition.relation-predicate <rdef>
	      ^grounded-predicate <pred>
	      ^common <cw1>
	      ^common <cw2>
	      ^common <cw3>)
   (<pred> ^p1 <obj1>
   	   ^p2 <obj2>
	   ^relation <rel>)
   (<cw1> ^goal-argument p1
   	  ^matched <rdef>)
   (<cw2> ^goal-argument p2
   	  ^matched <rdef>)
   (<cw3> ^goal-argument relation
   	  ^matched <rdef>)
-->
   (<s> ^matched-definition <rdef>)
}

sp {generate-revisions*match*state-predicate
   (state <s> ^name generate-revisions
   	      ^goal-definition.state-predicate <rdef>
	      ^grounded-predicate <pred>
	      ^common <cw1>
	      ^common <cw2>
	      ^common <cw3>)
   (<pred> ^p1 <obj1>
	   ^state <rel>)
   (<cw1> ^goal-argument p1
   	  ^matched <rdef>)
   (<cw2> ^goal-argument state
   	  ^matched <rdef>)
-->
   (<s> ^matched-definition <rdef>)
}
  
sp {generate-revisions*write*revise-predicate-defition*superstate
   (state <s> ^name generate-revisions
   	      ^superstate <ss>
	      ^grounded-predicate <pred>
   	      ^matched-definition <rdef>)
   (<ss> ^predicate-grounding-failure <pgf>)
   (<pgf> ^predicate-definition <rdef>)
-->
   (<ss> ^revise-predicate-definition <rpd>)
   (<rpd> ^grounded-predicate <pred>
   	  ^failure-details <pgf>
	  ^wmem-smem-pairs <pair>)
}

sp {generate-revisions*retract*revise-predicate-defition*superstate
   (state <s> ^name generate-revisions
   	      ^superstate <ss>
	      ^grounded-predicate <pred>
   	      ^matched-definition <rdef>)
    (<ss> ^operator <op>)
   -{(<ss> ^predicate-grounding-failure <pgf>)
   (<pgf> ^predicate-definition <rdef>)}
-->
   (<ss> ^operator <op> -)
}

sp {generate-revisions*propose*complete-generate-revisions
   (state <s> ^name generate-revisions)
-->
   (<s> ^operator <op> +)
   (<op> ^name complete-generate-revisions)
}


sp {generate-revisions*write*add-predicate*superstate
   (state ^name generate-revisions
   	  -^matched-definition <any>
	  ^superstate <ss>
	  ^operator.name complete-generate-revisions
	  ^grounded-predicate <gp>)
-->
   (<ss> ^revise-predicate-definition <rpd>)
   (<rpd> ^grounded-predicate <gp>
   	  ^type add-new-predicate
	  ^wmem-smem-pairs <pair>)
}