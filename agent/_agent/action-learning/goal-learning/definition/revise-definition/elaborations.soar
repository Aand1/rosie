sp {revise-definition*copy*goal-definition
   (state <s> ^name revise-definition
   	      ^superstate.operator <sop>)
   (<sop> ^purpose.details.goal-definition <gid>)
--> 
   (<s> ^goal-definition <gid>)
}

sp {revise-definition*elaborate*to-be-revised*predicates
   (state <s> ^name revise-definition
   	      ^superstate.operator.purpose.details <p>)
   (<p> ^predicate-grounding-failure <pred>)
-->
   (<s> ^predicate-grounding-failure <pred>)
}

sp {revise-definition*elaborate*map-id
   (state <s> ^name revise-definition 
   	      ^topstate.interaction.stack.segment.next.purpose.parameters.indexable.result <ia>)
   (<ia> ^_metadata <met>)
   (<met> ^map-id <map>)
-->
   (<s> ^map-id <map>
	^grounded-action <ia>)
}

sp {revise-definiton*elaborate*indexable
   (state <s> ^name revise-definition
   	      ^superstate.operator.purpose.parameters.indexable <index>)
-->
   (<s> ^indexable <index>)
}


sp {revise-definition*elaborate*new-goal
   (state <s> ^name revise-definition
   	      ^superstate.operator <sop>)
   (<sop> ^name revise-definition
   	  ^purpose.parameters.indexable.result <goal>)
-->
   (<s> ^grounded-goal <goal>)
}

###### caution: the rules below are very hacky: may not generalize to everything

sp {revise-definition*elaborate*predicate*failure*relation
   (state <s> ^name revise-definition
   	      ^predicate-grounding-failure <rp1>
	      -^predicate-grounding-failure {<rp2> <> <rp1>}
	      ^grounded-goal <gg>)
   (<gg> ^relation-predicate <rel>
   	 -^<< relation-predicate state-predicate >> {<rel2> <> <rel>})
   (<rp1> ^predicate-type relation-predicate)
-->
   (<s> ^revise-predicate-definition <rpd1>)
   (<rpd1> ^grounded-predicate <rel>
   	   ^failure-details <rp1>)
}


### if there is only one relation-predicate and one state predicate in goal definition; assign to each

sp {revise-definition*elaborate*predicate*failure*grounded*predicates  
   (state <s> ^name revise-definition
   	      ^predicate-grounding-failure <rp1> 
	      ^predicate-grounding-failure {<rp2> <> <rp1>}
	      -^predicate-grounding-failure {<rp3> <> <rp2> <> <rp1>}
	      ^grounded-goal <gg>)
   (<gg> ^relation-predicate <rgg>
   	 ^state-predicate <sgg>)
   (<rp1> ^predicate-type relation-predicate)
   (<rp2> ^predicate-type state-predicate)
-->
   (<s> ^revise-predicate-definition <rpd1>
   	^revise-predicate-definition <rpd2>)
   (<rpd1> ^grounded-predicate <rgg>
   	  ^failure-details <rp1>)
   (<rpd2> ^grounded-predicate <sgg>
   	  ^failure-details <rp2>)
}

sp {revise-definition*elaborate*predicate*failure*grounded*predicates*state*non-existant
   (state <s> ^name revise-definition
   	      ^predicate-grounding-failure <rp1> 
	      ^predicate-grounding-failure {<rp2> <> <rp1>}
	      -^predicate-grounding-failure {<rp3> <> <rp2> <> <rp1>}
	      ^grounded-goal <gg>
	      ^goal-definition <gid>)
   (<gg> ^relation-predicate <rgg>
   	 -^state-predicate <sgg>)
   (<gid> ^state-predicate <spid>)
   (<rp1> ^predicate-type relation-predicate)
-->
   (<s> ^revise-predicate-definition <rpd1>
   	^revise-predicate-definition <rpd2>)
   (<rpd1> ^grounded-predicate <rgg>
   	  ^failure-details <rp1>)
   (<rpd2> ^grounded-predicate non-existant
   	   ^failure-details <rp2>)
}

### if multiple relation predicates, assign them by relation types

sp {revise-definition*elaborate*predicate*failure*grounded*relation-predicate
   (state <s> ^name revise-definition
   	      ^predicate-grounding-failure <rp>
	      -^predicate-grounding-failure.predicate-type state-predicate
	      ^grounded-goal.relation-predicate <gg>)
   (<rp> ^predicate-type relation-predicate
   	 ^partial-grounds.relation <rel>)
   (<gg> ^relation <rel>)
-->
   (<s> ^revise-predicate-definition <rpd>)
   (<rpd> ^grounded-predicate <gg>
   	  ^failure-details <rp>)
}


### if multiple state predicates, assign them by state types
sp {revise-definition*elaborate*predicate*failure*grounded*state-predicate
   (state <s> ^name revise-definition
   	      ^predicate-grounding-failure <rp>
	      -^predicate-grounding-failure.predicate-type relation-predicate
	      ^grounded-goal.state-predicate <gg>)
   (<rp> ^predicate-type state-predicate
   	 ^partial-grounds.state <st>)
   (<gg> ^state <st>)
-->
   (<s> ^revise-predicate-definition <rpd>)
   (<rpd> ^grounded-predicate <gg>
   	  ^failure-details <rp>)
}