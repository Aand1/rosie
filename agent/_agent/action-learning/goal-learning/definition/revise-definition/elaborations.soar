sp {revise-definition*elaborate*map-id
   (state <s> ^name revise-definition 
   	      ^topstate.interaction.stack.segment.next.purpose.parameters.indexable.result <ia>)
   (<ia> ^_metadata <met>)
   (<met> ^map-id <map>)
   (<map> ^goal <gid>)
-->
   (<s> ^map-id <map>
   	^goal-id <gid>
	^grounded-action <ia>)
}

sp {revise-definition*elaborate*new-goal
   (state <s> ^name revise-definition
   	      ^superstate.operator <sop>)
   (<sop> ^name revise-definition
   	  ^purpose.parameters.indexable.result <goal>)
-->
   (<s> ^new-goal <goal>)
}

sp {revise-defintion*elaborate*partial-grounds	
   (state <s> ^name revise-definition
   	      ^superstate.operator <sop>)
   (<sop> ^name revise-definition
   	  ^purpose.details.partial-grounds <ground>)
-->
   (<s> ^grounded-goal <ground>)
}

### elaborate default values
sp {revise-definition*elaborate*commond-with-action*grounds
   (state <s> ^name revise-definition
   	      ^new-goal <gg>
	      ^grounded-action <ga>)
   (<gg> ^relation-predicate.<arg> <val>)
   (<ga> ^<aarg> <val>)
-->
   (<s> ^common-with-action <val>)
}

sp {revise-definition*elaborate*default-grounds
   (state <s> ^name revise-definition
   	      ^new-goal <gg>
	      -^common-with-action <val>)
   (<gg> ^relation-predicate.{<arg> <> _instantiation-of} <val>)
-->
   (<s> ^default-value <val>)
}


 