sp {revise-predicate-defintion*elaborate*partial-grounds	
   (state <s> ^name revise-predicate-definition
   	      ^superstate.operator <sop>)
   (<sop> ^name revise-predicate-definition
   	  ^revision-details <rd>)
-->
   (<s> ^revision-details <rd>
   	^pairs <pair>)
}

sp {revise-predicate-definition*elaborate*procedural-definition
   (state <s> ^name revise-predicate-definition
   	      ^grounded-action._metadata.map-id <map>)
   (<map> ^procedural <pid>
   	  ^linguistic <lid>)
-->
   (<s> ^procedural-definition <pid>
   	^linguistic-definition <lid>)
}

sp {revised-predicate-definition*elaborate*state
   (state <s> ^name revise-predicate-definition
   	      ^revision-details <rd>)
   (<rd> ^failure-details <fd>
   	 ^grounded-predicate <gp>)
   (<fd> ^predicate-definition <def>
   	 ^partial-grounds <pg>)
-->
   (<s> ^predicate-definition <def>
   	^grounded-predicate <gp>
	^partial-grounds <pg>)
}

sp {revise-predicate-definition*elaborate*indexable
   (state <s> ^name revise-predicate-definition
   	      ^revision-details.indexable <index>)
-->
   (<s> ^indexable <index>)
}


sp {revise-predicate-definition*elaborate*grounded-action
   (state <s> ^name revise-predicate-definition
   	      ^superstate.operator <sop>)
   (<sop> ^grounded-action <ga>)
-->
   (<s> ^grounded-action <ga>)
}


sp {revise-predicate-definition*copy*generalizable
   (state <s> ^name revise-predicate-definition
   	      ^superstate.generalizable <gen>)
-->
   (<s> ^generalizable <gen>)
}

sp {revise-predicate-definition*elaborate*defaults-to-be-updated
   (state <s> ^name revise-predicate-definition
   	      ^generalizable <gen>
	      ^predicate-definition <def>
	      ^grounded-predicate.<arg1> <obj>)
   (<gen> ^goal-argument <arg>)
   (<def> ^{<arg1> <> <arg> <> identifier <> relation} <val>)
   -{(<s> ^generalizable <gen2>)
     (<gen2> ^goal-argument <arg1>)}
-->
   (<s> ^change-default <cd>)
   (<cd> ^argument <arg1>
   	 ^features <ft>
	 ^object <obj>)
}

sp {revise-predicate-definition*elaborate*defaults-to-be-updated*features
   (state <s> ^name revise-predicate-definition
   	      ^change-default <cd>
	      ^indexable <index>)
   (<cd> ^argument <arg>
   	 ^features <ft>)
   (<index> ^<arg> <i3>)
   (<i3> ^indexable.result.sense <sense>)
   (<sense> ^label <label>
   	    ^property.name <attr>)
-->
   (<ft> ^<attr> <label>)
}

sp {revise-predicate-definition*elaborate*defaults-to-be-updated*relation
   (state <s> ^name revise-predicate-definition
	      ^predicate-definition <def>
	      ^grounded-predicate.relation <res>)
   (<def> ^relation <val>)
  -{(<s> ^generalizable <gen2>)
     (<gen2> ^goal-argument relation)}
-->
   (<s> ^change-default <cd>)
   (<cd> ^argument relation
   	 ^features <res>
	 ^object <res>)
}

sp {revise-predicate-definition*elaborate*defaults-to-be-updated*state
   (state <s> ^name revise-predicate-definition
	      ^predicate-definition <def>
	      ^grounded-predicate.relation <res>)
   (<def> ^state <val>)
 -{(<s> ^generalizable <gen2>)
     (<gen2> ^goal-argument state)}
-->
   (<s> ^change-default <cd>)
   (<cd> ^argument state
   	 ^features <res>)
}


###### elaborate partial grounds that match the change-default ################
sp {revise-predicate-definition*elaborate*change-default*partial-ground*match*relation
   (state <s> ^name revise-predicate-definition
   	      ^change-default <cd>
	      ^partial-grounds <pg>
	      ^pairs <pair>)
   (<cd> ^argument relation
   	 ^features.smem-rep <f>)
   (<pg> ^default-values.relation.value <f>)
-->
   (<pair> ^<cd> <pg>)
}

sp {revise-predicate-definition*elaborate*change-default*partial-grounds*match*object
   (state <s> ^name revise-predicate-definition
   	      ^change-default <cd>
	      ^partial-grounds <pg>
	      ^pairs <pair>)
   (<cd> ^argument {<arg> << p1 p2 >>}
   	 ^features <f>)
   (<pg> ^<arg> <any-val>
   	 ^default-values.<arg>.value <dv>)
  -{(<f> ^<attr> <label>)
    (<dv> -^<attr> <label>)}
-->
   (<pair> ^<cd> <pg>)
}


### elaborate merge nodes ####################################################
sp {revise-predicate-definition*elaborate*merge-nodes*level1
   (state <s> ^name revise-predicate-definition
   	      ^procedural-definition <pid>
	      ^predicate-definition <gid>
	      ^generalizable <gen>)
   (<gen> ^procedural-argument <parg>
   	  ^goal-argument <garg>)
   (<pid> ^<parg> <node>)
   (<node> -^argument)
   (<gid> ^<garg> {<node2> <> <node>})
-->
   (<s> ^merge-nodes <mn>)
   (<mn> ^node1 <node>
   	 ^node2 <node2>)
}

sp {revise-predicate-definition*elaborate*merge-nodes*level2
   (state <s> ^name revise-predicate-definition
   	      ^procedural-definition <pid>
	      ^predicate-definition <gid>
	      ^generalizable <gen>)
   (<gen> ^procedural-argument <parg>
   	  ^goal-argument <garg>
	  ^type <type>)
   (<pid> ^<parg>.<type> <node>)
   (<gid> ^<garg> {<node2> <> <node>})
-->
   (<s> ^merge-nodes <mn>)
   (<mn> ^node1 <node>
   	 ^node2 <node2>)
}


############# elaborate add defaults #############################
sp {revise-predicate-definition*elaborate*add-default*nodes
   (state <s> ^name revise-predicate-definition
   	      ^grounded-predicate <gp>
	      ^partial-grounds <pg>
	      ^predicate-definition <gid>)
   (<gp> ^<arg> <val>)
   (<pg> -^<arg> <anyval>)
 -{(<s> ^partial-grounds <npg>)
   (<npg> ^default-values.<arg> <any>)}
 -{(<s> ^generalizable <gen>)
   (<gen> ^goal-argument <arg>)}
   (<gid> ^<arg> <sobj>)
-->
   (<s> ^add-default <adf>)
   (<adf> ^node <sobj>
   	  ^value <val>
	  ^argument <arg>)
}
   
 
