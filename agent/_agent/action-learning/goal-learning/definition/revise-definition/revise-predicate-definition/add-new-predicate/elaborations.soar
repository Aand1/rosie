sp {add-new-predicate*copy*object-pairs
   (state <s> ^name add-new-predicate
   	      ^superstate.wmem-smem-pairs <pairs>)
-->
   (<s> ^wmem-smem-pairs <pairs>)
}

sp {add-new-predicate*elaborate*grounded-predicate
   (state <s> ^name add-new-predicate
   	      ^superstate.operator <sop>)
   (<sop> ^name add-new-predicate
   	  ^grounded-predicate <gp>)
-->
   (<s> ^grounded-predicate <gp>)
}

sp {add-new-predicate*copy*goal-id
   (state <s> ^name add-new-predicate
   	      ^superstate.superstate.goal-definition <gid>)
-->
   (<s> ^goal-definition <gid>)
}

sp {added-new-predicate*copy*grouded-action
   (state <s> ^name add-new-predicate
   	      ^superstate.grounded-action <ga>)
-->
   (<s> ^grounded-action <ga>)
}

sp {add-new-predicate*create*new-predicate*state
   (state <s> ^name add-new-predicate
   	      ^goal-definition <gid>
	      ^grounded-predicate.state
	      ^smem.command <scomm>)
-->
   (<gid> ^state-predicate <def>)
   (<def>  ^identifier (make-constant-symbol))
   (<s> ^predicate-definition <def>)
   (<scomm> ^store <gid> <def>)
}

sp {add-new-predicate*create*new-predicate*relation
   (state <s> ^name add-new-predicate
   	      ^goal-definition <gid>
	      ^grounded-predicate.relation
	      ^smem.command <scomm>)
-->
   (<gid> ^relation-predicate <def>)
   (<def>  ^identifier (make-constant-symbol))
   (<s> ^predicate-definition <def>)
   (<scomm> ^store <gid> <def>)
}

sp {add-new-predicate*store*generalizable
   (state <s> ^name add-new-predicate
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^wmem-smem-pairs.<obj> <sobj>
	      ^smem.command <scomm>)
   (<gp> ^{<arg> << p1 p2 relation >>} <obj>)
-->
   (<def> ^<arg> <sobj>)
   (<scomm> ^store <def> <sobj>)
}

sp {add-new-predicate*state*non-genralizable*p1*p2
   (state <s> ^name add-new-predicate
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^grounded-action.{<rarg> <> name} 
	      -^wmem-smem-pairs.<obj> 
	      ^smem.command <scomm>
	      -^ignore-set.attribute <rarg>)
   (<gp> ^{<arg> << p1 p2 >>} <obj>)
--> 
   (<def> ^<arg> <sobj>)
   (<sobj> ^default <default>)
   (<default> ^value <defobj>
   	     ^<rarg> <pobj>)
   (<scomm> ^store <def> <sobj> <default> <defobj> <pobj>)
   (<s> ^<arg> <defobj>
   	^<rarg> <pobj>)
}

sp {add-new-predicate*store*state
   (state <s> ^name add-new-predicate
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^smem.command <scomm>)
   (<gp> ^state <st>)
-->
   (<def> ^state <sobj>)
   (<sobj> ^default <default>)
   (<default> ^value <st>
   	      ^p1 <nobj>)
   (<scomm> ^store <def> <sobj> <default> <nobj>) 
   (<s> ^p1 <nobj>)
}

sp {add-new-predicate*store*relation
   (state <s> ^name add-new-predicate
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^smem.command <scomm>)
   (<gp> ^relation.predicate <srel>)
-->
   (<def> ^relation <sobj>)
   (<sobj> ^default <default>)
   (<default> ^value <srel>
   	      ^p1 <nobj1>
	      ^p2 <nobj2>)
   (<scomm> ^store <def> <sobj> <default> <nobj1> <nobj2>) 
   (<s> ^p1 <nobj1>
   	^p2 <nobj2>)
}

sp {add-new-predicate*store*state*p1*p2*features
   (state <s> ^name add-new-predicate	    
   	      ^{<arg> << p1 p2 >>} <nobj>
	      ^smem.command <scomm>
	      ^grounded-predicate <gp>)
   (<gp> ^<arg> <obj>)
   (<obj> ^property <prop>)
   (<prop> ^name {<name> <> door <> heat}
   	   ^label <label>)
-->
   (<nobj> ^<name> <label>)
   (<scomm> ^store <nobj>)
}

sp {add-new-predicate*store*state*action-argument*features
   (state <s> ^name add-new-predicate	    
   	      ^<arg> <nobj>
	      ^grounded-action.<arg> <obj>
	      ^smem.command <scomm>)
   (<obj> ^property <prop>)
   (<prop> ^name {<name> <> door <> heat}
   	   ^label <label>)
-->
   (<nobj> ^<name> <label>)
   (<scomm> ^store <nobj>)
}
  