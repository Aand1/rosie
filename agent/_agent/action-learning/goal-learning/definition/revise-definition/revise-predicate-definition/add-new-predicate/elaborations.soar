sp {add-new-predicate*copy*object-pairs
   (state <s> ^name add-new-predicate
   	      ^superstate.wmem-smem-pairs <pairs>)
-->
   (<s> ^wmem-smem-pairs <pairs>)
}

sp {add-new-predicate*elaborate*grounded-predicate
   (state <s> ^name add-new-predicate
   	      ^superstate.operator <sop>)
   (<sop> ^name add-new-predicate
   	  ^grounded-predicate <gp>)
-->
   (<s> ^grounded-predicate <gp>)
}

sp {add-new-predicate*copy*goal-id
   (state <s> ^name add-new-predicate
   	      ^superstate.superstate.goal-definition <gid>)
-->
   (<s> ^goal-definition <gid>)
}

sp {add-new-predicate*create*new-predicate
   (state <s> ^name add-new-predicate
   	      ^goal-definition <gid>
	      ^grounded-predicate.state
	      ^smem.command <scomm>)
-->
   (<gid> ^state-predicate <def>)
   (<s> ^predicate-definition <def>)
   (<scomm> ^store <gid> <def>)
}

sp {add-new-predicate*store*generalizables
   (state <s> ^name add-new-predicate
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^wmem-smem-pairs.<obj> <sobj>
	      ^smem.command <scomm>)
   (<gp> ^{<arg> << p1 p2 >>} <obj>)
-->
   (<def> ^<arg> <sobj>)
   (<scomm> ^store <def> <sobj>)
}

sp {add-new-predicate*store*state
   (state <s> ^name add-new-predicate
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^smem.command <scomm>)
   (<gp> ^state <st>)
-->
   (<def> ^state <sobj>)
   (<sobj> ^default <default>)
   (<default> ^value <st>
   	      ^p1 <nobj>)
   (<scomm> ^store <def> <sobj> <default> <nobj>) 
   (<s> ^p1 <nobj>)
}

sp {add-new-predicate*store*state*p1*features
   (state <s> ^name add-new-predicate	    
   	      ^p1 <nobj>
	      ^smem.command <scomm>
	      ^grounded-predicate <gp>)
   (<gp> ^p1 <obj>)
   (<obj> ^property <prop>)
   (<prop> ^name {<name> <> door <> heat}
   	   ^label <label>)
-->
   (<nobj> ^<name> <label>)
   (<scomm> ^store <nobj>)
}
  