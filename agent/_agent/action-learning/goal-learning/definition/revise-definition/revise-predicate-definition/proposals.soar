sp {revise-definition*propose*initialize-revisions
   (state <s> ^name revise-predicate-definition
   	      -^relax-filters
	      -^add-default
	      -^merge-node)
-->
   (<s> ^operator <op> +)
   (<op> ^name initialize-revisions)
}

sp {revise-definition*propose*relax-filters
   (state <s> ^name revise-predicate-definition
   	      ^relax-filters <rxf>
	      -^updated-node <node>
	      -^dont-relax <rxf>)
   (<rxf> ^default-value-node <rel-def>
   	  ^_node <node>)
-->
   (<s> ^operator <op> +)
   (<op> ^name relax-filters
   	 ^information <rxf>)
}

sp {revise-definition*propose*add-new-default
   (state <s> ^name revise-predicate-definition
   	      ^add-default <adf>
	      -^updated-node <node>)
   (<adf> ^default-value-node <node>
   	  ^_node <node>)
-->
   (<s> ^operator <op> +)
   (<op> ^name add-new-default
   	 ^information <adf>)
}

sp {revised-definition*prefer*relax-node*over*add-new-default	
   (state <s> ^name revise-predicate-definition
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^name relax-filters)
   (<op2> ^name add-new-default)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {revised-definition*preference*equal	
   (state <s> ^name revise-predicate-definition
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^name <name>)
   (<op2> ^name <name>)
-->
   (<s> ^operator <op1> = <op2>)
}


sp {revise-predicate-definition*propose*merge-nodes
   (state <s> ^name revise-predicate-definition
   	      ^merge-nodes <mn>
	      ^procedural-definition <pid>
	      ^linguistic-definition <lid>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name merge-nodes 
   	 ^nodes <mn>
	 ^procedural-definition <pid>
	 ^linguistic-definition <lid>)
}


sp {revise-predciate-definition*propose*add-predicate
   (state <s> ^name revise-predicate-definition
   	      ^revision-details <rpd>
	      -^added-predicate <gp>)
   (<rpd> ^type add-new-predicate
   	  ^grounded-predicate <gp>)
-->
   (<s> ^operator <op> + >)
   (<op> ^name add-new-predicate
   	 ^grounded-predicate <gp>)
}
   	      


sp {revise-definition*propose*complete-revise-predicate-pefinition
   (state <s> ^name revise-predicate-definition)
-->
   (<s> ^operator <op> + <)
   (<op> ^name complete-revise-predicate-definition)
}

