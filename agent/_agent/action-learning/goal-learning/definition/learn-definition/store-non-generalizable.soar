## non-generalizable structure are integral to verb definition
## 1. relation values are dependent on p1, p2
sp {store*general*goal*definition*1*no-generalization*relation
   (state <s> ^name learn-definition
   	      ^goal-id.relation-predicate <grel>
	      ^goal.relation-predicate <relp>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<relp> ^relation <obj>)
   (<obj> ^smem-rep <sobj>)
-->
   (<grel> ^relation <nobj>)
   (<nobj> ^default <def>)
   (<def> ^value <sobj>
   	  ^p1 <p1obj>
	  ^p2 <p2obj>)
   (<scomm> ^store <grel> <nobj> <sobj> <def>)
}

sp {store*general*goal*definition*1*no-generalization*p1*p2
   (state <s> ^name learn-definition
   	      ^goal-id.relation-predicate <grel>
	      ^goal.relation-predicate <relp>
	      ^smem.command <scomm>
	      ^indexed-action.{<rarg2> <> name} <any>
	      ^pairing <p>
	      -^ignore-set.attribute <rarg2>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<relp> ^{<rarg> << p1 p2 >>} <obj>)
-->
   (<grel> ^<rarg> <nobj>)
   (<nobj> ^default <def>)
   (<def> ^value <defobj>
   	  ^<rarg2> <p2obj>)
   (<scomm> ^store <grel> <nobj> <sobj> <def> <defobj>)
}

### version 1: to implement
### version 2
sp {store*goal-definition*p1*p2*attributes
   (state <s> ^name learn-definition
   	      ^goal-id.relation-predicate <grel>
	      ^goal.relation-predicate <relp>
	      ^smem.command <scomm>
	      ^pairing <pair>)
   (<pair> ^smem <grel>
   	   ^wmem <relp>)
   (<grel> ^{<arg> << p1 p2 >>}.default.value <val>)
   (<relp> ^<arg>.property <prop>)
   (<prop> ^label <label>
   	   ^name {<name> <> heat <> door})
-->
   (<val> ^<name> <label>)
   (<scomm> ^store <val>)
}

### version 1: to implement
### version 2
sp {store*goal-definition*other*object*attributes
   (state <s> ^name learn-definition
   	      ^goal-id.relation-predicate <grel>
      	      ^goal.relation-predicate <relp>
	      ^pairing <pair>
	      ^indexed-action.{<arg2> <> name}.property <prop>
	      ^smem.command <scomm>
	      -^ignore-set.attribute <arg2>)
   (<pair> ^smem <grel>
   	   ^wmem <relp>)
   (<grel> ^<arg>.default <def>)
   (<def> ^value <val>
   	  ^<arg2> <sobj>)
   (<prop> ^label <label>
   	   ^name {<name> <> heat <> door})
-->
   (<sobj> ^<name> <label>)
   (<scomm> ^store <sobj>)
}

sp {store*goal-definition*other*object*attributes*relation
   (state <s> ^name learn-definition
   	      ^goal-id.relation-predicate <grel>
      	      ^goal.relation-predicate <relp>
	      ^pairing <pair>
	      ^smem.command <scomm>)
   (<pair> ^smem <grel>
   	   ^wmem <relp>)
   (<grel> ^relation.default <def>)
   (<def> ^value <val>
   	  ^<arg2> <sobj>)
   (<relp> ^<arg2>.property <prop>)
   (<prop> ^label <label>
   	   ^name {<name> <> heat <> door})
-->
   (<sobj> ^<name> <label>)
   (<scomm> ^store <sobj>)
}


##################### defaults for state predicate #########################

sp {store*general*goal*definition*1*no-generalization*state
   (state <s> ^name learn-definition
   	      ^goal-id.state-predicate <grel>
	      ^goal.state-predicate <relp>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
   (<relp> ^state <label>)
-->
   (<grel> ^state <nobj>)
   (<nobj> ^default <def>)
   (<def> ^value <label>
   	  ^p1 <p1obj>)
   (<scomm> ^store <grel> <nobj> <def>)
}

sp {store*goal-definition*other*object*attributes*state
   (state <s> ^name learn-definition
   	      ^goal-id.state-predicate <grel>
      	      ^goal.state-predicate <relp>
	      ^pairing <pair>
	      ^smem.command <scomm>)
   (<pair> ^smem <grel>
   	   ^wmem <relp>)
   (<grel> ^<arg>.default <def>)
   (<def> ^value <val>
   	  ^<arg2> <sobj>)
   (<relp> ^<arg2>.property <prop>)
   (<prop> ^label <label>
   	   ^name {<name> <> door <> heat})
-->
   (<sobj> ^<name> <label>)
   (<scomm> ^store <sobj>)
}
 

sp {store*general*goal*definition*1*no-generalization*p1*state
   (state <s> ^name learn-definition
   	      ^goal-id.state-predicate <grel>
	      ^goal.state-predicate <relp>
	      ^indexed-action.{<rarg2> <> name}  <any>
	      ^smem.command <scomm>
	      ^pairing <p>
	      -^ignore-set.attribute <rarg2>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<relp> ^{<rarg> << p1 >>} <obj>)
-->
   (<grel> ^<rarg> <nobj>)
   (<nobj> ^default <def>)
   (<def> ^value <defobj>
   	  ^<rarg2> <p2obj>)
   (<scomm> ^store <grel> <nobj> <sobj> <def> <defobj>)
}

sp {store*general*goal*definition*1*no-generalization*p1*state*attributes
   (state <s> ^name learn-definition
   	      ^goal-id.state-predicate <grel>
	      ^goal.state-predicate <relp>
	      ^indexed-action.{<rarg2> <> name}  <obj>
	      ^smem.command <scomm>
	      ^pairing <p>
	      -^ignore-set.attribute <rarg2>)
   (<p> ^smem <grel>
   	^wmem <relp>)
   (<grel> ^<garg2>.default.<rarg2> <p2obj>)
   (<obj> ^property <prop>)
   (<prop> ^label <label>
   	   ^name {<name> <> door <> heat})
-->
   (<p2obj> ^<name> <label>)
   (<scomm> ^store <p2obj>)
}

sp {store*goal-definition*p1*p2*attributes*state
   (state <s> ^name learn-definition
   	      ^goal-id.state-predicate <grel>
	      ^goal.state-predicate <relp>
	      ^smem.command <scomm>
	      ^pairing <pair>)
   (<pair> ^smem <grel>
   	   ^wmem <relp>)
   (<grel> ^{<arg> << p1 >>}.default.value <val>)
   (<relp> ^<arg>.property <prop>)
   (<prop> ^label <label>
   	   ^name {<name> <> door <> heat})
-->
   (<val> ^<name> <label>)
   (<scomm> ^store <val>)
}
