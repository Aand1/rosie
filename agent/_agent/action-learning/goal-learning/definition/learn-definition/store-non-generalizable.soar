## non-generalizable structure are integral to verb definition
sp {store*general*goal*definition*1*no-generalization*relation
   (state <s> ^name learn-definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^<pred-link> <relp>)
   (<relp> ^relation <obj>)
   (<obj> ^smem-rep <sobj>)
   (<gid> ^<pred-link> <grel>)
-->
   (<grel> ^relation <nobj>)
   (<nobj> ^default <def>)
   (<def> ^value <sobj>)
   (<scomm> ^store <grel> <nobj> <sobj> <def>)
}


sp {complete*default*structure
   (state <s> ^name learn-definition
   	      ^goal-id <gid>
	      ^generalizable <gen>
	      ^smem.command <scomm>)
   (<gen> ^lt-id <pobj>)
   (<gid> ^<pred> <grel>)
   (<grel> ^<any>.default <def>
   	   ^<p> <pobj>)
-->
   (<def> ^<p> <pn>)
   (<scomm> ^store <def> <pn>)
}

sp {complete*default*structure*attributes
   (state <s> ^name learn-definition
   	      ^goal-id <gid>
	      ^generalizable <gen>
	      ^smem.command <scomm>)
   (<gen> ^lt-id <pobj>
   	  ^attributes.<attribute> <label>)
   (<gid> ^<pred> <grel>)
   (<grel> ^<any>.default <def>
   	   ^<p> <pobj>)
   (<def> ^<p> <pn>)
-->
   (<pn> ^<attribute> <label>)
   (<scomm> ^store <pn>)
}
   

sp {store*general*goal*definition*1*no-generalization*object
   (state <s> ^name learn-definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^<pred-link> <relp>)
   (<relp> ^{<relarg> <> relation} <obj>)
   (<gid> ^<pred-link> <grel>)
-->
   (<grel> ^<relarg> <nobj>)
   (<nobj> ^default <def>)
   (<def> ^value <snobj>)
   (<scomm> ^store <grel> <nobj> <snobj> <def>)
}

sp {store*general*goal*definition*1*no-generalization*object*attributes
   (state <s> ^name learn-definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>
	      ^indexable.<< relation state-predicate >> <ipred>)
   (<ipred> ^<relarg> <irel>)
   (<irel> ^indexable.result.sense <res>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^<pred-link> <relp>)
   (<relp> ^<relarg> <obj>)
   (<gid> ^<pred-link> <grel>)
   (<grel> ^<relarg> <nobj>)
   (<nobj> ^default.value <snobj>)
   (<res> ^label <label>
   	  ^property.name <type>)
-->
   (<snobj> ^<type> <label>)
}

sp {store*general*goal*definition*state
   (state <s> ^name learn-definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
   (<goal> ^state-predicate <relp>)
   (<gid> ^state-predicate <grel>)
   (<relp> ^state <type>)
-->
   (<grel> ^state <type>)
   (<scomm> ^store <grel>)
}