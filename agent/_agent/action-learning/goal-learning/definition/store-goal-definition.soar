sp {store*general*goal*definition*objects
   (state <s> ^name definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^generalizable <gen>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
   (<goal> ^<pred-link> <relp>)
   (<relp> ^<relarg> <obj>)
   (<gid> ^<pred-link> <grel>)
-->
   (<grel> ^<relarg> <sobj>)
   (<scomm> ^store <sobj> <gid> <grel>)
}

sp {store*general*goal*definition*relation
   (state <s> ^name definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^generalizable <gen>
	       ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
   (<goal> ^<pred-link> <relp>)
   (<relp> ^relation.smem-rep <obj>)
   (<gid> ^<pred-link> <grel>)
-->
   (<grel> ^relation <sobj>)
   (<scomm> ^store <sobj> <gid> <grel>)
}



## non-generalizable structure are integral to verb definition
sp {store*general*goal*definition*1*no-generalization*relation
   (state <s> ^name definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^<pred-link> <relp>)
   (<relp> ^relation <obj>)
   (<obj> ^smem-rep <sobj>)
   (<gid> ^<pred-link> <grel>)
-->
   (<grel> ^relation <nobj>)
   (<nobj> ^default <sobj>)
   (<scomm> ^store <grel> <nobj> <sobj>)
}

sp {store*general*goal*definition*1*no-generalization*object
   (state <s> ^name definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^<pred-link> <relp>)
   (<relp> ^{<relarg> <> relation} <obj>)
   (<gid> ^<pred-link> <grel>)
-->
   (<grel> ^<relarg> <nobj>)
   (<nobj> ^default <snobj>)
   (<scomm> ^store <grel> <nobj> <snobj>)
}

sp {store*general*goal*definition*1*no-generalization*object*attributes
   (state <s> ^name definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>
	      ^indexable.<< relation state-predicate >> <ipred>)
   (<ipred> ^<relarg> <irel>)
   (<irel> ^indexable.result.sense <res>)
   (<p> ^smem <grel>
   	^wmem <relp>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^<pred-link> <relp>)
   (<relp> ^<relarg> <obj>)
   (<gid> ^<pred-link> <grel>)
   (<grel> ^<relarg> <nobj>)
   (<nobj> ^default <snobj>)
   (<res> ^label <label>
   	  ^type <type>)
-->
   (<snobj> ^attribute <label>)
}

sp {store*general*goal*definition*state
   (state <s> ^name definition
   	      ^goal-id <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^pairing <p>)
   (<p> ^smem <grel>
   	^wmem <relp>)
   (<goal> ^state-predicate <relp>)
   (<gid> ^state-predicate <grel>)
   (<relp> ^state <type>)
-->
   (<grel> ^state <type>)
   (<scomm> ^store <grel>)
}