### copy desired goal state
sp {elaborate*learn-termination-rule*copy*goal
   (state <s> ^name learn-termination-rule
   	      ^superstate <ss>)
   (<ss> ^segment-desired <des>)
-->
   (<s> ^segment-desired <des>
   	^copy-objects false
	^environment <env>
	^duplicates <dup>)
}

### elaborate problemspace
sp {elaborate*learn-termination-rule*elaborate*problemspace
   (state <s> ^name learn-termination-rule  
   	      ^problemspace <ps>)
-->
   (<ps> ^dont-copy environment
	 ^dont-copy io
	 ^state hypothetical)
}

   	      
### copy action to be executed
sp {copy*indexed-action*from*segment
   (state <s> ^name learn-termination-rule
   	      ^topstate.interaction.stack.segment.next.purpose.parameters.indexable.result <iop>)
-->
   (<s> ^indexed-action <iop>)
}

### copy objects
sp {copy*objects*learn-termination-rule
   (state <s> ^name learn-termination-rule
   	      ^superstate.object <obj>
	      ^duplicates <dup>)
-->
   (<s> ^object <nobj>)
   (<dup> ^<obj> <nobj>)
}

sp {copy*object*focus*learn-termination-rule
   (state <s> ^name learn-termination-rule
   	      ^duplicates <dup>
	      ^object <obj>
	      ^object <nobj>)
   (<dup> ^<obj> <nobj>)
   (<obj> ^focus <id>)
-->
   (<nobj> ^focus <id>)
}


#### copy relation predicate
sp {copy*relation-predicate
   (state <s> ^name learn-termination-rule
   	      ^segment-desired <des>
	      ^environment <env>
	      ^object <nobj1>
	      ^object <nobj2>
	      ^duplicates <dup>)
   (<des> ^relation-predicate <pred>)
   (<pred> ^relation <rel>
   	   ^p1 <obj1>
	   ^p2 <obj2>)
   (<dup> ^<obj1> <nobj1>
   	  ^<obj2> <nobj2>)
-->
   (<env> ^relation <nprep>)
   (<nprep> ^a <nobj1>
   	    ^b <nobj2>
	    ^type <rel>)
}

### copy state predicate
sp {copy*state-predicate
   (state <s> ^name learn-termination-rule
   	      ^segment-desired <des>
	      ^environment <env>
	      ^duplicates <dup>)
   (<dup> ^<obj> <nobj>)
   (<des> ^state-predicate <dtr>)
   (<dtr> ^p1 <obj>
   	  ^state <state>)
-->
   (<env> ^<state> <nobj>)
}

### write success on superstate

sp {write*nsuccess*superstate*learn-termination-rule
   (state <s> ^name learn-termination-rule
   	      ^nsuccess <ny>
	      ^superstate <ss>)
-->
   (<ss> ^nsuccess <ny>)
}

# ### copy desired
sp {copy*desired*TEMP*OP1
   (state <s> ^problemspace.action-type composite
   	      ^attribute operator
	      ^superstate <ss>)
   (<ss> ^desired <des>
   	 ^name learn-termination-rule)
-->
   (<s> ^desired <des>)
}

sp {copy*success*TEMP*OP1
   (state <s> ^problemspace.action-type composite
   	      ^attribute operator
	      ^nsuccess <des>
	      ^desired <des>
	      ^superstate <ss>)
   (<ss> ^name learn-termination-rule)
-->
   (<ss> ^nsuccess <des>)
}
   	      

