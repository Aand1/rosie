#### retrieve relevant episode from episodic memory for forward projection
sp {elaborate*action-policy*retrieve*relevant*episode
   (state <s> ^name action-policy
   	      ^segment <seg>
	      ^epmem.command <epcomm>)
   (<seg> ^epmem-id <id>)
-->
   (<epcomm> ^retrieve <id>)
}


### extract relevant attributes from retrieved episode
sp {action-policy*copy*retrieved*environment*attributes
   (state <s> ^name action-policy
   	      ^environment <senv>
	      ^epmem.result.retrieved.environment <env>)
   (<env> ^{<> clear <> intermediate <> not-predicate <> not-state <attr> <> predicate} <val>)
-->
   (<senv> ^<attr> <val>)
}

sp {action-policy*copy*retrieved*environment*attributes*predicate
   (state <s> ^name action-policy
   	      ^environment <senv>
	      ^epmem.result.retrieved.environment <env>
	      ^topstate.found-prep <fp>)
   (<fp> ^smem-rep <id>)
   (<env> ^predicate <val>)
   (<val> ^a <obj1>
   	  ^b <obj2>
	  ^relation <rel>)
   (<rel> ^smem-rep <id>)
-->
   (<senv> ^predicate <pred>) 
   (<pred> ^a <obj1>
   	   ^b <obj2>
	   ^relation <fp>)
}

sp {action-policy*copy*retrieved*objects
   (state <s> ^name action-policy
	      ^epmem.result.retrieved.object <obj>)
-->
   (<s> ^object <obj>)
}

