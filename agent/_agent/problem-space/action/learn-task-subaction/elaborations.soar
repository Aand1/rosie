# Elaborate action-operator from superoperator
sp {learn-task-subaction*elaborate*action
   (state <s> ^name learn-task-subaction
              ^superstate.operator.action-operator <action>)
-->
   (<s> ^action-operator <action>)
}

# Elaborate action-id from the superoperator
sp {learn-task-subaction*elaborate*action-id
   (state <s> ^name learn-task-subaction
              ^superstate.operator.action-id <id>)
-->
   (<s> ^action-id <id>)
}

# Elaborate the subaction operator representation from the superoperator
sp {learn-task-subaction*elaborate*subaction
   (state <s> ^name learn-task-subaction
              ^superstate.operator.subaction <sub>)
-->
   (<s> ^subaction <sub>)
}

## Do an smem query to lookup the smem info about the subaction
sp {learn-task-subaction*elaborate*smem-query*subaction*handle
   (state <s> ^name learn-task-subaction
              ^subaction.action-handle <action-handle>)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 8)
   (<cue> ^handle <action-handle>)
}

# Elaborate subaction-id from the result of the smem query
sp {learn-task-subaction*elaborate*subaction-id*from*smem-query
   (state <s> ^name learn-task-subaction
              ^subaction.action-handle <action-handle>
              ^smem-query.result <res>)
   (<res> ^handle <action-handle>)
-->
   (<s> ^subaction-id <res>)
}
