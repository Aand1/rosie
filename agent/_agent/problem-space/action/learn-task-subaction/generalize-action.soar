# Use the generalize-action operator (found in action-utils) 
# to generate a generalized smem rep for the action based on the subaction operator

sp {learn-task-subaction*elaborate*generalize-action
   (state <s> ^name learn-task-subaction
              ^action-id <aid>
              ^action-operator <aop>
              ^subaction <sub>
              ^subaction-id <sub-id>)
-->
   (<s> ^generalize-action <ga>
        ^generalize-info <info>)
   (<ga> ^source-action <sub>
         ^source-id <sub-id>)
   (<info> ^reference-action <aop>
           ^reference-id <aid>)
}

sp {learn-task-subaction*apply*generalize-action*create*subaction*handle
   (state <s> ^name learn-task-subaction
              ^operator.name generalize-action
              ^generalize-action.generalized-action <gen-action>)
-->
   (<gen-action> ^handle (make-constant-symbol |subaction|))
}

sp {learn-task-subaction*apply*generalize-action*mark*learned*no
   (state <s> ^name learn-task-subaction
              ^operator.name generalize-action
              ^generalize-action.generalized-action <gen-action>)
-->
   (<gen-action> ^learned no)
}


# When the action generalization is finished, copy as the subaction-structure
sp {learn-task-subaction*apply*generalize-action*copy*result*as*subaction-structure
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^generalize-action <ga>)
   (<ga> ^generalized-action <gen-action>
         ^finished true)
   (<o> ^name generalize-action)
-->
   (<s> ^subaction-structure <gen-action>)
   (<s> ^operator <o> -)
}
