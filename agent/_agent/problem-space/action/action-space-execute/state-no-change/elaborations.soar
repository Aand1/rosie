sp {action*execute*SNC*elaborate*name*action-execute-SNC
   (state <s> ^superstate <ss>
              ^impasse no-change
              ^attribute state)
   (<ss> ^name <action-name>
         ^problem-space.name action
         ^action-operator <aop>)
   (<aop> ^name <action-name>)
-->
   (<s> ^name action-execute-SNC
        ^default-problem-space no
        ^action-operator <aop>)
}

sp {action*execute*SNC*turn*learning*on
   (state <s> ^name action-execute-SNC)
-->
   (force-learn <s>)
}

sp {action*execute*SNC*elaborate*world*superstate
   (state <s> ^name action-execute-SNC
              ^superstate.world <w>)
-->
   (<s> ^world <w>)
}

sp {action*execute*SNC*elaborate*action-operator*superstate
   (state <s> ^name action-execute-SNC
              ^superstate.action-operator <aop>)
-->
   (<s> ^action-operator <aop>)
}

sp {action*execute*SNC*elaborate*action-id*superstate
   (state <s> ^name action-execute-SNC
              ^superstate.action-id <aid>)
-->
   (<s> ^action-id <aid>)
}

sp {action*execute*SNC*elaborate*procedural-id*action-id
   (state <s> ^name action-execute-SNC
              ^action-id.procedural <pid>)
-->
   (<s> ^procedural-id <pid>)
}

sp {action*execute*SNC*elaborate*subactions-id*procedural-id
   (state <s> ^name action-execute-SNC
              ^procedural-id.subactions <subs-id>)
-->
   (<s> ^subactions-id <subs-id>)
}

sp {action*execute*SNC*elaborate*desired*superstate
   (state <s> ^name action-execute-SNC
              ^superstate.desired <des>)
-->
   (<s> ^desired <des>)
}
