sp {action*execute*propose*store-subaction
  (state <s> ^problem-space.name action
             ^problem-space.action.type execute
             ^store-subaction <sub>)
-->
  (<s> ^operator <o> + >)
  (<o> ^name store-subaction
       ^subaction <sub>)
}

sp {action*execute*apply*store-subaction*remove*subaction
  (state <s> ^problem-space.name action
             ^operator <o>
             ^store-subaction <sub>)
  (<o> ^name store-subaction
       ^subaction <sub>)
-->
  (<s> ^store-subaction <sub> -)
}

sp {action*execute*apply*store-subaction*store*root
   (state <s> ^problem-space.name action
              ^operator <o>
              ^action-id.procedural <proc>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction <sub>)
-->
   (<proc> ^subaction <sub>)
   (<cmd> ^store <sub> <proc>)
}

sp {action*execute*apply*store-subaction*argument
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type <any>)
-->
   (<cmd> ^store <arg>)
}

sp {action*execute*apply*store-subaction*arg*slots
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 predicate >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {action*execute*apply*store-subaction*predicate*arg*default
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}

sp {action*execute*apply*store-subaction*arg*predicate*slot
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {action*execute*apply*store-subaction*predicate*arg*predicate*default
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}
