sp {action*problem-space*execute*propose*report-success*desired*satisfied
   (state <s> ^problem-space.name action
              ^problem-space.action.type execute
              ^desired.satisfied true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {action*problem-space*execute*apply*report-success*mark*status*success
   (state <s> ^problem-space.name action
              ^problem-space.action.type execute
              ^operator.name report-success
              ^current-action <a>)
-->
   (<a> ^status success)
}

sp {action*problem-space*execute*apply*report-success*remove*output*commands
   (state <s> ^problem-space.name action
              ^problem-space.action.type execute
              ^operator.name report-success
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^<cmd-name> <cmd>)
   (<out> ^<out-name> <cmd>)
-->
   (<out> ^<out-name> <cmd> -)
}