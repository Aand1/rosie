sp {action*problem-space*execute*elaborate*match-action*child-action
   (state <s> ^problem-space.name action
              ^problem-space.action.type execute
              ^child-action <a>)
   (<a> -^validated true
         ^execution-operator <op>)
-->
   (<s> ^match-action.action-operator <op>)
}

sp {action*problem-space*execute*composite*elaborate*problem-space*propose-action-operators*child-action*not*validated
   (state <s> ^problem-space <ps>
              ^desired <des>
             -^superstate.operator.name op_find1
              ^child-action <child>
             -^default-problem-space no)
   (<child> -^validated true)
   (<ps> ^name action
         ^action <a>)
   (<a> ^type execute
        ^action-type composite)
-->
   (<a> ^propose-action-operators true)
}

sp {action*problem-space*apply*child-action*mark*validated
   (state <s> -^name execute-action
              ^problem-space.name action
              ^problem-space.action.type execute
              ^child-action <a>
              ^match-action <ma>
              ^operator <o>)
   (<a> ^execution-operator <ex>)
   (<ma> ^action-operator <ex>
         ^matched <o>)
-->
   (<a> ^validated true
        ^start-of-execution true)
}

sp {action*problem-space*apply*child-action*copy*subaction-handle
   (state <s> -^name execute-action
              ^problem-space.name action
              ^problem-space.action.type execute
              ^child-action <a>
              ^match-action <ma>
              ^operator <o>)
   (<a> ^execution-operator <ex>)
   (<ma> ^action-operator <ex>
         ^matched <o>)
   (<o> ^subaction-handle <handle>)
-->
   (<ex> ^subaction-handle <handle>)
}

sp {action*problem-space*apply*child-action*copy*perform
   (state <s> -^name execute-action
              ^problem-space.name action
              ^problem-space.action.type execute
              ^child-action <a>
              ^match-action <ma>
              ^operator <o>)
   (<a> ^execution-operator <ex>)
   (<ma> ^action-operator <ex>
         ^matched <o>)
  (<o> ^perform <perf>)
-->
  (<ex> ^perform <perf>)
}
