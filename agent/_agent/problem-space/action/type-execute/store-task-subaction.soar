sp {action*execute*propose*store-task-subaction
  (state <s> ^problem-space.name action
             ^problem-space.action.type execute
             ^action-id.procedural.subactions <subs>
             ^subaction-structure <sub>)
-->
  (<s> ^operator <o> + >)
  (<o> ^name store-task-subaction
       ^subaction <sub>
       ^subaction-list <subs>)
}

sp {action*execute*apply*store-task-subaction*store*root
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction <sub>
        ^subaction-list <subs>)
-->
   (<subs> ^subaction <sub>)
   (<cmd> ^store <subs> <sub>)
}

sp {action*execute*apply*store-task-subaction*argument
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type <any>)
-->
   (<cmd> ^store <arg>)
}

sp {action*execute*apply*store-task-subaction*arg*slots
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 predicate >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {action*execute*apply*store-task-subaction*predicate*arg*default
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}

sp {action*execute*apply*store-task-subaction*arg*predicate*slot
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {action*execute*apply*store-task-subaction*predicate*arg*predicate*default
   (state <s> ^problem-space.name action
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-task-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}

sp {action*execute*apply*store-task-subaction*success*remove*subaction-structure
  (state <s> ^problem-space.name action
             ^operator <o>
             ^subaction-structure <sub>
             ^smem.result.success <sub>)
  (<o> ^name store-task-subaction
       ^subaction <sub>)
-->
  (<s> ^subaction-structure <sub> -)
}
