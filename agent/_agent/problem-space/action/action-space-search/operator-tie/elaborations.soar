## A tied operator matches the action-operator in the superstate
sp {action*operator-tie*elaborate*matched*operator
  (state <s> ^name action-operator-tie
             ^item <o>
             ^superstate.match-action.matched <o>)
-->
  (<s> ^matched <o>)
}

## A tied operator did not match the action-operator in the superstate
sp {action*operator-tie*elaborate*unmatched*operator
  (state <s> ^name action-operator-tie
              ^item <o>
              ^superstate.match-action <ma>)
  (<ma> -^matched <o>)
-->
  (<s> ^unmatched <o>)
}

## Tied operators had multiple versions of an argument
sp {multiple-argument-instantiations*elaborate*duplicated-argument
  (state <s> ^name action-operator-tie
              ^item <o1>
              ^item { <o2> <> <o1> }
              ^superstate.match-action <ma>)
   (<ma> ^match-info <info>)
   (<info> ^op-info <info1>
           ^op-info <info2>)
   (<info1> ^operator <o1>
            ^argument-name <arg-name>
            ^matches-arg <arg1>)
   (<info2> ^operator <o2>
            ^argument-name <arg-name>
            ^matches-arg { <arg2> <> <arg1> })
-->
   (<s> ^duplicated-argument <arg-name>)
}
