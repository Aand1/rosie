sp {action-space*search*operator-tie*elaborate*state*name*selection
   (state <s> ^problem-space.action-space search
              ^impasse tie
              ^attribute operator)
-->
   (<s> ^name selection)
}

sp {action-space*search*selection*evaluate-operator*elaborate*default-desired-copy
   (state <s> ^name selection
              ^problem-space.action-space search
              ^operator <o> +)
   (<o> ^name evaluate-operator)
-->
   (<o> ^default-desired-copy yes)
   (<o> ^default-operator-copy yes)
}

sp {action-space*search*selection*apply*init-current-evaluation-depth*max
   (state <s> ^name selection
              ^problem-space.action-space search
              ^top-selection true
              ^superstate.desired.predicate <any>
              ^operator <o>
              ^agent-params.action-search-depth <d>)
   (<o> ^name init-current-evaluation-depth)
-->
   # Change to this set the iterative-deepening max search depth
   (<s> ^max-evaluation-depth <d>)
}

sp {action-space*search*selection*apply*init-current-evaluation-depth*zero*no*goal
   (state <s> ^name selection
              ^problem-space.action-space search
              ^top-selection true
             -^superstate.desired.predicate
              ^operator <o>)
   (<o> ^name init-current-evaluation-depth)
-->
   (<s> ^max-evaluation-depth 0)
}


# KIRKMOVE
sp {games*selection*elaborate*evaluate-operator*default-desired-copy
   (state <s> ^problemspace games
              ^operator <o> +)
   (<o> ^name evaluate-operator)
-->
   (<o> ^default-desired-copy yes)
   (<o> ^default-operator-copy yes)
}



