sp {multiple-argument-instantiations*elaborate*problem-space
   (state <s> ^name multiple-argument-instantiations)
-->
   (<s> ^problem-space <ps>)
   (<ps> ^name action
         ^action <action>)
}


sp {multiple-argument-instantiations*elaborate*duplicated-argument
   (state <s> ^name multiple-argument-instantiations
              ^superstate.match-action <ma>
              ^item <o1>
              ^item { <o2> <> <o1> })
   (<ma> ^match-info <info>)
   (<info> ^op-info <info1>
           ^op-info <info2>)
   (<info1> ^operator <o1>
            ^argument-name <arg-name>
            ^matches-arg <arg1>)
   (<info2> ^operator <o2>
            ^argument-name <arg-name>
            ^matches-arg { <arg2> <> <arg1> })
-->
   (<s> ^duplicated-argument <arg-name>)
}

sp {multiple-argument-instantiations*propose*report-failure-multiple-arguments
   (state <s> ^name multiple-argument-instantiations
              ^duplicated-argument <arg-name>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure-multiple-arguments
        ^arg-name <arg-name>)
}

sp {multiple-argument-instantiations*elaborate*report-failure-multiple-arguments
   (state <s> ^name multiple-argument-instantiations
              ^operator <o> +
              ^superstate.match-action.match-info.op-info <op-info>
              ^item <item>)
   (<op-info> ^operator <item>
              ^argument-name <arg-name>
              ^matches-arg <arg>)
   (<o> ^name report-failure-multiple-arguments
        ^arg-name <arg-name>)
-->
   (<o> ^argument <arg>)
}
