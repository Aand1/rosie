sp {multiple-argument-instantiations*elaborate*duplicated-argument
   (state <s> ^name multiple-argument-instantiations
              ^superstate.match-action <ma>
              ^item <o1>
              ^item { <o2> <> <o1> })
   (<ma> ^match-info <info>)
   (<info> ^op-info <info1>
           ^op-info <info2>)
   (<info1> ^operator <o1>
            ^argument-name <arg-name>
            ^matches-arg <arg1>)
   (<info2> ^operator <o2>
            ^argument-name <arg-name>
            ^matches-arg { <arg2> <> <arg1> })
-->
   (<s> ^duplicated-argument <arg-name>)
}

sp {multiple-argument-instantiations*elaborate*duplicate-info
   (state <s> ^name multiple-argument-instantiations
              ^duplicated-argument <arg-name>)
-->
   (<s> ^duplicate-info <dup>)
   (<dup> ^arg-name <arg-name>
          ^obj-preds <preds>)
}

sp {multiple-argument-instantiations*elaborate*duplicate-info*argument
   (state <s> ^name multiple-argument-instantiations
              ^superstate.match-action <ma>
              ^item <op>
              ^duplicate-info <dup>)
   (<dup> ^arg-name <arg-name>)
   (<ma> ^action-operator.<arg-name> <arg>
         ^match-info.op-info <op-info>)
   (<op-info> ^operator <op>
              ^matches-arg <arg>)
-->
   (<dup> ^argument <arg>)
}

sp {multiple-argument-instantiations*elaborate*duplicate-info*predicate
   (state <s> ^name multiple-argument-instantiations
              ^duplicate-info <dup>)
   (<dup> ^argument.<any> <arg1>
          ^obj-preds <preds>)
   (<arg1> ^item-type object
           ^predicates.<prop> <pred>)
   -{(<dup> ^argument.<any> { <arg2> <> <arg1> })
     (<arg2> -^predicates.<prop> <pred>)}
-->
   (<preds> ^<prop> <pred>)
}

sp {multiple-argument-instantiations*propose*report-multiple-arguments
   (state <s> ^name multiple-argument-instantiations
              ^duplicate-info <dup>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-multiple-arguments
        ^duplicate-info <dup>)
}

sp {multiple-argument-instantiations*apply*report-multiple-arguments*create*failure
   (state <s> ^name multiple-argument-instantiations
              ^operator <o>
              ^superstate.match-action.action-operator <aop>)
   (<o> ^name report-multiple-arguments
        ^duplicate-info.arg-name <arg-name>)
-->
   (<s> ^failure <f>)
   (<f> ^type multiple-arguments
        ^arg-name <arg-name>
        ^action-operator <aop>
        ^argument-info <arg-info>)
   (<arg-info> ^item-type object
               ^predicates <preds>)
}

sp {multiple-argument-instantiations*apply*report-multiple-arguments*copy*predicates
   (state <s> ^name multiple-argument-instantiations
              ^operator <o>
              ^failure.argument-info.predicates <preds>)
   (<o> ^name report-multiple-arguments
        ^duplicate-info.obj-preds.{ <prop> << color shape size category name >> } <pred>)
-->
   (<preds> ^<prop> <pred>)
}

sp {multiple-argument-instantiations*apply*report-multiple-arguments*copy*predicates*none
   (state <s> ^name multiple-argument-instantiations
              ^operator <o>
              ^failure.argument-info.predicates <preds>)
   (<o> ^name report-multiple-arguments
        -^duplicate-info.obj-preds.{ <prop> << color shape size category name >> } <pred>)
-->
   (<preds> ^category object)
}

sp {multiple-argument-instantiations*apply*report-multiple-arguments*report-failure
   (state <s> ^name multiple-argument-instantiations
              ^operator.name report-multiple-arguments
              ^failure <f>
              ^top-state.interaction.status <status>)
   (<f> ^argument-info.predicates.<any> <pred>)
-->
   (<status> ^failure <f>)
}
