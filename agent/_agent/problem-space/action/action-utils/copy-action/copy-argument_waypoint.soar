####################################################
# waypoint arguments

sp {action*problem-space*elaborate*action-operator*deep*copy*waypoint*argument
   (state <s> ^deep-copy-action-operator <src-op>
              ^action-operator <new-op>)
   # Source
   (<src-op> ^<arg-name> <src-arg>)
   (<src-arg> ^arg-type waypoint
              ^id <wp>)
-->
   (<new-op> ^<arg-name> <new-arg>)
   (<new-arg> ^arg-type waypoint
              ^id <wp>)
}

sp {copy-action*propose*copy-argument*waypoint
   (state <s> ^name copy-action
              ^copy-argument <ca>)
   (<ca> ^arg-type waypoint
         ^source-arg.id <wp>
        -^arg-copy)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-argument
        ^argument <ca>
        ^arg-type waypoint
        ^waypoint <wp>)
}

sp {copy-action*apply*copy-argument*waypoint
   (state <s> ^name copy-action
              ^operator <o>)
   (<o> ^name copy-argument
        ^argument <ca>
        ^arg-type waypoint
        ^waypoint <wp>)
-->
   (<ca> ^arg-copy <copy>)
   (<copy> ^arg-type waypoint
           ^id <wp>)
}