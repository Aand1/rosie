####################################################
# predicate arguments

sp {copy-action*expand*predicate*argument*copy*obj2
   (state <s> ^name copy-action
              ^copy-argument <ca>)
   (<ca> ^arg-type predicate
         ^source-arg <src>)
   (<src> ^handle <h>
          ^2 <obj2>)
-->
   (<ca> ^obj2-copy <obj2-copy>
         ^pred-handle <h>)
   (<s> ^copy-argument <obj2-copy>)
   (<obj2-copy> ^arg-type object
                ^source-arg.id <obj2>)
}

sp {copy-action*propose*copy-argument*predicate
   (state <s> ^name copy-action
              ^copy-argument <ca>)
   (<ca> ^arg-type predicate
         ^pred-handle <h>
         ^obj2-copy.arg-copy.id <obj2-copy>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-argument
        ^argument <ca>
        ^arg-type predicate
        ^pred-handle <h>
        ^obj2 <obj2-copy>)
}

sp {copy-action*apply*copy-argument*predicate
   (state <s> ^name copy-action
              ^operator <o>)
   (<o> ^name copy-argument
        ^argument <ca>
        ^arg-type predicate
        ^pred-handle <h>
        ^obj2 <obj2>)
-->
   (<ca> ^arg-copy <copy>)
   (<copy> ^arg-type predicate
           ^handle <h>
           ^2 <obj2>)
}