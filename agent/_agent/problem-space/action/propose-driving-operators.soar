###################################################################
#
# problem-space.action.propose-driving-operators << true false >>
#   If true, proposes operators for driving actions
#   	turn, orient, follow-wall, stop
#   These do not have action models or preconditions
#       (Only proposed when trying to match a primitive action command)
#
###################################################################


## op_turn
sp {composite-action*propose*driving-operator*op_turn1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^action.propose-action-operators true
         ^action.propose-driving-operators true)
-->
   (<s> ^operator <oleft> +
        ^operator <oright> +
        ^operator <oaround> +)
   (<oleft> ^name op_turn1
          ^action-handle turn1
          ^action-type primitive
          ^arg1 <left-arg1>)
   (<left-arg1> ^arg-type concept ^handle left1)
   
   (<oright> ^name op_turn1
             ^action-handle turn1
             ^action-type primitive
             ^arg1 <right-arg1>)
   (<right-arg1> ^arg-type concept ^handle right1)
   
   (<oaround> ^name op_turn1
             ^action-handle turn1
             ^action-type primitive
             ^arg1 <around-arg1>)
   (<around-arg1> ^arg-type concept ^handle around1)
}

## op_follow-wall1
sp {composite-action*propose*driving-operator*op_follow-wall1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^action.propose-action-operators true
         ^action.propose-driving-operators true)
-->
   (<s> ^operator <left-op> +
        ^operator <right-op> +)
   (<left-op> ^name op_follow-wall1
              ^action-handle follow-wall1
              ^action-type primitive
              ^arg1 <left-arg1>)
   (<left-arg1> ^arg-type object ^id <left-wall>)
   (<left-wall> ^item-type object ^handle left-wall1 ^predicates <left-preds>)
   (<left-preds> ^relative-direction1 left1 ^spatial-shape wall1)

   (<right-op> ^name op_follow-wall1
               ^action-handle follow-wall1
               ^action-type primitive
               ^arg1 <right-arg1>)
   (<right-arg1> ^arg-type object ^id <right-wall>)
   (<right-wall> ^item-type object ^handle right-wall1 ^predicates <right-preds>)
   (<right-preds> ^relative-direction1 right1 ^spatial-shape wall1)
}

## op_orient1
sp {composite-action*propose*driving-operator*op_orient1*each*direction
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^action.propose-action-operators true
         ^action.propose-driving-operators true)
-->
   (<s> ^operator <on> +
        ^operator <os> +
        ^operator <oe> +
        ^operator <ow> +)

   (<on> ^name op_orient1
         ^action-handle orient1
         ^action-type primitive
         ^arg1 <n-arg1>)
   (<n-arg1> ^arg-type concept ^handle north1)

   (<os> ^name op_orient1
         ^action-handle orient1
         ^action-type primitive
         ^arg1 <s-arg1>)
   (<s-arg1> ^arg-type concept ^handle south1)

   (<oe> ^name op_orient1
         ^action-handle orient1
         ^action-type primitive
         ^arg1 <e-arg1>)
   (<e-arg1> ^arg-type concept ^handle east1)

   (<ow> ^name op_orient1
         ^action-handle orient1
         ^action-type primitive
         ^arg1 <w-arg1>)
   (<w-arg1> ^arg-type concept ^handle west1)
}

## op_stop1
sp {composite-action*propose*driving-operator*op_stop1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^action.propose-action-operators true
         ^action.propose-driving-operators true)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_stop1
        ^action-handle stop1
        ^action-type primitive)
}
