sp {learn-subaction-proposal*propose*create-operator-argument
   (state <s> ^name learn-subaction-proposal
              ^ground-argument <gnd>
              ^subaction-id.<arg-name> <sub-arg>
              -^subaction-operator.<arg-name> <arg>)
  (<gnd> ^generalized-rep <sub-arg>
         ^instance <i>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-operator-argument
        ^argument <sub-arg>
        ^arg-name <arg-name>
        ^arg-instance <i>)
}

sp {learn-subaction-proposal*apply*create-operator-argument
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^subaction-operator <sub-op>)
   (<o> ^name create-operator-argument
        ^arg-name <arg-name>
        ^arg-instance <i>)
-->
   (<sub-op> ^<arg-name> <i>)
}

sp {learn-subaction-proposal*apply*create-operator-argument*mark*arg-status*abstract
   (state <s> ^name learn-subaction-proposal
              ^operator <o>)
   (<o> ^name create-operator-argument
        ^argument.arg-status abstract
        ^arg-instance <i>)
-->
  (<i> ^arg-status abstract)
}
