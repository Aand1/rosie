sp {desired-copy*elaborate*desired-to-copy*default
   (state <s> ^problem-space.state <state>)
   (<state> ^desired-copy <any>
            ^state-to-copy.desired <des>
           -^desired-to-copy <> <des>)
-->
   (<state> ^desired-to-copy <des>)
}

sp {desired-copy*shallow*copy*desired
   (state <s> ^problem-space.state <state>)
   (<state> ^desired-copy shallow
            ^desired-to-copy <des>)
-->
   (<s> ^desired <des>)
}

sp {desired-copy*deep*copy*state*predicate
   (state <s> ^problem-space.state <state>
              ^desired <des>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<state> ^desired-copy deep
            ^desired-to-copy.predicate <src-pred>)
   (<src-pred> ^type state
               ^id <pred-id>
               ^1 <src-obj>)
-->
   (<des> ^predicate <copy-pred>)
   (<copy-pred> ^type state
                ^id <pred-id>
                ^1 <copy-obj>)
}

sp {desired-copy*deep*copy*relation*predicate
   (state <s> ^problem-space.state.desired-copy deep
              ^superstate.desired.predicate <src-pred>
              ^desired <des>
              ^copy-object-map.<src-obj1> <copy-obj1>
              ^copy-object-map.<src-obj2> <copy-obj2>)
   (<src-pred> ^type relation
               ^id <pred-id>
               ^1 <src-obj1>
               ^2 <src-obj2>)
-->
   (<des> ^predicate <copy-pred>)
   (<copy-pred> ^type relation
                ^id <pred-id>
                ^1 <copy-obj1>
                ^2 <copy-obj2>)
}
