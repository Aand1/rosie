sp {problem-space*default-desired-copy*yes*elaborate*dont-copy
   (state <s> ^problem-space <ps>)
   (<ps> ^default-desired-copy yes)
-->
   (<ps> ^dont-copy predicate)
}

sp {desired-copy*deep*copy*state*predicate
   (state <s> ^problem-space.default-desired-copy yes
              ^desired <des>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<des> ^duplicate-of.predicate <src-pred>)
   (<src-pred> ^type state
               ^id <pred-id>
               ^1 <src-obj>)
-->
   (<des> ^predicate <copy-pred>)
   (<copy-pred> ^type state
                ^id <pred-id>
                ^1 <copy-obj>)
}

sp {desired-copy*deep*copy*relation*predicate
   (state <s> ^problem-space.default-desired-copy yes
              ^desired <des>
              ^copy-object-map.<src-obj1> <copy-obj1>
              ^copy-object-map.<src-obj2> <copy-obj2>)
   (<des> ^duplicate-of.predicate <src-pred>)
   (<src-pred> ^type relation
               ^id <pred-id>
               ^1 <src-obj1>
               ^2 <src-obj2>)
-->
   (<des> ^predicate <copy-pred>)
   (<copy-pred> ^type relation
                ^id <pred-id>
                ^1 <copy-obj1>
                ^2 <copy-obj2>)
}
