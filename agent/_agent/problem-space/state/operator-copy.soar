sp {problem-space*state*operator-copy*shallow*copy
   (state <s> ^problem-space.state <state>)
   (<state> ^operator-copy shallow
            ^state-to-copy.operator <o> +)
-->
   (<s> ^operator <o> +)
}

sp {problem-space*state*operator-copy*deep*create*map
   (state <s> ^problem-space.state <state>)
   (<state> ^operator-copy deep
            ^state-to-copy <stc>)
-->
   (<s> ^copy-operator-map <map>)
}

sp {problem-space*state*operator-copy*deep*copy*operator
   (state <s> ^problem-space.state <state>
              ^copy-operator-map <map>)
   (<state> ^operator-copy deep
            ^state-to-copy.operator <src-op> +)
-->
   (<s> ^operator <copy-op> +)
   (<map> ^<src-op> <copy-op>)
}

sp {problem-space*state*operator-copy*deep*copy*level1*attributes
   (state <s> ^problem-space.state.operator-copy deep
              ^copy-operator-map.<src-op> <copy-op>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<src-op> ^{ <att-name> << name action-type action-name >> } <att-val>)
-->
   (<copy-op> ^<att-name> <att-val>)
}

sp {problem-space*state*operator-copy*deep*copy*object*arg
   (state <s> ^problem-space.state.operator-copy deep
              ^copy-operator-map.<src-op> <copy-op>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<src-op> ^<arg-name> <src-arg>)
   (<src-arg> ^arg-type object
              ^id <src-obj>)
-->
   (<copy-op> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type object
              ^id <copy-obj>)
}

sp {problem-space*state*operator-copy*deep*copy*concept*arg
   (state <s> ^problem-space.state.operator-copy deep
              ^copy-operator-map.<src-op> <copy-op>)
   (<src-op> ^<arg-name> <src-arg>)
   (<src-arg> ^arg-type concept
              ^id <concept>)
-->
   (<copy-op> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type concept
               ^id <concept>)
}

sp {problem-space*state*operator-copy*deep*copy*predicate*arg
   (state <s> ^problem-space.state.operator-copy deep
              ^copy-operator-map.<src-op> <copy-op>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<src-op> ^<arg-name> <src-arg>)
   (<src-arg> ^arg-type predicate
              ^id <pred>
              ^2 <src-obj>)
-->
   (<copy-op> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type predicate
               ^id <pred>
               ^2 <copy-obj>)
}