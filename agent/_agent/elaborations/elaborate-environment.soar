### elaborate symbolic environmental state
### details in http://github.com/InstructoSoar/rosie/wiki/elaborate-environment.soar

##### elaborate perceptual state ####################################
## elaborate closed/open
sp {elaborate*object*state-property*open*closed
   (state <s> ^topstate <s>
   	      ^object <obj>
	      ^environment <env>)
   (<obj> ^property <prop>)
   (<prop> ^name door
   	   ^label <val>)
-->
   (<env> ^<val> <obj>)
}

### elaborate on/off
sp {elaborate*object*state-property*on*off
   (state <s> ^topstate <s>
   	      ^object <obj>
	      ^environment <env>)
   (<obj> ^property <prop>)
   (<prop> ^name heat
   	   ^label <val>)
-->
   (<env> ^<val> <obj>)
}

### elaborate cooked
sp {elaborate*object*cooked
   (state <s> ^topstate <s>
   	      ^object <obj>
	      ^environment <env>)
   (<obj> ^property <prop>)
   (<prop> ^name cooked
   	   ^label true)
-->
   (<env> ^cooked <obj>)
}

#### elaborate if an object is clear to be picked up

sp {elaborate*not-clear*object
   (state <s> ^topstate <s>
   	      ^object <obj1>
	      ^object <obj2>
	      ^prepositions.prep <prep>
	      ^environment <env>)
   (<env> ^intermediate <int>)
   (<obj1> ^category block
   	  ^id <id1>) 
   (<obj2> ^id <id2>)
   (<prep> ^name on
   	   ^pair <pair>)
   (<pair> ^a <id2>
   	   ^b <id1>)
-->
   (<int> ^not-clear <obj1>)
}

sp {elaborate*clear*object
   (state <s> ^topstate <s>
   	      ^object <obj1>
	      ^environment <env>)
   (<env> -^intermediate.not-clear <obj1>)
   (<obj1> ^category block)
-->
   (<env> ^clear <obj1>)
}


### elaborate predicates for objects and relations in focus
sp {elaborate*relations*objects*relation*in*conversation-focus
   (state <s> ^topstate <s>
   	      ^environment <env>
	      ^interaction.referents.relations.relation <rel-id>
	      ^interaction.referents.objects.object <obj1>
	      ^prepositions.prep <prep>
	      ^object <obj2>)
   (<rel-id> ^name <rel-name>)
   (<prep> ^name <rel-name>
   	   ^pair <pair>)
   (<obj1> ^id <obj-id>)
   (<obj2> ^id <obj-id2>)
   (<pair> ^<attr> <obj-id>
   	   ^{<attr2> > <attr>} <obj-id2>)
   	  
-->
   (<env> ^relation <relation>)
   (<relation> ^type <rel-id>)
   (<relation> ^<attr> <obj1>
   	       ^<attr2> <obj2>)
}

### elaborate grabbed object
sp {elaborate*grabbed-object*from*robot-state
   (state <s> ^topstate <s>
   	      ^environment <env>
   	      ^robot-state.arm.grabbed <obj>)
-->
   (<env> ^grabbed <obj>)
}


### copy environment
sp {copy*environment-state*superstate
   (state <s> ^superstate.environment <env>
   	      -^problemspace.dont-copy environment)
--> 
   (<s> ^environment <env>)
}
   	  