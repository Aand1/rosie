# Elaborate svs object id's
# belief objects have an id-int that corresponds to an id on a working memory object
# perception objects have an id-as-integer that is the cast form of the id

sp {svs*elaborate*svs*objects*type*node
   (state <s> #^superstate nil
   	          ^svs.spatial-scene <sc>)
   (<sc> ^child <C>)
   (<C> ^object-source belief)
 --> 
   (<C> ^type node)
 }

# Create property on belief objects that mark their source 
#    (Used to only grab the subset of svs objects that are belief objects)
sp {svs*apply*any-op*property-command*object-source*belief
   (state <s> ^topstate.scene-monitor.object.belief-obj <bel-obj>
              ^operator <o>
              ^svs.command <cmd>)
   (<bel-obj> ^id <bel-id>
             -^object-source belief)
-->
   (<cmd> ^set_tag <prop>)
   (<prop> ^id <bel-id>
           ^tag_name object-source
           ^tag_value belief)
}

# Cleans up the svs command link after commands have been  completed


sp {topstate*apply*any-op*remove*monitor*filters
   (state <s> ^topstate <s>
              ^operator <o>
              ^svs.command <cmd>
              ^svs.spatial-scene <scene>)
   (<cmd> ^extract <filter>)
   (<filter> ^type << monitor_position monitor_volume >>
             ^a.id <id>)
   (<scene> -^child.id <id>)
-->
   (<cmd> ^extract <filter> -)
}

sp {svs*apply*any-op*remove*completed*commands
   (state <s> ^topstate <s>
              ^operator <o>
              ^svs.command <cmd>)
   (<cmd> ^{<type> << add_node copy_node delete_node set_transform copy_transform set_tag delete_tag >> } <val>)
   (<val> ^status success)
-->
   (<cmd> ^<type> <val> -)
}

sp {svs*apply*any-op*report*filters*errors
   (state <s> ^topstate <s>
              ^operator <o>
              ^svs.command <cmd>)
   (<cmd> ^{ << extract extract_once >> } <val>)
   (<val> ^type <type>
          ^status {<status> <> success})
-->
   (write |Filter Error (| <type> |): | <status> (crlf))
}

sp {svs*apply*any-op*remove*completed*commands*errors
   (state <s> ^topstate <s>
              ^operator <o>
              ^svs.command <cmd>)
   (<cmd> ^{<type> <> extract <> extract_once } <val>)
   (<val> ^status {<status> <> success})
-->
   (<cmd> ^<type> <val> -)
   (write |Command Error (| <type> |): | <status> (crlf))
}