# Elaborations for topstate and substates

# name
# Name a state using the selected superstate operator's name
sp {all*elaborate*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

sp {topstate*elaborate*intermediate
   (state <s> ^superstate nil)
-->
   (<s> ^intermediate <int>)
}
   	  


# topstate/top-state
# Copy direct top-state link to all substates
# (yes, having both is dumb)
sp {all*elaborate*top-state*topstate
   (state <s> ^superstate.topstate <ts>)
-->
   (<s> ^topstate <ts>
   	^top-state <ts>)
			
}

sp {topstate*elaborate*top-state
   (state <s> ^topstate <ts>)
-->
   (<s> ^top-state <ts>)
}

# io
# Copy the io link to all substates
sp {all*elaborate*io
   (state <s> ^superstate.io <io>
   	      -^name action-policy)
-->
   (<s> ^io <io>)
}

# nl
### elaborate nl to all substates
sp {all*elaborate*nl
   (state <s> ^superstate.nl <snl>)
-->
   (<s> ^nl <snl>)
}

# self
# Copy the self link from the input-link to the topstate
sp {topstate*elaborate*self
   (state <s> ^io.input-link <il>
   	      ^topstate <s>)
   (<il> ^self <self>)
 -->
   (<s> ^self <self>)
}

# object
# Copy objects to substate, if not in language problem-space
sp {all*elaborate*object
   (state <s> ^superstate <ss>
      	  -^problem-space << lgsoar message-interpretation >>
	  -^name action-policy
	  -^name selection
	  -^dont-copy object)
   (<ss> -^problem-space << lgsoar message-interpretation >>
   	 ^object <obj>)
-->
  (<s> ^object <obj>)
}

# grabbed
# Elaborate the grabbed object onto substates
sp {topstate*elaborate*grabbed
   (state <s> ^self <self>
   	      ^object <obj>
          ^topstate <s>)
   (<self> ^grabbed-object <id> <> -1)
   (<obj> ^id <id>
   	  ^category block)
-->
   (<s> ^grabbed <obj>)
}   

sp {all*elaborate*grabbed
   (state <s> ^superstate <ss>
   	      ^io)
   (<ss> ^grabbed <obj>)
-->
   (<s> ^grabbed <obj>)
}

# pointed-object
# Elaborate the topstate with which object is being pointed at
sp {topstate*apply*operator*remove*old*pointed
   (state <s> ^superstate nil
              ^operator <o>
              ^io.input-link.pointed-object <new-id>
              ^pointed-object {<old-id> <> <new-id>})
-->
   (<s> ^pointed-object <old-id> -)
}

sp {topstate*apply*operator*add*pointed
   (state <s> ^superstate nil
              ^operator <o>
              ^io.input-link.pointed-object <id>
             -^pointed-object <id>)
-->
   (<s> ^pointed-object <id>)
}

# Elaborate topstate: top-segment
sp {topstate*elaborate*top-segment
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
-->
   (<s> ^top-segment <seg>)
}


# kind
## Elaborate a state as non-atomic/atomic if the operator proposed in superstate in non-atomic/atomic

sp {elaborate*atomicity
   (state <s> ^superstate.operator <sop>)
   (<sop> ^kind <atom>)
-->
   (<s> ^kind <atom>)
}

# track-relation
# copy track-relations to substates
sp {all*elaborate*track-relations
   (state <s> ^superstate.track-relation <rel>)
-->
   (<s> ^track-relation <rel>)
}
   

# If clear-interaction-stack is present, terminate the top segment
sp {topstate*clear-stack*elaborate*terminate-segment
   (state <s> ^topstate <s>
              ^clear-interaction-stack true
              ^interaction.stack.segment <seg>)
   (<seg> ^id <> 0)
-->
   (<s> ^terminate-segment <seg>)
}


##### elaborate perceptual state ####################################
## elaborate closed/open
sp {elaborate*object*state-property*open*closed
   (state <s> ^topstate <s>
   	      ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^name door
   	   ^label <val>)
-->
   (<s> ^<val> <obj>)
}

# copy closed/open
sp {copy*state-property*open
   (state <s> ^object <obj>
   	      ^io
	      ^superstate.open <obj>
	      -^name learn-termination-rule)
-->
   (<s> ^open <obj>)
}

sp {copy*state-property*closed
   (state <s> ^io
	      ^superstate.closed <obj>
	      -^name learn-termination-rule)
-->
   (<s> ^closed <obj>)
}

### elaborate on/off
sp {elaborate*object*state-property*on*off
   (state <s> ^topstate <s>
   	      ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^name heat
   	   ^label <val>)
-->
   (<s> ^<val> <obj>)
}

sp {copy*state-property*on
   (state <s> ^io
	      ^superstate.on <obj>
	      -^name learn-termination-rule)
-->
   (<s> ^on <obj>)
}

sp {copy*state-property*off
   (state <s> ^io
	      ^superstate.off <obj>
	      -^name learn-termination-rule)
-->
   (<s> ^off <obj>)
}

### elaborate cooked
sp {elaborate*object*cooked
   (state <s> ^topstate <s>
   	      ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^name cooked
   	   ^label true)
-->
   (<s> ^cooked <obj>)
}

sp {copy*state-property*cooked
   (state <s> ^io
	      ^superstate.cooked <obj>
	      -^name learn-termination-rule)
-->
   (<s> ^cooked <obj>)
}


#### elaborate if an object is clear to be picked up

sp {elaborate*not-clear*object
   (state <s> ^topstate <s>
   	      ^object <obj1>
	      ^object <obj2>
	      ^prepositions.prep <prep>
	      ^intermediate <int>)
   (<obj1> ^category block
   	  ^id <id1>) 
   (<obj2> ^id <id2>)
   (<prep> ^name on
   	   ^pair <pair>)
   (<pair> ^a <id2>
   	   ^b <id1>)
-->
   (<int> ^not-clear <obj1>)
}

sp {elaborate*clear*object
   (state <s> ^topstate <s>
   	      ^object <obj1>
	     -^intermediate.not-clear <obj1>)
   (<obj1> ^category block)
-->
   (<s> ^clear <obj1>)
}

sp {copy*clear*substate
   (state <s> ^superstate.clear <obj1>)
-->
   (<s> ^clear <obj1>)
}

   	  