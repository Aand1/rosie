sp {execute-pick-up*propose*internal-pick-up
   (state <s> ^name execute-pick-up1
              ^target-object <obj>
              ^top-state.world.robot.arm.state internal)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-pick-up
        ^object <obj>)
}

sp {execute-pick-up*apply*internal-pick-up*send-message
   (state <s> ^name execute-pick-up1
              ^operator <o>
              ^current-action <a>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-pick-up
        ^object.predicates.shape <shape>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type get-item-request
          ^item <shape>)
   (<a> ^sent-message true)
}

sp {execute-pick-up*apply*internal-pick-up*mark*complete
   (state <s> ^name execute-pick-up1
              ^operator.name internal-pick-up
              ^current-action <a>)
   (<a> ^received-yes true)
-->
   (<a> ^status complete)
}

sp {execute-pick-up*apply*internal-pick-up*mark*grabbed
   (state <s> ^name execute-pick-up1
              ^operator <o>
              ^current-action.received-yes true)
   (<o> ^name internal-pick-up
        ^object.predicates <preds>)
-->
   (<preds> ^arm-status grabbed)
}

