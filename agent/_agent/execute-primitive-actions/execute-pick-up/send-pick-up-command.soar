###############################################################################
#
# Inside the execute-pick-up1 substate, this will put a grab command on the output link
# If the pick-up fails and needs to be re-issued, this will cause that to occur
#
###############################################################################

sp {execute-pick-up1*propose*send-pick-up-command
   (state <s> ^name execute-pick-up1
              ^target-object <obj>
              -^world.robot.arm.action << failure home >>
              -^world.robot.arm.state internal
             -^current-action.command-status)
   (<obj> ^predicates.status live)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-pick-up-command
        ^object <obj>
        ^command-operator true)
}

sp {execute-pick-up1*apply*send-pick-up-command*send
   (state <s> ^name execute-pick-up1
              ^operator <o>
              ^io.output-link <ol>
              ^current-action <a>)
   (<o> ^name send-pick-up-command
				 ^object.handle <obj-handle>)
-->
   (<ol> ^pick-up <cmd>)
   (<cmd> ^object-handle <obj-handle>)
   (<a> ^sent-command <cmd>)
}

sp {execute-pick-up1*apply*send-pick-up-command*mark*object*moving
   (state <s> ^name execute-pick-up1
              ^operator <o>)
   (<o> ^name send-pick-up-command
        ^object.predicates <preds>)
-->
   (<preds> ^arm-status moving)
}

# Don't attend to new-stable-objects in case they are the 
#   thing we are trying to grab (won't know until the action finishes)
sp {send-pick-up-command*reject*evaluate-new-stable-object
   (state <s> ^name send-pick-up-command
              ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type new-stable-object)
-->
   (<s> ^operator <o> -)
}

sp {execute-pick-up1*apply*send-pick-up-command*copy*command-status
   (state <s> ^name execute-pick-up1
              ^operator.name send-pick-up-command
              ^current-action <a>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status <status>)
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>)
}

sp {execute-pick-up1*apply*send-pick-up-command*remove*moving*status
   (state <s> ^name execute-pick-up1
              ^operator.name send-pick-up-command
              ^current-action.sent-command.status <status>
              ^target-object.predicates <preds>)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -)
}
