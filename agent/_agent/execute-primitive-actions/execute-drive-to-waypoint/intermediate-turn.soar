sp {execute-drive-to-waypoint1*propose*intermediate-turn
   (state <s> ^name execute-drive-to-waypoint1
              ^current-action <a>
              ^waypoint <wp>
              ^intermediate-turn { <yaw> <> none })
   (<a> ^drive-command.status success
       -^turn-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name intermediate-turn
        ^turn-amount <yaw>)
}

sp {execute-drive-to-waypoint1*apply*intermediate-turn*remove*drive-command
   (state <s> ^name execute-drive-to-waypoint1
              ^operator.name intermediate-turn
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^drive-command <cmd>)
   (<out> ^do-control-law <cmd>)
-->
   (<a> ^drive-command <cmd> -)
   (<out> ^do-control-law <cmd> -)
}

sp {execute-drive-to-waypoint1*apply*intermediate-turn*right
   (state <s> ^name execute-drive-to-waypoint1
              ^operator <o>
              ^current-action <a>
              ^top-state.io.input-link.self <self>
              ^top-state.io.output-link <out>)
   (<o> ^name intermediate-turn
        ^turn-amount { <yaw> < 0 })
-->
   (<out> ^do-control-law <law>)
   (<law> ^name turn
          ^parameters.direction -1
          ^termination-condition <cond>)
   (<cond> ^name rotation
           ^parameters.yaw (- <yaw>))
   (<a> ^turn-command <law>)
}

sp {execute-drive-to-waypoint1*apply*intermediate-turn*left
   (state <s> ^name execute-drive-to-waypoint1
              ^operator <o>
              ^current-action <a>
              ^top-state.io.input-link.self <self>
              ^top-state.io.output-link <out>)
   (<o> ^name intermediate-turn
        ^turn-amount { <yaw> > 0 })
-->
   (<out> ^do-control-law <law>)
   (<law> ^name turn
          ^parameters.direction 1
          ^termination-condition <cond>)
   (<cond> ^name rotation
           ^parameters.yaw <yaw>) 
   (<a> ^turn-command <law>)
}
