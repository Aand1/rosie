sp {execute-drive-to-waypoint1*propose*drive-to-waypoint*no-turn
   (state <s> ^name execute-drive-to-waypoint1
              ^intermediate-turn none
              ^current-action <a>
              ^waypoint.handle <handle>)
   (<a> ^face-command.status success
       -^drive-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name drive-to-waypoint
        ^termination-condition <term>)
   (<term> ^name count
           ^parameters <params>)
   (<params> ^count 1
             ^class <handle>)
}

sp {execute-drive-to-waypoint1*propose*drive-to-waypoint*turn*part1
   (state <s> ^name execute-drive-to-waypoint1
              ^intermediate-turn <> none
              ^current-action <a>)
   (<a> ^face-command.status success
       -^turn-command
       -^drive-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name drive-to-waypoint
        ^termination-condition <term>)
   (<term> ^name stabilized)
}

sp {execute-drive-to-waypoint1*propose*drive-to-waypoint*turn*part2
   (state <s> ^name execute-drive-to-waypoint1
              ^intermediate-turn <> none
              ^current-action <a>
              ^waypoint.handle <handle>)
   (<a> ^turn-command.status success
       -^drive-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name drive-to-waypoint
        ^termination-condition <term>)
   (<term> ^name count
           ^parameters <params>)
   (<params> ^count 1
             ^class <handle>)
}

sp {execute-drive-to-waypoint1*apply*drive-to-waypoint
   (state <s> ^name execute-drive-to-waypoint1
              ^operator <o>
              ^current-action <a>
              ^top-state.io.output-link <out>
              ^wall-side <side>)
   (<o> ^name drive-to-waypoint
        ^termination-condition <term>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name follow-wall
          ^parameters.side <side>
          ^termination-condition <term>)
   (<a> ^drive-command <law>)
}



   