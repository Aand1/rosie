sp {execute-primitive-action*elaborate*until-clause*from*superoperator
   (state <s> ^problem-space.name execute-primitive-action
              ^superstate.operator.until-clause <until>)
-->
   (<s> ^until-clause <until>)
}

sp {execute-primitive-action*elaborate*until-clause*state*predicate*satisfied
   (state <s> ^problem-space.name execute-primitive-action
              ^until-clause.predicate <pred>
              ^world.objects.object <obj>)
   (<pred> ^type state
           ^handle <pred-handle>
           ^{1} <obj>)
   (<obj> ^predicates.<prop> <pred-handle>)
-->
   (<s> ^satisfied <pred>)
}

sp {execute-primitive-action*elaborate*until-clause*state*relation*satisfied
   (state <s> ^problem-space.name execute-primitive-action
              ^until-clause.predicate <until-pred>
              ^world <world>)
   (<until-pred> ^type relation
           ^handle <pred-handle>
           ^1 <obj1>
           ^2 <obj2>)
   (<world> ^predicates.predicate <pred>
            ^objects.object <obj1>
            ^objects.object <obj2>)
   (<pred> ^handle <pred-handle>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<s> ^satisfied <until-pred>)
}

sp {execute-primitive-action*elaborate*until-clause*predicate*satisfied*false
   (state <s> ^problem-space.name execute-primitive-action
              ^until-clause.predicate <pred>)
   (<s> -^satisfied <pred>)
-->
   (<s> ^unsatisfied <pred>)
}

sp {execute-primitive-action*elaborate*current-action*status*complete*all*satisfied
   (state <s> ^problem-space.name execute-primitive-action
              ^satisfied <pred>
             -^unsatisfied <any>
              ^current-action <a>)
-->
   (<a> ^status complete)
}
              
