sp {execute-follow-wall1*elaborate*until-clause*from*superoperator
   (state <s> ^name execute-follow-wall1
              ^superstate.operator.until-clause <term>)
-->
   (<s> ^until-clause <term>)
}

sp {execute-follow-wall1*elaborate*direction*from*superoperator
   (state <s> ^name execute-follow-wall1
              ^superstate.operator.arg1.id.predicates.relative-direction1 {<dir-handle> <> wall1})
-->
   (<s> ^direction-handle <dir-handle>)
}

sp {execute-follow-wall1*elaborate*smem-query-cue*direction
   (state <s> ^name execute-follow-wall1
              ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
	(<cue> ^handle <dir-handle>)
}

sp {execute-follow-wall1*elaborate*direction-id*from*retrieval
   (state <s> ^name execute-follow-wall1
              ^smem-query.result <dir-id>
              ^direction-handle <dir-handle>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}

sp {execute-follow-wall*elaborate*wall-side*from*direction-id
   (state <s> ^name execute-follow-wall1
              ^direction-id.side <side>)
-->
   (<s> ^wall-side <side>)
}

sp {execute-follow-wall*elaborate*wall-side*default
   (state <s> ^name execute-follow-wall1
             -^direction-handle)
-->
   (<s> ^wall-side 1)
}
