###############################################################################
#
# Inside the execute-turn-right1 substate, this will put a turn-right command on the output link
#
###############################################################################

sp {execute-turn-right1*propose*send-turn-right-command
   (state <s> ^name execute-turn-right1
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-turn-right-command
        ^command-operator true)
}

sp {execute-turn-right1*apply*send-turn-right-command
   (state <s> ^name execute-turn-right1
              ^operator.name send-turn-right-command
              ^current-action <a>
              ^top-state.io.output-link <out>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name turn
          ^parameters.direction -1
          ^termination-condition <cond>)
   (<cond> ^name rotation
           ^parameters.yaw 1.107) # Not = pi/2 due to overshooting
   (<a> ^sent-command <law>)
}

sp {top-state*apply*execute-turn-right1*copy*command-status
   (state <s> ^top-state <s>
              ^operator.name execute-turn-right1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}