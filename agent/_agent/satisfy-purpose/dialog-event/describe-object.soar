sp {dialog-event*propose*describe-object
   (state <s> ^name dialog-event
              ^purpose <p>)
   (<p> ^type describe-object
        ^parameters.indexable <obj>)
   (<obj> ^type object
          ^result <wm-obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name describe-object
        ^object <wm-obj>)
}

sp {dialog-event*apply*describe-object*mark*performed
   (state <s> ^name dialog-event
              ^operator <o>
              ^topstate.interaction.status <status>)
   (<o> ^name describe-object
        ^description <desc>)
-->
   (<status> ^performed.dialog-event.type describe)
}

sp {dialog-event*apply*describe-object*describe
   (state <s> ^name dialog-event
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name describe-object
        ^description {<desc> <> none})
-->
   (<out> ^send-message <msg>)
   (<msg> ^type describe-object
          ^fields.object <desc>)
}

sp {dialog-event*apply*describe-object*no-object
   (state <s> ^name dialog-event
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name describe-object
        ^description none)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type no-object
          ^fields <f>)
}