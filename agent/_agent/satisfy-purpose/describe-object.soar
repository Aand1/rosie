sp {satisfy-purpose*propose*describe-object
   (state <s> ^name satisfy-purpose
              ^purpose <p>)
   (<p> ^type describe-object
        ^parameters.indexable <obj>)
   (<obj> ^type object
          ^internal <wm-obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name describe-object
        ^object <wm-obj>)
}

sp {satisfy-purpose*apply*describe-object*mark*performed
   (state <s> ^name satisfy-purpose
              ^operator <o>
              ^topstate.interaction.status <status>)
   (<o> ^name describe-object
        ^description <desc>)
-->
   (<status> ^performed.dialog-event.type describe)
}

sp {satisfy-purpose*apply*describe-object*describe
   (state <s> ^name satisfy-purpose
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name describe-object
        ^description {<desc> <> none})
-->
   (<out> ^send-message <msg>)
   (<msg> ^type describe-object
          ^fields.object <desc>)
}

sp {satisfy-purpose*apply*describe-object*no-object
   (state <s> ^name satisfy-purpose
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name describe-object
        ^description none)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type no-object
          ^fields <f>)
}