sp {retrospective-learning*composite-action*copy-subactions
   (state <s> ^problem-space <ps>
              ^superstate.subactions <src-subs>)
   (<src-subs> ^next <src-sub>)
   (<ps> ^name retrospective-learning
         ^name composite-action)
-->
   (<s> ^subactions <copy-subs>
        ^copy-subaction-map <map>)
   (<map> ^<src-sub> <copy-sub>)
   (<copy-subs> ^next <copy-sub>)
}


sp {retrospective-learning*composite-action*copy-subactions*copy*subaction*next
   (state <s> ^problem-space <ps>
              ^copy-subaction-map <map>
              ^subactions <subs>)
   (<ps> ^name retrospective-learning
         ^name composite-action)
   (<map> ^<src-sub> <copy-sub>)
   (<src-sub> ^next <new-src-sub>)
-->
   (<map> ^<new-src-sub> <new-copy-sub>)
   (<copy-sub> ^next <new-copy-sub>)
}

sp {retrospective-learning*composite-action*copy-subactions*copy*subaction*info
   (state <s> ^problem-space <ps>
              ^copy-subaction-map.<src-sub> <copy-sub>)
   (<ps> ^name retrospective-learning
         ^name composite-action)
   (<src-sub> ^name <sub-name>)
-->
   (<copy-sub> ^name <sub-name>
               ^action-type primitive)
}

sp {retrospective-learning*composite-action*copy-subactions*copy*subaction*object*arg
   (state <s> ^problem-space <ps>
              ^copy-subaction-map.<src-sub> <copy-sub>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<ps> ^name retrospective-learning
         ^name composite-action)
   (<src-sub> ^<arg-name> <arg>)
   (<arg> ^arg-type object
          ^id <src-obj>)
-->
   (<copy-sub> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type object
               ^id <copy-obj>)
}

sp {retrospective-learning*composite-action*copy-subactions*copy*subaction*concept*arg
   (state <s> ^problem-space <ps>
              ^copy-subaction-map.<src-sub> <copy-sub>)
   (<ps> ^name retrospective-learning
         ^name composite-action)
   (<src-sub> ^<arg-name> <arg>)
   (<arg> ^arg-type concept
          ^id <concept-id>)
-->
   (<copy-sub> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type concept
               ^id <concept-id>)
}

sp {retrospective-learning*composite-action*copy-subactions*copy*subaction*predicate*arg
   (state <s> ^problem-space <ps>
              ^copy-subaction-map.<src-sub> <copy-sub>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<ps> ^name retrospective-learning
         ^name composite-action)
   (<src-sub> ^<arg-name> <arg>)
   (<arg> ^arg-type predicate
          ^id <pred-id>
          ^2 <src-obj>)
-->
   (<copy-sub> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type predicate
               ^id <pred-id>
               ^2 <copy-obj>)
}
