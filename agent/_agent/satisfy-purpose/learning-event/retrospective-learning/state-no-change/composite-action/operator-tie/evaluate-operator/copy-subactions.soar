sp {retrospective-learning*copy-subactions*deep
   (state <s> ^problem-space.state.subactions-copy deep
              ^state-to-copy.subactions <src-subs>)
   (<src-subs> ^next <src-sub>)
-->
   (<s> ^subactions <copy-subs>
        ^copy-subaction-map <map>)
   (<map> ^<src-sub> <copy-sub>)
   (<copy-subs> ^next <copy-sub>)
}

sp {retrospective-learning*copy-subactions*deep*copy*subaction*next
   (state <s> ^problem-space.state.subactions-copy deep
              ^copy-subaction-map <map>
              ^subactions <subs>)
   (<map> ^<src-sub> <copy-sub>)
   (<src-sub> ^next <new-src-sub>)
-->
   (<map> ^<new-src-sub> <new-copy-sub>)
   (<copy-sub> ^next <new-copy-sub>)
}

sp {retrospective-learning*copy-subactions*deep*copy*subaction*info
   (state <s> ^problem-space.state.subactions-copy deep
              ^copy-subaction-map.<src-sub> <copy-sub>)
   (<src-sub> ^name <sub-name>)
-->
   (<copy-sub> ^name <sub-name>
               ^action-type primitive)
}

sp {retrospective-learning*copy-subactions*deep*copy*subaction*object*arg
   (state <s> ^problem-space.state.subactions-copy deep
              ^copy-subaction-map.<src-sub> <copy-sub>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<src-sub> ^<arg-name> <arg>)
   (<arg> ^arg-type object
          ^id <src-obj>)
-->
   (<copy-sub> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type object
               ^id <copy-obj>)
}

sp {retrospective-learning*copy-subactions*deep*copy*subaction*concept*arg
   (state <s> ^problem-space.state.subactions-copy deep
              ^copy-subaction-map.<src-sub> <copy-sub>)
   (<src-sub> ^<arg-name> <arg>)
   (<arg> ^arg-type concept
          ^id <concept-id>)
-->
   (<copy-sub> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type concept
               ^id <concept-id>)
}

sp {retrospective-learning*copy-subactions*deep*copy*subaction*predicate*arg
   (state <s> ^problem-space.state.subactions-copy deep
              ^copy-subaction-map.<src-sub> <copy-sub>
              ^copy-object-map.<src-obj> <copy-obj>)
   (<src-sub> ^<arg-name> <arg>)
   (<arg> ^arg-type predicate
          ^id <pred-id>
          ^2 <src-obj>)
-->
   (<copy-sub> ^<arg-name> <copy-arg>)
   (<copy-arg> ^arg-type predicate
               ^id <pred-id>
               ^2 <copy-obj>)
}
