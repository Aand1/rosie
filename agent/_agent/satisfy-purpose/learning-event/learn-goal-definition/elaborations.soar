## goal-structure - the goal lti from the action-id
sp {learn-goal-definition*elaborate*goal-structure*from*action-id
   (state <s> ^name learn-goal-definition
              ^action-id.goal <goal>)
-->
   (<s> ^goal-structure <goal>)
}

## action-operator - the action the goal is for
sp {learn-goal-definition*elaborate*action-operator*from*top-state*action-stack
   (state <s> ^name learn-goal-definition
              ^top-state.action-stack.top.execution-operator <op>)
-->
   (<s> ^action-operator <op>)
}

## we want to generalize the goal rep
sp {learn-goal-definition*elaborate*generalize-argument*predicate-set
  (state <s> ^name learn-goal-definition)
-->
  (<s> ^generalize-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^instance <i>)
}

# copy predicates from superoperator
sp {learn-goal-definition*elaborate*goal-predicate*superoperator
   (state <s> ^name learn-goal-definition
              ^generalize-argument <arg>
              ^superstate.operator.goal-predicate <pred>)
   (<arg> ^arg-type predicate-set
          ^instance <i>)
-->
   (<i> ^predicate <pred>)
}

# copy the result of the generalization process
sp {learn-goal-definition*elaborate*generalized-goal*from*generalize-argument
  (state <s> ^name learn-goal-definition
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate-set
         ^generalized-rep <rep>)
-->
  (<s> ^generalized-goal <rep>)
}
