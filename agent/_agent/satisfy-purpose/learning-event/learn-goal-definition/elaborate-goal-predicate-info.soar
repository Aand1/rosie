sp {learn-goal-definition*elaborate*goal-predicate-info
   (state <s> ^name learn-goal-definition
              ^goal-predicate <pred>)
   (<pred> ^type <type>)
-->
   (<s> ^goal-predicate-info <info>)
   (<info> ^type <type>
           ^instance <pred>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*arg*default*predicates
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.arg <arg>)
   (<arg> ^instance.predicates <preds>
          ^default <def>)
   (<preds> ^{ <prop-handle> << color shape size category property >> } <pred-handle>)
-->
   (<def> ^<prop-handle> <pred-handle>)
}

# Elaborate a generalized predicate structure (what will be stored in smem)
sp {learn-goal-definition*elaborate*goal-predicate-info*generalized-structure
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^instance.type <type>)
-->
   (<info> ^generalized-structure <gen>)
   (<gen> ^type <type>)
}
                                              
sp {learn-goal-definition*elaborate*goal-predicate-info*generalized-structure*copy*id*matches
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^instance <pred>)
   (<pred> ^type state
           ^handle <pred-handle>
           ^1 <obj1>)
-->
   (<s> ^generalize-argument <gen-pred> <gen-obj1>)
   (<gen-pred> ^arg-type predicate
               ^arg-status desired
               ^instance <pred-handle>)
   (<gen-obj1> ^arg-type object
              ^arg-status imagined
              ^instance <obj1>)
   (<info> ^id <gen-pred>
           ^1 <gen-obj1>)
}

sp {learn-goal-definition*elaborate*generalize-argument*relation*predicate
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^instance <pred>)
   (<pred> ^type relation
           ^handle <pred-handle>
           ^1 <obj1>
           ^2 <obj2>)
-->
   (<s> ^generalize-argument <gen-pred> <gen-obj1> <gen-obj2>)
   (<gen-pred> ^arg-type predicate
               ^arg-status desired
               ^instance <pred-handle>)
   (<gen-obj1> ^arg-type object
              ^arg-status imagined
              ^instance <obj1>)
   (<gen-obj2> ^arg-type object
              ^arg-status imagined
              ^instance <obj2>)
   (<info> ^id <gen-pred>
           ^1 <gen-obj1>
           ^2 <gen-obj2>)
}
