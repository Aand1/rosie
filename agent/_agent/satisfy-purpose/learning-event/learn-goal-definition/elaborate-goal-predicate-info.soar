sp {learn-goal-definition*elaborate*goal-predicate-info
   (state <s> ^name learn-goal-definition
              ^goal-predicate <pred>)
-->
   (<s> ^goal-predicate-info <info>)
   (<info> ^instance <pred>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*id
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^instance.handle <pred-handle>)
-->
   (<info> ^id.predicate-handle <pred-handle>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*arg
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^instance.{ <arg-num> << 1 2 3 4 5 >> } <arg>)
-->
   (<info> ^arg <new-arg>)
   (<new-arg> ^number <arg-num>
              ^instance <arg>)
}

# Elaborate the id and arguments with matches to procedural structures

sp {learn-goal-definition*elaborate*goal-predicate-info*id*matches
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.id <pred-id>
              ^action-context.<arg-name>.handle <pred-handle>
              ^action-id.procedural.<arg-name> <parg>)
   (<pred-id> ^predicate-handle <pred-handle>)
   (<parg> ^arg-type predicate
           ^id <pred-slot>)
-->
   (<pred-id> ^matches <pred-slot>)
}

sp {learn-goal-defintion*elaborate*goal-predicate-info*arg*matches*simple
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.arg <arg-info>
              ^action-context.<arg-name>.id <arg>
              ^action-id.procedural <proc>)
   (<arg-info> ^instance <arg>)
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type { <arg-type> << object concept >> } 
           ^id <item-slot>)
-->
   (<arg-info> ^matches <item-slot>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*arg*matches*procedural-arg*predicate
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.arg <arg-info>
              ^action-context.<arg-name>.{2} <arg>
              ^action-id.procedural <proc>)
   (<arg-info> ^instance <arg>)
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type predicate
           ^2 <obj-slot>)
-->
   (<arg-info> ^matches <obj-slot>)
}

# Elaborate the id and arguments with default match info
sp {learn-goal-definition*elaborate*goal-predicate-info*id*default
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.id <id>)
   (<id> ^predicate-handle <pred-handle>)
-->
   (<id> ^default <def>)
   (<def> ^predicate-handle <pred-handle>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*arg*default
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.arg <arg>)
-->
   (<arg> ^default <def>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*arg*default*predicates
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.arg <arg>)
   (<arg> ^instance.predicates <preds>
          ^default <def>)
   (<preds> ^{ <prop-handle> << color shape size category name >> } <pred-handle>)
-->
   (<def> ^<prop-handle> <pred-handle>)
}

# Elaborate a generalized predicate structure (what will be stored in smem)
sp {learn-goal-definition*elaborate*goal-predicate-info*generalized-structure
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^instance.type <type>)
-->
   (<info> ^generalized-structure <gen>)
   (<gen> ^type <type>)
}
                                              
sp {learn-goal-definition*elaborate*goal-predicate-info*generalized-structure*copy*id*matches
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^generalized-structure <gen>
           ^id.matches <match>)
-->
   (<gen> ^id <match>)
}
                                              
sp {learn-goal-definition*elaborate*goal-predicate-info*generalized-structure*copy*id*default
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^generalized-structure <gen>
           ^id <id>)
   (<id> -^matches <match>
          ^default <def>)
-->
   (<gen> ^id <new-id>)
   (<new-id> ^default <def>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*generalized-structure*copy*arg*matches
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^generalized-structure <gen>
           ^arg <arg>)
   (<arg> ^number <arg-num>
          ^matches <match>)
-->
   (<gen> ^<arg-num> <match>)
}
                                              
sp {learn-goal-definition*elaborate*goal-predicate-info*generalized-structure*copy*arg*default
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^generalized-structure <gen>
           ^arg <arg>)
   (<arg> ^number <arg-num>
          -^matches <match>
          ^default <def>)
-->
   (<gen> ^<arg-num> <new-arg>)
   (<new-arg> ^default <def>)
}