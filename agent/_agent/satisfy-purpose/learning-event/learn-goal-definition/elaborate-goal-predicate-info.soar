sp {learn-goal-definition*elaborate*goal-predicate-info
   (state <s> ^name learn-goal-definition
              ^goal-predicate <pred>)
-->
   (<s> ^goal-predicate-info <info>)
   (<info> ^predicate <pred>)
}

sp {learn-goal-definition*elaborate*goal-predicate-info*matches*relation
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>
              ^action-context.<arg-name>.handle <pred-handle>
              ^action-id.procedural.<arg-name> <parg>)
   (<info> ^predicate.handle <pred-handle>)
   (<parg> ^arg-type predicate
           ^id <pred-slot>)
-->
   (<info> ^matches <pred-slot>)
}

sp {learn-goal-definition*elaborate*predicate-arg-info
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^predicate <pred>)
   (<pred> ^{ <arg-num> << 1 2 >> } <arg>)
-->
   (<info> ^arg-info <arg-info>)
   (<arg-info> ^argument <arg>
               ^num <arg-num>)
}

sp {learn-goal-definition*elaborate*predicate-arg-info*match*procedural-arg*simple
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.arg-info <arg-info>
              ^action-context.<arg-name>.id <arg>
              ^action-id.procedural <proc>)
   (<arg-info> ^argument <arg>)
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type { <arg-type> << object concept >> } ^id <item-slot>)
-->
   (<arg-info> ^matches <item-slot>)
}

sp {learn-goal-definition*elaborate*predicate-arg-info*match*procedural-arg*predicate
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.arg-info <arg-info>
              ^action-context.<arg-name>.{2} <arg>
              ^action-id.procedural <proc>)
   (<arg-info> ^argument <arg>)
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type predicate
           ^2 <obj-slot>)
-->
   (<arg-info> ^matches <obj-slot>)
}

