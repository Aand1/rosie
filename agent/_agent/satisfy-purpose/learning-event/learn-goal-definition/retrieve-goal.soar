sp {learn-goal-definition*elaborate*smem-query-cue*action
   (state <s> ^name learn-goal-definition
              ^action-context.action-handle <action-handle>)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^handle <action-handle>)
}

sp {learn-goal-definition*elaborate*action-id*smem-query-cue
   (state <s> ^name learn-goal-definition
              ^action-context.action-handle <action-handle>
              ^retrieved-lti-cue <ret>)
  (<ret> ^cue.handle <action-handle>
          ^retrieved-lti <id>)
-->
   (<s> ^action-id <id>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*goal
   (state <s> ^name learn-goal-definition
              ^action-id.goal <goal>
             -^created-lti <goal>)
-->
   (<s> ^retrieve-lti <goal>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*goal*predicate
   (state <s> ^name learn-goal-definition
              ^action-id.goal.predicate <pred>
             -^created-lti <pred>)
-->
   (<s> ^retrieve-lti <pred>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*procedural
   (state <s> ^name learn-goal-definition
              ^action-id.procedural <proc>)
-->
   (<s> ^retrieve-lti <proc>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*procedural*argument
   (state <s> ^name learn-goal-definition
              ^action-id.procedural.{ << arg1 arg2 >> } <arg>)
-->
   (<s> ^retrieve-lti <arg>)
}

sp {learn-goal-definition*elaborate*smem-query-cue*predicate
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.predicate.handle <pred-handle>)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^handle <pred-handle>)
}

sp {learn-goal-definition*elaborate*predicate-id*smem-query-cue*retrieved
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info.predicate <pred>
              ^retrieved-lti-cue <ret>)
   (<pred> ^handle <pred-handle>)
  (<ret> ^cue.handle <pred-handle>
          ^retrieved-lti <id>)
-->
   (<pred> ^predicate-id <id>)
}