sp {learn-goal-definition*elaborate*smem-query-cue*action
   (state <s> ^name learn-goal-definition
              ^action-context.action-name <name>)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^name <name>)
}

sp {learn-goal-definition*elaborate*action-id*smem-query-cue
   (state <s> ^name learn-goal-definition
              ^action-context.action-name <action-name>
              ^retrieved-lti-cue <ret>)
   (<ret> ^cue.name <action-name>
          ^retrieved-lti <id>)
-->
   (<s> ^action-id <id>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*goal
   (state <s> ^name learn-goal-definition
              ^action-id.goal <goal>
             -^created-lti <goal>)
-->
   (<s> ^retrieve-lti <goal>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*goal*predicate
   (state <s> ^name learn-goal-definition
              ^action-id.goal.predicate <pred>
             -^created-lti <pred>)
-->
   (<s> ^retrieve-lti <pred>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*procedural
   (state <s> ^name learn-goal-definition
              ^action-id.procedural <proc>)
-->
   (<s> ^retrieve-lti <proc>)
}

sp {learn-goal-definition*elaborate*retrieve-lti*procedural*argument
   (state <s> ^name learn-goal-definition
              ^action-id.procedural.{ << arg1 arg2 >> } <arg>)
-->
   (<s> ^retrieve-lti <arg>)
}