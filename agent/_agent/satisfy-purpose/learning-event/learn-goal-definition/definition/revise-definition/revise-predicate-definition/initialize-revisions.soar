#### elaborate relax filters ################################################
sp {revise-predicate-definition*elaborate*relax-filters*relations
   (state <s> ^name revise-predicate-definition
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^partial-grounds <pg>
	      ^top-state.found-prep <fp>
	      ^operator.name initialize-revisions)
   (<fp> ^type <prep-name>)
   (<def> ^relation <rel-node>)
   (<rel-node> ^default <rel-def>)
   (<rel-def> ^value.prep-name <prep-name>)
   (<gp> ^relation <relp>)
   (<relp> ^type <prep-name>)
   (<pg> -^relation <any>) 
   -{(<s> ^generalizable <gen>)
     (<gen> ^wmem-object <relp>)}
-->
   (<s> ^relax-filters <rxf>)
   (<rxf> ^type relation-node
   	  ^default-value-node <rel-def>
	  ^grounded-predicate <gp>
	  ^_node <rel-node>)
}

sp {revise-predicate-definition*elaborate*relax-filters*state
   (state <s> ^name revise-predicate-definition
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^partial-grounds <pg>
	      ^operator.name initialize-revisions)
   (<def> ^state <st-node>)
   (<st-node> ^default <st-def>)
   (<st-def> ^value <state-label>)
   (<gp> ^state <state-label>)
   (<pg> -^state <any>)
   -{(<s> ^generalizable <gen>)
     (<gen> ^wmem-object <state-label>)}
-->
   (<s> ^relax-filters <rxf>)
   (<rxf> ^type state-node
   	  ^default-value-node <st-def>
	  ^grounded-predicate <gp>
	  ^_node <st-node>)
}

sp {revise-predicate-definition*elaborate*relax-filters*p1*p2
   (state <s> ^name revise-predicate-definition
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>	
	      ^grounded-action <ga>
	      ^partial-grounds <pg>
	      ^operator.name initialize-revisions)
   (<def> ^{<arg> << p1 p2 >>} <pnode>)
   (<pnode> ^default <p-def>)
   (<gp> ^<arg> <gobj>)
   (<pg> -^<arg> <any>)
   -{(<s> ^generalizable <gen>)
     (<gen> ^wmem-object <gobj>)}
 -->
   (<s> ^relax-filters <rxf>)
   (<rxf> ^type p1-p2-node
   	  ^default-value-node <p-def>
	  ^goal-argument <arg>
	  ^grounded-predicate <gp>
	  ^grounded-action <ga>
	  ^_node <pnode>)
}


sp {revise-predicate-definition*elaborate*relax-filters*non-existant*state
   (state <s> ^name revise-predicate-definition
   	      ^grounded-predicate non-existant
	      ^predicate-definition <gid>
	      ^grounded-action <ga>
	      ^operator.name initialize-revisions)
   (<gid> ^state <st-node>
   	  ^p1 <p1-node>)
   (<st-node> ^default <sdef>)
   (<sdef> ^value non-existant)
-->
   (<s> ^relax-filters <rxf>)
   (<rxf> ^type state-node
   	  ^default-value-node <sdef>
	  ^goal-argument state
	  ^grounded-predicate non-existant
	  ^grounded-action <ga>
	  ^_node <st-node>)

}

### elaborate merge nodes ####################################################
sp {revise-predicate-definition*elaborate*merge-nodes*level1
   (state <s> ^name revise-predicate-definition
   	      ^procedural-definition <pid>
	      ^predicate-definition <gid>
	      ^generalizable <gen>
	      ^operator.name initialize-revisions)
   (<gen> ^procedural-argument <parg>
   	  ^goal-argument <garg>)
   (<pid> ^<parg> <node>)
   (<node> -^argument)
   (<gid> ^<garg> {<node2> <> <node>})
-->
   (<s> ^merge-nodes <mn>)
   (<mn> ^node1 <node>
   	 ^node2 <node2>)
}

sp {revise-predicate-definition*elaborate*merge-nodes*level2
   (state <s> ^name revise-predicate-definition
   	      ^procedural-definition <pid>
	      ^predicate-definition <gid>
	      ^generalizable <gen>
	      ^operator.name initialize-revisions)
   (<gen> ^procedural-argument <parg>
   	  ^goal-argument <garg>
	  ^type <type>)
   (<pid> ^<parg>.<type> <node>)
   (<gid> ^<garg> {<node2> <> <node>})
-->
   (<s> ^merge-nodes <mn>)
   (<mn> ^node1 <node>
   	 ^node2 <node2>)
}


############# elaborate add defaults #############################
sp {revise-predicate-definition*elaborate*add-default*nodes*non-existant*state
   (state <s> ^name revise-predicate-definition
   	      ^grounded-predicate non-existant
	      ^predicate-definition <gid>
	      ^grounded-action <ga>
	      ^operator.name initialize-revisions)
   (<gid> ^state <st-node>
   	  ^p1 <p1-node>)
-->
   (<s> ^add-default <adf1> <adf2>)
   (<adf2> ^default-value-node <st-node>
   	   ^value non-existant
	   ^grounded-action <ga>
	   ^goal-argument state
	   ^_node <st-node>)
}

sp {revise-predicate-definition*elaborate*add-default*nodes*non-existant*relation
   (state <s> ^name revise-predicate-definition
   	      ^grounded-predicate non-existant
	      ^predicate-definition <gid>
	      ^grounded-action <ga>
	      ^operator.name initialize-revisions)
   (<gid> ^relation <st-node>)
 -->
   (<s> ^add-default <adf2>)
   (<adf2> ^default-value-node <st-node>
   	   ^value non-existant
	   ^grounded-action <ga>
	   ^goal-argument relation
	   ^_node <st-node>)
}
   
sp {revise-predicate-definition*elaborate*add-default*add-new-default-relation
   (state <s> ^name revise-predicate-definition
   	      ^predicate-definition <def>
   	      ^grounded-predicate <gp>
	      ^partial-grounds <pg>
	      ^operator.name initialize-revisions)
   (<def> ^relation <rel-node>)
   (<rel-node> ^default <rel-def>)
   (<rel-def> ^value.prep-name <prep-name>)
   (<gp> ^relation <relp>)
   (<relp> ^type {<prep-name2> <> <prep-name>})
   (<pg> -^relation <any>)
  -{(<s> ^generalizable <gen>)
     (<gen> ^wmem-object <relp>)}
-->
   (<s> ^add-default <adf>)
   (<adf> ^default-value-node <rel-node>
   	  ^value <relp>
	  ^goal-argument relation
	  ^grounded-predicate <gp>
	  ^_node <rel-node>)
}

sp {revise-predicate-definition*elaborate*add-default*p1*p2
   (state <s> ^name revise-predicate-definition
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^grounded-action <ga>
	      ^operator.name initialize-revisions)
   (<def> ^{<arg> << p1 p2 >>} <p1-p2-node>)
   (<gp> ^<arg> <obj>)
  -{(<s> ^partial-grounds <pg>)
    (<pg> ^<arg>)}
  -{(<s> ^generalizable <gen>)
     (<gen> ^wmem-object <obj>)}
-->
   (<s> ^add-default <adf>)
   (<adf> ^default-value-node <p1-p2-node>
          ^value <obj>
	  ^goal-argument <arg>
	  ^grounded-predicate <gp>
	  ^_node <p1-p2-node>
	  ^grounded-action <ga>)
}
   
sp {revise-predicate-definition*elaborate*add-default*add-relation
   (state <s> ^name revise-predicate-definition
   	      ^predicate-definition <def>
	      ^grounded-predicate <gp>
	      ^grounded-action <ga>
	      ^operator.name initialize-revisions)
   (<def> ^relation <rel-node>)
   (<gp> ^relation <relp>)
   -{(<s> ^partial-grounds <pg>)
     (<pg> ^relation <relp>)}
  -{(<s> ^generalizable <gen>)
     (<gen> ^wmem-object <relp>)}
-->
    (<s> ^add-default <adf>)
    (<adf> ^default-value-node <rel-node>
    	   ^value <relp>
	   ^goal-argument relation
	   ^grounded-predicate <gp>
	   ^_node <rel-node>)
}

sp {retract*initialize-revisions
   (state <s> ^name initialize-revisions
   	      ^superstate <ss> )
   (<ss> ^operator <sop>)
-->
   (<ss> ^operator <sop> -)
}
   