sp {revise-definition*copy*goal-definition
   (state <s> ^name revise-definition
   	      ^superstate.operator <sop>)
   (<sop> ^purpose.details.goal-definition <gid>)
--> 
   (<s> ^goal-definition <gid>)
}

sp {revise-definition*elaborate*to-be-revised*predicates
   (state <s> ^name revise-definition
   	      ^superstate.operator.purpose.details <p>)
   (<p> ^predicate-grounding-failure <pred>)
-->
   (<s> ^predicate-grounding-failure <pred>)
}

sp {revise-definition*elaborate*map-id
   (state <s> ^name revise-definition 
   	      ^top-state.interaction.stack.segment.next.purpose.parameters.indexable.result <ia>)
   (<ia> ^_metadata <met>)
   (<met> ^map-id <map>)
-->
   (<s> ^map-id <map>
	^grounded-action <ia>)
}

sp {revise-definiton*elaborate*indexable
   (state <s> ^name revise-definition
   	      ^superstate.operator.purpose.parameters.indexable <index>)
-->
   (<s> ^indexable <index>)
}


sp {revise-definition*elaborate*new-goal
   (state <s> ^name revise-definition
   	      ^superstate.operator <sop>)
   (<sop> ^name revise-definition
   	  ^purpose.parameters.indexable.result <goal>)
-->
   (<s> ^grounded-goal <goal>)
}

sp {revise-definition*elaborate*revise-predicate-definition*non-existant
   (state <s> ^name revise-definition
   	      ^predicate-grounding-failure <pgf>)
   -{(<s> ^revise-predicate-definition <rpd>)
   (<rpd> ^failure-details <pgf>)}
-->
   (<s> ^revise-predicate-definition-non-existant <rpred>)
   (<rpred> ^grounded-predicate non-existant
   	    ^failure-details <pgf>
	    ^wmem-smem-pairs <pair>)
}


sp {revise-definition*elaborate*smem-wmem
   (state <s> ^name revise-definition
   	      ^revise-predicate-definition.wmem-smem-pairs <pairs>)
   (<rpred> ^grounded-predicate <gp>
   	    ^failure-details.predicate-definition <def>)
   (<def> ^<arg> <sobj>)
   (<gp> ^<arg> <obj>)
-->
   (<pairs> ^<obj> <sobj>)
}

sp {revise-predicate-definition*elaborate*common-with-actions*level1
   (state <s> ^name revise-definition
   	      ^grounded-action <ga>
	      ^revise-predicate-definition <rpd>)
   (<rpd> ^grounded-predicate <gp>
   	  ^wmem-smem-pairs <pair>)
   (<gp> ^<arg> <obj>)
   (<ga> ^<aarg> <obj>
   	 ^_metadata.map-id.procedural.<aarg> <sobj>)
-->
   (<pair> ^<obj> <sobj>)
}

sp {revise-predicate-definition*elaborate*common-with-actions*level2
   (state <s> ^name revise-definition
   	      ^grounded-action <ga>
	      ^revise-predicate-definition <rpd>)
   (<rpd> ^grounded-predicate <gp>
   	  ^wmem-smem-pairs <pair>)
   (<gp> ^<arg> <obj>)
   (<ga> ^<aarg>.{<valarg> << argument relation >>} <obj>
   	 ^_metadata.map-id.procedural.<aarg>.<valarg> <sobj>)
-->
   (<pair> ^<obj> <sobj>)
}




