sp {train-on-object*propose*send-training-example
   (state <s> ^name train-on-object
              ^example-object <obj>
             -^trained
              ^sense.property.type <> measurable )
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-training-example)
}

sp {train-on-object*apply*send-training-example*mark*trained
   (state <s> ^name train-on-object
              ^operator.name send-training-example
              ^ol-command.status complete)
-->
   (<s> ^trained true)
}

sp {train-on-object*send-training-example*propose*wait
   (state <s> ^name send-training-example
             -^operator.name wait)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name wait)
}

# Send training labels via the output link
sp {train-on-object*apply*send-training-example*send*example
   (state <s> ^name train-on-object
              ^operator.name send-training-example
              ^io.output-link <ol>
              ^example-object.id <id>
              ^sense <sense>)
   (<sense> ^label <label>
            ^property.name <prop-name>)
-->
   (<ol> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^property-name <prop-name>)
   (<s> ^ol-command <stl>)
}