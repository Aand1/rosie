sp {put-down*elaborate*above-prep
   (state <s> ^name op_put-down-object-location
              ^io
              ^topstate.prepositions.prep <above>)
   (<above> ^name above)
-->
   (<s> ^above-prep <above>)
}

sp {put-down*elaborate*target-bin*to*bin
   (state <s> ^name op_put-down-object-location
              ^target-location <bin>)
   (<bin> ^category bin
          ^id <bid>)
-->
   (<s> ^target-bin <bin>)
}

sp {put-down*elaborate*target-bin*to*object
   (state <s> ^name op_put-down-object-location
              ^target-location <obj>
              ^topstate.object <bin>
              ^above-prep.pair <p>)
   (<obj> ^category <> bin
          ^id <oid>)
   (<bin> ^category bin
          ^id <bid>)
   (<p> ^a <oid> ^b <bid>)
-->
   (<s> ^target-bin <bin>)
}

sp {put-down*propose*open-bin
   (state <s> ^name op_put-down-object-location
              ^target-bin <bin>)
   (<bin> ^open false)
-->
   (<s> ^operator <o> + >)
   (<o> ^name open-bin
        ^bin <bin>)
}

sp {put-down*prefer*open-bin*over*issue-put-down*find-projection
   (state <s> ^name op_put-down-object-location
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name open-bin)
   (<o2> ^name << issue-put-down find-projection >>)
-->
   (<s> ^operator <o1> > <o2>)
}