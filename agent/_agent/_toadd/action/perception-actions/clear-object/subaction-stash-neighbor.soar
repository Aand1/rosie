sp {clear-object*propose*select-subaction-stash-neighbor
   (state <s> ^name clear-object
              ^object-in-bin <obj>
             -^higher-object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name select-subaction-stash-neighbor
        ^action-type custom
        ^object <obj>)
}

sp {clear-object*apply*select-subaction-stash-neighbor
   (state <s> ^name clear-object
              ^operator <o>
              ^current-action <a>)
   (<o> ^name select-subaction-stash-neighbor
        ^object <obj>)
-->
   (<a> ^subaction <sub>)
   (<sub> ^name stash-object
          ^object <obj>)
}

sp {clear-object*prefer*select-subaction-stash-neighbor*higher
   (state <s> ^name clear-object
              ^operator <o1> +
              ^operator <o2> +
              ^higher-than-prep.pair <p>)
   (<o1> ^name select-subaction-stash-neighbor
         ^object.id <obj1>)
   (<o2> ^name select-subaction-stash-neighbor
         ^object.id <obj2>)
   (<p> ^a <obj1> ^b <obj2>)
-->
   (<s> ^operator <o1> > <o2>)
}