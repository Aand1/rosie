sp {topstate*elaborate*action-stack
   (state <s> ^topstate <s>
              ^interaction.stack.segment.action-stack <stack>)
-->
   (<s> ^action-stack <stack>)
}

## Topstate: propose action where there is a new action on the stack
sp {topstate*propose*action*perform*action
   (state <s> ^topstate <s>
              ^action-stack <stack>)
   (<stack> ^action <action>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name action
        ^action-stack <stack>
        ^subaction <action>)
}

sp {topstate*apply*action*remove*completed*action
   (state <s> ^topstate <s>
              ^operator <o>
              ^action-stack <stack>)
   (<stack> ^action <action>)
   (<o> ^name action
        ^subaction <action>)
   (<action> ^complete true)
-->
   (<stack> ^action <action> -)
}

# action problemspace, elaborate action-stack and current-action
sp {action*elaborate*current-action*from*purpose
   (state <s> ^name action
              ^superstate.operator.purpose.parameters.verb <v>)
-->
   (<s> ^current-action <v>)
}

sp {action*elaborate*name*current-action
   (state <s> ^name action
              ^current-action.name <name>)
-->
   (<s> ^name <name>)
}

sp {action*elaborate*action-stack
   (state <s> ^problemspace action
              ^superstate.action-stack <stack>)
-->
   (<s> ^action-stack <stack>)
}

sp {action*elaborate*current-action*subaction
   (state <s> ^problemspace action
              ^superstate.operator.subaction <sub>)
-->
   (<s> ^current-action <sub>)
}

# when the top level action is complete, mark satisfied
sp {action*propose*complete-action
   (state <s> ^name action
              ^current-action.complete true
              ^superstate.operator.purpose.satisfaction <sat>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete-action
        ^satisfaction <sat>)
}

sp {action*apply*complete-action
   (state <s> ^name action
              ^operator <o>
              ^topstate.interaction.status <status>)
   (<o> ^name complete-action
        ^satisfaction <sat>)
-->
   (<status> ^performed <sat>)
}

# when there is a subaction, execute it
sp {action*propose*execute-subaction
   (state <s> ^problemspace action
              ^current-action.subaction <sub>)
   (<sub> ^name <name>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name <name>
        ^subaction <sub>)
}

sp {action*problemspace*elaborate*achieve*subgoal*parameters
   (state <s> ^problemspace.name action
              ^operator <o> +)
   (<o> ^name <name>
        ^subaction.parameters.<att> <val>)
-->
   (<o> ^<att> <val>)
}

sp {action*apply*execute-subaction*complete*remove
   (state <s> ^problemspace action
              ^operator <o>
              ^current-action <action>)
   (<o> ^name <name>
        ^subaction <sub>)
   (<sub> ^name <name>
          ^complete true)
   (<action> ^subaction <sub>)
-->
   (<action> ^subaction <sub> -)
}

# prefer subactions over primitive
sp {action*prefer*subaction*over*primitive
   (state <s> ^problemspace action
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^action-type custom)
   (<o2> ^action-type primitive)
-->
   (<s> ^operator <o1> > <o2>)
}
   
