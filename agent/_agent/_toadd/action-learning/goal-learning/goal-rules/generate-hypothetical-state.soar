### copy objects
sp {copy*objects*goal-rules
   (state <s> ^name goal-rules
   	      ^superstate.object <obj>
	      ^duplicates <dup>)
-->
   (<s> ^object <nobj>)
   (<dup> ^<obj> <nobj>)
}


## hypothesize the desired state
sp {copy*relation-predicate
   (state <s> ^name goal-rules
   	      ^indexed-action.desired <des>
	      ^environment <env>
	      ^topstate.found-prep <rel>)
   (<des> ^track-relation <pred>)
   (<pred> ^relation.smem-rep <pred-rel>
   	   ^p1 <obj1>
	   ^p2 <obj2>)
   (<rel> ^smem-rep <pred-rel>)
-->
   (<env> ^predicate <nprep>)
   (<nprep> ^a <obj1>
   	    ^b <obj2>
	    ^relation <rel>)
}

### copy state predicate
sp {copy*state-predicate
   (state <s> ^name goal-rules
   	      ^indexed-action.desired <des>
	      ^environment <env>)
   (<des> ^achieve-state <dtr>)
   (<dtr> ^p1 <obj>
   	  ^state <state>)
-->
   (<env> ^<state> <obj>)
}


### copy indexed action
sp {copy*indexed-action*generate-hypothetical-state*objects*one-level
   (state <s> ^name goal-rules
   	      ^topstate-indexed-action <iop>
	      ^duplicates <d>
	      ^indexed-action <ia>)
   (<d> ^<sobj> <obj>)
   (<iop> ^<arg> <sobj>)
-->
   (<ia> ^<arg> <obj>)
}

sp {copy*indexed-action*generate-hypothetical-state*objects*two-level
   (state <s> ^name goal-rules
   	      ^topstate-indexed-action <iop>
	      ^duplicates <d>
	      ^indexed-action <ia>)
   (<d> ^<sobj> <obj>)
   (<iop> ^<arg>.argument <sobj>)
-->
   (<ia> ^<arg>.argument <obj>)
}

sp {copy*indexed-action*generate-hypothetical-state*relation
   (state <s> ^name goal-rules
   	      ^topstate-indexed-action <iop>
	      ^duplicates <d>
	      ^indexed-action.<arg> <iarg>
	      ^topstate.found-prep <rel>)
   (<d> ^<sobj> <obj>)
   (<iop> ^<arg> <iop-val>)
   (<iop-val> ^argument <sobj>
   	  ^relation.smem-rep <rel-id>)
   (<iarg> ^argument <obj>)
   (<rel> ^smem-rep <rel-id>)
-->
   (<iarg> ^relation <rel>)
}
   
sp {copy*indexed-action*generate-hypothetical-state*other
   (state <s> ^name goal-rules
   	      ^topstate-indexed-action <iop>
	      ^operator.name generate-hypothetical-state
	      ^indexed-action <ia>)
   (<iop> ^{<< _metadata action-type name >> <arg>} <val>)
-->
   (<ia> ^<arg> <val>)
}