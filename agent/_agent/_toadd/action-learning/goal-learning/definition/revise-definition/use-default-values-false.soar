############## if use-default-values parameter is false; elaborate desired

sp {revise-definition*elaborate*desired*use-default-values*false
   (state <s> ^name revise-definition
   	      ^grounded-action <ga>
	      ^top-state.agent-params.use-default-values false)
-->
   (<s> ^desired-goal <d>)
   (<d> ^list <l>) 
}

sp {revise-definition*propose*initialize-list
   (state <s> ^name revise-definition
 	      ^top-state.agent-params.use-default-values false
	      -^desired-goal.list.next)
-->
   (<s> ^operator <op> +)
   (<op> ^name initialize-list)
}

sp {revise-definition*apply*initialize-list
   (state <s> ^name revise-definition
 	      ^top-state.agent-params.use-default-values false
	      ^operator.name initialize-list
	      ^desired-goal.list <l>)
-->
   (<l> ^next null)
}

sp {revise-definition*propose*assign-predicates
   (state <s> ^name revise-definition
   	      ^top-state.agent-params.use-default-values false
	      ^revise-predicate-definition <pred>
	      -^assigned-predicate <pred>
	      ^desired-goal.list.next)
-->
   (<s> ^operator <op> +)
   (<op> ^name assign-goal-predicates
   	 ^details <pred>)
}

sp {revise-definition*propose*assign-predicates*preference
   (state <s> ^name revise-definition
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^name assign-goal-predicates
	  ^details.failure-details.predicate-definition <id1>)
   (<op2> ^name assign-goal-predicates
   	  ^details.failure-details.predicate-definition {<id2> > <id1>})
-->
   (<s> ^operator <op1> > <op2>)
}

sp {revise-definition*propose*assign-predicates*preference*relation
   (state <s> ^name revise-definition
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^name assign-goal-predicates
	  ^details.grounded-predicate.relation <id1>)
   (<op2> ^name assign-goal-predicates
   	  ^details.grounded-predicate.relation {<id2> > <id1>})
-->
   (<s> ^operator <op1> > <op2>)
}

sp {revise-definition*apply*assign-predicates*preference*relation-predicate
   (state <s> ^name revise-definition
   	      ^operator <op>
	      ^desired-goal <d>)
   (<op> ^name assign-goal-predicates
   	 ^details <det>)
   (<det> ^failure-details.predicate-definition <def>
   	  ^grounded-predicate <gp>)
   (<d> ^list <l>)
   (<l> ^next <next>)
   (<gp> ^relation)
-->
   (<d> ^track-relation <gp>)
   (<gp> ^_instantiation-of <def>
	^next <next>)
   (<l> ^next <next> -
   	^next <gp>)
   (<s> ^assigned-predicate <det>)
}

sp {revise-definition*apply*assign-predicates*preference*relation-predicate*none
   (state <s> ^name revise-definition
   	      ^operator <op>
	      ^desired-goal <d>)
   (<op> ^name assign-goal-predicates
   	 ^details <det>)
   (<det> ^grounded-predicate <gp>)
   (<d> ^list <l>)
   (<l> ^next <next>)
   (<gp> ^relation.predicate <relpred>)
-->
   (<d> ^track-relation <gp>)
   (<gp> ^next <next>
   	 ^_instantiation-of <relpred>)
   (<l> ^next <next> -
   	^next <gp>)
   (<s> ^assigned-predicate <det>)
}

sp {revise-definition*apply*assign-predicates*preference*state-predicate
   (state <s> ^name revise-definition
   	      ^operator <op>
	      ^desired-goal <d>)
   (<op> ^name assign-goal-predicates
   	 ^details <det>)
   (<det> ^failure-details.predicate-definition <def>
   	  ^grounded-predicate <gp>)
   (<d> ^list <l>)
   (<l> ^next <next>)
   (<gp> ^state)
-->
   (<d> ^achieve-state <gp>)
   (<gp> ^next <next>
   	 ^_instantiation-of <def>)
   (<l> ^next <next> -
   	^next <gp>)
   (<s> ^assigned-predicate <det>)
}

sp {revise-definition*apply*assign-predicates*preference*state-predicate*none
   (state <s> ^name revise-definition
   	      ^operator <op>
	      ^desired-goal <d>)
   (<op> ^name assign-goal-predicates
   	 ^details <det>)
   (<det> ^grounded-predicate <gp>)
   (<d> ^list <l>)
   (<l> ^next <next>)
   (<gp> ^state)
-->
   (<d> ^achieve-state <gp>)
   (<gp> ^next <next>)
   (<l> ^next <next> -
   	^next <gp>)
   (<s> ^assigned-predicate <det>)
}

sp {revise-definition*apply*complete-revise-definition
   (state <s> ^name revise-definition
   	      ^top-state.agent-params.use-default-values false
	      ^operator.name complete-revise-definition
	      ^grounded-action <ga>
	      ^desired-goal <d>
	      ^superstate.superstate <sss>)
-->
   (<ga> ^desired <d>)
   (<sss> ^learned rules)
}
