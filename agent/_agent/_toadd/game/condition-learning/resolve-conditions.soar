sp {resolve-conditions*propose*expand-compositions
   (state <s> ^name resolve-conditions
             ^expanded true
              ^nlp-set <nlp>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name expand-compositions
        ^nlp-set <nlp>)
}
sp {all-states*elaborate*linkstate
   (state <s> ^superstate <ts>)
   (<ts> ^linkstate <ss>)
-->
   (<s> ^linkstate <ss>)
}

sp {resolve-conditions*elaborate*nlp-set
   (state <s> ^name resolve-conditions
             ^linkstate <ts>)
   (<ts> ^<type>.condition <c> ^gtype <type>)
-->
   (<s> ^condition <c>)
}
sp {resolve-conditions*elaborate*parameter-set
   (state <s> ^name resolve-conditions
             ^linkstate <ts>)
   (<ts> ^<type>.parameter-set <ps>
         ^gtype <type>
         ^<type>.potential-args <po>)
-->
   (<s> ^parameter-set <ps>
        ^potential-args <po>)
}


#try to evaluate conditions whose arguments have been determined
sp {resolve-conditions*propose*eval-condition
   (state <s> ^name resolve-conditions
            #  ^top-state.game <game>
              ^condition <co>)
  #(<co> -^calculated true)#-^result.<s2>.<i2>.<elem> ^parameter.result.link.link.<s2>.<i2>.<elem> holder)
-->
   (<s> ^operator <o> + =)
   (<o> ^name eval-condition
        ^condition <co>)
}
sp {resolve-conditions*prefer*eval-condition*childresults
   (state <s> ^name resolve-conditions
              ^operator <o> +)
   (<o> ^name eval-condition
        ^condition <co>)
   (<co> ^args.<< 1 2 3 4 5 6 7 8 9 10 >> <c1>)
   (<c1> -^result.<s2>.<i2>.<elem> ^parameter.result.link.link.<s2>.<i2>.<elem> holder )
-->
   (<s> ^operator <o> <)
}
sp {resolve-conditions*prefer*eval-condition*reject-complete
   (state <s> ^name resolve-conditions
              ^operator <o> +)
   (<o> ^name eval-condition
        ^condition <co>)
   (<co> ^result.<s2>.<i2>.<elem> ^parameter.result.link.link.<s2>.<i2>.<elem> holder )
-->
   (<s> ^operator <o> -)
}

sp {resolve-conditions*propose*eval-parameters
   (state <s> ^name resolve-conditions
              ^potential-args <po>
              ^parameter-set <ps>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name eval-parameters
        ^potential-args <po>
        ^parameter-set <ps>)
}

sp {resolve-conditions*prefer*eval-condition*over*eval-parameters
   (state <s> ^name resolve-conditions
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name eval-condition)
   (<o2> ^name eval-parameters)
-->
   (<s> ^operator <o1> > <o2>)
}
