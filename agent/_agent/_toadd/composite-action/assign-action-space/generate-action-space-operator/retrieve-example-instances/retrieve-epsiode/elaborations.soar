sp {retrieve-episode*elaborate*cue
   (state <s> ^name retrieve-episode
   	      ^superstate.operator <op>
	      ^epmem.command <ecomm>)
   (<op> ^cue <cue>)
-->
   (<ecomm> ^query <cue>)
}

sp {retrieve-episode*elaborate*ncue
   (state <s> ^name retrieve-episode
   	      ^superstate.operator <op>
	      ^epmem.command <ecomm>)
   (<op> ^neg-cue <ncue>)
-->
   (<ecomm> ^neg-query <ncue>)
}

sp {retrieve-episode*elaborate*before
   (state <s> ^name retrieve-episode
   	      ^superstate.operator.before <b>
	      ^epmem.command <ecomm>)
-->
   (<ecomm> ^before <b>)
}

sp {retrieve-episode*write*superstate
   (state <s> ^name retrieve-episode
   	      ^superstate <ss>
	      ^epmem.result <eres>)
   (<eres> ^graph-match 1
   	   ^retrieved <ep>
	   ^memory-id <new-id>)
   (<ss> ^operator <sop>)
-->
   (<s> ^retrieved-episode <re>)
   (<re> ^episode <ep>
   	 ^memory-id <new-id>)
}

sp {retrieve-episode*write*superstate*failure
   (state <s> ^name retrieve-episode
   	      ^superstate <ss>
	      ^epmem.result <eres>)
   (<eres> ^graph-match <> 1
   	   ^retrieved <ep>
	   ^memory-id <new-id>)
-->
   (<ss> ^retrieved all)
}

sp {retrieve-example-instances*apply*retrieve-episode*first
   (state <s> ^name retrieve-episode
   	      ^superstate <ss>
	      ^retrieved-episode <re>)
   (<op> ^name retrieve-episode
   	 ^cue <cue>
	 ^neg-cue <ncue>
	 ^type retrieval)
   (<re> ^episode.interaction.stack.segment.purpose.parameters.indexable.result <action>
   	 ^memory-id <id>)
   (<ss> ^operator <op>)
-->
   (<ss> ^retrieved-action <action>
   	^before-1 <id>)
}

sp {retrieve-example-instances*apply*retrieve-episode*before-2
   (state <s> ^name retrieve-episode
   	      ^superstate <ss>
	      ^retrieved-episode <re>)
   (<op> ^name retrieve-episode
   	 ^cue <cue>
	 ^neg-cue <ncue>
	 ^type retrieval)
   (<re> ^episode <ep>
   	 ^memory-id <nid>)
   (<ss> ^before-2 <id>
          ^operator <op>)
-->
   (<ss> ^before-2 <id> -)
}

sp {retrieve-example-instances*apply*retrieve-episode*before-1
   (state <s> ^name retrieve-episode
   	      ^superstate <ss>
	      ^retrieved-episode <re>)
   (<op> ^name retrieve-episode
   	 ^cue <cue>
	 ^type walk)
   (<re> ^memory-id <nid>)
   (<ss> ^operator <op>
	 ^before-1 <id>)
-->
   (<ss> ^before-2 <nid>
   	 ^before-1 <id> -)
}

