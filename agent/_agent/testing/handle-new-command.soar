### create the initial command
sp {topstate*apply*init-agent*create*current-command
   (state <s> ^topstate <s>
              ^operator.name init-agent)
-->
   (<s> ^current-command <cmd>)
   (<cmd> ^type none
          ^pushed true)
}

sp {handle-new-command*elaborations
   (state <s> ^name handle-new-command)
-->
   (<s> ^allow-wait true
        ^attend-to-all-scene-changes true)
}


### If there's a new command on the input-link, handle it
sp {topstate*propose*handle-new-command
   (state <s> ^topstate <s>
              ^current-command <cur-cmd>
              ^io.input-link.commands.command { <new-cmd> <> <cur-cmd> })
-->
   (<s> ^operator <o> +)
   (<o> ^name handle-new-command
        ^new-command <new-cmd>)
}

sp {topstate*apply*handle-new-command*stopped*change*command
   (state <s> ^topstate <s>
              ^operator <o>
              ^world.robot.moving-state stopped
              ^current-command <cur-cmd>)
   (<o> ^name handle-new-command
        ^new-command <new-cmd>)
-->
   (<s> ^current-command <cur-cmd> -
        ^current-command <new-cmd>)
}

sp {topstate*apply*handle-new-command*remove*current-action
   (state <s> ^topstate <s>
              ^operator.name handle-new-command
              ^current-action <cur-action>
              ^interaction.changes <chgs>)
-->
   (<s> ^current-action <cur-action> -)
   (<chgs> ^terminate-segment true)
}

sp {topstate*apply*handle-new-command*stopped*change*command
   (state <s> ^topstate <s>
              ^operator <o>
              ^current-command <cur-cmd>)
   (<o> ^name handle-new-command
        ^new-command <new-cmd>)
-->
   (<s> ^current-command <cur-cmd> -
        ^current-command <new-cmd>)
}

## retrieve smem info about the command
sp {topstate*elaborate*smem-query-cue*current-command
   (state <s> ^topstate <s>
              ^current-command <cmd>)
   (<cmd> ^type <cmd-name>)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^name <cmd-name>)
}

sp {topstate*best*preference*testing*operators
   (state <s> ^topstate <s>
              ^operator <o> +)
   (<o> ^name << smem-query push-command handle-new-command >>)
-->
   (<s> ^operator <o> >)
}

sp {topstate*current-command*copy*query*results
   :o-support
   (state <s> ^topstate <s>
              ^smem-query-cue <cue>
              ^retrieved-lti-cue <ret>
              ^current-command <cmd>)
   (<ret> ^cue <cue>
          ^retrieved-lti <lti>)
   (<lti> ^name <cmd-name>)
   (<cmd> ^type <cmd-name>)
-->
   (<cmd> ^action-id <lti>)
   (<s> ^smem-query-cue <cue> -
        ^retrieved-lti-cue <ret> -)
}

### Push a new segment to handle the command
sp {topstate*propose*push-command
   (state <s> ^topstate <s>
              ^current-command <cmd>)
   (<cmd> -^pushed
          ^action-id <id>)
-->
   (<s> ^operator <o> +) 
   (<o> ^name push-command
        ^command <cmd>)
}

sp {topstate*apply*push-command*create*action-command
   (state <s> ^topstate <s>
              ^operator <o>)
   (<o> ^name push-command
        ^command <cmd>)
-->
   (<cmd> ^action-command <action-cmd>)
}

sp {topstate*apply*push-command*copy*action-id*action-command
   (state <s> ^topstate <s>
              ^operator <o>)
   (<o> ^name push-command
        ^command <cmd>)
   (<cmd> ^action-id <id>
          ^action-command <action-cmd>)
-->
   (<action-cmd> ^id <id>)
}

sp {topstate*apply*push-command*copy*args*action-command
   (state <s> ^topstate <s>
              ^operator <o>)
   (<o> ^name push-command
        ^command <cmd>)
   (<cmd> ^{ <arg> << object-type object-count direction waypoint-id >> } <val>
          ^action-command <action-cmd>)
-->
   (<action-cmd> ^<arg> <val>)
}

sp {topstate*apply*push-command*create*segment
   (state <s> ^topstate <s>
              ^operator <o>
              ^interaction.changes <chgs>)
   (<o> ^name push-command
        ^command <cmd>)
   (<cmd> ^type <action-name>
          ^action-command <action-cmd>)
-->
   (<chgs> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type action
        ^parameters.action-command <action-cmd>
        ^satisfaction <sat>)
   (<sat> ^action-event.type <action-name>)
   (<cmd> ^pushed true)
} 

