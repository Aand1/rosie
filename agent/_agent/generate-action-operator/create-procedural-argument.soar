sp {generate-action-operator*propose*create-procedural-argument
   (state <s> ^name generate-action-operator
              ^argument-info <arg-info>
              ^action-id.procedural <proc>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type << object location concept predicate >>)
   (<proc> -^<arg-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-procedural-argument
        ^argument-info <arg-info>)
}

sp {generate-action-operator*apply*create-procedural-argument*object
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-id.procedural <proc>)
   (<o> ^name create-procedural-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type object
               ^object-status <obj-status>)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type object
           ^object-status <obj-status>
           ^id <new-obj-slot>)
   (<arg-info> ^created-procedural-argument true)
   (<s> ^to-store <proc> <parg>)
}
   
sp {generate-action-operator*apply*create-procedural-argument*location
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-id.procedural <proc>)
   (<o> ^name create-procedural-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^object-status <obj-status>
               ^arg-type location)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type location
           ^object-status <obj-status>
           ^id <new-loc-slot>)
   (<arg-info> ^created-procedural-argument true)
   (<s> ^to-store <proc> <parg>)
}

sp {generate-action-operator*apply*create-procedural-argument*concept
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-id.procedural <proc>)
   (<o> ^name create-procedural-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type concept)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type concept
           ^id <new-conc-slot>)
   (<arg-info> ^created-procedural-argument true)
   (<s> ^to-store <proc> <parg>)
}

sp {generate-action-operator*apply*create-procedural-argument*predicate
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-id.procedural <proc>)
   (<o> ^name create-procedural-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-status <status>
               ^arg-type predicate)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type predicate
           ^arg-status <status>
           ^id <new-pred-slot>
           ^2 <new-obj-slot>)
   (<arg-info> ^created-procedural-argument true)
   (<s> ^to-store <proc> <parg>)
}
