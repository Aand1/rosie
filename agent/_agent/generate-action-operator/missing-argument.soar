# If a procedural argument has ^importance required, and that argument isn't on the command,
#   Propose missing-argument and report a no-proposed-action failure

sp {generate-action-operator*propose*missing-argument
   (state <s> ^name generate-action-operator
              ^action-id.procedural.<arg-name> <parg>
             -^argument-info.arg-name <arg-name>)
   (<parg> ^importance required)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name missing-argument
        ^arg-name <arg-name>)
}

# Report failure
sp {generate-action-operator*apply*missing-argument*report*no-proposed-action
   (state <s> ^name generate-action-operator
              ^operator.name missing-argument
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure.type no-proposed-action)
}

# If debug-mode is on, print error info
sp {generate-action-operator*apply*missing-argument*debug-mode*print
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^top-state.agent-params.debug-mode on)
   (<o> ^name missing-argument
        ^arg-name <arg-name>)
-->
   (write |generate-action-operator: missing | <arg-name> (crlf))
}