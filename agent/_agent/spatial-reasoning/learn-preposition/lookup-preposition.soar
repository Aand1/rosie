
sp {learn-preposition*extract-relations*propose*lookup-preposition
   (state <s> ^name learn-preposition
             -^found-prep
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name lookup-preposition
        ^category svs
        ^type <type>)
}

sp {learn-preposition*lookup-preposition*success*copy*predicate
   (state <s> ^name learn-preposition
              ^found-prep <fprep>)
   (<fprep> ^stat old
            ^predicate <p>)
-->
   (<s> ^predicate <p>
        ^new-prep false)
}

sp {learn-preposition*lookup-preposition*failure*create*predicate
   (state <s> ^name learn-preposition
              ^found-prep.stat default
              ^prep-name <name>)
-->
   (<s> ^predicate <p>
        ^new-prep true)
   (<p> ^name <name>
        ^prep-name <name>
        ^relation <x> <y> <z>)
   (<x> ^axis x ^count 0)
   (<y> ^axis y ^count 0)
   (<z> ^axis z ^count 0)
}
