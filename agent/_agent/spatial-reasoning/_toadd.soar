# TODO: clean up extract_once filters after use?
sp {clean-up*extract_once*after*svs
   (state <s> ^operator.name pop-segment
              ^name interaction
              ^top-state.svs.command <cmd>)
   (<cmd> ^extract_once <E11>)
   (<E11> -^type closest)
-->
   (<cmd> ^extract_once <E11> -)
}

# TODO: extract smaller-than on top-state predicates
# SPECIAL-CASE: smaller-than
sp {anystate*extract-relations*smaller-than*create*filter*base*a
   (state <s> ^extract-relations.extract-info <e>)
   (<e> ^object-id <id>
        ^prep.name smaller-than
        ^base a)
-->
   (<e> ^x <filter>)
   (<filter> ^type larger_select
             # we do larger_select to find all objects b such that larger(a, b) is true
             # (that is, b is smaller than a)
             ^volume_type scale
             ^a <a>
             ^b <belief-nodes>)   
   (<belief-nodes> ^type tag_select
                   ^a.type all_nodes
                   ^tag_name object-source
                   ^tag_value belief)
   (<a> ^type node 
        ^id <id>)
}
# base = b -> find objects that a is smaller than
sp {anystate*extract-relations*smaller-than*create*filter*base*b
   (state <s> ^extract-relations.extract-info <e>)
   (<e> ^object-id <id>
        ^prep.name smaller-than
        ^base b)
-->
   (<e> ^x <filter>)
   (<filter> ^type smaller_select
             ^volume_type scale
             ^a <a>
             ^b <belief-nodes>)   
   (<belief-nodes> ^type tag_select
                   ^a.type all_nodes
                   ^tag_name object-source
                   ^tag_value belief)
   (<a> ^type node 
        ^id <id>)
}
