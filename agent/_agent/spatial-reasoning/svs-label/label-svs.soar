sp {svs-label*direct-compare
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
   			  ^topstate <sss>
#             -^topstate.track-relation.tracking-extract_once <E11>
              ^topstate.axis-filters <af>
              ^label-svs <l>)
   (<sss> ^svs.command <cmd>)
   (<sss> ^svs.spatial-scene <sc>)
   (<af> ^filters <fil>)
   (<fil> ^filter <t>
          ^axis <axis>)
   (<objs> ^type <type>
           ^obj <oa>
           ^obj <ob>)
   (<ob> ^index <i2> ^id <objb>)
   (<oa> ^index <i1> < <i2>
         ^id <obja>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>)
   (<cmd> ^extract_once <E>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<E> ^type <t> ^result.record <rec>)
   #(<rec> ^value t ^params <A>)
   (<rec> ^value <objb1> ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)   
-->
   (<l> ^result <r>)
   (<r> ^axis <axis> ^type <t>)
}

sp {svs-label*extract_once*distance
   (state <s> ^name svs-label
   			  ^topstate <sss>
              ^superstate.operator.message.fields.objects <objs>
              ^label-svs <l>)
   (<sss> ^svs.command <cmd>)
   (<sss> ^svs.spatial-scene <sc>)
   (<objs> ^type <type>
           ^obj <oa>
           ^obj <ob>)
   (<ob> ^index <i2> ^id <objb>)
   (<oa> ^index <i1> < <i2>
         ^id <obja>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<l> ^result <r>)
   (<r> ^axis <axis>)
-->
   (<cmd> ^extract_once <P11>)
   (<P11> ^a <N7> ^b <N8> ^type distance_xyz ^axis <axis>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}

sp {svs-label*attach*distance
   (state <s> ^name svs-label
   			  ^topstate <sss>
              ^label-svs <l>)
   (<sss> ^svs.command <cmd>)
   (<cmd> ^extract_once <E>)
   (<E> ^type distance_xyz ^axis <axis> ^result.record <rec>)
   (<rec> ^value <dist>)
   (<l> ^result <r>)
   (<r> ^axis <axis>)
-->
   (<r> ^dist <dist>)
}

sp {svs-label*direct-create*new*prep
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
   			  ^superstate <ss>
              ^superstate.superstate <sss>
             -^stat old
              ^stat default)
   (<objs> ^type <type>)
-->
   (<s> ^label-svs <l>)
   (<s> ^created true)
   (<s> ^predicate <p1>)
   (<p1> ^name <type> ^all-aligned false) 
}

sp {svs-label*direct-edit*old*prep
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
   			  ^superstate <ss>
              ^superstate.superstate <sss>
              ^stat old
             -^stat default)
   (<objs> ^type <type>)
-->
   (<s> ^label-svs <l>)
}

sp {svs*label*propose*lookup-preposition
   (state <s> ^name svs-label
             -^found-prep
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-preposition
        ^category svs
        ^type <type>)
}

sp {svs*label*propose*lookup-preposition-list
   (state <s> ^name svs-label
             -^prep-list
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-preposition-list
        ^category svs)
}

sp {svs*label*extract_once-filters
   (state <s> ^name svs-label
              ^topstate.filters.filter <fil>
              ^topstate.svs.command <cmd>
              ^topstate.svs.spatial-scene <sc>
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>
           ^obj <oa>
           ^obj <ob>)
   (<ob> ^index <i2> ^id <objb>)
   (<oa> ^index <i1> < <i2>
         ^id <obja>)   
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>) 
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<cmd> ^extract_once <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <fil>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}
