#######################################################
# 
# Propose: push-segment
#
########################################################

### initiate a segment in the interaction-stack by proposing a push-segment operator.
sp {topstate*propose*push-segment*new-segment-info
   (state <s> ^topstate <s>
              ^interaction.status.new-segment-info <seg>)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-segment
        ^new-segment-info <seg>)
}


#######################################################
# 
# Propose: interpret-message
#
########################################################

sp {topstate*propose*interpret-message*nl-input-link
   (state <s> ^topstate <s>
              ^nl.input-link.message <msg>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-message
        ^message <msg>)
   (interrupt)
}

sp {topstate*propose*interpret-message*input-link
   (state <s> ^topstate <s>
              ^io.input-link.message <msg>)
   (<msg> -^processed)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-message
        ^message <msg>)
   (interrupt)
}
   


#######################################################
# 
# Propose: index-parameters
#
########################################################

sp {topstate*elaborate*need-indexing
   (state <s> ^topstate <s>
              ^top-segment.purpose.parameters.indexable <i>)
   (<i> ^lexical
       -^internal)
-->
   (<s> ^need-indexing true)
}

sp {topstate*propose*index-parameters
   (state <s> ^topstate <s>
              ^need-indexing true)
-->
   (<s> ^operator <o> +)
   (<o> ^name index-parameters)
}


#######################################################
# 
# Propose: satisfy-purpose
#
########################################################

sp {topstate*propose*satisfy-purpose
   (state <s> ^topstate <s>
              ^top-segment.purpose {<p> <> none})
-->
   (<s> ^operator <o> +)
   (<o> ^name satisfy-purpose
        ^purpose <p>)
}