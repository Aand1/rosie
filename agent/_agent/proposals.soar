#######################################################
# 
# Propose: interaction 
#
########################################################

# sp {topstate*propose*interaction*new-segment-info
#    (state <s> ^topstate <s>
#               ^interaction.status.new-segment-info <info>)
# -->
#    (<s> ^operator <o> + =)
#    (<o> ^name interaction
#         ^new-segment-info <info>)
# }

sp {topstate*propose*interaction*clear-interaction-stack
   (state <s> ^topstate <s>
              ^clear-interaction-stack true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction
        ^clear-interaction-stack true)
}

sp {topstate*propose*interaction*performed
   (state <s> ^topstate <s>
#              ^interaction.status.performed <perf>)
	       ^interaction.status.<any> <stat>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction
        ^status <stat>)
}

sp {topstate*propose*interaction*nl-input-link
   (state <s> ^topstate <s>
              ^nl.input-link.message <msg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction 
        ^message <msg>)
}

sp {topstate*propose*interaction*input-link
   (state <s> ^topstate <s>
              ^io.input-link.message <msg>)
   (<msg> -^processed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction 
        ^message <msg>)
}
   
#######################################################
# 
# Propose: comprehesion
#
########################################################

sp {topstate*elaborate*non-indexed-param
   (state <s> ^topstate <s>
              ^top-segment.purpose.parameters.indexable <i>)
   (<i> ^lexical
       -^result)
-->
   (<s> ^non-indexed-param true)
}

sp {topstate*propose*comprehension
   (state <s> ^topstate <s>
              ^non-indexed-param true)
-->
   (<s> ^operator <o> +)
   (<o> ^name comprehension)
}


#######################################################
# 
# Propose: satisfy-purpose
#
########################################################

sp {topstate*propose*satisfy-purpose
   (state <s> ^topstate <s>
              ^top-segment.purpose {<p> <> none})
-->
   (<s> ^operator <o> +)
   (<o> ^name satisfy-purpose
        ^purpose <p>)
}

#########################################################
#
# Propose: attend-to-scene-change
#
#########################################################
sp {propose*attend-to-scene-change
   (state <s> ^monitor-scene-changes true
              ^scene-changes.change <change>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name attend-to-scene-change
        ^category perception
        ^change <change>)
}
