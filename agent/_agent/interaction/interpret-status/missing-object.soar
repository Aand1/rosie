# For an missing-object, where multiple objects match..
# Will ask a which question to get more information

sp {interpret-status*propose*interpret-missing-object
   (state <s> ^name interpret-status
              ^status <status>
             -^handled <status>)
   (<status> ^type missing-object
             ^missing-object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-missing-object
        ^missing-object <obj>)
}

sp {interpret-status*apply*interpret-missing-object*mark*handled
   (state <s> ^name interpret-status
              ^operator.name interpret-missing-object
              ^status <status>)
-->
   (<s> ^handled <status>)
}

sp {interpret-status*apply*interpret-missing-object*create*new-segment
   (state <s> ^name interpret-status
              ^operator <o>)
   (<o> ^name interpret-missing-object
        ^missing-object <obj>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
        ^originator agent)
   (<p> ^type find-missing-object 
        ^missing-object <obj>
        ^satisfaction.dialog-event <de>)
   (<de> ^type object-message
         ^originator instructor)
}

sp {interpret-status*apply*interpret-missing-object*create*message
   (state <s> ^name interpret-status
              ^operator <o>)
   (<o> ^name interpret-missing-object
        ^missing-object <obj>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type missing-object 
          ^target-object <obj>)
}

   