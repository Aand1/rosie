############# propose get-goal #########################################
sp {interpret-status*propose*unknown-goal
   (state <s> ^name interpret-status
   	      ^status <stat>
	      -^new-segment-info)
   (<stat> ^type failure
   	   ^information << unknown-goal incorrect-goal >>
	   ^details <det>)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-goal
   	^details <det>)
}


############ interpret get goal ###################################
sp {interpret-status*apply*unknown-goal*add*purpose
   (state <s> ^name interpret-status
   	      ^operator <o>)
   (<o> ^name get-goal
   	^details <det>)
   (<det> ^type unknown-goal
   	  ^goal-definition <gid>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-next-subaction
   	^satisfaction <sat>
	^reason <con>)
   (<sat> ^dialog-event <de1>)
   (<de1> ^originator instructor
   	  ^type goal-relation-message)
   (<con> ^type unknown-goal
   	  ^details <det>)
}

sp {interpret-status*apply*overspecific-definition*add*purpose
   (state <s> ^name interpret-status
   	      ^operator <o>)
   (<o> ^name get-goal
   	^details <d>)
   (<d> ^predicate-grounding-failure)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-goal
   	^reason <con>
	^satisfaction <sat>)
   (<con> ^type overspecific-definition
   	  ^details <d>)
   (<sat> ^dialog-event <de1>)
   (<de1> ^orginator instructor
   	  ^type goal-relation-message)
}


############ write out goal message #######################################
sp {interpret-status*apply*unknown-goal*message
   (state <s> ^name interpret-status
   	      ^operator <o>
	      ^topstate.interaction.stack.segment.purpose.satisfaction.action-event.type.verb.word <verb>)
   (<o> ^name get-goal)
--> 
   (<s> ^outgoing-message <msg>)  
   (<msg> ^type get-goal
   	  ^field.verb <verb>)
}

sp {interpret-status*apply*confirm*hypothesis*message
   (state <s> ^name interpret-status
   	      ^operator <o>)
   (<o> ^name verify-goal
   	^message <msg>)
-->
   (<s> ^outgoing-message <msg>)}



   	  
	      	