
sp {send-message*propose*create-put-down-item-request
   (state <s> ^name send-message
              ^message.type put-down-item-request
              ^message.item <item>
             -^created-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-put-down-item-request
        ^item <item>)
}

sp {send-message*apply*create-put-down-item-request*no*loc
   (state <s> ^name send-message
              ^operator <o>
             -^message.location)
   (<o> ^name create-put-down-item-request
        ^item <item>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type put-down-item-request
          ^originator agent
          ^fields.item <item>)
}

sp {send-message*apply*create-put-down-item-request*loc
   (state <s> ^name send-message
              ^operator <o>
              ^message.location <loc>)
   (<o> ^name create-put-down-item-request
        ^item <item>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type put-down-item-request
          ^originator agent
          ^fields <f>)
   (<f> ^item <item>
        ^location <loc>)
}
