# Failure: unknown-goal
#   When there is no goal information for a composite task
# 
# (<s> ^failure <f>)
# (<f> ^type unknown-goal
#      ^action <action>) # matches top-state.action-stack.action
#  
# Effects:
#   Push Segment: get-goal-info
#   Send Message: get-goal-info

sp {interpret-failure*propose*interpret-unknown-goal
   (state <s> ^name interpret-failure
   	          ^failure <f>
             -^handled <f>)
   (<f> ^type unknown-goal
        ^action <action>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-unknown-goal
        ^action <action>)
}

# Mark the failure as handled
sp {interpret-failure*apply*interpret-unknown-goal*mark*handled
   (state <s> ^name interpret-failure
              ^operator.name interpret-unknown-goal
              ^failure <f>)
-->
   (<s> ^handled <f>)
}

# Create segment get-goal-info
sp {interpret-failure*apply*interpret-unknown-goal*add*purpose
   (state <s> ^name interpret-failure
              ^operator <o>)
   (<o> ^name interpret-unknown-goal
        ^action <action>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-goal-info
        ^parameters.action <action>
   	^satisfaction.dialog-event <de>)
   (<de> ^type goal-description
         ^originator instructor)
}

# Create message get-goal-info
sp {interpret-failure*apply*interpret-unknown-goal*send*message
   (state <s> ^name interpret-failure
              ^operator.name interpret-unknown-goal)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type simple-message
          ^message-type get-goal-info)
}
