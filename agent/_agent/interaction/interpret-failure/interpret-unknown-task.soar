# Failure: unknown-task
#   when the agent does not have a task to do
#
# (<s> ^failure <f>)
# (<f> ^type unknown-task)
#  
# Effects:
#   Push Segment: get-next-task
#   Send Message: get-next-task

sp {interpret-failure*propose*interpret-unknown-task
   (state <s> ^name interpret-failure
              ^failure <f>
             -^handled <f>)
   (<f> ^type unknown-task)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-unknown-task)
}

# Mark the failure as handled
sp {interpret-failure*apply*interpret-unknown-task*mark*handled
   (state <s> ^name interpret-failure
              ^operator.name interpret-unknown-task
              ^failure <f>)
-->
   (<s> ^handled <f>)
}

# Create get-next-task segment
sp {interpret-failure*apply*interpret-unknown-task*add*purpose
   (state <s> ^name interpret-failure
   	      ^operator.name interpret-unknown-task)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-next-task
   	^satisfaction.dialog-event <de>)
   (<de> ^type command
         ^originator instructor)
}

# Create get-next-task message
sp {interpret-failure*apply*interpret-unknown-task*send*message
   (state <s> ^name interpret-failure
              ^operator.name interpret-unknown-task)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type simple-message
          ^message-type get-next-task)
}