sp {interpret-failure*propose*interpret-unknown-predicate
   (state <s> ^name interpret-failure
   	      ^failure.type << unknown-predicate unknown-word >>
         -^new-segment-info)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-unknown-predicate)
}

sp {interpret-failure*apply*interpret-unknown-predicate*add*purpose
   (state <s> ^name interpret-failure
   	      ^operator.name interpret-unknown-predicate)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-predicate-info
   	^satisfaction.dialog-event <de>)
   (<de> ^type adjective-definition
         ^originator instructor)
}

sp {interpret-failure*apply*interpret-unknown-predicate*send*message
   (state <s> ^name interpret-failure
              ^operator.name interpret-unknown-predicate
              ^failure.predicate.name <name>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type get-predicate-info
          ^predicate-name <name>)
}


sp {interpret-failure*apply*interpret-unknown-predicate*send*message*word
   (state <s> ^name interpret-failure
              ^operator.name interpret-unknown-predicate
              ^failure.word <name>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type get-predicate-info
          ^predicate-name <name>)
}