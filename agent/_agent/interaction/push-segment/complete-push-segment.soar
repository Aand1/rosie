sp {push-segment*propose*complete-push-segment
   (state <s> ^name push-segment)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-push-segment)
}

# Remove the new-segment-info from the status link (retracts operator)
sp {push-segment*apply*complete-push-segment*remove*status
   (state <s> ^name push-segment
              ^operator.name complete-push-segment
              ^topstate.interaction.status <status>)
   (<status> ^<any> <stat>)
-->
   (<status> ^<any> <stat> -)
}

sp {push-segment*apply*complete-push-segment*remove*message
   (state <s> ^name push-segment
              ^operator.name complete-push-segment
              ^topstate.nl.input-link <nil>)
   (<nil> ^message <msg>)
-->
   (<nil> ^message <msg> -)
}

sp {push-segment*apply*complete-push-segment*message*mark-processed
   (state <s> ^name push-segment
              ^operator.name complete-push-segment
              ^io.input-link.message <msg>)
-->
   (<msg> ^processed true)
}

sp {push-segment*increment*counter
   (state <s> ^name push-segment
   	      ^operator.name complete-push-segment
	      ^segment <seg>
	      ^topstate.interaction.stack.segment <prev>)
   (<seg> ^purpose.type action-command)
   (<prev> ^counter <count>)
-->
   (<prev> ^counter <count> -
   	   ^counter (+ <count> 1))
}

# Pushes the actual segment onto the stack
sp {push-segment*apply*complete-push-segment*push-segment
   (state <s> ^name push-segment
              ^operator.name complete-push-segment
              ^segment <seg>
              ^epmem.present-id <epmem-id>
              ^topstate.interaction.stack <stack>)
   (<stack> ^segment <prev-seg>)
-->
   (<stack> ^segment <prev-seg> -
            ^segment <seg>)
   (<seg> ^epmem-id <epmem-id>)
   (<prev-seg> ^prev <seg>)
}



sp {push-segment*apply*complete-push-segment*send*message
   (state <s> ^name push-segment
              ^operator.name complete-push-segment
              ^new-segment-info.message <msg>
              ^io.output-link <out>)
-->
   (<out> ^send-message <msg>)
}