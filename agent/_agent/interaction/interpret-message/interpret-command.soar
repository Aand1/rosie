###################################################################
#
# interpret-command
#   A command given by the instructor to the agent to do something
#   Construction: *-VP-T
#     E.g. 'Store the red sphere', 'Put the green large block in the pantry'
#
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*generate-action-operator
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              -^top-state.dialog-context
             -^action-operator <op>)
   (<msg> ^message-type command -^action.word-structure.spelling |what's| -^action.word-structure.spelling |whats|)
-->
   (<s> ^operator <o> +)
   (<o> ^name generate-action-operator
        ^command-message <msg>)
}

sp {interpret-message*propose*interpret-conditional-predicate*command
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              ^top-state.dialog-context.type game-learning)
   (<msg> ^message-type command
          -^action.handle attend
          -^interaction-status.failure
          -^action.handle ignore
          -^interpreted true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interpret-conditional-predicate
        ^message <msg>)
}
sp {interpret-message*propose*interpret-attend-list
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              ^top-state.dialog-context <dc>)
   (<msg> ^message-type command
          ^action.handle attend
          -^interpreted true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interpret-attend-list
        ^message <msg>)
}
sp {interpret-message*propose*interpret-ignore-list
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              ^top-state.dialog-context <dc>)
   (<msg> ^message-type command
          ^action.handle ignore
          -^interpreted true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interpret-ignore-list
        ^message <msg>)
}

sp {generate-action-operator*elaborate*allow-substate
   (state <s> ^name generate-action-operator
              ^superstate.name interpret-message)
-->
   (<s> ^allow-substate true)
}

sp {interpret-message*propose*interpret-command
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              ^action-operator <op>)
   (<msg> ^message-type command
         -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-command)
}

sp {interpret-message*apply*interpret-command
   (state <s> ^name interpret-message
              ^instructor-message <msg>
              ^operator.name interpret-command
              ^action-operator <op>)
   (<op> ^action-handle <action-handle>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type action
        ^parameters.action-operator <op>
        ^satisfaction.action-event.type <action-handle>)
   (<msg> ^interpreted true)
}

sp {interpret-message*apply*interpret-command*pop-segment*existing-action
   (state <s> ^name interpret-message
              ^operator.name interpret-command
              ^top-state.interaction.stack.segment.purpose.type action)
-->
   (<s> ^terminate-segment true)
}

