###########################################################
#
# object-description
#   Teaching message about an object
#   E.g. 'This is a red triangle'
#
###########################################################

# Proposal
sp {interpret-message*propose*object-description
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type object-description
         -^processed)
-->
   (<s> ^operator <o> +)
   (<o> ^name object-description
        ^category communication
        ^message <msg>)
}

# Elaborate useful information from the message to the operator
sp {interpret-message*elaborate*object-description*indicated-object
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name object-description
        ^message.indicated-object <obj>)
-->
   (<o> ^indicated-object <obj>)
}

sp {interpret-message*elaborate*object-description*predicate
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name object-description
        ^message.predicate <pred>)
-->
   (<o> ^predicate <pred>)
}

# Mark Processed
sp {interpret-message*apply*object-description*mark*processed1
   (state <s> ^name interpret-message
              ^operator <o>
              ^quiescence t)
   (<o> ^name object-description
        ^message <msg>)
-->
   (<msg> ^processed true)
}

# Create the new segment 
#   (purpose is to learn something about an object)
sp {interpret-message*apply*object-description
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name object-description
	      ^indicated-object <obj>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type train-on-object
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type object)
	 (<params> ^indicated-object <obj>)
}

# Copy any predicates to the new segment
sp {interpret-message*apply*object-description*copy*predicate
   (state <s> ^name interpret-message
              ^operator <o>
              ^new-segment-info.purpose.parameters <params>)
   (<o> ^name object-description
	      ^predicate <pred>)
-->
	(<params> ^predicate <pred>)
}

