###################################################################
#
# interpret-what-is-question
# - Description of message here
#
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*interpret-what-is-question
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type what-is-question
         -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-what-is-question
        ^message <msg>)
}

######
# OPERATOR ELABORATIONS
######

# Elaborate useful information from the message onto the operator

# relation: OPTIONAL - indicates we are looking for a block that satisfies the given relation
sp {interpret-message*elaborate*interpret-what-is-question*relation
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-what-is-question
        ^message.relation <rel>)
-->
   (<o> ^relation <rel>)
}

# object: OPTIONAL - indicates we want to describe the given object
sp {interpret-message*elaborate*interpret-what-is-question*object
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-what-is-question
        ^message.arg1 <obj>)
-->
   (<o> ^object <obj>)
}

#####
# OPERATOR APPLICATION
#####

# Mark the message as interpreted (retracts operator)
sp {interpret-message*apply*interpret-what-is-question*mark*interpreted
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-what-is-question
        ^message <msg>)
-->
   (<s> ^mark-interpreted <msg>)
}

# No indicated object, only a relation
#   Want to identify what object satisfies that relation and describe it
#   E.g. 'What is in the pantry?'
sp {interpret-message*apply*interpret-what-is-question*create*segment
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-what-is-question
        ^relation <rel>
       -^object)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type identify-object
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^relation <rel>)
   (<sat> ^dialog-event.type object-description)
}

# The object is already identified, we just want a description of it
#   E.g. 'What is this?'
sp {interpret-message*apply*interpret-what-is-question*create*segment
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-what-is-question
        ^object <obj>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type describe-object
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^object <obj>)
   (<sat> ^dialog-event.type object-description)
}
