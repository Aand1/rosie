sp {interpret-message*propose*complete-interpret-message
   (state <s> ^name interpret-message)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-interpret-message)
}

# We have finished interpreting the message
#   Remove it from interaction.status
sp {interpret-message*apply*complete-interpret-message*delete*nl-il
    (state <s> ^name interpret-message
               ^operator.name complete-interpret-message
				^instructor-message <msg>
				 ^topstate.interaction.status <status>)
		(<status> ^new-message <msg>)
 -->
    (<status> ^new-message <msg> -)
 }

# If there is a new segment to create
#   Add the new-segment-info to interaction.changes
sp {interpret-message*apply*complete-interpret-message*copy*new-segment-info
   (state <s> ^name interpret-message
              ^operator.name complete-interpret-message
              ^topstate.interaction.changes <c>
              ^new-segment-info <info>)
-->
   (<c> ^new-segment-info <info>) 
}

# If there is information about modifying the current segment,
#   Add the modify-segment-info to interaction.changes
sp {interpret-message*apply*complete-interpret-message*copy*modify-segment-info
   (state <s> ^name interpret-message
              ^operator.name complete-interpret-message
              ^modify-segment-info <info>
	      ^topstate.interaction.changes <c>)
-->
   (<c> ^modify-segment-info <info>) 
}

# If the instructor message is expected by a segment
#   Then designate the segment to be terminated on interaction.changes
sp {interpret-message*apply*complete-interpret-message*expected*message*terminate
   (state <s> ^name interpret-message
              ^operator.name complete-interpret-message
	      ^current-purpose.satisfaction.dialog-event <de>
	      ^instructor-message.message-type <type>
	      ^topstate.interaction.changes <c>)
   (<de> ^type <type>
         ^originator instructor)
-->
   (<c> ^terminate-segment true)
}

# If there is a flag telling the agent to terminate the current segment
#   Then add ^terminate-segment true to interaction.changes
sp {interpret-message*apply*complete-interpret-message*terminate-segment
   (state <s> ^name interpret-message
              ^operator.name complete-interpret-message
              ^terminate-segment true
              ^topstate.interaction.changes <c>)
-->
   (<c> ^terminate-segment true)
}

# If there is a flag telling the agent to clear the current interaction
#   Then add ^clear-interaction-stack true to interaction.changes
sp {interpret-message*apply*complete-interpret-message*clear-interaction-stack
   (state <s> ^name interpret-message
              ^operator.name complete-interpret-message
              ^clear-interaction-stack true
              ^topstate.interaction.changes <c>)
-->
   (<c> ^clear-interaction-stack true)
}