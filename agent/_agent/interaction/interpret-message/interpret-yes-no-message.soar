###################################################################
#
# interpret-yes-no-message
#   Occurs when the intructor wishes to give additional information about an object
#   Construction: *-What-is-DP-QT 
#   	E.g. 'What is this?'
#
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*interpret-yes-no-message
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type { << yes no >> <type> }
         -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-yes-no-message
        ^message <msg>
        ^message-type <type>)
}

#####
# OPERATOR APPLICATION
#####

# Don't even need to do any special processing, just create a response
sp {interpret-message*apply*interpret-yes-no-message*mark
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-yes-no-message
        ^message <msg>)
-->
   (<msg> ^interpreted true)
}

sp {interpret-message*apply*interpret-yes-no-message*mark*received-yes*pick-up*put-down
   (state <s> ^name interpret-message
              ^operator <o>
              ^top-state.action-stack.top <top>)
   (<o> ^name interpret-yes-no-message
        ^message-type yes)
   (<top> ^execution-operator.action-handle << pick-up1 put-down1 >>)
-->
   (<top> ^received-yes true)
}

sp {interpret-message*apply*interpret-yes-no-message*mark*received-yes*start-leading
   (state <s> ^name interpret-message
              ^operator <o>
              ^top-state.action-stack.top <top>)
   (<o> ^name interpret-yes-no-message
        ^message-type yes)
   (<top> ^execution-operator.action-handle << start-leading1 >>)
-->
   (<top> ^received-yes true)
}

sp {interpret-message*apply*interpret-yes-no-message*yes*mark*object*visible
   (state <s> ^name interpret-message
              ^operator <o>
              ^current-purpose.type ask-about-object
              ^current-purpose.parameters.object <obj>)
   (<o> ^name interpret-yes-no-message
        ^message-type yes)
   (<obj> ^predicates <preds>)
-->
   (<preds> ^visible true
            ^visible false -)
}
    
