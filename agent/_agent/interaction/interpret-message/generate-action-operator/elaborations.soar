sp {generate-action-operator*elaborate*message*from*superoperator
   (state <s> ^name generate-action-operator
              ^superstate.operator.command-message <msg>)
-->
   (<s> ^command-message <msg>)
}

sp {generate-action-operator*elaborate*action-id
   (state <s> ^name generate-action-operator
              ^command-message.action <id>)
-->
   (<s> ^action-id <id>)
}

sp {generate-action-operator*elaborate*retrieve-lti*command-message*action
   (state <s> ^name generate-action-operator
              ^action-id <id>
             -^retrieved-lti <id>
             -^to-store <id>)
-->
   (<s> ^retrieve-lti <id>)
}

sp {generate-action-operator*elaborate*retrieve-lti*goal
   (state <s> ^name generate-action-operator
              ^action-id <id>
             -^retrieved-lti <goal>
             -^to-store <goal>)
   (<id> ^goal <goal>)
-->
   (<s> ^retrieve-lti <goal>)
}

sp {generate-action-operator*elaborate*retrieve-lti*goal*predicate
   (state <s> ^name generate-action-operator
              ^action-id.goal.predicate <pred>
             -^retrieved-lti <pred>
             -^to-store <pred>)
-->
   (<s> ^retrieve-lti <pred>)
}

sp {generate-action-operator*elaborate*retrieve-lti*goal*arguments
   (state <s> ^name generate-action-operator
              ^action-id.goal.predicate.{ << id 1 2 >> } <arg>
             -^retrieved-lti <arg>
             -^to-store <arg>)
-->
   (<s> ^retrieve-lti <arg>)
}

sp {generate-action-operator*elaborate*retrieve-lti*goal*argument*defaults
   (state <s> ^name generate-action-operator
              ^action-id.goal.predicate.{ << id 1 2 >> }.default <default>
             -^retrieved-lti <default>
             -^to-store <default>)
-->
   (<s> ^retrieve-lti <default>)
}

sp {generate-action-operator*elaborate*retrieve-lti*procedural
   (state <s> ^name generate-action-operator
              ^action-id <id>
             -^retrieved-lti <proc>
             -^to-store <proc>)
   (<id> ^procedural <proc>)
-->
   (<s> ^retrieve-lti <proc>)
}

sp {generate-action-operator*elaborate*retrieve-lti*procedural*arguments
   (state <s> ^name generate-action-operator
              ^action-id.procedural.{ <arg-name> <> op_name <> execution-operator <> arg-name} <arg>
             -^retrieved-lti <arg>
             -^to-store <arg>)
-->
   (<s> ^retrieve-lti <arg>)
}