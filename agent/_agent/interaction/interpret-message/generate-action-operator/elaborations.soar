sp {generate-action-operator*elaborate*message*from*superoperator
   (state <s> ^name generate-action-operator
              ^superstate.operator.command-message <msg>)
-->
   (<s> ^command-message <msg>)
}

sp {generate-action-operator*elaborate*action-handle
   (state <s> ^name generate-action-operator
              ^command-message.action.handle <handle>)
-->
   (<s> ^action-handle <handle>)
}

sp {generate-action-operator*elaborate*smem-query*command-message*action
   (state <s> ^name generate-action-operator
              ^action-handle <handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 8)
   (<cue> ^handle <handle>)
}

sp {generate-action-operator*elaborate*action-id*from*query*result
   (state <s> ^name generate-action-operator
              ^action-handle <handle>
              ^smem-query.result <res>)
   (<res> ^handle <handle>)
-->
    (<s> ^action-id <res>)
}


