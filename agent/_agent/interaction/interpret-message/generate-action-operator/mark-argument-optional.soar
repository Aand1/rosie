
# If this is not a new action, but we created a new procedural argument, 
#   Then that argument is optional (we've encountered the action without it before)
sp {generate-action-operator*propose*mark-argument-optional*new*procedural*arg
   (state <s> ^name generate-action-operator
             -^new-action true
              ^created-procedural-arg <parg>)
   (<parg> -^optional true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-argument-optional
        ^argument <arg>)
}

# If there is a procedural argument not included on the command, it must be optional
sp {generate-action-operator*propose*mark-argument-optional*simple
   (state <s> ^name generate-action-operator
              ^action-id.procedural <proc>
             -^command-message.<arg-name>)
   (<proc> ^<arg-name> <arg>)
   (<arg> ^arg-type << object concept >>
         -^optional true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-argument-optional
        ^argument <arg>)
}

# If there is a procedural argument not included on the command, it must be optional
sp {generate-action-operator*propose*mark-argument-optional*predicate
   (state <s> ^name generate-action-operator
              ^action-id.procedural <proc>)
   (<proc> ^<arg-name> <arg>)
   (<arg> ^arg-type predicate
         -^optional true)
   -{(<s> ^action-operator.{ << arg2 relation >> } <op-arg>)
     (<op-arg> ^handle <rel-handle> ^2 <obj2>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-argument-optional
        ^argument <arg>)
}

sp {generate-action-operator*apply*mark-argument-optional
   (state <s> ^name generate-action-operator
              ^operator <o>)
   (<o> ^name mark-argument-optional
        ^argument <arg>)
-->
   (<arg> ^optional true)
   (<s> ^to-store <arg>)
}
   
