###################################################################
#
# interpret-yes-no-message
#   Occurs when the intructor wishes to give additional information about an object
#   Construction: *-What-is-DP-QT 
#   	E.g. 'What is this?'
#
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*interpret-yes-no-message
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type { << yes no >> <type> }
         -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-yes-no-message
        ^message <msg>
        ^message-type <type>)
}

#####
# OPERATOR APPLICATION
#####

# Don't even need to do any special processing, just create a response
sp {interpret-message*apply*interpret-yes-no-message*mark
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-yes-no-message
        ^message <msg>)
-->
   (<msg> ^interpreted true)
}

sp {interpret-message*apply*interpret-yes-no-message*yes*mark*received-yes*action
   (state <s> ^name interpret-message
              ^top-state.current-action <a>
              ^operator <o>)
   (<o> ^name interpret-yes-no-message
        ^message-type yes)
-->
   (<a> ^received-yes true)
}
    
sp {interpret-message*apply*interpret-yes-no-message*yes*mark*received-no*action
   (state <s> ^name interpret-message
              ^top-state.current-action <a>
              ^operator <o>)
   (<o> ^name interpret-yes-no-message
        ^message-type no)
-->
   (<a> ^received-no true)
}          