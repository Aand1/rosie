sp {topstate*apply*init-object-list
	(state <s> ^superstate nil
               ^name storage-agent
			   ^operator <op>
               ^svs <svs>)
    (<op> ^name init-object-list)
-->
	(<s> ^to-store-list <t>)
 	(<t> ^update true)
}

#############################################
# Count how many objects are visible
#sp {update-object-list*propose*count-objects
#	(state <s> ^name update-object-list
#              -^object-count
#               ^topstate <ts>)
#-->
#	(<s> ^operator <op> + =)
# 	(<op> ^name count-objects)
#}

#############################################
# Start keeping track of how many we've updated
sp {update-object-list*propose*init-new-list
	(state <s> ^name update-object-list
              -^new-list)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name init-new-list)
}

sp {update-object-list*apply*init-new-list
	(state <s> ^name update-object-list
               ^topstate.to-store-list <list>
               ^operator.name init-new-list)
-->
	(<s> ^new-list <l>)
}

###########################################
# Add an object we haven't seen before
sp {update-object-list*propose*add-object
	(state <s> ^name update-object-list
               ^new-list
               ^topstate <ts>
              -^finished <id>)
 	(<ts> ^svs.spatial-scene <svs>
          ^to-store-list <list>)
 	(<svs> ^child <c>)
    (<c> ^id <id>
         ^main-color red
         ^object-source perception)
  	(<list> -^object.id <id>)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name add-object
          ^object <c>)
}

sp {update-object-list*apply*add-object
	(state <s> ^name update-object-list
               ^operator <op>
               ^new-list <nl>
               ^topstate.to-store-list <list>)
     (<op> ^name add-object
          ^object <obj>)
    (<obj> ^id <id>)
-->
	(<nl> ^object <o>)
 	(<o> ^id <id>
       	 ^seen-count 1
         ^not-seen-count 0)
  	(<s> ^finished <id>)
}

sp {update-object-list*elaborate*updatable-object-new
	(state <s> ^name update-object-list
               ^topstate <ts>
              -^finished <id>)
 	(<ts> ^svs.spatial-scene <svs>
          ^to-store-list <list>)
 	(<svs> ^child <c>)
    (<c> ^id <id>
         ^main-color red
         ^object-source perception)
  	(<list> -^object.id <id>)
-->
	(<s> ^has-updatable-object new)
}

############################################
# Update counts on an object we've already seen
sp {update-object-list*propose*update-seen-count
	(state <s> ^name update-object-list
               ^new-list
               ^topstate <ts>
              -^finished <id>)
 	(<ts> ^svs.spatial-scene <svs>
          ^to-store-list <list>)
 	(<svs> ^child <c>)
    (<c> ^id <id>
         ^main-color red
         ^object-source perception)
  	(<list> ^object <obj>)
    (<obj> ^id <id>)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name update-seen-count
          ^id <id>)
}

sp {update-object-list*apply*update-seen-count
	(state <s> ^name update-object-list
               ^operator <op>
               ^new-list <nl>
               ^topstate.to-store-list <list>)
 	(<op> ^name update-seen-count
          ^id <id>)
  	(<list> ^object <obj>)
    (<obj> ^id <id>
           ^seen-count <sc>
           ^not-seen-count <nsc>)
-->
	(<nl> ^object <o>)
	(<o> ^seen-count (+ <sc> 1)
       	 ^not-seen-count 0
         ^id <id>)
 	(<s> ^finished <id>)
}

sp {update-object-list*elaborate*updatable-object-seen
	(state <s> ^name update-object-list
               ^topstate <ts>
              -^finished <id>)
 	(<ts> ^svs.spatial-scene <svs>
          ^to-store-list <list>)
 	(<svs> ^child <c>)
    (<c> ^id <id>
         ^main-color red
         ^object-source perception)
  	(<list> ^object <obj>)
    (<obj> ^id <id>)
-->
	(<s> ^has-updatable-object seen)
}

sp {update-object-list*propose*update-not-seen-count
	(state <s> ^name update-object-list
               ^new-list
               ^topstate <ts>
              -^finished <id>)
 	(<ts> ^svs.spatial-scene <svs>
          ^to-store-list <list>)
  	(<list> ^object <obj>)
    (<obj> ^not-seen-count <nsc>)
    (<obj> ^id <id>)
    (<svs> -^child.id <id>)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name update-not-seen-count
          ^count <nsc>
          ^id <id>)
}

sp {update-object-list*apply*update-not-seen-count
	(state <s> ^name update-object-list
               ^operator <op>
               ^new-list <nl>
               ^topstate <ts>)
 	(<op> ^name update-not-seen-count
          ^count <c> < 10
          ^id <id>)
 	(<ts> ^to-store-list <list>)
  	(<list> ^object <obj>)
    (<obj> ^id <id>
           ^seen-count <sc>
           ^not-seen-count <nsc>)
-->
	(<nl> ^object <o>)
	(<o> ^not-seen-count (+ <nsc> 1)
      	   ^seen-count 0
           ^id <id>)
 	(<s> ^finished <id>)
}

sp {update-object-list*elaborate*updatable-object-not-seen
	(state <s> ^name update-object-list
               ^topstate <ts>
              -^finished <id>)
 	(<ts> ^svs.spatial-scene <svs>
          ^to-store-list <list>)
  	(<list> ^object <obj>)
    (<obj> ^not-seen-count <nsc>)
    (<obj> ^id <id>)
    (<svs> -^child.id <id>)
-->
	(<s> ^has-updatable-object not-seen)
}

sp {update-object-list*apply*update-not-seen-count*deletion
	(state <s> ^name update-object-list
               ^operator <op>
               ^new-list <nl>
               ^topstate <ts>)
 	(<op> ^name update-not-seen-count
          ^count <c> >= 10
          ^id <id>)
 	(<ts> ^to-store-list <list>)
  	(<list> ^object <obj>)
    (<obj> ^id <id>
           ^seen-count <sc>
           ^not-seen-count <nsc>)
-->
 	(<s> ^finished <id>)
}
##############################################
# Same number of updates as objects, finished
sp {update-object-list*propose*updates-finished
	(state <s> ^name update-object-list
               ^new-list
               ^topstate <ts>
              -^has-updatable-object)
  	(<ts> ^to-store-list <list>)
    (<list> ^update true)
-->
	(<s> ^operator.name updates-finished +)
}

sp {update-object-list*apply*updates-finished
	(state <s> ^name update-object-list
               ^new-list <nl>
               ^operator.name updates-finished
               ^topstate <ts>)
 	(<ts> ^to-store-list <old>)
-->
	(<ts> ^to-store-list <nl>
          ^to-store-list <old> -)
}