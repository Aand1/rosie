sp {decide-put-down-params*propose*init
	(state <s> ^name decide-put-down-params
              -^param)
-->
	(<s> ^operator.name init +)
}

sp {decide-put-down-params*apply*init
	(state <s> ^name decide-put-down-params
               ^operator.name init)
-->
	(<s> ^param <p>)
}

sp {decide-put-down-params*propose*set-step-size
	(state <s> ^name decide-put-down-params
               ^param <p>)
 	(<p> -^step-size)
-->
	(<s> ^operator.name set-step-size + =)
}

sp {decide-put-down-params*apply*set-step-size-small
	(state <s> ^name decide-put-down-params
               ^topstate.svs.spatial-scene <sc>
               ^param <p>)
 	(<s> ^operator.name set-step-size)
  	(<sc> ^child.main-color blue)
-->
	(<p> ^step-size 0.2)
}

sp {decide-put-down-params*apply*set-step-size-medium
	(state <s> ^name decide-put-down-params
               ^topstate.svs.spatial-scene <sc>
               ^param <p>)
 	(<s> ^operator.name set-step-size)
  	(<sc> -^child.main-color blue
          ^child.main-color green)
-->
	(<p> ^step-size 0.4)
}

sp {decide-put-down-params*apply*set-step-size-large
	(state <s> ^name decide-put-down-params
               ^topstate.svs.spatial-scene <sc>
               ^param <p>)
 	(<s> ^operator.name set-step-size)
  	(<sc> -^child.main-color blue
          -^child.main-color green)
-->
	(<p> ^step-size 0.6)
}

sp {decide-put-down-params*propose*finish
	(state <s> ^name decide-put-down-params
               ^param <p>)
 	(<p> ^step-size)
-->
	(<s> ^operator.name finish +)
}

sp {decide-put-down-params*apply*finish
	(state <s> ^name decide-put-down-params
               ^superstate <ss>
               ^operator.name finish
               ^param <p>)
-->
	(<ss> ^params <p>)
}