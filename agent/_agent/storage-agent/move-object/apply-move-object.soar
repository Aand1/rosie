sp {move-object*apply*request-pick-up
	(state <s> ^superstate nil
			   ^name storage-agent
               ^operator <op>)
 	(<op> ^name request-pick-up
          ^id <id>)
-->
	(<s> ^request <r>
      	 ^pick-up <id>)
 	(<r> ^target <t>
         ^speed 0.8
         ^type grasp
         ^time-limit 3
         ^step-size 0.4
         ^hard-time-limit true)
  	(<t> ^id <id>)
}

sp {move-object*apply*select-drop-off-location
	(state <s> ^superstate nil
               ^name storage-agent
               ^operator <op>)
 	(<op> ^name select-drop-off-location
          ^location <l1>)
-->
	(<s> ^selected-location <l1>)
    (<l1> ^used true)
}

sp {move-object*apply*request-drop-off
	(state <s> ^superstate nil
			   ^name storage-agent
               ^operator <op>)
 	(<op> ^name request-drop-off
          ^id <id>
          ^location <loc>)
  	(<loc> ^x <x>
           ^y <y>)
-->
	(<s> ^request <r>
      	 ^drop-off <id>)
 	(<r> ^target <t>
         ^speed 0.8
         ^type drop
         ^time-limit 3
         ^step-size 0.4
         ^hard-time-limit true)
  	(<t> ^x <x>
         ^y <y>
         ^z 0.0)
}

sp {move-object*apply*mark-stored-not-finished
	(state <s> ^superstate nil
			   ^name storage-agent
      		   ^open-location
               ^operator <op>)
	(<op> ^name mark-stored
          ^id <id>
          ^location <loc>)
-->	
	(<s> ^stored-object <id>
     	 ^selected-object <id> -
       	 ^selected-location <loc> -
         ^pick-up <id> -
         ^put-down <id> -)
}

sp {move-object*apply*mark-stored-finished
	(state <s> ^superstate nil
               ^name storage-agent
               ^operator.name mark-stored
              -^open-location)
-->
	(write (crlf) |Task complete|)
 	(halt)
}