sp {expand-smem-structure*apply*report-failure*create*failure
   (state <s> ^name expand-smem-structure
              ^operator.name report-failure)
-->
   (<s> ^failure <f>)
}

sp {expand-smem-structure*apply*report-failure*create*type
   (state <s> ^name expand-smem-structure
              ^operator <o>
              ^failure <f>)
   (<o> ^name report-failure
        ^failure-type <type>)
-->
   (<f> ^type <type>)
}

sp {expand-smem-structure*apply*report-failure*copy*info
   (state <s> ^name expand-smem-structure
              ^operator <o>
              ^failure <f>)
   (<o> ^name report-failure
        ^{<att> <> name <> failure-type} <val>)
-->
   (<f> ^<att> <val>)
}

sp {expand-smem-structure*apply*report-failure*create*status
   (state <s> ^name expand-smem-structure
              ^operator.name report-failure
              ^topstate.interaction.status <status>
              ^failure <f>)
   (<f> ^type <type>)
-->
   (<status> ^failure <f>)
}