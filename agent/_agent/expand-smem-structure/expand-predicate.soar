sp {expand-smem-structure*predicate*retrieve*lti
   (state <s> ^name expand-smem-structure
              ^smem-structure <struct>)
   (<struct> ^type predicate
             ^id <pred>)
   (<pred> -^name)
-->
   (<s> ^retrieve-lti <pred>)
}

sp {expand-smem-structure*predicate*report-failure*no*name
   (state <s> ^name expand-smem-structure
              ^smem-structure <struct>
              ^retrieved-lti <pred>)
   (<struct> ^type predicate
             ^id <pred>)
   (<pred> -^name)
-->
   (<s> ^operator <o> + !)
   (<o> ^name report-failure
        ^failure-type bad-smem-structure
        ^smem-structure <pred>
        ^structure-type predicate)
}

sp {expand-smem-structure*predicate*elaborate*property*predicate
   (state <s> ^name expand-smem-structure
              ^smem-structure <struct>)
   (<struct> ^type predicate
             ^id <pred>)
   (<pred> ^name <name>
           ^property <prop>)
-->
   (<s> ^smem-structure <st2>)
   (<st2> ^type property
          ^id <prop>)
}

sp {expand-smem-structure*predicate*report-failure*no*property
   (state <s> ^name expand-smem-structure
              -^topstate.interaction.stack.segment.purpose.type learn-predicate-info
              ^smem-structure <struct>)
   (<struct> ^type predicate
             ^id <pred>)
   (<pred> ^name <name>
          -^property)
-->
   (<s> ^operator <o> + !)
   (<o> ^name report-failure
        ^failure-type unknown-predicate
        ^predicate <pred>)
}
   