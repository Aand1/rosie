# When there is a topstate.current-action <a>
#   Propose code to execute that action
#   Requires an execution-op <name> on the current-action
# Specific execution operators are under the action folder (e.g. execute-pick-up)

sp {topstate*propose*execute*current-action
   (state <s> ^topstate <s>
              ^current-action <a>)
   (<a> ^execution-op <op-name>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name <op-name>
        ^action-phase execution)
   (interrupt)
}

sp {topstate*elaborate*execute*current-action*arguments
   (state <s> ^topstate <s>
              ^current-action <a>
              ^operator <o> +)
   (<a> ^arguments.<arg-name> <arg>)
-->
   (<o> ^<arg-name> <arg>)
}

sp {topstate*apply*action*execution*complete*remove*current-action
   (state <s> ^topstate <s>
              ^operator <o>
              ^current-action <a>)
   (<o> ^action-phase execution)
   (<a> ^status complete)
-->
   (<s> ^current-action <a> -)
}

sp {topstate*apply*action*execution*complete*copy*next-action
   (state <s> ^topstate <s>
              ^operator <o>
              ^current-action.next-action { <next> <> none })
   (<o> ^action-phase execution)
-->
   (<s> ^current-action <next>)
}

sp {topstate*apply*action*execution*complete*report*performed*action-event
   (state <s> ^topstate <s>
              ^operator <o>
              ^current-action <a>
              ^interaction.status <status>)
   (<o> ^action-phase execution)
   (<a> ^name <name>
        ^status complete)
-->
   (<status> ^performed.action-event.type <name>)
}