# When there is a topstate.current-action <a>
#   Propose code to execute that action
#   Requires an execution-operator <name> on the current-action
# Specific execution operators are under the action folder (e.g. execute-pick-up)
                                                           
sp {topstate*elaborate*current-action*segment
   (state <s> ^topstate <s>
              ^interaction.stack.segment.current-action <a>)
-->
   (<s> ^current-action <a>)
}

sp {topstate*propose*execute*current-action
   (state <s> ^topstate <s>
              ^current-action <a>)
   (<a> ^execution-operator <op-name>)
-->
   (<s> ^operator <o> +)
   (<o> ^name <op-name>
        ^action-phase execution)
}

sp {topstate*elaborate*execute*current-action*arguments
   (state <s> ^topstate <s>
              ^current-action <a>
              ^operator <o> +)
   (<a> ^arguments.<arg-name> <arg>)
-->
   (<o> ^<arg-name> <arg>)
}

sp {topstate*elaborate*execute-operator*termination-condition*stabilized*driving
   (state <s> ^topstate <s>
              ^current-action <a>
              ^operator <o> +)
   (<o> ^action-name << drive-forward1 follow-left-wall1 follow-right-wall1 >>
        ^action-phase execution
       -^until)
-->
   (<o> ^termination-condition <term>)
   (<term> ^name stabilized)
}

sp {topstate*elaborate*execute-operator*termination-condition*object-count
   (state <s> ^topstate <s>
              ^current-action <a>
              ^operator <o> +)
   (<o> ^action-phase execution
        ^until <until>)
   (<until> ^object-type <type>
            ^object-count <count>)
-->
   (<o> ^termination-condition <term>)
   (<term> ^name count
           ^parameters <params>)
   (<params> ^count <count>
             ^class <type>)
}

sp {topstate*elaborate*execute-operator*termination-condition*timeout
   (state <s> ^topstate <s>
              ^current-action <a>
              ^operator <o> +)
   (<o> ^action-phase execution
        ^until.timeout <t>)
-->
   (<o> ^termination-condition <term>)
   (<term> ^name timeout
           ^parameters <params>)
   (<params> ^timeout <t>)
}
#
#sp {topstate*elaborate*execute-operator*termination-condition*object-detection
#   (state <s> ^topstate <s>
#              ^current-action <a>
#              ^operator <o> +)
#   (<o> ^action-phase execution
#        ^until <until>)
#   (<until> ^object-type <type>
#            ^object-count <count>)
#-->
#   (<o> ^termination-condition <term>)
#   (<term> ^name count
#           ^parameters <params>)
#   (<params> ^count <count>
#             ^class <type>)
#}

sp {topstate*apply*action*execution*complete*remove*current-action
   (state <s> ^topstate <s>
              ^operator <o>
              ^interaction.stack.segment <seg>)
   (<o> ^action-phase execution)
   (<seg> ^current-action <a>)
   (<a> ^status complete)
-->
   (<seg> ^current-action <a> -)
}

sp {topstate*apply*action*execution*complete*copy*next-action
   (state <s> ^topstate <s>
              ^operator <o>
              ^interaction.stack.segment <seg>)
   (<o> ^action-phase execution)
   (<seg> ^current-action.next-action { <next> <> none })
-->
   (<seg> ^current-action <next>)
}

sp {topstate*apply*action*execution*complete*report*performed*action-event
   (state <s> ^topstate <s>
              ^operator <o>
              ^current-action <a>
              ^interaction.status <status>)
   (<o> ^action-phase execution)
   (<a> ^name <name>
        ^status complete)
-->
   (<status> ^performed.action-event.type <name>)
}