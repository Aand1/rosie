sp {action-goal-definiton*elaborate*indexed-operator
   (state <s> ^name action-goal-definition
   	      ^topstate.interaction.stack.segment.next.purpose.parameters.indexable.result <ia>)
-->
   (<s> ^indexed-action <ia>)
}

sp {action-goal-definiton*elaborate*goal
   (state <s> ^name action-goal-definition
   	      ^superstate.operator.purpose.parameters.indexable.result <goal>)
-->
   (<s> ^goal <goal>)
}

sp {action-goal-definition*elaborate*goal-id*relation-predicate
   (state <s> ^name action-goal-definition
   	      ^goal.relation-predicate <rel>
	      ^indexed-action._metadata.map-id.goal <gid>
	      ^smem.command <scomm>)
-->
   (<gid> ^relation-predicate <nrel>)
   (<nrel> ^link <rel>)
   (<scomm> ^store <nrel> <gid>)
}

### elaborate objects that can be generalized away
sp {action-goal-learning*objects*generalizable*level1
   (state <s> ^name action-goal-definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^relation-predicate <relp>)
   (<ia> ^<argument> <obj>)
   (<relp> ^<relarg> <obj>)
   (<proc-id> ^<argument> <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
}

sp {action-goal-learning*objects*generalizable*level2
   (state <s> ^name action-goal-definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^relation-predicate <relp>)
   (<ia> ^<argument> <arg-obj>)
   (<arg-obj> ^<narg> <obj>)
   (<relp> ^<relarg> <obj>)
   (<proc-id> ^<argument> <sarg-obj>)
   (<sarg-obj> ^<narg> <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
}