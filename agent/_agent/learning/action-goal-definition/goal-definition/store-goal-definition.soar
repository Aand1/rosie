sp {store*general*goal*definition*objects
   (state <s> ^name goal-definition
   	      ^indexed-action._metadata.map-id.goal <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
   (<goal> ^relation-predicate <relp>)
   (<relp> ^<relarg> <obj>)
   (<gid> ^relation-predicate <grel>)
   (<grel> ^link <relp>)
-->
   (<grel> ^<relarg> <sobj>)
   (<scomm> ^store <sobj> <gid>)
}

sp {store*general*goal*definition*relation
   (state <s> ^name goal-definition
   	      ^indexed-action._metadata.map-id.goal <gid>
	      ^goal <goal>
	      ^smem.command <scomm>
	      ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
   (<goal> ^relation-predicate <relp>)
   (<relp> ^<relarg>.smem-rep <obj>)
   (<gid> ^relation-predicate <grel>)
   (<grel> ^link <relp>)
-->
   (<grel> ^<relarg> <sobj>)
   (<scomm> ^store <sobj> <gid>)
}



## non-generalizable structure are integral to verb definition
sp {store*general*goal*definition*1*no-generalization*object
   (state <s> ^name action-goal-definition
   	      ^indexed-action._metadata.map-id.goal <gid>
	      ^goal <goal>
	      ^smem.command <scomm>)
  -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^relation-predicate <relp>)
   (<relp> ^<relarg> <obj>)
   (<obj> ^smem-rep <sobj>
   	  ^bbox)
   (<gid> ^relation-predicate <grel>)
   (<grel> ^link <relp>)
-->
   (<grel> ^<relarg> <nobj>)
   (<nobj> ^default <sobj>)
   (<scomm> ^store <sobj> <gid> <nobj>)
}

sp {store*general*goal*definition*no-generalization*relation
   (state <s> ^name goal-definition
   	      ^indexed-action._metadata.map-id.goal <gid>
	      ^goal <goal>
	      ^smem.command <scomm>)
 -{(<s> ^generalizable <gen>)
    (<gen> ^wme-id <obj>)}
   (<goal> ^relation-predicate <relp>)
   (<relp> ^<relarg> <q>)
   (<q> -^bbox
   	^smem-rep <obj>)
   (<gid> ^relation-predicate <grel>)
   (<grel> ^link <relp>)
-->
   (<grel> ^<relarg> <nobj>)
   (<nobj> ^default <obj>)
   (<scomm> ^store <nobj> <gid> <obj>)
}