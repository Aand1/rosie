

sp {state-diff*propose*copy-relationships
   (state <s> ^name state-diff
             -^relationships.relations)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relationships
        ^category svs)
}
sp {state-diff*apply*copy-relationships
   (state <s> ^name state-diff
              ^relationships <r>
              ^operator.name copy-relationships
              ^initial.relationships <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> -^3)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^instance <instance2>)
    (<instance2> ^1 <ida> ^2 <idb>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^instance <instance>)
}

sp {state-diff*apply*copy-relationships*3arg
   (state <s> ^name state-diff
              ^relationships <r>
              ^operator.name copy-relationships
              ^initial.relationships <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> ^3 <idc>)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^instance <instance2>)
    (<instance2> ^1 <ida> ^2 <idb> ^3 <idc>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^instance <instance>)
}

sp {state-diff*propose*copy-objects
   (state <s> ^name state-diff
             -^object-set.object)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-objects
        ^category svs)
}
sp {state-diff*apply*copy-objects
   (state <s> ^name state-diff
              ^relationships.relations <p>
              ^object-set <os>
              ^final.objects.object <obj>
              ^operator.name copy-objects
              ^relationships <rels>)
   (<p> ^name <name> ^instance <instance>)
   (<instance> ^<< 1 2 3 >> <id>)
   (<obj> ^id <id>)
-->
   (<os> ^object <obj>)
}

sp {state-diff*propose*copy-extra-relationships
   (state <s> ^name state-diff
             -^extra-relationships.relations)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-extra-relationships
        ^category svs)
}
sp {state-diff*apply*copy-extra-relationships
   (state <s> ^name state-diff
              ^extra-relationships <r>
              ^object-set.object.id <ida>
              ^object-set.object.id <idb>
              ^relationships <rels>
              ^operator.name copy-extra-relationships
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> -^3)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^instance <instance2>)
    (<instance2> ^1 <ida> ^2 <idb>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^instance <instance>)
}

sp {state-diff*apply*copy-extra-relationships*3args
   (state <s> ^name state-diff
              ^extra-relationships <r>
              ^object-set.object.id <ida>
              ^object-set.object.id <idb>
              ^object-set.object.id <idc>
              ^relationships <rels>
              ^operator.name copy-extra-relationships
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> ^3 <idc>)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^instance <instance2>)
    (<instance2> ^1 <ida> ^2 <idb> ^3 <idc>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^instance <instance>)
}



sp {state-diff*propose*complete-state
   (state <s> ^name state-diff)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-state
        ^category svs)
}

sp {state-diff*apply*complete-state*copyrels
   (state <s> ^name state-diff
              ^object-set <os>
              ^extra-relationships.relations <rel>
              ^relationships <rels>
              ^operator.name complete-state)
-->
   (<rels> ^relations <rel>)
}

sp {state-diff*apply*complete-state
   (state <s> ^name state-diff
              ^object-set <os>
              ^relationships <rels>
              ^extra-relationships <rels2>
              ^superstate.operator <op>
              ^operator.name complete-state)
  -{(<rels2> ^relations <rr>)
    (<rr> ^name <name> ^instance <instance>)
    -{(<rels> ^relations.instance <instance>)}}

-->
#(interrupt)
   (<op> ^state-result <res>)
   (<res> ^relationships <rels> ^objects <os>)
}
