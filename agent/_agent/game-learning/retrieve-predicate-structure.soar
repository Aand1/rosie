sp {retrieve-predicate-structure*elaborate*predicate-handle
   (state <s> ^name retrieve-predicate-structure
              ^superstate.operator <o>)
   (<o> ^predicate-handle <handle>
        ^condition <co>)
-->
   (<s> ^predicate-handle <handle>
        ^condition <co>)
}
sp {retrieve-predicate-structure*elaborate*predicate-lti
   (state <s> ^name retrieve-predicate-structure
              ^superstate.operator <o>)
   (<o> ^predicate-lti <lti>)
-->
   (<s> ^predicate-lti <lti>)
}

sp {retrieve-predicate-structure*elaborate*smem-query
   (state <s> ^name retrieve-predicate-structure
              ^predicate-handle <handle>)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 8)
   (<cue> ^handle <handle>)
}

sp {retrieve-predicate-structure*elaborate*smem-retrieve
   (state <s> ^name retrieve-predicate-structure
              ^predicate-lti <lti>)
-->
   (<s> ^smem-retrieve <query>)
   (<query> ^identifier <lti>
            ^depth 8)
}

sp {retrieve-predicate-structure*propose*complete
   (state <s> ^name retrieve-predicate-structure
              ^smem-query.result <pred>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete)
}

#TODO make copy?
sp {retrieve-predicate-structure*elaborate*smem-query*result
   (state <s> ^name retrieve-predicate-structure
              ^operator.name complete
              ^condition <co>
              ^superstate <ss>)
   (<s> ^smem-query.result <pred>)
   (<pred> ^predicate-structure.conditions <cond> ^handle <name> ^argnum <an>)
-->
   (<ss> ^predicate <pred> ^gtype predicate) 
   (<pred> ^nlp-set <nlps> ^formated predicate ^parameter-set <P124> ^potential-args <P125> ^link <co>)
(<P124> ^argnum <an>)
  (<nlps> ^nlp <cond> ^aname <name>)#^nlp <cs> <cs1>
}
   
