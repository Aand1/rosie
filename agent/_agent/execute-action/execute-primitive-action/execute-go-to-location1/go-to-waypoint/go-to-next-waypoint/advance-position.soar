sp {go-to-next-waypoint*propose*advance-position*drive-xy
   (state <s> ^name go-to-next-waypoint
              ^current-action.drive-command.status success
              ^current-action.position-set.next.next { <next> <> none })
-->
   (<s> ^operator <o> +)
   (<o> ^name advance-position)
}

sp {go-to-next-waypoint*apply*advance-position*change*next*position
   (state <s> ^name go-to-next-waypoint
              ^operator.name advance-position
              ^current-action.position-set <set>)
   (<set> ^next <cur>)
   (<cur> ^next <next>)
-->
   (<set> ^next <cur> -
          ^next <next>)
}

sp {go-to-next-waypoint*apply*advance-position*cleanup*command
   (state <s> ^name go-to-next-waypoint
              ^operator.name advance-position
              ^io.output-link <out>
              ^current-action <a>)
   (<a> ^{ <cmd-type> << face-command drive-command >> } <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^<cmd-type> <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
 