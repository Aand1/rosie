sp {go-to-next-waypoint*propose*init-go-to-next-waypoint
   (state <s> ^name go-to-next-waypoint
             -^current-action.position-set)
-->
   (<s> ^operator <o> + >)
   (<o> ^name init-go-to-next-waypoint)
}

sp {go-to-next-waypoint*apply*init-go-to-next-waypoint*create*position-set*no*door
   (state <s> ^name go-to-next-waypoint
              ^operator.name init-go-to-next-waypoint
              ^current-action <a>
              ^edge <edge>)
   (<edge> ^doorway false
           ^end <end>)
   (<end> ^x <ex> ^y <ey>)
-->
   (<a> ^position-set <pos>)
   (<pos> ^next <p1>)
   (<p1> ^x <ex> ^y <ey> ^next none)
}

sp {go-to-next-waypoint*apply*init-go-to-next-waypoint*create*position-set*door
   (state <s> ^name go-to-next-waypoint
              ^operator.name init-go-to-next-waypoint
              ^current-action <a>
              ^edge <edge>)
   (<edge> ^doorway true
           ^door_sx <dsx>
           ^door_sy <dsy>
           ^door_ex <dex>
           ^door_ey <dey>
           ^end <end>)
   (<end> ^x <ex> ^y <ey>)
-->
   (<a> ^position-set <pos>)
   (<pos> ^next <p1>)
   (<p1> ^x <dsx> ^y <dsy> ^next <p2>)
   (<p2> ^x <dex> ^y <dey> ^next <p3>)
   (<p3> ^x <ex> ^y <ey> ^next none)
}