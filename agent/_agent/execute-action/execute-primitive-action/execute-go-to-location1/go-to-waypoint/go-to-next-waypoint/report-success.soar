sp {go-to-next-waypoint*propose*report-success
   (state <s> ^name go-to-next-waypoint
              ^center-command.status success)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-success)
}

sp {go-to-next-waypoint*propose*report-success*drive-xy
   (state <s> ^name go-to-next-waypoint
              ^drive-command <cmd>)
   (<cmd> ^name drive-xy
          ^status success)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-success)
}

sp {go-to-next-waypoint*prefer*attend
   (state <s> ^name go-to-next-waypoint
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change)
   (<o2> ^name report-success)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {go-to-next-waypoint*apply*report-success
   (state <s> ^name go-to-next-waypoint
              ^operator.name report-success)
-->
   (<s> ^status complete)
}

sp {go-to-next-waypoint*apply*report-success*cleanup*command
   (state <s> ^name go-to-next-waypoint
              ^operator.name report-success
              ^io.output-link <out>)
   (<s> ^{ <cmd-type> << face-command drive-command turn-command center-command >> } <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<s> ^<cmd-type> <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
   