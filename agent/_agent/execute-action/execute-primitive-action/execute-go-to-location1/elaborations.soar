sp {op_go-to-location*elaborate*desired-location
   (state <s> ^name op_go-to-location1
              ^execution-operator.arg2.{2} <loc>)
   (<loc> -^predicates.spatial-shape end1
          -^linked-to)
-->
   (<s> ^desired-location <loc>)
}

sp {op_go-to-location*elaborate*desired-location*linked-to
   (state <s> ^name op_go-to-location1
              ^execution-operator.arg2.{2}.linked-to <loc>)
   (<loc> -^predicates.spatial-shape end1)
-->
   (<s> ^desired-location <loc>)
}

sp {op_go-to-location*elaborate*desired-waypoint
   (state <s> ^name op_go-to-location1
              ^desired-location.waypoint <wp-handle>
              ^top-state.maps.map.waypoint <wp>)
   (<wp> ^handle <wp-handle>)
-->
   (<s> ^desired-waypoint <wp>)
}

sp {op_go-to-location*elaborate*until-clause*from*current-action
   (state <s> ^name op_go-to-location1
              ^execution-operator.until-clause <until>)
-->
   (<s> ^until-clause <until>)
}
