#sp {execute-recall*interrupt
#   (state <s> ^problem-space.name execute-primitive-action
#              ^action-handle recall1)
#-->
#   (interrupt)
#}

sp {execute-recall*elaborate*epmem-query
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle recall1)
-->
   (<s> ^epmem-query <q>)
   (<q> ^world <w>)
   (<w> ^objects <objs>
        ^predicates <preds>)
}

sp {execute-recall*elaborate*epmem-query*object*arg
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle recall1
              ^execution-operator.<arg-name> <arg>
              ^epmem-query.world.objects <objs>)
   (<arg> ^arg-type << object location >>
          ^id <obj>)
-->
   (<s> ^deep-copy-object <dco>)
   (<dco> ^source <obj>
          ^copy <new-obj>)
   (<new-obj> ^predicates <preds>)
   (<objs> ^object <new-obj>)
}

sp {execute-recall*elaborate*epmem-query*deep-copy-object*predicates
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle recall1
              ^deep-copy-object <dco>)
   (<dco> ^source.predicates <src-preds>
          ^copy.predicates <copy-preds>)
   (<src-preds> ^{ <prop-name> << category name shape property >> } <pred-name>)
-->
   (<copy-preds> ^<prop-name> <pred-name>)
}
   
sp {execute-recall*elaborate*epmem-query*predicate*arg
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle recall1
              ^execution-operator.<arg1-name> <arg1>
              ^execution-operator.<arg2-name> <arg2>
              ^epmem-query.world.objects <objs>
              ^epmem-query.world.predicates <preds>
              ^deep-copy-object <dco1>)
   (<arg1> ^arg-type << object location >>
           ^id <obj1>)
   (<arg2> ^arg-type predicate
           ^handle <pred-handle>
           ^2 <obj2>)
   (<dco1> ^source <obj1>
           ^copy <new-obj1>)
-->
   (<s> ^deep-copy-object <dco2>)
   (<dco2> ^source <obj2>
           ^copy <new-obj2>)
   (<new-obj2> ^predicates <preds2>)
   (<objs> ^object <new-obj2>)
   (<preds> ^predicate <new-pred>)
   (<new-pred> ^handle <pred-handle>
               ^instance <i>)
   (<i> ^1 <new-obj1> ^2 <new-obj2>)
}
   