sp {execute-put-down1*propose*send-put-down-command
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle put-down1
              ^projection-result <res>
              ^world.robot.arm.action wait
             -^current-action.put-down-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-put-down-command
        ^destination <res>)
}

# Put the command on the output link
sp {execute-put-down1*apply*send-put-down-command*send
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle put-down1
              ^operator <o>
              ^io.output-link <out>
              ^current-action <a>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (<out> ^put-down <cmd>)
   (<cmd> ^location <loc>)
   (<loc> ^x <x> ^y <y> ^z <z>)
   (<a> ^put-down-command <cmd>)
}

# Relocate the belief object in svs
sp {execute-put-down1*apply*send-put-down-command*relocate*object
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle put-down1
              ^operator <o>
              ^held-object.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (write |Moving object to tracked location| (crlf))
   (<cmd> ^set_transform <setxform>)
   (<setxform> ^id <id>
               ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}

              
