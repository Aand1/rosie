sp {execute-scan*propose*report-success
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle scan1
              ^current-action.turn-command.status success)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}


sp {execute-scan*apply*report-success*add*status
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle scan1
              ^operator.name report-success
              ^current-action <a>)
-->
   (<a> ^status complete)
}

sp {execute-scane*apply*report-success*mark*current-location*scanned
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle scan1
              ^operator.name report-success
              ^top-state.world.robot <rob>)
   (<rob> ^current-location <loc>
          ^havent-scanned <loc>)
-->
   (<rob> ^havent-scanned <loc> -
          ^scanned <loc>)
}

sp {execute-scan*apply*report-success*remove*command
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle scan1
              ^operator.name report-success
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^turn-command <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^turn-command <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
              