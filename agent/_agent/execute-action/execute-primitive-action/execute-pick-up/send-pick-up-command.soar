###############################################################################
#
# Inside the execute-pick-up1 substate, this will put a grab command on the output link
# If the pick-up fails and needs to be re-issued, this will cause that to occur
#
###############################################################################

sp {execute-pick-up1*propose*send-pick-up-command
   (state <s> ^name execute-pick-up1
              ^target-object <obj>
              ^world.robot.arm.action wait
              -^world.robot.arm.state internal
             -^current-action.pick-up-command)
   (<obj> ^predicates.visible true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-pick-up-command
        ^object <obj>)
}

sp {execute-pick-up1*apply*send-pick-up-command*send
   (state <s> ^name execute-pick-up1
              ^operator <o>
              ^io.output-link <ol>
              ^current-action <a>)
   (<o> ^name send-pick-up-command
		^object.handle <obj-handle>)
-->
   (<ol> ^pick-up <cmd>)
   (<cmd> ^object-handle <obj-handle>)
   (<a> ^pick-up-command <cmd>)
}

sp {execute-pick-up1*apply*send-pick-up-command*mark*object*moving
   (state <s> ^name execute-pick-up1
              ^operator <o>)
   (<o> ^name send-pick-up-command
        ^object.predicates <preds>)
-->
   (<preds> ^arm-status moving)
}

sp {execute-pickup*apply*send-pick-up-command*remove*reset-command
   (state <s> ^name execute-pick-up1
              ^operator.name send-pick-up-command
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^reset-command <cmd>)
   (<out> ^reset <cmd>)
-->
   (<a> ^reset-command <cmd> -)
   (<out> ^reset <cmd> -)
}
