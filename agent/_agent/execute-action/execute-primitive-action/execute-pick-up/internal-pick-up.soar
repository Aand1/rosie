sp {execute-pick-up*propose*internal-pick-up
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^target-object <obj>
              ^top-state.world.robot.arm.state internal)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-pick-up
        ^object <obj>)
}

sp {execute-pick-up*skip*ask
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
               ^top-state.agent-params.confirm-arm-actions no
              ^current-action <a>)
-->
   (<a> ^received-yes true)
}

sp {execute-pick-up*apply*internal-pick-up*send-confirmation
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator <o>
               ^top-state.agent-params.confirm-arm-actions no
              ^current-action.received-yes true
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-pick-up)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type simple-message
          ^message-type confirm-pick-up)
}

sp {execute-pick-up*apply*internal-pick-up*send-message
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^operator <o>
              ^top-state.agent-params.confirm-arm-actions yes
              ^current-action <a>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-pick-up
        ^object.predicates.{ << shape name >> } <shape>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type get-item-request
          ^item <shape>)
   (<a> ^sent-message true)
}

sp {execute-pick-up*apply*internal-pick-up*mark*complete
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator.name internal-pick-up
              ^current-action <a>
              ^top-state.action-stack.changes <c>)
   (<a> ^received-yes true)
-->
   (<c> ^pop <a>)
}

sp {execute-pick-up*apply*internal-pick-up*mark*grabbed
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator <o>
              ^current-action.received-yes true)
   (<o> ^name internal-pick-up
        ^object.predicates <preds>)
-->
   (<preds> ^arm-status grabbed)
}

sp {execute-pick-up*apply*report-success*remove*in*relation*grabbed
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator <o>
              ^current-action.received-yes true
              ^top-state.world <world>)
   (<o> ^name internal-pick-up
        ^object <obj>)
   (<world> ^predicates.predicate <pred>)
   (<pred> ^handle in1
           ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
   (<loc> ^predicates.category location)
-->
   (<pred> ^instance <i> -)
}
