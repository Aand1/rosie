sp {execute-pick-up1*elaborate*target-object*superoperator
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^execution-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

sp {execute-pick-up1*elaborate*perception-obj
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
               ^top-state.perception-monitor.object-monitor.object-info <obj-info>
               ^target-object <obj>)
   (<obj-info> ^wm-obj <obj>
               ^perception-obj <perc-obj>)
-->
    (<s> ^perception-obj <perc-obj>)
}

sp {execute-pick-up1*elaborate*no-new-objects*true
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
             -^top-state.perception-monitor.object-monitor.new-object-info)
-->
   (<s> ^no-new-objects true)
}

sp {execute-pick-up1*elaborate*no-new-objects*false
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^top-state.perception-monitor.object-monitor.new-object-info)
-->
   (<s> ^no-new-objects false)
}

sp {execute-pick-up1*elaborate*executing-command*true
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
               ^current-action.{ << drive-to-xy-command pick-up-command reset-command >> } <cmd>)
-->
    (<s> ^executing-command true)
}