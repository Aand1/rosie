# Don't attend to stale-object on the object being grabbed
sp {execute-pick-up1*reject*attend-to-stale-object
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^operator <o> +
              ^target-object <obj>)
   (<o> ^name attend-to-scene-change
        ^change.type stale-object
        ^change.object-info.wm-obj <obj>)
-->
   (<s> ^operator <o> -)
}

## Don't attend to new objects while the arm is moving
sp {execute-pick-up1*reject*attend-to-new-object
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^operator <o> +
              ^current-action.pick-up-command <cmd>)
   (<o> ^name attend-to-scene-change
        ^change.type new-object)
   (<cmd> -^status)
-->
   (<s> ^operator <o> -)
}

# Don't attend at all to the object being picked up while the arm is moving
sp {execute-pick-up1*reject*attend-to-scene-change*grabbed*object
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^operator <o> +
              ^target-object.handle <handle>
              ^current-action.pick-up-command <cmd>)
   (<o> ^name attend-to-scene-change
        ^change.object-info.object-handle <handle>)
   (<cmd> -^status)
-->
   (<s> ^operator <o> -)
}

sp {execute-pick-up1*elaborate*evaluate-new-object*candidate-for-missing-object
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^operator <o> +
              ^target-object.handle <obj-handle>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name attend-to-scene-change
        ^change.type new-stable-object
        ^change.object-info <cand>)
   (<obj-info> ^object-handle <obj-handle>)
-->
   (<cand> ^candidate-for-missing-object <obj-info>)
}

