# Report failure if there are no more candidates for the missing object
sp {execute-pick-up1*propose*report-failure
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^current-action.pick-up-command.status failure
             -^top-state.perception-monitor.object-monitor.new-object-info.status
              ^target-object.predicates.visible false)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-failure)
}

sp {execute-pick-up1*apply*report-failure*remove-arm-status
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^operator.name report-failure
              ^target-object.predicates <preds>)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -)
}


# Report a failure, may lead to more interactions
sp {execute-pick-up1*apply*report-failure*report*failure
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^operator.name report-failure
              ^target-object <obj>
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure <f>)
   (<f> ^type missing-object
        ^missing-object <obj>)
}

# Remove the commands from the output-link/current-action
sp {execute-pick-up1*apply*report-failure*remove*commands
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle pick-up1
              ^current-action <a>
              ^operator.name report-failure
              ^io.output-link <out>)
   (<a> ^{ <cmd-type> << pick-up-command reset-command >> } <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^<cmd-type> <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
