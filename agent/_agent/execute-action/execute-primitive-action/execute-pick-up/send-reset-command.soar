###############################################################################
#
# Inside the execute-pick-up1 substate, this will put a reset command on the output link
# If the pick-up fails this will reset the arm
#
###############################################################################

sp {execute-pick-up1*propose*send-reset-command
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^world.robot.arm.action << failure home >>
             -^current-action.reset-command)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-reset-command
        ^actions.reset <r>)
}

sp {execute-pickup*apply*send-reset-command
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator.name send-reset-command
              ^io.output-link <ol>
              ^current-action <a>)
-->
   (<ol> ^reset <r>)
   (<a> ^reset-command <r>)
}

sp {execute-pickup*apply*send-reset-command*remove*pick-up-command
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator.name send-reset-command
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^pick-up-command <cmd>)
   (<out> ^pick-up <cmd>)
-->
   (<a> ^pick-up-command <cmd> -)
   (<out> ^pick-up <cmd> -)
}
