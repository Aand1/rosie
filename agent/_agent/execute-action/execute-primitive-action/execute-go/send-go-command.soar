###############################################################################
#
# Inside the execute-go1 substate, this will put a go command on the output link
#
###############################################################################

sp {execute-go1*propose*send-go-command
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle go1
             -^current-action.arguments.arg2
             -^current-action.go-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-go-command)
}

sp {execute-go1*apply*send-go-command*no*until
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle go1
              ^operator.name send-go-command
              ^current-action <a>
              ^top-state.io.output-link <out>
             -^until-clause)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name drive-forward
          ^termination-condition.name stabilized)
   (<a> ^go-command <law>)
}

sp {execute-go1*apply*send-go-command*until
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle go1
              ^operator.name send-go-command
              ^current-action <a>
              ^top-state.io.output-link <out>
              ^until-clause <until>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name drive-forward
          ^termination-condition <until>)
   (<a> ^go-command <law>)
}