sp {execute-go*propose*report-success
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle go1
              ^current-action.go-command.status success)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {execute-go*apply*report-success*add*status
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle go1
              ^operator.name report-success
              ^current-action <a>)
-->
   (<a> ^status complete)
}

sp {execute-go*apply*report-success*remove*command
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle go1
              ^operator.name report-success
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^go-command <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^go-command <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
              