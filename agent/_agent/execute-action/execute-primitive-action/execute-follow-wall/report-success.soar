sp {execute-follow-wall*propose*report-success
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle follow-wall1
              ^current-action.follow-wall-command.status success)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {execute-follow-wall*apply*report-success*add*status
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle follow-wall1
              ^operator.name report-success
              ^current-action <a>)
-->
   (<a> ^status complete)
}

sp {execute-follow-wall*apply*report-success*remove*command
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle follow-wall1
              ^operator.name report-success
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^follow-wall-command <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^follow-wall-command <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
              