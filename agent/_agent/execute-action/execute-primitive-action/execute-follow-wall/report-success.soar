sp {op_follow-wall*propose*report-success*finished
   (state <s> ^name op_follow-wall1
              ^current-action.follow-wall-command.status success)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {op_follow-wall*propose*report-success*stopped
   (state <s> ^name op_follow-wall1
              ^current-action.stop-command.status success)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {op_follow-wall*apply*report-success*add*status
   (state <s> ^name op_follow-wall1
              ^operator.name report-success
              ^current-action <a>)
-->
   (<a> ^status complete)
}

sp {op_follow-wall*apply*report-success*remove*follow-wall*command
   (state <s> ^name op_follow-wall1
              ^operator.name report-success
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^follow-wall-command <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^follow-wall-command <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
              
sp {op_follow-wall*apply*report-success*remove*stop*command
   (state <s> ^name op_follow-wall1
              ^operator.name report-success
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^stop-command <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^stop-command <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}
