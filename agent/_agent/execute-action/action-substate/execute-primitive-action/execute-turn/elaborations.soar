sp {execute-turn1*elaborate*direction*from*superoperator
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle turn1
              ^execution-operator.arg1.handle <dir-handle>)
-->
   (<s> ^direction-handle <dir-handle>)
}

sp {execute-turn1*elaborate*smem-query-cue*direction
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle turn1
              ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
   (<cue> ^handle <dir-handle>)
}

sp {execute-turn1*elaborate*direction-id*from*retrieval
   (state <s> ^problem-space.name execute-primitive-action
              ^action-handle turn1
              ^direction-handle <dir-handle>
              ^smem-query.result <dir-id>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}
