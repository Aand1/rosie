sp {execute-pick-up*propose*link-objects
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^target-object <obj>)
   (<obj> ^handle <dest-handle>
          ^found-as <src>)
   (<src> ^handle <src-handle>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^type link
          ^destination-handle <dest-handle>
          ^source-handle <src-handle>)
}
          
sp {execute-pick-up*apply*link-objects*send*modify-scene*command
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
-->
   (<out> ^modify-scene <cmd>)
}

sp {execute-pick-up*apply*link-objects*found-as*mark-for-deletion
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator.name link-objects
              ^target-object.found-as <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^wm-obj <obj>)
-->
   (<obj-info> ^mark-for-deletion true)
}
   

sp {execute-pick-up*apply*link-objects*remove*missing*flag
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator.name link-objects
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^target-object <obj>)
   (<obj-info> ^wm-obj <obj>
               ^missing true)
-->
   (<obj-info> ^missing true -)
}

sp {execute-pick-up*apply*link-objects*remove*found-as*flag
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator.name link-objects
              ^target-object <obj>)
   (<obj> ^found-as <other>)
-->
   (<obj> ^found-as <other> -)
}

sp {execute-pick-up*apply*link-objects*monitor*perception
   (state <s> ^problem-space.name execute-primitive-action
               ^action-handle pick-up1
              ^operator <o>
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^source-handle <src>
          ^destination-handle <dest>)
-->
   (write |Linking object | <src> | to | <dest> (crlf))
}
        
        
