# name of problem-space is execute-composite-action
sp {execute-composite-action*elaborate*name
   (state <s> ^superstate.name execute-action
              ^superstate.operator.action-type composite
              ^problem-space <ps>)
-->
   (<ps> ^name execute-composite-action)
}

# copy action-operator from superstate.operator
sp {execute-composite-action*elaborate*action-operator
   (state <s> ^problem-space.name execute-composite-action
              ^superstate.operator <op>)
-->
   (<s> ^action-operator <op>)
}

# copy current-action from superstate
sp {execute-composite-action*elaobrate*current-action
   (state <s> ^problem-space.name execute-composite-action
              ^superstate.current-action <action>)
-->
   (<s> ^current-action <action>)
}

# elaborate current-action.prev as parent-action
sp {execute-composite-action*elaborate*parent-action
   (state <s> ^problem-space.name execute-composite-action
              ^current-action.prev { <prev> <> none })
-->
   (<s> ^parent-action <prev>)
}

# elaborate current-action.next as child-action
sp {execute-composite-action*elaborate*child-action
   (state <s> ^problem-space.name execute-composite-action
              ^current-action.next { <next> <> none })
-->
   (<s> ^child-action <next>)
}

# elaborate child-action.execution-operator as action-to-match
sp {execute-composite-action*elaborate*action-to-match*child
   (state <s> ^problem-space.name execute-composite-action
              ^child-action.execution-operator <op>)
-->
   (<s> ^action-to-match <op>)
}

# elaborate action-to-match.subaction-matched true if a match exists under operator-matches
sp {execute-composite-action*elaborate*subaction-matched*true
   (state <s> ^problem-space.name execute-composite-action
              ^action-to-match <a>
              ^operator-matches.match-info.op-matched yes)
-->
   (<s> ^subaction-matched true)
}

# elaborate action-to-match.subaction-matches false if a match doesn't exist under operator-matches
sp {execute-composite-action*elaborate*subaction-matched*false
   (state <s> ^problem-space.name execute-composite-action
              ^action-to-match <a>
              ^operator-matches <matches>)
   (<matches> -^match-info.op-matched yes)
-->
   (<s> ^subaction-matched false)
}
   
# mark the action as complete when desired.satisfied is true
sp {execute-composite-action*elaborate*current-action*complete
   (state <s> ^problem-space.name execute-composite-action
              ^current-action <action>
              ^desired.satisfied true)
-->
   (<action> ^status complete)
}

# elaborate until-clause onto the state from the aciton-operator
sp {execute-composite-action*elaborate*until-clause
   (state <s> ^problem-space.name execute-composite-action
              ^action-operator.until-clause <until>)
-->
   (<s> ^until-clause <until>)
}