sp {execute-composite-action*elaborate*name
   (state <s> ^superstate.name execute-action
              ^superstate.operator.action-type composite
              ^problem-space <ps>)
-->
   (<ps> ^name execute-composite-action)
}

sp {execute-composite-action*elaborate*action-operator
   (state <s> ^problem-space.name execute-composite-action
              ^superstate.operator <op>)
-->
   (<s> ^action-operator <op>)
}

sp {execute-composite-action*elaobrate*current-action
   (state <s> ^problem-space.name execute-composite-action
              ^superstate.current-action <action>)
-->
   (<s> ^current-action <action>)
}

sp {execute-composite-action*elaborate*parent-action
   (state <s> ^problem-space.name execute-composite-action
              ^current-action.prev { <prev> <> none })
-->
   (<s> ^parent-action <prev>)
}

sp {execute-composite-action*elaborate*child-action
   (state <s> ^problem-space.name execute-composite-action
              ^current-action.next { <next> <> none })
-->
   (<s> ^child-action <next>)
}

sp {execute-composite-action*elaborate*action-to-match*child
   (state <s> ^problem-space.name execute-composite-action
              ^child-action.execution-operator <op>)
-->
   (<s> ^action-to-match <op>)
}

sp {execute-composite-action*elaborate*subaction-matched*true
   (state <s> ^problem-space.name execute-composite-action
              ^action-to-match <a>
              ^operator-matches.match-info.op-matched yes)
-->
   (<s> ^subaction-matched true)
}

sp {execute-composite-action*elaborate*subaction-matched*false
   (state <s> ^problem-space.name execute-composite-action
              ^action-to-match <a>
              ^operator-matches <matches>)
   (<matches> -^match-info.op-matched yes)
-->
   (<s> ^subaction-matched false)
}
   

sp {execute-composite-action*elaborate*current-action*complete
   (state <s> ^problem-space.name execute-composite-action
              ^current-action <action>
              ^desired.satisfied true)
-->
   (<action> ^status complete)
}

sp {execute-composite-action*elaborate*until-clause
   (state <s> ^problem-space.name execute-composite-action
              ^action-operator.until-clause <until>)
-->
   (<s> ^until-clause <until>)
}