sp {execute-explore*elaborate*handle-manually*true
   (state <s> ^name op_explore1
              ^problem-space.action.type execute)
-->
   (<s> ^handle-manually true)
}

sp {execute-explore*elaborate*until-clause
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^superstate.operator.until-clause <until>)
-->
   (<s> ^until-clause <until>)
}

sp {execute-explore*elaborate*distance-map
   (state <s> ^name op_explore1
              ^problem-space.action.type execute)
-->
   (<s> ^distance-map <map>)
}

sp {execute-explore*elaborate*distance-map*distance*0
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^distance-map <map>
              ^world.robot.current-waypoint.handle <wp-handle>)
-->
   (<map> ^<wp-handle> 0)
}

sp {execute-explore*elaborate*distance-map*neighbors
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^distance-map <map>
              ^top-state.maps.map.waypoint <wp>)
   (<map> ^<wp-handle> <d>)
   (<wp> ^handle <wp-handle>
         ^next.handle <next-handle>)
   -{(<map> ^<next-handle> <= <d>)}
-->
   (<map> ^<next-handle> (+ <d> 1))
}


   
              