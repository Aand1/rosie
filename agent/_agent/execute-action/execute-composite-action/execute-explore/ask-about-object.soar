sp {execute-explore*propose*ask-about-object
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^world.robot.current-location <loc>
              ^until-clause.predicate <pred>)
   (<loc> -^searched true)
   (<pred> ^handle visible1
           ^1 <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name ask-about-object
        ^object <obj>)
}

sp {execute-explore*apply*ask-about-object*send-message*ask-about-item
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^operator <o>
              ^top-state.interaction.changes <changes>)
   (<o> ^name ask-about-object
        ^object.predicates.{ << shape name >> } <name>)
-->
   (<changes> ^outgoing-message <msg>)
   (<msg> ^type ask-about-item
          ^item <name>)
}

sp {execute-explore*apply*ask-about-object*push-segment
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^operator <o>
              ^top-state.interaction.changes <changes>)
   (<o> ^name ask-about-object
        ^object <obj>)
-->
   (<changes> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type ask-about-object
        ^parameters.object <obj>
   	^satisfaction.dialog-event <de>)
   (<de> ^type yes
         ^type no
         ^originator instructor)
}
             
sp {execute-explore*apply*ask-about-object*mark*searched
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^operator.name ask-about-object
              ^world.robot.current-location <loc>)
-->
   (<loc> ^searched true)
}
