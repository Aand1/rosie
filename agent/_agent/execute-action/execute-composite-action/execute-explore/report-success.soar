sp {execute-explore*state*no-change*propose*report-success
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^until-clause <until>
              ^satisfied <until>)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-success)
}

sp {execute-explore*state*no-change*apply*report-success*pop*action
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^operator.name report-success
              ^current-action <a>
              ^top-state.action-stack.changes <changes>)
-->
   (<a> ^status complete)
   (<changes> ^pop <a>)
}

sp {execute-explore*state*no-change*apply*report-success*remove*visited
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^operator.name report-success
              ^top-state.world.objects.object <obj>)
   (<obj> ^visited true)
-->
   (<obj> ^visited true -)
}
   
sp {execute-explore*state*no-change*apply*report-success*remove*searched
   (state <s> ^name op_explore1
              ^problem-space.action.type execute
              ^operator.name report-success
              ^top-state.world.objects.object <obj>)
   (<obj> ^searched true)
-->
   (<obj> ^searched true -)
}
