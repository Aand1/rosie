sp {execute-explore*state*no-change*propose*report-failure
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate.action-handle explore1)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-failure)
}

sp {execute-explore*state*no-change*apply*report-failure*send-message*execution-failure
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate.action-handle explore1
              ^operator.name report-failure
              ^superstate.current-action <a>
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure.type execution-failure)
}

sp {execute-explore*state*no-change*apply*report-failure*pop*action
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate.action-handle explore1
              ^operator.name report-failure
              ^superstate.current-action <a>
              ^top-state.action-stack.changes <changes>)
-->
   (<changes> ^pop <a>)
}

sp {execute-explore*state*no-change*apply*report-failure*remove*visited
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate.action-handle explore1
              ^operator.name report-failure
              ^top-state.world.objects.object <obj>)
   (<obj> ^visited true)
-->
   (<obj> ^visited true -)
}
   

