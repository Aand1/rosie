sp {apply*interpret-motion-request*propose-create-target
	(state <s> ^name interpret-motion-request
               ^topstate <t>
              -^plan)
  	(<t> ^request)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name create-target)
}

sp {apply*interpret-motion-request*apply-create-target
	(state <s> ^name interpret-motion-request
               ^operator.name create-target
               ^topstate <t>)
 	(<t> ^request.target <r>)
-->
	(<s> ^plan <pla>)
 	(<pla> ^target <r>)
}

sp {apply*interpret-motion-request*elaborate-plan-type
	(state <s> ^name interpret-motion-request
               ^topstate <t>)
 	(<t> ^request <r>)
  	(<s> ^plan <pla>)
  	(<r> ^type <tp>)
-->
 	(<pla> ^type <tp>)
}

sp {apply*interpret-motion-request*elaborate-target-step-size
	(state <s> ^name interpret-motion-request
               ^topstate <t>)
 	(<t> ^request <r>)
  	(<s> ^plan <pla>)
  	(<r> ^step-size <ss>)
-->
 	(<pla> ^step-size <ss>)
}

sp {apply*interpret-motion-request*elaborate-target-step-size*default
	(state <s> ^name interpret-motion-request
               ^topstate <t>)
 	(<t> ^request <r>)
  	(<s> ^plan <pla>)
  	(<r> -^step-size)
-->
 	(<pla> ^step-size 0.5)
}

sp {apply*interpret-motion-request*propose-put-output-link
	(state <s> ^name interpret-motion-request
               ^topstate.io.output-link <ol>
               ^plan <pla>)
 	(<pla> ^target
          	  ^step-size)
  	(<ol> -^plan)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name put-output-link)
}

sp {apply*interpret-motion-request*apply-put-output-link
	(state <s> ^name interpret-motion-request
               ^operator.name put-output-link
               ^topstate.io.output-link <ol>
               ^plan <pla>)
-->
	(<ol> ^plan <pla>)
}