sp {apply*interpret-motion-request*propose-create-target
	(state <s> ^name interpret-motion-request
               ^topstate <t>
              -^search)
  	(<t> ^request)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name create-target)
}

sp {apply*interpret-motion-request*apply-create-target
	(state <s> ^name interpret-motion-request
               ^operator.name create-target
               ^topstate <t>)
 	(<t> ^request.target <r>)
  	(<r> ^x <xval>
         ^y <yval>
         ^z <zval>)
-->
	(<s> ^search <sr>)
 	(<sr> ^target <targ>)
 	(<targ> ^x <xval>
 		    ^y <yval>
         	^z <zval>)
}

sp {apply*interpret-motion-request*elaborate-target-step-size
	(state <s> ^name interpret-motion-request
               ^topstate <t>)
 	(<t> ^request <r>)
  	(<s> ^search <search>)
  	(<r> ^step-size <ss>)
-->
 	(<search> ^step-size <ss>)
}

sp {apply*interpret-motion-request*elaborate-target-step-size*default
	(state <s> ^name interpret-motion-request
               ^topstate <t>)
 	(<t> ^request <r>)
  	(<s> ^search <search>)
  	(<r> -^step-size)
-->
 	(<search> ^step-size 0.5)
}

sp {apply*interpret-motion-request*propose-put-output-link
	(state <s> ^name interpret-motion-request
               ^topstate.io.output-link <ol>
               ^search <search>)
 	(<search> ^target
          	  ^step-size)
  	(<ol> -^search)
-->
	(<s> ^operator <op> + =)
 	(<op> ^name put-output-link)
}

sp {apply*interpret-motion-request*apply-put-output-link
	(state <s> ^name interpret-motion-request
               ^operator.name put-output-link
               ^topstate.io.output-link <ol>
               ^search <search>)
-->
	(<ol> ^search <search>)
}