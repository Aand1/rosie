### elaborate state 
sp {elaborate*state*name*action*operator-tie
   (state <s> ^superstate.name action
   	      ^attribute operator
	      ^impasse tie)
-->
   (<s> ^name multiple-argument-instantiations)
}

sp {multiple-argument-instantiations*elaborate*indexed-action
   (state <s> ^name multiple-argument-instantiations
   	      ^superstate.indexed-action <ia>)
-->
   (<s> ^indexed-action <ia>)
}


sp {multiple-argument-instantiations*elaborate*indexable-objects*direct-obect
   (state <s> ^name multiple-argument-instantiations
   	      ^superstate.superstate.operator.purpose.parameters.indexable <param>)
   (<param> ^direct-object <do>)
-->
   (<s> ^object-link <do>)
}

sp {multiple-argument-instantiations*elaborate*indexable-objects*pp-object
   (state <s> ^name multiple-argument-instantiations
   	      ^superstate.superstate.operator.purpose.parameters.indexable <param>)
   (<param> ^pp-object.object <do>)
-->
   (<s> ^object-link <do>)
}

sp {multiple-argument-instantiations*elaborate*arguments
   (state <s> ^name multiple-argument-instantiations
   	      ^indexed-action <ia>)
   (<ia> ^{<arg> <> name} <val1>
   	 ^<arg> {<val2> <> <val1>})
-->
   (<s> ^multiple-instantiations-of <arg>)
}

sp {multiple-argument-instantians*elaborate*object-link*relevant
   (state <s> ^name multiple-argument-instantiations
   	      ^multiple-instantiations-of <arg>
	      ^indexed-action <ia>
	      ^object-link <ol>)
   (<ia> ^<arg> <val>)
   (<ol> ^result <val>)
-->
   (<s> ^ambiguous-indexable-parameter <ol>)
}

sp {multiple-object*apply*status*multiple-argument-instantiation
   (state <s> ^name multiple-argument-instantiations
   	      ^ambiguous-indexable-parameter <ol>
	      ^topstate.interaction.status <status>)
-->
   (<status> ^failure <f>)
   (<f> ^type index-object-tie
   	^target-object <ol>)
}

sp {multiple-argument-instantiotions*delete*result
   (state <s> ^name multiple-argument-instantiations
   	      ^ambiguous-indexable-parameter <ol>)
   (<ol> ^result <val>)
-->
   (<ol> ^result <val> -)
}
   
sp {multiple-argument-instantiations*elaborate*indexable-action*delete-result
   (state <s> ^name multiple-argument-instantiations
   	      ^superstate.superstate.operator.purpose.parameters.indexable <param>
	      ^ambiguous-indexable-parameter <o>)
   (<param> ^result <res>)
 -->
   (<param> ^result <res> -)
}
	      



