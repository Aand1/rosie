### elaborate state 
sp {elaborate*state*name*action*operator-tie*multiple-argument-instantiations
   (state <s> ^superstate <ss>
   	      ^attribute operator
	      ^impasse tie
       ^item.op-matched yes)
   (<ss> ^name action
   	 ^problemspace.action.type execute)
-->
   (<s> ^name multiple-argument-instantiations)
}

sp {elaborate*state*name*action*operator-tie*no-action-selection-knowledge
   (state <s> ^superstate.problemspace.action.type execute
              ^attribute operator
              ^impasse tie
             -^item.op-matched yes)
-->
   (<s> ^name no-action-selection-knowledge)
}
              

sp {multiple-argument-instantiations*elaborate*action-command
   (state <s> ^name multiple-argument-instantiations
              ^superstate.action-command <cmd>)
-->
   (<s> ^action-command <cmd>)
}

sp {multiple-argument-instantiations*elaborate*arguments
   (state <s> ^name multiple-argument-instantiations
   	      ^action-command <cmd>)
   (<cmd> ^_metadata.arg-name <arg-name>
          ^<arg-name> <arg1> { <arg2> <> <arg1> })
-->
   (<s> ^multiple-instantiations-of <arg-name>)
}


