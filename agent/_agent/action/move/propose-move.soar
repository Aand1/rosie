sp {action*propose*execute-move1*action-command
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^id.name move1
          ^arg1 <obj>
          ^arg2.arg2 <dest>
          ^arg2.id <rel>)
-->
   (<s> ^operator <o> +)
   (<o> ^name execute-move1
        ^arg1 <obj>
        ^arg2 <new-arg2>
        ^action-type composite)
   (<new-arg2> ^id <rel>
               ^arg2 <dest>)
}

sp {action*execute-move1*elaborate*problemspace
   (state <s> ^name execute-move1
              ^problemspace <p>)
-->
   (<p> ^action.type execute)
}

sp {action*execute-move1*prefer*pickup
   (state <s> ^name execute-move1
              ^superstate.operator.arg1 <obj>
              ^operator <o> +)
   (<o> ^name initiate-pick-up1
        ^arg1 <obj>)
   (<obj> -^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> >)
}

sp {action*execute-move1*elaborate*put-down*destination
   (state <s> ^name execute-move1
              ^superstate.operator.arg1 <arg1>
              ^superstate.operator.arg2 <arg2>
              ^operator <o> +)
   (<arg2> ^id <rel> ^arg2 <dest>)
   (<o> ^name initiate-put-down1
        ^arg1 <arg1>)
-->
   (<o> ^arg2 <new-arg2>)
   (<new-arg2> ^id <rel> ^arg2 <dest>)
}

sp {action*execute-move1*prefer*put-down
   (state <s> ^name execute-move1
              ^superstate.operator.arg1 <obj>
              ^operator <o> +)
   (<o> ^name initiate-put-down1
        ^arg1 <obj>)
   (<obj> ^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> >)
}

sp {action*execute-move1*elaborate*world*superstate
   (state <s> ^name execute-move1
              ^superstate.world <world>)
-->
   (<s> ^world <world>)
}

sp {action*execute-move1*propose*report-success
   (state <s> ^name execute-move1
              ^superstate.operator <sop>
              ^world.predicates.predicate <pred>)
   (<sop> ^arg1 <obj1>
          ^arg2.id.name <rel>
          ^arg2.arg2 <obj2>)
   (<pred> ^name <rel>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {action*execute-move1*apply*report-success
   (state <s> ^name execute-move1
              ^operator.name report-success
              ^topstate.interaction.status <status>)
-->
   (<status> ^performed.action-event.type move1)
}

sp {action*execute-move1*prefer*report-succes*over*other
   (state <s> ^name execute-move1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name report-success)
   (<o2> ^name initiate-pick-up1)
-->
   (<s> ^operator <o1> > <o2>)
}
