sp {action*propose*op_move*action-command
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^id.name move1
          ^arg1 <obj>
          ^arg2.arg2 <dest>
          ^arg2.id <rel>)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_move
        ^arg1 <obj>
        ^arg2 <new-arg2>
        ^action-type composite)
   (<new-arg2> ^id <rel>
               ^arg2 <dest>)
   (interrupt)
}

sp {action*op_move*elaborate*problemspace
   (state <s> ^name op_move
              ^problemspace <p>)
-->
   (<p> ^action.type execute)
}

sp {action*op_move*prefer*pickup
   (state <s> ^name op_move
              ^superstate.operator.arg1 <obj>
              ^operator <o> +)
   (<o> ^name op_pick-up-object
        ^arg1 <obj>)
   (<obj> -^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> >)
}

sp {action*op_move*elaborate*put-down*destination
   (state <s> ^name op_move
              ^superstate.operator.arg1 <arg1>
              ^superstate.operator.arg2 <arg2>
              ^operator <o> +)
   (<arg2> ^id <rel> ^arg2 <dest>)
   (<o> ^name op_put-down-object
        ^arg1 <arg1>)
-->
   (<o> ^arg2 <new-arg2>)
   (<new-arg2> ^id <rel> ^arg2 <dest>)
}

sp {action*op_move*prefer*put-down
   (state <s> ^name op_move
              ^superstate.operator.arg1 <obj>
              ^operator <o> +)
   (<o> ^name op_put-down-object
        ^arg1 <obj>)
   (<obj> ^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> >)
}

sp {action*op_move*elaborate*world*superstate
   (state <s> ^name op_move
              ^superstate.world <world>)
-->
   (<s> ^world <world>)
}

sp {action*op_move*propose*report-success
   (state <s> ^name op_move
              ^superstate.operator <sop>
              ^world.predicates.predicate <pred>)
   (<sop> ^arg1 <obj1>
          ^arg2.id.name <rel>
          ^arg2.arg2 <obj2>)
   (<pred> ^name <rel>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {action*op_move*apply*report-success
   (state <s> ^name op_move
              ^operator.name report-success
              ^topstate.interaction.status <status>)
-->
   (<status> ^performed.action-event.type move1)
}

sp {action*op_move*prefer*report-succes*over*other
   (state <s> ^name op_move
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name report-success)
   (<o2> ^name op_pick-up-object)
-->
   (<s> ^operator <o1> > <o2>)
}
