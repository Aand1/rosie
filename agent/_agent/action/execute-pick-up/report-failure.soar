# Report failure if there are no more candidates for the missing object
sp {execute-pick-up1*propose*report-failure
   (state <s> ^name execute-pick-up1
              ^current-action.command-status failure
             -^top-state.scene-monitor.new-object.status
              ^target-object.predicates.status stale)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-failure)
}

# Report a failure, may lead to more interactions
sp {execute-pick-up1*apply*report-failure*report*failure
   (state <s> ^name execute-pick-up1
              ^operator.name report-failure
              ^target-object <obj>
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure <f>)
   (<f> ^type missing-object
        ^missing-object <obj>)
}

# Remove the command-status so that if we come back and try it again later, 
#   we can start fresh
sp {execute-pick-up1*apply*report-failure*remove*status
   (state <s> ^name execute-pick-up1
              ^current-action <a>
              ^operator.name report-failure)
   (<a> ^command-status <status>)
-->
   (<a> ^command-status <status> -)
}
