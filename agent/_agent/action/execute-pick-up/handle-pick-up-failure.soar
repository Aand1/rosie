sp {topstate*apply*execute-pick-up*failure*remove*arm-status*moving
   (state <s> ^topstate <s>
              ^operator <o>
              ^current-action.command-status failure)
   (<o> ^name execute-pick-up
        ^arg1.predicates <preds>)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -)
}

sp {execute-pick-up*elaborate*attend-to-object-status*moved
   (state <s> ^name execute-pick-up
              ^current-action.command-status failure
              ^target-object.id <id>)
-->
   (<s> ^attend-to-object <id>)
}

sp {execute-pick-up*reject*attend-to-stale-object
   (state <s> ^name excute-pick-up
              ^current-action.command-status failure
              ^target-object.id <id>
              ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type stale-object
        ^change.object.id <id>)
-->
   (<s> ^operator <o> -)
}

sp {execute-pick-up*propose*handle-pick-up-failure
   (state <s> ^name execute-pick-up
              ^current-action.command-status failure)
-->
   (<s> ^operator <o> + >)
   (<o> ^name handle-pick-up-failure)
}

sp {execute-pick-up*apply*handle-pick-up-failure*remove*command-status
   (state <s> ^name execute-pick-up
              ^operator.name handle-pick-up-failure
              ^target-object.predicates.status live
              ^current-action <a>)
   (<a> ^command-status <status>)
-->
   (<a> ^command-status <status> -)
}

sp {execute-pick-up*apply*handle-pick-up-failure*remove*moving
   (state <s> ^name execute-pick-up
              ^operator.name handle-pick-up-failure
              ^target-object.predicates <preds>)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -)
}
