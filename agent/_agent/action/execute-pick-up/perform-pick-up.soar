###############################################################################
#
# Inside the execute-pick-up substate, this will put a grab command on the output link
# If the pick-up fails and needs to be re-issued, this will cause that to occur
#
###############################################################################

sp {execute-pick-up*propose*perform-pick-up
   (state <s> ^name execute-pick-up
              ^target-object <obj>
              -^world.robot.arm.action failure
             -^current-action.command-status)
   (<obj> ^predicates.status live)
-->
   (<s> ^operator <o> + >)
   (<o> ^name perform-pick-up
        ^object <obj>)
}

sp {execute-pick-up*apply*perform-pick-up*send
   (state <s> ^name execute-pick-up
              ^operator <o>
              ^io.output-link <ol>
              ^current-action <a>)
   (<o> ^name perform-pick-up
        ^object.id <id>)
-->
   (<ol> ^pick-up <cmd>)
   (<cmd> ^object-id <id>)
   (<a> ^sent-command <cmd>)
}


sp {execute-pick-up*apply*perform-pick-up*mark*object*moving
   (state <s> ^name execute-pick-up
              ^operator <o>)
   (<o> ^name perform-pick-up
        ^object.predicates <preds>)
-->
   (<preds> ^arm-status moving)
}

sp {perform-pick-up*elaborate*attend-to-all-scene-changes*true
   (state <s> ^name perform-pick-up)
-->
   (<s> ^attend-to-all-scene-changes true)
}

sp {perform-pick-up*elaborate*allow-wait*true
   (state <s> ^name perform-pick-up)
-->
   (<s> ^allow-wait true)
}

sp {execute-pick-up*apply*perform-pick-up*copy*command-status
   (state <s> ^name execute-pick-up
              ^operator.name perform-pick-up
              ^current-action <a>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status <status>)
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>)
}