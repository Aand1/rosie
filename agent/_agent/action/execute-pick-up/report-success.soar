sp {execute-pick-up*propose*report-success
   (state <s> ^name execute-pick-up
              ^current-action.command-status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-success)
}

sp {execute-pick-up*apply*report-success*mark*finished
   (state <s> ^name execute-pick-up
              ^operator.name report-success
              ^target-object.predicates.arm-status grabbed
              ^current-action <a>)
-->
   (<a> ^status complete)
}

sp {execute-pick-up*apply*report-success*add*grabbed
   (state <s> ^name execute-pick-up
              ^operator.name report-success
              ^target-object.predicates <preds>)
-->
   (<preds> ^arm-status grabbed)
}

sp {execute-pick-up*apply*report-success*move*belief*to*arm
   (state <s> ^name execute-pick-up
              ^operator.name report-success
              ^target-object.svs-obj.id <id>
              ^io.input-link.self.pose <pose>
              ^topstate.svs.command <cmd>)
   (<pose> ^x <x> ^y <y> ^z <z>)
-->
   (<cmd> ^set_transform <xform>)
   (<xform> ^id <id>
            ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}