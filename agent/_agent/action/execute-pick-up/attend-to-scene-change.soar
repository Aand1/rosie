sp {execute-pick-up1*elaborate*attend-to-all-scene-changes*true
   (state <s> ^name execute-pick-up1
              ^current-action.command-status <status>)
-->
   (<s> ^attend-to-robot true)
}

sp {execute-pick-up1*elaborate*attend-to-object*finished
   (state <s> ^name execute-pick-up1
              ^current-action.command-status <status>
              ^target-object.id <id>)
-->
   (<s> ^attend-to-object <id>)
}

sp {execute-pick-up1*elaborate*attend-to-all-scene-changes*missing*object
   (state <s> ^name execute-pick-up1
              ^current-action.command-status failure
              ^target-object.predicates.status stale)
-->
   (<s> ^attend-to-all-scene-changes true)
}

# Don't attend to stale-object on the object being grabbed
sp {execute-pick-up1*reject*attend-to-stale-object
   (state <s> ^name execute-pick-up1
              ^operator <o> +
              ^target-object.id <id>)
   (<o> ^name attend-to-scene-change
        ^change.type stale-object
        ^change.object.id <id>)
-->
   (<s> ^operator <o> -)
}

# Don't attend to new objects
sp {execute-pick-up1*reject*attend-to-new-object
   (state <s> ^name execute-pick-up1
              ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type new-object)
-->
   (<s> ^operator <o> -)
}

#sp {execute-pick-up1*best*preference*attend-to-object*target
#   (state <s> ^name execute-pick-up1
#              ^operator <o> +
#              ^target-object.id <id>
#              ^current-action.command-status failure)
#   (<o> ^name attend-to-scene-change
#        ^change.object.id <id>)
#-->
#   (<s> ^operator <o> >)
#}

sp {execute-pick-up1*elaborate*evaluate-new-object*candidate-for-missing-object
   (state <s> ^name execute-pick-up1
              ^operator <o> +
              ^target-object.id <id>
              ^top-state.scene-monitor.object <obj>)
   (<o> ^name attend-to-scene-change
        ^change.type new-stable-object
        ^change.object <cand>)
   (<obj> ^id <id>)
-->
   (<cand> ^candidate-for-missing-object <obj>)
}

