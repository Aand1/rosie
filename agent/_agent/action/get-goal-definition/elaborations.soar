sp {get-goal-definition*elaborate*state
   (state <s> ^name get-goal-definition
   	      ^superstate.operator.goal-id <gid>
	      ^problemspace <p>)
-->
   (<s> ^goal-id <gid>)
}


sp {elaborate*retrieve-predicates*get-goal-definition
   (state <s> ^name get-goal-definition
   	      ^goal-id <gid>
	      -^retrieved-lti <gid>)
   (<gid> -^<any>)
-->
   (<s> ^nretrieve-lti <gid>)
}

sp {elaborate*retrieve-predicates*get-predicate-definition
   (state <s> ^name get-goal-definition
   	      ^goal-id.<< state-predicate relation-predicate >> <pred>
	      -^retrieved-lti <pred>)
   (<pred> -^<any>)
-->
   (<s> ^nretrieve-lti <pred>)
}

sp {elaborate*retrieve-predicates*get-predicate-definition*defaults
   (state <s> ^name get-goal-definition
   	      ^goal-id.<< state-predicate relation-predicate >>.<< p1 p2 relation >> <val>
	      -^retrieved-lti <val>)
   (<val> -^<any>)
-->
   (<s> ^nretrieve-lti <val>)
}



### if no goal definition is found
sp {unknown-goal*get-goal-definition
   (state <s> ^name get-goal-definition
   	      ^retrieved-lti <gid>
	      ^goal-id <gid>
	      ^topstate.interaction.status <stat>)
   (<gid> -^<pred>)
-->
   (<stat> ^failure <f>)
   (<f> ^type failure
   	   ^information unknown-goal)
}