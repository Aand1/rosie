###############################################################################
#
# Inside the execute-turn-around1 substate, this will put a turn-around command on the output link
#
###############################################################################

sp {execute-turn-around1*propose*send-turn-around-command
   (state <s> ^name execute-turn-around1
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-turn-around-command)
}

sp {execute-turn-around1*apply*send-turn-around-command
   (state <s> ^name execute-turn-around1
              ^operator.name send-turn-around-command
              ^current-action <a>
              ^top-state.io.output-link <out>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name turn
          ^parameters.direction -1
          ^termination-condition <cond>)
   (<cond> ^name rotation
           ^parameters.yaw 2.8) # Not = pi due to overshooting
   (<a> ^sent-command <law>)
}

sp {send-turn-around-command*elaborations
   (state <s> ^name send-turn-around-command)
-->
   (<s> ^attend-to-all-scene-changes true
        ^allow-wait true)
}

sp {top-state*apply*execute-turn-around1*copy*command-status
   (state <s> ^top-state <s>
              ^operator.name execute-turn-around1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}