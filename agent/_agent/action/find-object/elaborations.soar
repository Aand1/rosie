sp {find-object*elaborate*object-desc
   (state <s> ^name op_find-object
              ^superstate.operator.object-desc <desc>)
-->
   (<s> ^object-desc <desc>)
}

sp {find-object*elaborate*indexable-object
   (state <s> ^name op_find-object
              ^object-desc.object <obj>)
-->
   (<s> ^indexable-object <i>)
   (<i> ^type object
        ^lexical <obj>
        ^index-info <info>)
   (<info> ^use-interaction false
           ^return-all true)
}

sp {find-object*elaborate*move-metadata
   (state <s> ^name op_find-object
              ^topstate.interaction.stack.segment.purpose.parameters <params>)
   (<params> ^indexable <move>)
   (<move> ^type action-command 
           ^lexical.verb.word move 
           ^result._metadata <meta>)
-->
   (<s> ^move-metadata <meta>)
}

sp {find-object*apply*any-op*remove*found-object
   (state <s> ^name find-object
              ^operator <o>
              ^found-object <obj>
              ^topstate <top>)
   (<obj> ^id <id>)
   (<top> -^object.id <id>)
-->
   (<s> ^found-object <obj> -)
}