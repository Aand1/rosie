sp {find-object*elaborate*object-desc
   (state <s> ^name op_find-object
              ^superstate.operator.object-desc <desc>)
-->
   (<s> ^object-desc <desc>)
}

sp {find-object*elaborate*indexable-object
   (state <s> ^name op_find-object
              ^object-desc.object <obj>)
-->
   (<s> ^indexable-object <i>)
   (<i> ^type object
        ^lexical <obj>
        ^index-info <info>)
   (<info> ^use-interaction false
           ^return-all true)
}

sp {find-object*elaborate*move-metadata
   (state <s> ^name op_find-object
              ^topstate.interaction.stack.segment.purpose.parameters <params>)
   (<params> ^indexable <move>)
   (<move> ^type action-command 
           ^lexical.verb.word move 
           ^result._metadata <meta>)
-->
   (<s> ^move-metadata <meta>)
}

sp {find-object*elaborate*goal-location
   (state <s> ^name find-object
              ^topstate.object <goal>)
   (<goal> ^property <name-prop>)
   (<name-prop> ^name name
                ^word home)
-->
   (<s> ^goal-location <goal>)
}
   

sp {find-object*elaborate*found-object*from-purpose
   (state <s> ^name op_find-object
              ^topstate.interaction.stack.segment.purpose.parameters.found-object <obj>)
-->
   (<s> ^found-object <obj>)
}

sp {topstate*apply*any-op*remove*found-object*from*purpose
   (state <s> ^topstate <s>
              ^operator <o>
              ^interaction.stack.segment.purpose <p>
             -^object <obj>)
   (<p> ^type action-command
        ^parameters <params>)
   (<params> ^verb.word find
             ^found-object <obj>)
-->
   (<params> ^found-object <obj> -)
}