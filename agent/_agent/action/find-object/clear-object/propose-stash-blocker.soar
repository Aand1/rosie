sp {clear-object*propose*prepare-to-stash-object
   (state <s> ^name clear-object
              ^blocking-object <blocker>
             -^stash-object <blocker>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name prepare-to-stash-object
        ^object <blocker>)
   (interrupt)
}

sp {clear-object*reject*prepare-to-stash-object*under*another
   (state <s> ^name clear-object
              ^operator <o> +
              ^topstate.prepositions.prep <on>)
   (<o> ^name prepare-to-stash-object
        ^object.id <id>)
   (<on> ^name on
         ^a <other>
         ^b <id>)
-->
   (<s> ^operator <o> -)
}

sp {clear-object*apply*prepare-to-stash-object
   (state <s> ^name clear-object
              ^operator <o>)
   (<o> ^name prepare-to-stash-object
        ^object <blocker>)
-->
   (<s> ^stash-object <blocker>)
}

sp {clear-object*propose*stash*blocker
   (state <s> ^name clear-object
              ^stash-object <blocker>
              ^current-bin <bin>
             -^stashed-object <blocker>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name stash-object
        ^object <blocker>
        ^exclude-bin <bin>)
}