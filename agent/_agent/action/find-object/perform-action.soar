sp {topstate*perform-action*propose*operator
   (state <s> ^topstate <s>
              ^perform-action.indexed-action <action>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name action
        ^purpose <p>
        ^indexed-action <action>)
}

sp {topstate*perform-action*elaborate*parameters
   (state <s> ^topstate <s>
              ^perform-action <a>
              ^operator <o1> +)
   (<o1> ^name action
         ^indexed-action <i>)
   (<a> ^indexed-action <i>
        ^parameters.<param> <val>)
-->
   (<o1> ^<param> <val>)
}

#   (<top> ^perform-action.op <action-op>)
#   (<action-op> ^name op_1
#        ^_metadata <move>
#        ^action-type composite
#        ^argument3 <obj>
#        ^argument4 <dest>)
#}
#
#sp {do-action*propose*topstateaction
#   (state <s> ^topstate <s>
#              ^action-todo <op>)
#-->
#   (<s> ^operator <o> +)
#   (<o> ^name action
#        ^purpose <p>
#        ^indexed-action <op>)
#}

sp {topstate*prefer*perform-action*preference
   (state <s> ^topstate <s>
              ^operator <o1> +
              ^operator <o2> +
              ^perform-action <a1>
              ^perform-action <a2>)
   (<o1> ^name action
         ^indexed-action <i1>)
   (<o2> ^name action
         ^indexed-action <i2>)
   (<a1> ^indexed-action <i1>
         ^preference <p1>)
   (<a2> ^indexed-action <i2>
         ^preference {<p2> < <p1>})
-->
   (<s> ^operator <o1> > <o2>)
}

sp {topstate*perform-action*indifferent*pref
   (state <s> ^topstate <s>
              ^operator <o> +
              ^perform-action <a>)
   (<o> ^name action
        ^indexed-action <i>)
   (<a> ^indexed-action <i>
       -^preference)
-->
   (<s> ^operator <o> =)
}

sp {action*apply*indicate-completed-action*remove*perform-action
   (state <s> ^name action
              ^operator <o>
              ^topstate <top>)
   (<o> ^name indicate-completed-action
        ^indexed-action <i>)
   (<top> ^perform-action <a>)
   (<a> ^indexed-action <i>)
-->
   (<top> ^perform-action <a> -)
}
   