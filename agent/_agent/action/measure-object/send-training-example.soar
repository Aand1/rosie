sp {measure-object*propose*send-training-example
   (state <s> ^name measure-object
              ^moved-to-location true
              ^train-sense.label <label>
              ^property-to-measure.name <prop-name>
              ^object-to-measure <obj>
             -^sent-training-example)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-training-example
        ^label <label>
        ^property-name <prop-name>
        ^object <obj>)
}

sp {measure-object*apply*send-training-example*mark*trained
   (state <s> ^name measure-object
              ^operator.name send-training-example)
-->
   (<s> ^sent-training-example true)
}

sp {measure-object*apply*send-training-example*send*example
   (state <s> ^name measure-object
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name send-training-example
        ^label <label>
        ^property-name <prop-name>
        ^object.id <id>)
-->
   (<out> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^property-name <prop-name>)
}