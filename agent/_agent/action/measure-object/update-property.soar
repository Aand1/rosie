sp {measure-object*propose*update-property
   (state <s> ^name measure-object
              ^sent-training-example true
             -^updated-property
              ^object-to-measure <obj>
              ^property-to-measure.name <prop-name>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name update-property
        ^object <obj>
        ^property-name <prop-name>)
   (interrupt)
}

sp {measure-object*update-property*elaborate*parameters
   (state <s> ^name update-property
              ^superstate.operator <o>)
   (<o> ^object <obj>
        ^property-name <prop-name>)
-->
   (<s> ^object-to-update <obj>
        ^property-name <prop-name>)
}

sp {measure-object*update-property*elaborate*attention-info
   (state <s> ^name update-property
              ^object-to-update.id <id>)
-->
   (<s> ^attend-to-all-scene-changes true
        ^attend-to-object-id <id>)
}

sp {measure-object*update-property*remove*action*problemspace
   (state <s> ^name update-property)
-->
   (<s> ^problemspace action -)
}

sp {measure-object*update-property*propose*wait
   (state <s> ^name update-property
             -^operator.name wait)
-->
   (<s> ^operator <o> + <)
   (<o> ^name wait)
}

sp {measure-object*update-property*mark*updated
   (state <s> ^name attend-to-scene-change
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name attend-to-different-feature-val
        ^object.monitor-id <id>
        ^property.name <prop-name>)
   (<ss> ^name update-property
         ^property-name <prop-name>
         ^object-to-update.id <id>
         ^superstate <sss>)
-->
   (<sss> ^updated-property true)
}
