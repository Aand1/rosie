###############################################################################
#
# Inside the execute-go1 substate, this will put a go command on the output link
#
###############################################################################

sp {execute-go1*propose*send-go-command
   (state <s> ^name execute-go1
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-go-command
        ^command-operator true)
}

sp {execute-go1*apply*send-go-command*no*until
   (state <s> ^name execute-go1
              ^operator.name send-go-command
              ^current-action <a>
              ^top-state.io.output-link <out>
             -^until-clause)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name drive-forward
          ^termination-condition.name stabilized)
   (<a> ^sent-command <law>)
}

sp {execute-go1*apply*send-go-command*until
   (state <s> ^name execute-go1
              ^operator.name send-go-command
              ^current-action <a>
              ^top-state.io.output-link <out>
              ^until-clause <until>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name drive-forward
          ^termination-condition <until>)
   (<a> ^sent-command <law>)
}

sp {top-state*apply*execute-go1*copy*command-status
   (state <s> ^top-state <s>
              ^operator.name execute-go1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}