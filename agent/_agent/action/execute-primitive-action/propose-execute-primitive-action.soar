# When there is a top-state.current-action <a>
#   Propose code to execute that action
#   Requires an execution-operator <name> on the current-action
# Specific execution operators are under the action folder (e.g. execute-pick-up)
                                                           
sp {top-state*elaborate*current-action*segment
   (state <s> ^top-state <s>
              ^interaction.stack.segment.current-action <a>)
-->
   (<s> ^current-action <a>)
}

sp {top-state*propose*execute-primitive-action*current-action
   (state <s> ^top-state <s>
              ^current-action <a>)
   (<a> ^execution-operator <op-name>)
-->
   (<s> ^operator <o> +)
   (<o> ^name <op-name>
        ^problem-space execute-primitive-action)
}

sp {top-state*elaborate*execute-primitive-action*arguments
   (state <s> ^top-state <s>
              ^current-action <a>
              ^operator <o> +)
   (<a> ^arguments.<arg-name> <arg>)
   (<o> ^problem-space execute-primitive-action)
-->
   (<o> ^<arg-name> <arg>)
}
