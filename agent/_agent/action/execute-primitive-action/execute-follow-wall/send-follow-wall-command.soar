###############################################################################
#
# Inside the execute-follow-wall1 substate, this will put a follow-wall command on the output link
#
###############################################################################

sp {execute-follow-wall1*propose*send-follow-wall-command
   (state <s> ^name execute-follow-wall1
              ^direction-id.side <side>
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-follow-wall-command
        ^side <side>
        ^command-operator true)
}

sp {execute-follow-wall1*apply*send-follow-wall-command*no*until
   (state <s> ^name execute-follow-wall1
              ^operator <o>
              ^current-action <a>
              ^top-state.io.output-link <out>
             -^until-clause)
   (<o> ^name send-follow-wall-command
        ^side <side>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name follow-wall
          ^parameters.side 1
          ^termination-condition.name stabilized)
   (<a> ^sent-command <law>)
}

sp {execute-follow-wall1*apply*send-follow-wall-command*until
   (state <s> ^name execute-follow-wall1
              ^operator <o>
              ^current-action <a>
              ^top-state.io.output-link <out>
              ^until-clause <until>)
   (<o> ^name send-follow-wall-command
        ^side <side>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name follow-wall
          ^parameters.side 1
          ^termination-condition <until>)
   (<a> ^sent-command <law>)
}

sp {top-state*apply*execute-follow-wall1*copy*command-status
   (state <s> ^top-state <s>
              ^operator.name execute-follow-wall1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}