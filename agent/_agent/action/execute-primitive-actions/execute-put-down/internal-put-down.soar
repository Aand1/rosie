sp {execute-put-down*propose*internal-put-down
   (state <s> ^name execute-put-down1
              ^held-object <obj>
              ^top-state.world.robot.arm.state internal)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-put-down
        ^object <obj>
        ^command-operator true)
}

sp {execute-put-down*apply*internal-put-down*send-message
   (state <s> ^name execute-put-down1
              ^operator <o>
              ^current-action <a>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-put-down
        ^object.predicates.shape <shape>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type give-item-request
          ^item <shape>)
   (<a> ^sent-message true)
}

sp {execute-put-down*apply*internal-put-down*mark*complete
   (state <s> ^name execute-put-down1
              ^operator.name internal-put-down
              ^current-action <a>)
   (<a> ^received-yes true)
-->
   (<a> ^status complete)
}

sp {execute-put-down*apply*internal-put-down*remove*grabbed
   (state <s> ^name execute-put-down1
              ^operator <o>
              ^current-action.received-yes true)
   (<o> ^name internal-put-down
        ^object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -)
}
