###############################################################################
#
# Inside the execute-stop1 substate, this will put a stop command on the output link
#
###############################################################################

sp {execute-stop1*propose*send-stop-command
   (state <s> ^name execute-stop1
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-stop-command
        ^command-operator true)
}

sp {execute-stop1*apply*send-stop-command
   (state <s> ^name execute-stop1
              ^operator.name send-stop-command
              ^current-action <a>
              ^top-state.io.output-link <out>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name stop
          ^termination-condition.name none)
   (<a> ^sent-command <law>)
}

sp {top-state*apply*execute-stop1*copy*command-status
   (state <s> ^top-state <s>
              ^operator.name execute-stop1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}