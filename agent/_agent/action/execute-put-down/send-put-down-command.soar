sp {execute-put-down1*propose*send-put-down-command
   (state <s> ^name execute-put-down1
              ^projection-result <res>
             -^world.robot.arm.action failure
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-put-down-command
        ^destination <res>)
}

# Elaborations in the substate

sp {send-put-down-command*elaborate*allow-wait*true
   (state <s> ^name send-put-down-command)
-->
   (<s> ^allow-wait true)
}
   
sp {send-put-down-command*elaborate*attend-to-all-scene-changes*true
   (state <s> ^name send-put-down-command)
-->
   (<s> ^attend-to-all-scene-changes true)
}

sp {send-put-down-command*reject*attending*destination
   (state <s> ^name send-put-down-command
              ^operator <o> +
              ^superstate.destination.id <id>)
   (<o> ^name attend-to-scene-change
        ^change.object.id <id>)
-->
   (<s> ^operator <o> -)
}

sp {send-put-down-command*reject*attend*new-stable-object
   (state <s> ^name send-put-down-command
              ^operator <o> +
              ^superstate.destination.id <id>)
   (<o> ^name attend-to-scene-change
        ^change.type new-stable-object)
-->
   (<s> ^operator <o> -)
}

#### Application

# Put the command on the output link
sp {execute-put-down1*apply*send-put-down-command*send
   (state <s> ^name execute-put-down1
              ^operator <o>
              ^io.output-link <out>
              ^current-action <a>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (<out> ^put-down <cmd>)
   (<cmd> ^location <loc>)
   (<loc> ^x <x> ^y <y> ^z <z>)
   (<a> ^put-down-command <cmd>)
}

# Relocate the belief object in svs
sp {execute-put-down1*apply*send-put-down-command*relocate*object
   (state <s> ^name execute-put-down1
              ^operator <o>
              ^held-object.svs-obj.id <id>
              ^topstate.svs.command <cmd>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (write |Moving object to tracked location| (crlf))
   (<cmd> ^set_transform <setxform>)
   (<setxform> ^id <id>
               ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}

##### When finished

# copy the command-status onto current-action
sp {exeucte-put-down*apply*send-put-down-command*copy*status
   (state <s> ^name execute-put-down1
              ^operator.name send-put-down-command
              ^current-action <a>)
   (<a> ^put-down-command.status <status>)
-->
   (<a> ^command-status <status>)
}

# Remove the grabbed predicate from the object being put down
sp {execute-put-down1*apply*send-put-down-command*remove*grabbed
   (state <s> ^name execute-put-down1
              ^operator.name send-put-down-command
              ^current-action.put-down-command.status <status>
              ^held-object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -)
}

              
