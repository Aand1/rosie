sp {anystate*elaborate*open-bin*bin-to-open*bin-to-close
   (state <s> ^problemspace action
              ^operator <o> +
              ^topstate.object {<open-bin> <> <bin>})
   (<o> ^name open-bin
        ^bin <bin>)
   (<open-bin> ^open true)
-->
   (<o> ^bin-to-open <bin>
        ^bin-to-close <open-bin>)
}

sp {anystate*apply*open-bin*send*open*command
   (state <s> ^problemspace action
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name open-bin
        ^bin-to-open.blocker.id <id>)
-->
   (<out> ^set-state <open>)
   (<open> ^id <id>
           ^name enabled
           ^value false)
}

sp {anystate*apply*open-bin*send*close*command
   (state <s> ^problemspace action
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name open-bin
        ^bin-to-close.blocker.id <id>)
-->
   (<out> ^set-state <close>)
   (<close> ^id <id>
            ^name enabled
            ^value true)
}

sp {open-bin*elaborate*attend-to-scene-changes
   (state <s> ^name open-bin)
-->
   (<s> ^attend-to-all-scene-changes true)
}

sp {open-bin*propose*wait
   (state <s> ^name open-bin
             -^operator.name wait)
-->
   (<s> ^operator <o> + <)
   (<o> ^name wait)
}

sp {open-bin*remove*action*problemspace
   (state <s> ^name open-bin)
-->
   (<s> ^problemspace action -)
}