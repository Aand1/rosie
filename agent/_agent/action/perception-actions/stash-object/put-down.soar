sp {stash-object*select*put-down
   (state <s> ^name stash-object
              ^object-to-stash <obj> 
              ^stash-bin <bin>
              ^bin-counts.bin-count <bc>
              ^operator <o> +
              ^topstate.found-prep <prep>)
   (<bc> ^destination <dest>
         ^bin <bin>)
   (<bin> ^open true)
   (<o> ^name op_put-down-object-location
        ^argument1 <obj>
        ^argument2 <arg2>)
   (<arg2> ^argument <dest>)
   (<prep> ^type on
           ^predicate <pred>)
-->
   (<s> ^operator <o> > =)
   (<arg2> ^relation.smem-rep <pred>)
}

sp {stash-object*apply*put-down*mark*issued
   (state <s> ^name stash-object
              ^operator.name op_put-down-object-location
              ^current-action <a>)
-->
   (<a> ^issued-put-down true)
}

sp {stash-object*propose*mark-complete
   (state <s> ^name stash-object
              ^current-action.issued-put-down true
              ^object-to-stash <obj>
             -^robot-state.arm.grabbed <obj>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name mark-complete)
}

sp {stash-object*apply*mark-complete
   (state <s> ^name stash-object
              ^operator.name mark-complete
              ^current-action <a>)
-->
   (<a> ^complete true)
}
