sp {stash-object*elaborate*object-to-stash
   (state <s> ^name stash-object
              ^current-action.object <obj>)
-->
   (<s> ^object-to-stash <obj>)
}

sp {stash-object*elaborate*exclude-bin
   (state <s> ^name stash-object
              ^current-action.exclude-bin <bin>)
-->
   (<s> ^exclude-bin <bin>)
}

sp {stash-object*elaborate*bin-counts
   (state <s> ^name stash-object)
-->
   (<s> ^bin-counts <b>)
}

sp {stash-object*elaborate*bin-counts*bin-count
   (state <s> ^name stash-object
              ^bin-counts <bc>
             -^exclude-bin <bin>
              ^topstate.object <bin>)
   (<bin> ^category bin)
-->
   (<bc> ^bin-count.bin <bin>)
}

sp {stash-object*elaborate*above-prep
   (state <s> ^name stash-object
              ^topstate.prepositions.prep <above>)
   (<above> ^name above)
-->
   (<s> ^above-prep <above>)
}

sp {stash-object*elaborate*bin-count*occupant
   (state <s> ^name stash-object
             -^object-to-stash <obj>
              ^topstate.object <obj>
              ^bin-counts.bin-count <bc>
              ^above-prep.pair <p>)
   (<obj> ^category block
          ^id <oid>)
   (<bc> ^bin.id <bid>)
   (<p> ^a <oid> ^b <bid>)
-->
   (<bc> ^occupant.object <obj>)
}

sp {stash-object*elaborate*bin-count*occupant*above
   (state <s> ^name stash-object
              ^bin-counts.bin-count <bc>
              ^above-prep.pair <p>)
   (<bc> ^occupant <obj1>
         ^occupant <obj2>)
   (<obj1> ^object.id <id1>)
   (<obj2> ^object.id <id2>)
   (<p> ^a <id1> ^b <id2>)
-->
   (<obj1> ^above <obj2>)
   (<obj2> ^below <obj1>)
}

sp {stash-object*elaborate*bin-count*occupant*counted*false
   (state <s> ^name stash-object
              ^bin-counts.bin-count.occupant <obj>)
   (<obj> -^counted true)
-->
   (<obj> ^counted false)
}

sp {stash-object*elaborate*bin-count*occupant*top-uncounted
   (state <s> ^name stash-object
              ^bin-counts.bin-count.occupant <obj>)
   (<obj> ^counted false
         -^below.counted false)
-->
   (<obj> ^top-uncounted true)
}

sp {stash-object*elaborate*bin-count*destination*bin
   (state <s> ^name stash-object
              ^bin-counts.bin-count <bc>)
   (<bc> -^occupant
          ^bin <bin>)
-->
   (<bc> ^destination <bin>)
}

sp {stash-object*elaborate*bin-count*destination*object
   (state <s> ^name stash-object
              ^bin-counts.bin-count <bc>)
   (<bc> ^occupant <occ>)
   (<occ> ^object <obj>
         -^below)
-->
   (<bc> ^destination <obj>)
}

sp {stash-object*apply*subaction*complete*no*object*to*grab
   (state <s> ^name stash-object
              ^operator.name op_pick-up-object
              ^object-to-stash <obj>
              ^robot-state.arm <arm>
              ^current-action <a>)
   (<obj> -^perception-obj
          -^moving-object true)
   (<arm> -^grabbed <obj>
           ^action wait)
-->
   (<a> ^complete true)
}
       
#
#sp {stash-object*elaborate*bin-count*top-object
#   (state <s> ^name stash-object
#              ^bin-counts.bin-count <bc>)
#   (<bc> ^
#   (<bc> ^counted <top-id>)
#   (<on> ^name on
#        -^pair.b <top-id>)	# Nothing is on the object
#   (<top> ^id <top-id>)
#-->
#   (<bc> ^top-object <top>)
#}
#
#sp {stash-object*elaborate*bin-count*top-object*no-objects
#   (state <s> ^name stash-object
#              ^bin-counts.bin-count <bc>)
#   (<bc> ^bin <bin>
#         ^count 0)
#-->
#   (<bc> ^top-object <bin>)
#}
