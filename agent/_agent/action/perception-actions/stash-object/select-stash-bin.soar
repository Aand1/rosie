sp {stash-object*propose*select-stash-bin
   (state <s> ^name stash-object
              ^object-to-stash <obj>
              ^bin-counts.bin-count <bc>
             -^stash-bin)
   (<bc> ^bin <dest>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name select-stash-bin
        ^action-type custom
        ^bin <dest>
        ^bin-count <bc>)
}

sp {stash-object*apply*select-stash-bin
   (state <s> ^name stash-object
              ^operator <o>)
   (<o> ^name select-stash-bin
        ^bin <dest>)
-->
   (<s> ^stash-bin <dest>)
}

sp {stash-object*reject*select-stash-bin*to*exclude-bin
   (state <s> ^name stash-object
              ^operator <o> +
              ^exclude-bin <bin>)
   (<o> ^name select-stash-bin
        ^bin <bin>)
-->
   (<s> ^operator <o> -)
}

sp {stash-object*prefer*select-stash-bin*lower*bin*count
   (state <s> ^name stash-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name select-stash-bin
         ^bin-count.count <c>)
   (<o2> ^name select-stash-bin
         ^bin-count.count > <c>)
-->
   (<s> ^operator <o1> > <o2>)
}

#sp {stash-object*prefer*select-stash-bin*open*bin
#   (state <s> ^name stash-object
#              ^operator <o1> +
#              ^operator <o2> +)
#   (<o1> ^name select-stash-bin
#         ^bin.open true
#         ^bin-count.count <c>)
#   (<o2> ^name select-stash-bin
#         ^bin.open false
#         ^bin-count.count <c>)
#-->
#   (<s> ^operator <o1> > <o2>)
#}