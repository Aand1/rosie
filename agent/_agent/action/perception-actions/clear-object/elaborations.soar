sp {clear-object*elaborate*object-to-clear
   (state <s> ^name clear-object
              ^current-action.object <obj>)
-->
   (<s> ^object-to-clear <obj>)
}

sp {clear-object*elaborate*above-prep
   (state <s> ^name clear-object
              ^topstate.prepositions.prep <above>)
   (<above> ^name above)
-->
   (<s> ^above-prep <above>)
}

sp {clear-object*elaborate*current-bin
   (state <s> ^name clear-object
              ^object-to-clear.id <oid>
              ^topstate.object <bin>
              ^above-prep.pair <p>)
   (<bin> ^category bin
          ^id <bid>)
   (<p> ^a <oid> ^b <bid>)
-->
   (<s> ^current-bin <bin>)
}

sp {clear-object*elaborate*blocking-object
   (state <s> ^name clear-object
              ^object-to-clear.id <clearid>
              ^topstate.object <blocker>
              ^above-prep.pair <p>)
   (<blocker> ^id <bid>)
   (<p> ^a <bid> ^b <clearid>)
-->
   (<s> ^blocking-object <blocker>)
}

sp {clear-object*elaborate*clear-blocker
   (state <s> ^name clear-object
              ^blocking-object <blocker>
              ^above-prep <above>)
   (<above> -^pair.b <blocker>)
-->
   (<s> ^clear-blocker <blocker>)
}