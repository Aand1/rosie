sp {clear-object*elaborate*object-to-clear
   (state <s> ^name clear-object
              ^current-action.object <obj>)
-->
   (<s> ^object-to-clear <obj>)
}

sp {clear-object*elaborate*above-prep
   (state <s> ^name clear-object
              ^topstate.prepositions.prep <above>)
   (<above> ^name above)
-->
   (<s> ^above-prep <above>)
}

sp {clear-object*elaborate*higher-than-prep
   (state <s> ^name clear-object
              ^topstate.prepositions.prep <h>)
   (<h> ^name higher-than)
-->
   (<s> ^higher-than-prep <h>)
}

sp {clear-object*elaborate*current-bin
   (state <s> ^name clear-object
              ^object-to-clear.id <oid>
              ^topstate.object <bin>
              ^above-prep.pair <p>)
   (<bin> ^category bin
          ^id <bid>)
   (<p> ^a <oid> ^b <bid>)
-->
   (<s> ^current-bin <bin>)
}

sp {clear-object*elaborate*blocking-object
   (state <s> ^name clear-object
              ^object-to-clear.id <clearid>
              ^topstate.object <blocker>
              ^above-prep.pair <p>)
   (<blocker> ^id <bid>
              ^perception-obj) # Has to be live
   (<p> ^a <bid> ^b <clearid>)
-->
   (<s> ^blocking-object <blocker>)
}

sp {clear-object*elaborate*blocking-object
   (state <s> ^name clear-object
              ^object-to-clear.id <clearid>
              ^topstate.object <blocker>
              ^above-prep.pair <above>
              ^higher-than-prep.pair <higher>
              ^current-bin.id <bin>)
   (<blocker> ^id <bid>
              ^perception-obj) # Has to be live
   (<above> ^a <bid> ^b <bin>)
   (<higher> ^a <bid> ^b <clearid>)
-->
   (<s> ^blocking-object <blocker>)
}

sp {clear-object*elaborate*clear-blocker
   (state <s> ^name clear-object
              ^blocking-object <blocker>
             -^above-prep.pair.b <blocker>)
-->
   (<s> ^clear-blocker <blocker>)
}