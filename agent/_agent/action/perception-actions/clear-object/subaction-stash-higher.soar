sp {clear-object*propose*select-subaction-stash-higher
   (state <s> ^name clear-object
              ^higher-object <higher>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name select-subaction-stash-higher
        ^action-type custom
        ^object <higher>)
}

sp {clear-object*apply*select-subaction-stash-higher
   (state <s> ^name clear-object
              ^operator <o>
              ^current-action <a>)
   (<o> ^name select-subaction-stash-higher
        ^object <obj>)
-->
   (<a> ^subaction <sub>)
   (<sub> ^name stash-object
          ^object <obj>)
}

sp {clear-object*prefer*select-subaction-stash-higher*higher
   (state <s> ^name clear-object
              ^operator <o1> +
              ^operator <o2> +
              ^higher-than-prep.pair <p>)
   (<o1> ^name select-subaction-stash-higher
         ^object.id <obj1>)
   (<o2> ^name select-subaction-stash-higher
         ^object.id <obj2>)
   (<p> ^a <obj1> ^b <obj2>)
-->
   (<s> ^operator <o1> > <o2>)
}