sp {move-object*propose*select-subaction-clear-object
   (state <s> ^name move-object
              ^object-to-move <obj>
             -^current-action.cleared-object true)
-->
   (<s> ^operator <o> +)
   (<o> ^name select-subaction-clear-object
        ^action-type custom
        ^object <obj>)
}

sp {move-object*apply*select-subaction-clear-object
   (state <s> ^name move-object
              ^operator <o>
              ^current-action <a>)
   (<o> ^name select-subaction-clear-object
        ^object <obj>)
-->
   (<a> ^subaction <sub>)
   (<sub> ^name clear-object
          ^object <obj>)
}

sp {move-object*apply*clear-object*mark*cleared
   (state <s> ^name move-object
              ^operator <o>
              ^current-action <a>)
   (<o> ^name clear-object
        ^subaction.complete true)
-->
   (<a> ^cleared-object true)
}