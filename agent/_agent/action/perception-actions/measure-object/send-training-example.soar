sp {measure-object*propose*send-training-example
   (state <s> ^name measure-object
              ^object-on-location true 
             -^action-goal.achieved-subgoal.name sent-training-example
              ^train-sense.label <label>
              ^property-to-measure.name <prop-name>
              ^object-to-measure <obj>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-training-example
        ^label <label>
        ^property-name <prop-name>
        ^object <obj>)
}

sp {measure-object*apply*send-training-example*mark*trained
   (state <s> ^name measure-object
              ^operator.name send-training-example
              ^action-goal <goal>
              ^io.input-link.time.microseconds <t>)
-->
   (<goal> ^achieved-subgoal <subgoal>)
   (<subgoal> ^name sent-training-example)
   (<s> ^measure-after-time <t>)
}

sp {measure-object*apply*send-training-example*send*example
   (state <s> ^name measure-object
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name send-training-example
        ^label <label>
        ^property-name <prop-name>
        ^object.id <id>)
-->
   (<out> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^property-name <prop-name>)
}