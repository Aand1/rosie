sp {measure-object*propose*select-subaction-take-measurement*on
   (state <s> ^name measure-object
              ^property-to-measure <prop>
              ^object-on-location true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name select-subaction-take-measurement
        ^action-type custom
        ^property <prop>)
}


sp {measure-object*propose*select-subaction-take-measurement*moved
   (state <s> ^name measure-object
              ^property-to-measure <prop>
              ^current-action.moved-to-location true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name select-subaction-take-measurement
        ^action-type custom
        ^property <prop>)
}

sp {measure-object*apply*select-subaction-take-measurement
   (state <s> ^name measure-object
              ^operator <o>
              ^current-action <a>)
   (<o> ^name select-subaction-take-measurement
        ^property <prop>)
-->
   (<a> ^subaction <sub>)
   (<sub> ^name take-measurement
          ^property <prop>)
}

sp {measure-object*take-measurement*elaborate*attend-to-property
   (state <s> ^name take-measurement
              ^current-action.property.name <prop-name>)
-->
   (<s> ^attend-to-property <prop-name>)
}

sp {measure-object*take-measurement*remove*action*problemspace
   (state <s> ^name take-measurement)
-->
   (<s> ^problemspace action -)
}

sp {measure-object*take-measurement*elaborate*current-action
   (state <s> ^name take-measurement
              ^superstate.operator.subaction <a>)
-->
   (<s> ^current-action <a>)
}

sp {measure-object*take-measurement*elaborate*no-attention-ops
   (state <s> ^name take-measurement)
   -{(<s> ^operator <o> +)
     (<o> ^name attend-to-scene-change)}
-->
   (<s> ^no-attention-ops true)
}

sp {measure-object*take-measurement*propose*complete-take-measurement
   (state <s> ^name take-measurement
              ^no-attention-ops true)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-take-measurement
        ^action-type custom)
}

sp {measure-object*take-measurement*apply*complete-take-measurement
   (state <s> ^name take-measurement
              ^operator.name complete-take-measurement
              ^current-action <a>)
-->
   (<a> ^complete true)
}

sp {measure-object*apply*take-measurement*complete
   (state <s> ^name measure-object
              ^operator <o>
              ^current-action <a>)
   (<o> ^name take-measurement
        ^subaction.complete true)
-->
   (<a> ^complete true)
}