sp {find-object*elaborate*object-desc
   (state <s> ^name find-object
              ^current-action.object-to-find.object <desc>)
-->
   (<s> ^object-desc <desc>)
}

sp {find-object*elaborate*goal-location
   (state <s> ^name find-object
              ^topstate.object <goal>)
   (<goal> ^property <name-prop>)
   (<name-prop> ^name name
                ^word goal)
-->
   (<s> ^goal-location <goal>)
}

sp {find-object*elaborate*above-prep
   (state <s> ^name find-object
              ^topstate.prepositions.prep <above>)
   (<above> ^name above)
-->
   (<s> ^above-prep <above>)
}

sp {find-object*elaborate*opened-bin
   :o-support
   (state <s> ^name find-object
              ^current-action <a>
              ^topstate.object <bin>)
   (<bin> ^category bin
          ^open true)
-->
   (<a> ^opened-bin <bin>)
}

sp {find-object*elaborate*unopened-bin
   :o-support
   (state <s> ^name find-object
              ^topstate.object <bin>
              ^current-action <a>)
   (<bin> ^category bin
          ^open false)
   (<a> -^opened-bin <bin>)
-->
   (<a> ^unopened-bin <bin>)
}

sp {find-object*elaborate*remove*unopened-bin
   :o-support
   (state <s> ^name find-object
              ^current-action <a>)
   (<a> ^opened-bin <bin>
        ^unopened-bin <bin>)
-->
   (<a> ^unopened-bin <bin> -)
}

sp {find-object*elaborate*candidate*above-bin
   (state <s> ^name find-object
              ^matching-info.candidate <cand>
              ^topstate.object <bin>
              ^above-prep.pair <p>)
   (<bin> ^id <bid>
          ^category bin)
   (<cand> ^object.id <oid>)
   (<p> ^a <oid> ^b <bid>)
-->
   (<cand> ^above-bin <bin>)
}

sp {find-object*elaborate*candidate*above-location
   (state <s> ^name find-object
              ^matching-info.candidate <cand>
              ^topstate.object <loc>
              ^above-prep.pair <p>)
   (<loc> ^id <lid>
          ^category location)
   (<cand> ^object.id <oid>)
   (<p> ^a <oid> ^b <lid>)
-->
   (<cand> ^above-location <loc>)
}

sp {find-object*elaborate*candidate*above-object
   (state <s> ^name find-object
              ^matching-info.candidate <cand>
              ^topstate.object <obj2>
              ^above-prep.pair <p>)
   (<obj2> ^id <o2id>
           ^category block)
   (<cand> ^object.id <oid>)
   (<p> ^a <oid> ^b <o2id>)
-->
   (<cand> ^above-object <obj2>)
}

sp {find-object*elaborate*candidate*clear
   (state <s> ^name find-object
              ^matching-info.candidate <cand>
              ^above-prep <above>)
   (<cand> ^object.id <oid>)
   (<above> -^pair.b <oid>)
-->
   (<cand> ^clear true)
}
   
sp {find-object*elaborate*object-on-location
   (state <s> ^name find-object
              ^goal-location <goal>
              ^matching-info.candidate <cand>)
   (<cand> ^above-location { <loc> <> <goal> }
           ^object <obj>)
-->
   (<s> ^object-on-location <obj>)
}

sp {find-object*elaborate*object-on-goal
   (state <s> ^name find-object
              ^goal-location <goal>
              ^matching-info.candidate <cand>)
   (<cand> ^above-location <goal>
           ^object <obj>)
-->
   (<s> ^object-on-goal <obj>)
}

sp {find-object*elaborate*homeless-object
   (state <s> ^name find-object
              ^matching-info.candidate <cand>)
   (<cand> -^above-location
           -^above-bin
            ^object <obj>)
-->
   (<s> ^homeless-object <obj>)
}

sp {find-object*elaborate*desired-attribute
   (state <s> ^name find-object
              ^object-desc.word {<word> <>  UNKNOWN <> none <> object <> block })
-->
   (<s> ^desired-attribute.word <word>)
}


sp {find-object*elaborate*desired-attribute*info
   (state <s> ^name find-object
              ^desired-attribute <att>
              ^current-action.indexed-word <i>)
   (<i> ^word <word>
        ^sense <info>)
   (<att> ^word <word>)
-->
   (<att> ^info <info>)
}

sp {find-object*elaborate*desired-attribute*indexable
   (state <s> ^name find-object
              ^desired-attribute <att>)
   (<att> ^word <word>)
-->
   (<att> ^indexable <i>)
   (<i> ^type word
        ^lexical <word>
        ^index-info.use-interaction true)
}

sp {find-object*elaborate*desired-attribute*indexed-word
   (state <s> ^name find-object
              ^operator.name index-parameter
              ^desired-attribute <att>
              ^current-action <a>)
   (<att> ^indexable.result <res>)
-->
   (<a> ^indexed-word <res>)
}

