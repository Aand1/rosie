sp {find-object*elaborate*matching-info
   (state <s> ^name find-object)
-->
   (<s> ^matching-info <info>)
}

sp {find-object*elaborate*matching-info*candidates
   (state <s> ^name find-object
              ^topstate.object <obj>
              ^matching-info <info>)
   (<obj> ^category block)
-->
   (<info> ^candidate.object <obj>)
} 

############################################################
#
# Matching rules for attributes
#   Determine how a candidate compares with the desired-attribute
#
############################################################

# Mark as missing-property if the desired property isn't there
sp {find-object*elaborate*candidate*missing-property
   (state <s> ^name find-object
              ^desired-attribute.info.property.name <prop-name>
              ^matching-info.candidate <cand>)
   (<cand> -^object.property.name <prop-name>)
-->
   (<cand> ^missing-property <prop-name>)
}

# Mark as missing-value if the desired value isn't there for a superlative
sp {find-object*elaborate*candidate*missing-value*superlative
   (state <s> ^name find-object
              ^desired-attribute.info <info>
              ^matching-info.candidate <cand>)
   (<info> ^property.name <prop-name>
           ^type superlative)
   (<cand> ^object.property <prop>)
   (<prop> ^name <prop-name>
          -^feature-val)
-->
   (<cand> ^missing-value <prop-name>)
}

# Mark as unknown-value if the property is either none (unknown) or uncertain
sp {find-object*elaborate*candidate*unknown-value*adjective
   (state <s> ^name find-object
              ^desired-attribute.info <info>
              ^matching-info.candidate <cand>)
   (<info> ^property.name <prop-name>
           ^type adjective)
   (<cand> ^object.property <prop>)
   (<prop> ^name <prop-name>
           ^prev-case << none uncertain >>)
-->
   (<cand> ^unknown-value <prop-name>)
}

# Mark as different-value if the label is certain, but doesn't match
sp {find-object*elaborate*candidate*different-value*adjective
   (state <s> ^name find-object
              ^desired-attribute.info <info>
              ^matching-info.candidate <cand>)
   (<info> ^property.name <prop-name>
           ^type adjective
           ^label <label>)
   (<cand> ^object.property <prop>)
   (<prop> ^name <prop-name>
           ^prev-case certain
           ^label <> <label>)
-->
   (<cand> ^different-value <prop-name>)
}

# Mark as same-value if the label is certain and matches
sp {find-object*elaborate*candidate*same-value*adjective
   (state <s> ^name find-object
              ^desired-attribute.info <info>
              ^matching-info.candidate <cand>)
   (<info> ^property.name <prop-name>
           ^type adjective
           ^label <label>)
   (<cand> ^object.property <prop>)
   (<prop> ^name <prop-name>
           ^prev-case certain
           ^label <label>)
-->
   (<cand> ^same-value <prop-name>)
}

############################################################
#
# Scoring Rules for attributes
#   Determines how good a match a candidate is
#
############################################################

sp {find-object*elaborate*candidate*attribute-match*perfect
   (state <s> ^name find-object
              ^matching-info.candidate <cand>)
   (<cand> -^<< missing-property missing-value unknown-value different-value >> <prop-name>)
-->
   (<cand> ^attribute-match perfect)
}

sp {find-object*elaborate*candidate*attribute-match*unknown
   (state <s> ^name find-object
              ^matching-info.candidate <cand>)
   (<cand> ^<< missing-property missing-value unknown-value >> <prop-name>
          -^<< different-value >>)
-->
   (<cand> ^attribute-match unknown)
}

sp {find-object*elaborate*candidate*attribute-match*bad
   (state <s> ^name find-object
              ^matching-info.candidate <cand>)
   (<cand> ^<< different-value >> <prop-name>)
-->
   (<cand> ^attribute-match bad)
}

sp {find-object*elaborate*matching-info*all-attributes-known*true
   (state <s> ^name find-object
              ^matching-info <info>)
   (<info> -^candidate.attribute-match unknown)
-->
   (<info> ^all-attributes-known true)
}

sp {find-object*elaborate*matching-info*all-attributes-known*false
   (state <s> ^name find-object
              ^matching-info <info>)
   (<info> ^candidate.attribute-match unknown)
-->
   (<info> ^all-attributes-known false)
}

############################################################
#
# Matching rules for superlatives
#   Determine how a candidate compares with the desired-attribute
#
############################################################

## Mark as poor-superlative if it is a lowest superlative 
##   and there is another candidate with a lower value
#sp {find-object*elaborate*candidate*poor-superlative*lowest
#   (state <s> ^name find-object
#              ^desired-attribute.info <info>
#              ^matching-info.candidate <cand>
#              ^matching-info.candidate <better>)
#   (<info> ^property.name <prop-name>
#           ^type superlative
#           ^value lowest)
#   (<cand> ^adjective-match << perfect unknown >>
#           ^object.property <prop>)
#   (<prop> ^name <prop-name>
#           ^feature-val <val>)
#   (<better> ^adjective-match << perfect unknown >>
#             ^object.property <lower-prop>)
#   (<lower-prop> ^name <prop-name>
#                 ^feature-val < <val>)
#-->
#   (<cand> ^poor-superlative <prop-name>)
#}
#
## Mark as poor-superlative if it is a highest superlative 
##   and there is another candidate with a higher value
#sp {find-object*elaborate*candidate*poor-superlative*highest
#   (state <s> ^name find-object
#              ^desired-attribute.info <info>
#              ^matching-info.candidate <cand>
#              ^matching-info.candidate <better>)
#   (<info> ^property.name <prop-name>
#           ^type superlative
#           ^value highest)
#   (<cand> ^adjective-match << perfect unknown >>
#           ^object.property <prop>)
#   (<prop> ^name <prop-name>
#           ^feature-val <val>)
#   (<better> ^adjective-match << perfect unknown >>
#             ^object.property <higher-prop>)
#   (<higher-prop> ^name <prop-name>
#                 ^feature-val > <val>)
#-->
#   (<cand> ^poor-superlative <prop-name>)
#}
#
#sp {find-object*elaborate*candidate*unknown-superlative
#   (state <s> ^name find-object
#              ^desired-attribute.
#
#sp {find-object*elaborate*candidate*top-superlative
#   (state <s> ^name find-object
#              ^desired-attribute.info <info>
#              ^matching-info.candidate <cand>)
#   (<info> ^property.name <prop-name>
#           ^type superlative)
#   (<cand> ^adjective-match perfect
#          -^poor-superlative)
#-->
#   (<s> ^top-superlative <prop-name>)
#}
