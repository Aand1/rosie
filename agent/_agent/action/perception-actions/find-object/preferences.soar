# Subaction Ordering
# First: stash homeless or used blocks (ensure every block is in a stack)
# Second: lookup words - make sure all the desired attributes have been looked up
# Third: measure object - measure an object that has an unknown attribute
# Fourth: move object to goal - once we have a match, move it to the goal
# Fifth: clear object for shape - if we don't have any matches and we want to know a shape, clear one

sp {find-object*prefer*stash-homeless*stash-used*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-stash-homeless-object select-subaction-stash-used-object >>)
   (<o2> ^name << select-subaction-lookup-word select-subaction-measure-object select-subaction-move-object-to-goal select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*lookup-word*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-lookup-word >>)
   (<o2> ^name << select-subaction-measure-object select-subaction-move-object-to-goal select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*measure-object*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-measure-object >>)
   (<o2> ^name << select-subaction-move-object-to-goal select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*move-to-goal*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-move-object-to-goal >>)
   (<o2> ^name << select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*clear-object*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-clear-object-for-shape >>)
   (<o2> ^name << report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*reject*move-to-goal*unknown*superlatives
   (state <s> ^name find-object
              ^desired-attribute.info.type superlative
              ^matching-info.candidate.attribute-match unknown
              ^operator <o> +)
   (<o> ^name select-subaction-move-object-to-goal)
-->
   (<s> ^operator <o> -)
}

sp {find-object*prefer*stash-homeless-object*clear
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name select-subaction-stash-homeless-object
         ^candidate.clear true)
   (<o2> ^name select-subaction-stash-homeless-object
         -^candidate.clear true)
-->
   (<s> ^operator <o1> > <o2>)
}

