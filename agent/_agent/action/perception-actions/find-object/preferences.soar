# Subaction Ordering

sp {find-object*prefer*stash-object-on-goal*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-stash-object-on-goal >>)
   (<o2> ^name << select-subaction-lookup-word select-subaction-move-object-to-goal select-subaction-stash-object-on-location select-subaction-stash-homeless-object select-subaction-measure-object select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*lookup-word*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-lookup-word >>)
   (<o2> ^name << select-subaction-move-object-to-goal select-subaction-stash-object-on-location select-subaction-stash-homeless-object select-subaction-measure-object select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*move-to-goal*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-move-object-to-goal >>)
   (<o2> ^name << select-subaction-stash-object-on-location select-subaction-stash-homeless-object select-subaction-measure-object select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*stash-objects*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-stash-object-on-location select-subaction-stash-homeless-object >>)
   (<o2> ^name << select-subaction-measure-object select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*measure-object*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-measure-object >>)
   (<o2> ^name << select-subaction-clear-object-for-shape report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*clear-object*over*others
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << select-subaction-clear-object-for-shape >>)
   (<o2> ^name << report-failure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*reject*move-to-goal*unknown*superlatives
   (state <s> ^name find-object
              ^desired-attribute.info.type superlative
              ^matching-info.all-attributes-known false
              ^operator <o> +)
   (<o> ^name select-subaction-move-object-to-goal)
-->
   (<s> ^operator <o> -)
}

sp {find-object*prefer*move-to-goal*better*superlative*lowest
   (state <s> ^name find-object
              ^desired-attribute.info <info>
              ^operator <o1> +
              ^operator <o2> +)
   (<info> ^property.name <prop-name>
           ^type superlative
           ^value lowest)
   (<o1> ^name select-subaction-move-object-to-goal
         ^candidate.object.property <p1>)
   (<o2> ^name select-subaction-move-object-to-goal
         ^candidate.object.property <p2>)
   (<p1> ^name <prop-name>
         ^feature-val <lower-val>)
   (<p2> ^name <prop-name>
         ^feature-val > <lower-val>)
-->
   (<s> ^operator <o1> > <o2>)
}
   
sp {find-object*prefer*move-to-goal*better*superlative*highest
   (state <s> ^name find-object
              ^desired-attribute.info <info>
              ^operator <o1> +
              ^operator <o2> +)
   (<info> ^property.name <prop-name>
           ^type superlative
           ^value highest)
   (<o1> ^name select-subaction-move-object-to-goal
         ^candidate.object.property <p1>)
   (<o2> ^name select-subaction-move-object-to-goal
         ^candidate.object.property <p2>)
   (<p1> ^name <prop-name>
         ^feature-val <higher-val>)
   (<p2> ^name <prop-name>
         ^feature-val < <higher-val>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {find-object*prefer*stash-homeless-object*clear
   (state <s> ^name find-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name select-subaction-stash-homeless-object
         ^candidate.clear true)
   (<o2> ^name select-subaction-stash-homeless-object
         -^candidate.clear true)
-->
   (<s> ^operator <o1> > <o2>)
}

