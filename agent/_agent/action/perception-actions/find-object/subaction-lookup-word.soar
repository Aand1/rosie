sp {find-object*propose*select-subaction-lookup-word
   (state <s> ^name find-object
              ^desired-attribute <att>)
   (<att> ^indexable <i>
         -^indexed-word)
-->
   (<s> ^operator <o> + =)
   (<o> ^name select-subaction-lookup-word
        ^action-type custom
        ^indexable <i>)
}

sp {find-object*apply*select-subaction-lookup-word
   (state <s> ^name find-object
              ^operator <o>
              ^current-action <a>)
   (<o> ^name select-subaction-lookup-word
        ^indexable <i>)
-->
   (<a> ^subaction <sub>)
   (<sub> ^name index-parameter
          ^parameters.parameter <i>)
}

sp {find-object*apply*index-parameter*mark*complete
   (state <s> ^name find-object
              ^operator <o>)
   (<o> ^name index-parameter
        ^subaction <sub>
        ^parameter.result <res>)
-->
   (<sub> ^complete true)
}

sp {find-object*index-parameter*remove*action*problemspace
   (state <s> ^name index-parameter
              ^superstate.name find-object)
-->
   (<s> ^problemspace action -)
}