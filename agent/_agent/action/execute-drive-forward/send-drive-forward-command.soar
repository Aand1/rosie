###############################################################################
#
# Inside the execute-drive-forward1 substate, this will put a drive-forward command on the output link
#
###############################################################################

sp {execute-drive-forward1*propose*send-drive-forward-command
   (state <s> ^name execute-drive-forward1
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-drive-forward-command)
}

sp {execute-drive-forward1*apply*send-drive-forward-command
   (state <s> ^name execute-drive-forward1
              ^operator.name send-drive-forward-command
              ^current-action <a>
              ^topstate.io.output-link <out>
              ^termination-condition <term>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name drive-forward
          ^termination-condition <term>)
   (<a> ^sent-command <cmd>)
}

sp {send-drive-forward-command*elaborations
   (state <s> ^name send-drive-forward-command)
-->
   (<s> ^attend-to-all-scene-changes true
        ^allow-wait true)
}

sp {topstate*apply*execute-drive-forward1*copy*command-status
   (state <s> ^topstate <s>
              ^operator.name execute-drive-forward1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}