################### elaborate problemspace
sp {action*elaborate*problem-space
   (state <s> ^name action
   	      ^problemspace <ps>
	      ^superstate <ss>
	      ^topstate <ss>)
   (<ss> ^operator.name action)
-->
   (<ps> ^action <act>
   	 ^state <st>
	 ^learning <l>)
   (<act> ^type execute)
   (<st> ^type current
   	 ^elaborate closed-world)
   (<l> ^type exploration)
}

# elaborate world
sp {action*elaborate*world*topstate
   (state <s> ^name action
              ^problemspace.action.type execute
              ^topstate.world <world>)
-->
   (<s> ^world <world>)
}

# elaborate action-command
sp {action*elaborate*action-command*superoperator
   (state <s> ^name action
              ^superstate.operator.action-command <cmd>
              ^problemspace.action.type execute)
-->
   (<s> ^action-command <cmd>)
}

# elaborate action-command._metadata
sp {action*elaborate*action-command*metadata
   (state <s> ^name action
              ^action-command <cmd>)
-->
   (<cmd> ^_metadata <meta>)
}

sp {action*elaborate*action-command*metadata*op_name
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>
          ^id.procedural.op_name <name>)
-->
   (<meta> ^op_name <name>)
}

sp {action*elaborate*action-command*metadata*arg-name
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>
          ^id.procedural.{<arg-name> <> op_name} <arg>)
-->
   (<meta> ^arg-name <arg-name>)
}

sp {action*elaborate*action-command*metadata*level1-arg
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>)
          #^id.procedural.<arg-name> <arg>)
   (<meta> ^arg-name { <arg-name> << arg1 >> })
   #(<arg> -^relation)
-->
   (<meta> ^level1-arg <arg-name>)
}

sp {action*elaborate*action-command*metadata*level2-arg
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>)
   (<meta> ^arg-name { <arg-name> << arg2 >> })
          #^id.procedural.<arg-name> <arg>)
   #(<meta> ^arg-name <arg-name>)
   #(<arg> ^relation)
-->
   (<meta> ^level2-arg <arg-name>)
}

