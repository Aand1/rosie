################### elaborate problem-space
sp {action*elaborate*problem-space*execute
   (state <s> ^name action
              ^superstate.operator.name action
              ^problem-space <ps>)
-->
   (<ps> ^state <state>
         ^action <action>
         ^learning <learning>)
   (<state> ^type current
            ^elaborate closed-world)
   (<action> ^type execute)
   (<learning> ^type exploration)
}

# elaborate world
sp {action*elaborate*world*top-state
   (state <s> ^name action
              ^problem-space.action.type execute
              ^top-state.world <world>)
-->
   (<s> ^world <world>)
}

# elaborate action-command
sp {action*elaborate*action-command*superoperator
   (state <s> ^name action
              ^problem-space.action.type execute
              ^superstate.operator.action-command <cmd>)
-->
   (<s> ^action-command <cmd>)
}

# elaborate action-command._metadata
sp {action*elaborate*action-command*metadata
   (state <s> ^name action
              ^action-command <cmd>)
-->
   (<cmd> ^_metadata <meta>)
}

sp {action*elaborate*action-command*metadata*operator*names
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>
          ^id.procedural <proc>)
   (<proc> ^{ <op-type> << initiation-operator execution-operator >>} <op-name>)
-->
   (<meta> ^<op-type> <op-name>)
}

sp {action*elaborate*action-command*metadata*arg-name
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>
          ^id.procedural.{<arg-name> <> initiation-operator <> execution-operator} <arg>)
-->
   (<meta> ^arg-name <arg-name>)
}

sp {action*elaborate*action-command*metadata*level1-arg
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>)
          #^id.procedural.<arg-name> <arg>)
   (<meta> ^arg-name { <arg-name> << arg1 direction waypoint >> })
   #(<arg> -^relation)
-->
   (<meta> ^level1-arg <arg-name>)
}

sp {action*elaborate*action-command*metadata*level2-arg
   (state <s> ^name action
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>)
   (<meta> ^arg-name { <arg-name> << arg2 >> })
          #^id.procedural.<arg-name> <arg>)
   #(<meta> ^arg-name <arg-name>)
   #(<arg> ^relation)
-->
   (<meta> ^level2-arg <arg-name>)
}

