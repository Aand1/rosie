sp {execute-drive-to-waypoint1*propose*drive-to-waypoint
   (state <s> ^name execute-drive-to-waypoint1
              ^current-action <a>
              ^waypoint <wp>)
   (<a> -^drive-command <cmd>
         ^faced-waypoint true
        -^near-waypoint)
-->
   (<s> ^operator <o> + >)
   (<o> ^name drive-to-waypoint
        ^waypoint <wp>
        ^command-operator true)
}

sp {execute-drive-to-waypoint1*apply*drive-to-waypoint
   (state <s> ^name execute-drive-to-waypoint1
              ^operator <o>
              ^current-action <a>
              ^top-state.io.output-link <out>)
   (<o> ^name drive-to-waypoint
        ^waypoint.id <id>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name follow-wall
          ^parameters.side -1
          ^termination-condition <term>)
   (<term> ^name count
           ^parameters <params>)
   (<params> ^count 1
             ^class <id>)
   (<a> ^drive-command <law>)
}

sp {top-state*execute-drive-to-waypoint1*apply*drive-to-waypoint*cleanup
   (state <s> ^superstate nil
              ^operator.name execute-drive-to-waypoint1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^drive-command <cmd>)
   (<cmd> ^status success)
-->
   (<a> ^drive-command <cmd> -
        ^near-waypoint true)
   (<out> ^drive-point <cmd> -)
}
              


   