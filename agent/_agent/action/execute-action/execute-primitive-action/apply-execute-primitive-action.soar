
sp {top-state*apply*action*execution*complete*remove*current-action
   (state <s> ^top-state <s>
              ^operator <o>
              ^interaction.stack.segment <seg>)
   (<o> ^problem-space execute-primitive-action)
   (<seg> ^current-action <a>)
   (<a> ^status complete)
-->
   (<seg> ^current-action <a> -)
}

sp {top-state*apply*action*execution*complete*copy*next-action
   (state <s> ^top-state <s>
              ^operator <o>
              ^interaction.stack.segment <seg>)
   (<o> ^problem-space execute-primitive-action)
   (<seg> ^current-action.next-action { <next> <> none })
-->
   (<seg> ^current-action <next>)
}

sp {top-state*apply*action*execution*complete*report*performed*action-event
   (state <s> ^top-state <s>
              ^operator <o>
              ^current-action <a>
              ^interaction.status <status>)
   (<o> ^problem-space execute-primitive-action)
   (<a> ^name <name>
        ^status complete)
-->
   (<status> ^performed.action-event.type <name>)
}