sp {go-to-adjacent-area*propose*go-to-next-waypoint*all*adjacent*same*map
   (state <s> ^name go-to-adjacent-area
              ^current-location <cur>)
   (<cur> ^map.id <map-id>
          ^next <next>)
   (<next> ^map.id <map-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name go-to-next-waypoint
        ^current-location <cur>
        ^waypoint <next>)
}

sp {go-to-adjacent-area*propose*go-to-next-waypoint*adjacent*area
   (state <s> ^name go-to-adjacent-area
              ^current-location <cur>
              ^desired.area.id <area-id>)
   (<cur> ^next <next>)
   (<next> ^map.id <area-id>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name go-to-next-waypoint
        ^current-location <cur>
        ^waypoint <next>)
}

sp {go-to-adjacent-area*prefer*go-to-next-waypoint*desired*waypoint
   (state <s> ^name go-to-adjacent-area
              ^operator <o> +
              ^desired.waypoint.id <id>)
   (<o> ^name go-to-next-waypoint
         ^waypoint.id <id>)
-->
   (<s> ^operator <o> >)
}
