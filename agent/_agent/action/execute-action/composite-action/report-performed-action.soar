sp {composite-action*execute*propose*report-performed-action
   (state <s> ^problem-space.name composite-action
               ^problem-space.action.type execute
              ^desired.satisfied true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-performed-action)
}

sp {composite-action*execute*prefer*report-performed-action*over*others
   (state <s> ^problem-space.name composite-action
              ^problem-space.action.type execute
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name report-performed-action)
   (<o2> ^name <> report-performed-action)
-->
   (<s> ^operator <o1> > <o2>)
}
         

sp {composite-action*apply*report-performed-action
   (state <s> ^problem-space.name composite-action
              ^operator.name report-performed-action
              ^action-context.action-id.name <action-name>
              ^top-state.interaction.status <status>)
-->
   (<status> ^performed.action-event.type <action-name>)
}
   
sp {composite-action*apply*report-performed-action*push*learning*segment
   (state <s> ^problem-space.name composite-action
              ^operator.name report-performed-action
              ^action-context <a>
              ^top-state.interaction.changes <changes>)
   (<a> ^needed-policy-help true)
-->
   (<changes> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type learn-action-policy
        ^parameters <params>
        ^satisfaction.learning-event.type action-policy)
   (<params> ^action-context <a>)
}
