sp {execute-action*apply*primitive*initiate-operator*create*local*current-action
   (state <s> ^problem-space.action.type execute
              ^operator <o>)
   (<o> ^action-type primitive
        ^action-name <action-name>)
-->
   (<s> ^current-action-copy <ca>)
   (<ca> ^name <action-name>
         ^execution-operator (concat |execute-| <action-name>)
         ^arguments <args>)
}

sp {execute-action*apply*primitive*initiate-operator*copy*action-context*composte
   (state <s> ^problem-space.name execute-composite-action
              ^operator <o>
              ^action-context <action>
              ^current-action-copy <current-action>)
   (<o> ^action-type primitive
        ^action-name <action-name>)
   (<current-action> ^name <action-name>)
-->
   (<current-action> ^action-context <action>)
}

sp {execute-action*apply*primitive*initiate-operator*mark*copied-arguments
   (state <s> ^problem-space.action.type execute
              ^operator <o>
              ^current-action-copy <ca>)
   (<o> ^action-type primitive
        ^action-name <action-name>)
   (<ca> ^name <action-name>)
-->
   (<s> ^copied-arguments true)
}

sp {execute-action*apply*primitive*initiate-operator*copy*level1*args
   (state <s> ^problem-space.action.type execute
              ^operator <o>
              ^current-action-copy <ca>)
   (<o> ^action-type primitive
        ^action-name <action-name>
        ^<arg-name> <arg>)
   (<arg> ^arg-type { <arg-type> << object concept >> }
          ^id <arg-id>)
   (<ca> ^name <action-name>
         ^arguments <args>)
-->
   (<args> ^<arg-name> <new-arg>)
   (<new-arg> ^arg-type <arg-type>
              ^id <arg-id>)
}

sp {execute-action*apply*primitive*initiate-operator*copy*level2*args
   (state <s> ^problem-space.action.type execute
              ^operator <o>
              ^current-action-copy <ca>)
   (<o> ^action-type primitive
        ^action-name <action-name>
        ^<arg-name> <arg>)
   (<arg> ^arg-type predicate
           ^id <pred-id>
           ^2 <pred-obj>)
   (<ca> ^name <action-name>
         ^arguments <args>)
-->
   (<args> ^<arg-name> <new-arg>)
   (<new-arg> ^arg-type predicate
              ^id <pred-id>
              ^2 <pred-obj>)
}

sp {execute-action*apply*primitive*initiate-operator*copy*until*args
   (state <s> ^problem-space.action.type execute
              ^operator <o>
              ^current-action-copy <ca>)
   (<o> ^action-type primitive
        ^action-name <action-name>
        ^until-clause <until>)
   (<ca> ^name <action-name>
         ^arguments <args>)
-->
   (<args> ^until-clause <until>)
}

sp {execute-action*apply*primitive*initiate-operator*copy*current-action*top-state*segment
   (state <s> ^problem-space.action.type execute
              ^operator <o>
              ^current-action-copy <ca>
              ^copied-arguments true
              ^top-state.interaction.stack.segment <seg>)
   (<o> ^action-type primitive
        ^action-name <action-name>)
   (<ca> ^name <action-name>)
-->
   (<seg> ^current-action <ca>)
}