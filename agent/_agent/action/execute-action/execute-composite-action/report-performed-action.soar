sp {execute-composite-action*propose*report-performed-action
   (state <s> ^problem-space.name execute-composite-action
              ^desired.satisfied true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-performed-action)
}

sp {execute-composite-action*apply*report-performed-action
   (state <s> ^problem-space.name execute-composite-action
              ^operator.name report-performed-action
              ^action-context.action-id.name <action-name>
              ^top-state.interaction.status <status>)
-->
   (<status> ^performed.action-event.type <action-name>)
}
   
sp {execute-composite-action*apply*report-performed-action*push*learning*segment
   (state <s> ^problem-space.name execute-composite-action
              ^operator.name report-performed-action
              ^action-context <a>
              ^top-state.interaction.changes <changes>)
   (<a> ^needed-policy-help true)
-->
   (<changes> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type learn-action-policy
        ^parameters <params>
        ^satisfaction.learning-event.type action-policy)
   (<params> ^action-operator <a>)
}
