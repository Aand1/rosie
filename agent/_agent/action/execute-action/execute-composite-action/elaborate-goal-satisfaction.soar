sp {execute-composite-action*elaborate*desired-state*state*predicate*satisfied
   (state <s> ^problem-space.name execute-composite-action
              ^desired-state.predicate <p>)
   (<p> ^type state
        ^id.name <pred-name>
        ^1 <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^<pred-type> <pred-name>)
-->
   (<p> ^satisfied true)
}

sp {execute-composite-action*elaborate*desired-state*relation*predicate*satisfied
   (state <s> ^problem-space.name execute-composite-action
              ^desired-state.predicate <p>
              ^world.predicates <preds>)
   (<p> ^type relation
        ^id <rel-id>
        ^1 <obj1>
        ^2 <obj2>)
   (<preds> ^predicate <pred>)
   (<pred> ^predicate-info <rel-id>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<p> ^satisfied true)
}

sp {execute-composite-action*elaborate*predicate*not*satisfied
   (state <s> ^problem-space.name execute-composite-action
              ^desired-state.predicate <p>)
   (<p> -^satisfied true)
-->
   (<p> ^satisfied false)
}

sp {execute-composite-action*elaborate*desired-state*satisfied
   (state <s> ^problem-space.name execute-composite-action
              ^desired-state <d>)
   (<d> ^predicate.satisfied true
       -^predicate.satisfied false)
-->
   (<d> ^satisfied true)
}