##############################################################
#### elaborate composite-action problemspace*action
sp {composite-action*elaborate*problemspace*action
   (state <s> ^superstate <ss> 
	      ^problemspace <ps>)
   (<ss> ^operator.action-type composite
   	 ^problemspace <sps>)
   (<sps> ^action <val>)
-->
   (<ps> ^action <val>)
   (<val> ^category composite)
}

#### elaborate composite-action problemspace*learning
sp {composite-action*elaborate*problemspace*learning
   (state <s> ^superstate <ss> 
	      ^problemspace <ps>)
   (<ss> ^operator.action-type composite
   	 ^problemspace <sps>)
   (<sps> ^learning <val>)
-->
   (<ps> ^learning <val>)
}

#### elaborate composite-action problemspace*state
sp {composite-action*elaborate*problemspace*state
   (state <s> ^superstate <ss> 
	      ^problemspace <ps>)
   (<ss> ^operator.action-type composite
   	 ^problemspace <sps>)
   (<sps> ^state.type <type>)
-->
   (<ps> ^state.type <type>)
}

####################################################################

####################################################################
#### elaborate map, goal, procedural ids
sp {composite-operator*elaborate*map-id
   (state <s> ^problemspace.action <act>
	      ^superstate.operator <sop>)
   (<sop> ^_metadata.map-id <map-id>)
   (<act> ^category composite)
-->
   (<s> ^map-id <map-id>)
}

sp {composite-operator*elaborate*other-ids
   (state <s> ^problemspace.action <act>
	      ^map-id <map-id>)
   (<map-id> ^procedural <proc-id>
   	     ^action-space <as-id>
	     ^goal <gid>)
   (<act> ^category composite)
-->
   (<s> ^procedural-id <proc-id>
   	^action-space-id <as-id>
	^goal-id <gid>
	^action-marker composite)
}




#################### elaborate smem-query #######################
sp {composite-operator*query*map-id
   (state <s> ^problemspace.action <act>
	      ^superstate.operator <sop>)
   (<sop> ^_metadata.procedural <pid>)
   (<act> ^category composite)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^procedural <pid>)
}

sp {composite-operator*elaborate*map-id*procedural*query
   (state <s> ^problemspace.action <act>
	      ^smem-query-cue <scue>
	      ^retrieved-lti-cue <rlc>)
   (<rlc> ^cue <scue>
   	  ^retrieved-lti <lti>)
   (<scue> ^procedural <any>)
   (<act> ^category composite)
-->
   (<s> ^map-id <lti>)
}