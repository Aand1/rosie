##############################################################
#### elaborate composite-action problemspace
sp {composite-action*elaborate*problemspace
   (state <s> ^superstate <ss> 
	      ^problemspace <ps>)
   (<ss> ^operator.action-type composite
   	 ^problemspace <sps>)
   (<sps> ^<arg> <val>)
-->
   (<ps> ^<arg> <val>)
}

sp {composite-action*elaborate*problemspace*action
   (state <s> ^superstate <ss> 
	      ^problemspace <ps>)
   (<ps> ^action <act>)
-->
   (<act> ^category composite)
}


####################################################################

####################################################################
#### elaborate map, goal, procedural ids
sp {composite-operator*elaborate*map-id
   (state <s> ^problemspace <ps>
	      ^superstate.operator <sop>)
   (<sop> ^_metadata.map-id <map-id>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^map-id <map-id>)
}

sp {composite-operator*elaborate*other-ids
   (state <s> ^problemspace <ps>
	      ^map-id <map-id>)
   (<map-id> ^procedural <proc-id>
   	     ^action-space <as-id>
	     ^goal <gid>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^procedural-id <proc-id>
   	^action-space-id <as-id>
	^goal-id <gid>
	^action-marker composite)
}

################## elaborate desired ###########################
sp {composite-action*elaborate*desired
   (state <s> ^problemspace <ps>
	      ^superstate.operator.desired <des>)
   (<ps> ^action-type composite)
-->
   (<s> ^ndesired <des>)
}

#################### elaborate smem-query #######################
sp {composite-operator*query*map-id
   (state <s> ^problemspace <ps>
	      ^superstate.operator <sop>)
   (<sop> ^_metadata.procedural <pid>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^procedural <pid>)
}

sp {composite-operator*elaborate*map-id*procedural*query
   (state <s> ^problemspace <ps>
	      ^smem-query-cue <scue>
	      ^retrieved-lti-cue <rlc>)
   (<rlc> ^cue <scue>
   	  ^retrieved-lti <lti>)
   (<scue> ^procedural <any>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^map-id <lti>)
}

#############################################################
sp {composite-action*unknown-goal
   (state <s> ^problemspace.action <act> 
   	      ^superstate.operator <sop>
	      ^topstate.interaction <ts>)
   (<sop> -^desired)
   (<ts> ^status <stat>)
   (<act> ^category composite
   	  ^type execute)
-->
   (<stat> ^failure <f>)
   (<f> ^type failure
   	   ^information unknown-goal)
}