sp {generate-desired-copy*apply*copy*predicate*track-relation
   (state <s> ^name generate-desired-copy
   	      ^operator <op>
	      ^ndesired <n>
	      ^copy-desired <cd>
	      ^copy-iterator <ntrel>
	      ^object <nobj1>
	      ^object <nobj2>
	      ^duplicates.<obj1> <nobj1>
	      ^duplicates.<obj2> <nobj2>)
   (<op> ^name copy-predicate
   	 ^iterator <trel>)
   (<n> ^track-relation <pred>)
   (<trel> ^_instantiation-of <inst>
	   ^p1 <obj1>
	   ^p2 <obj2> 
	   ^relation <rel>)
-->
   (<cd> ^track-relation <ntrel>)
   (<ntrel> ^_instantiation-of <inst>
	   ^p1 <nobj1>
	   ^p2 <nobj2> 
	   ^relation <rel>)
}

sp {generate-desired-copy*apply*copy*predicate*achieve-state
   (state <s> ^name generate-desired-copy
   	      ^operator <op>
	      ^ndesired <n>
	      ^copy-desired <cd>
	      ^iterator <trel>
	      ^copy-iterator <ntrel>
	      ^object <nobj1>
	      ^duplicates.<obj1> <nobj1>)
   (<op> ^name copy-predicate
   	 ^iterator <trel>)
   (<n> ^achieve-state <pred>)
   (<trel> ^_instantiation-of <inst>
	   ^p1 <obj1>
	   ^state <state>)
-->
   (<cd> ^achieve-state <ntrel>)
   (<ntrel> ^_instantiation-of <inst>
	   ^p1 <nobj1>
	   ^state <state>)
}

sp {generate-desired-copy*increment*counters*not-null
   (state <s> ^name generate-desired-copy
   	      ^operator <op>
	      ^iterator <trel>
	      ^copy-iterator <ntrel>
	      ^generator <gen>)
   (<ntrel> ^_instantiation-of <any>)
   (<trel> ^next {<next> <> null})
-->
   (<s> ^iterator <trel> -
   	^iterator <next>
	^copy-iterator <ntrel> -
	^copy-iterator <gen>
	^generator <gen> -
	^generator <ngen>)
   (<ntrel> ^next <gen>)
}

sp {generate-desired-copy*increment*counters*null
   (state <s> ^name generate-desired-copy
   	      ^operator <op>
	      ^iterator <trel>
	      ^copy-iterator <ntrel>)
   (<ntrel> ^_instantiation-of <any>)
   (<trel> ^next null)
-->
   (<s> ^iterator <trel> -
   	^iterator null)
   (<ntrel> ^next null)
}



