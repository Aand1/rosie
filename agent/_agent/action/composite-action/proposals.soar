#propose assign-goal if there is no goal
sp {composite-action*propose*assign-goal
   (state <s> ^problemspace <ps>
	      -^ndesired <any>
	      -^desired <any>
	      ^goal-id <gid>
	      ^procedural-id <pid>
	      ^superstate <ss>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^operator <op> +)   
   (<op> ^name assign-goal
   	 ^goal-id <gid>
	 ^procedural-id <pid>)
}

### elaborate desired

sp {composite-action*elaborate*desired
   (state <s> ^problemspace <ps>
	      ^superstate.operator.desired <des>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^ndesired <des>)
}


### create copy of the desired attribute when in the learning problemspace
sp {composite-action*elaborate*not-id*propose*generate*copy
   (state <s> ^problemspace <ps>
	      -^name generate-desired-copy
	      ^ndesired <d>
	      -^desired)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^operator <op> + !)
   (<op> ^name generate-desired-copy
   	 ^ndesired <d>)
}


### propose assign-action-space if there is no action-space
sp {composite-action*propose*assign-action-space
   (state <s> ^problemspace <ps>
	      -^action-space-operator <any>
	      ^action-space-id <aid>
	      ^desired <d>
	      ^procedural-id <pid>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^operator <op> +)   
   (<op> ^name assign-action-space
   	 ^action-space-id <aid>
	 ^procedural-id <pid>)
}

### if action-space-operators exist, propose them
sp {composite-action*propose*action-space-operators
   (state <s> ^problemspace <ps>
	      ^desired <d>
	      ^action-space-operator {<op> <> failure}
	      -^applied <op>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<s> ^operator <op> +)   
}


### if the desired state of an action-space-operator is reached, retract it
sp {composite-action*retract*action-space-operator
   (state <s> ^problemspace <ps>
   	      ^superstate <ss>
	      ^desired <d>
	      ^success <d>
	      ^action-marker composite)
   (<ps> ^name action
   	 ^action-type composite)
   (<ss> ^operator <op> 
   	 ^action-space-operator <op>
	 ^problemspace.name action
	 ^action-type composite)
-->
   (<ss> ^applied <op>)
}


### propose successful completion if the desired state is reached
sp {composite-action*propose*successful
   (state <s> ^problemspace <ps>
	      -^problem-space learning
	      ^desired <d>
	      ^success <d>
	      ^superstate.indexed-action <ia>)
   (<ps> ^name action
   	 ^action-type composite)
-->
   (<ia> ^applied yes)
}