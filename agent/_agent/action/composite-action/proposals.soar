### propose assign-goal if there is no goal
sp {composite-action*propose*assign-goal
   (state <s> ^problemspace action
   	      ^action-type composite
	      -^ndesired <any>
#	      -^desired <any>
	      ^goal-id <gid>
	      ^procedural-id <pid>
	      ^superstate <ss>)
-->
   (<s> ^operator <op> +)   
   (<op> ^name assign-goal
   	 ^goal-id <gid>
	 ^procedural-id <pid>
	 ^operator-proposal-state <ss>)
}

#### copy the desired operator if the state has a io link
# sp {composite-action*elaborate*io*desired
#    (state <s> ^problemspace action
#    	      ^action-type composite
# #	      ^io (test)
# 	      ^ndesired <d>)
# -->
#    (<s> ^desired <d>)
# }

### create copy of the desired attribute when in the learning problemspace
sp {composite-action*elaborate*not-id*propose*generate*copy
   (state <s> ^problemspace action
   	      ^action-type composite
	      -^name generate-desired-copy
	      ^ndesired <d>
	      -^desired)
-->
   (<s> ^operator <op> + !)
   (<op> ^name generate-desired-copy
   	 ^ndesired <d>)
}


### propose assign-action-space if there is no action-space
sp {composite-action*propose*assign-action-space
   (state <s> -^name assign-action-space
   	      ^problemspace action
   	      ^action-type composite
	      -^action-space-operator <any>
	      ^action-space-id <aid>
	      ^desired <d>
	      ^procedural-id <pid>)
-->
   (<s> ^operator <op> +)   
   (<op> ^name assign-action-space
   	 ^action-space-id <aid>
	 ^procedural-id <pid>)
}

### if action-space-operators exist, propose them
sp {composite-action*propose*action-space-operators
   (state <s> ^problemspace action
   	      ^action-type composite
	      ^desired <d>
	      ^action-space-operator {<op> <> failure}
	      -^applied <op>)
-->
   (<s> ^operator <op> +)   
}


### if the desired state of an action-space-operator is reached, retract it
sp {composite-action*retract*action-space-operator
   (state <s> ^problemspace action
   	      ^superstate <ss>
	      ^action-type composite
	      ^desired <d>
	      ^success <d>
	      ^action-marker composite)
   (<ss> ^operator <op> 
   	 ^action-space-operator <op>
	 ^problemspace action
	 ^action-type composite)
-->
   (<ss> ^applied <op>)
}


### propose successful completion if the desired state is reached
sp {composite-action*propose*successful
   (state <s> ^problemspace action
   	      ^action-type composite
	      -^problem-space learning
	      ^desired <d>
	      ^success <d>
	      ^superstate.indexed-action <ia>)
-->
   (<ia> ^applied yes)
}