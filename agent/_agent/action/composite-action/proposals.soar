### create copy of the desired attribute when in the learning problemspace
sp {composite-action*elaborate*not-id*propose*generate*copy
   (state <s> ^problemspace <ps>
	      -^name generate-desired-copy
	      ^superstate.operator.desired <d>
	      -^desired)
   (<ps> ^action.category composite)
-->
   (<s> ^operator <op> + >)
   (<op> ^name generate-desired-copy
   	 ^ndesired <d>)
}


### propose assign-action-space if there is no action-space
sp {composite-action*propose*assign-action-space
   (state <s> ^problemspace.action <act>
	      -^action-space-operator <any>
	      ^action-space-id <aid>
	      ^desired <d>
	      ^procedural-id <pid>)
   (<act> ^type execute
   	  ^category composite)
-->
   (<s> ^operator <op> + >)   
   (<op> ^name assign-action-space
   	 ^action-space-id <aid>
	 ^procedural-id <pid>)
}

### if action-space-operators exist, propose them
sp {composite-action*propose*action-space-operators
   (state <s> ^problemspace.action <act>
	      ^desired <d>
	      ^action-space-operator {<op> <> failure}
	      -^applied <op>)
   (<act> ^type execute
   	  ^category composite)
-->
   (<s> ^operator <op> +)   
}


### if the desired state of an action-space-operator is reached, retract it
sp {composite-action*retract*action-space-operator
   (state <s> ^problemspace.action <act>
   	      ^superstate <ss>
	      ^desired <d>
	      ^success <d>
	      ^action-marker composite)
   (<act> ^category composite
   	  ^type execute)
   (<ss> ^operator <op> 
   	 ^action-space-operator <op>
	 ^problemspace.name action
	 ^action-type composite)
-->
   (<ss> ^applied <op>)
}


### propose successful completion if the desired state is reached
sp {composite-action*propose*successful
   (state <s> ^problemspace <ps>
	      ^desired <d>
	      ^success <d>
	      ^superstate.operator <ia>)
   (<ps> ^action.category composite)
-->
   (<ia> ^applied yes)
}