sp {generate-grounded-predicate*elaborate*objects*level1
   (state <s> ^name generate-grounded-predicate
   	      ^grounded-predicate <gp>
	      ^indexed-action <iop>
	      ^procedural-id.<varg> <sobj>
	      ^predicate-definition <p1>)
   (<p1> ^<parg> <sobj>)
   (<iop> ^<varg> <obj>)
-->
   (<gp> ^<parg> <obj>)
   (<s> ^connected <sobj>)
}

sp {generate-grounded-predicate*elaborate*objects*relation*level2
   (state <s> ^name generate-grounded-predicate
   	      ^grounded-predicate <tr>
	      ^indexed-action <iop>
	      ^procedural-id.<narg>.argument <sobj>
	      ^predicate-definition <p1>)
   (<tr> ^_instantiation-of <p1>)
   (<p1> ^<parg> <sobj>)
   (<iop> ^<narg>.argument <obj>)
-->
   (<tr> ^<parg> <obj>)
   (<s> ^connected <sobj>)
}

sp {generate-grounded-predicate*elaborate*objects*relation*level2*default*object
   (state <s> ^name generate-grounded-predicate
   	      ^grounded-predicate <tr>
	      ^indexed-action <iop>
	      ^predicate-definition <p1>
	      -^connected <sobj>
	      ^object <obj>)
   (<tr> ^_instantiation-of <p1>)
   (<p1> ^<parg> <sobj>)
   (<sobj> ^default <id>)
   (<obj> ^id <id>)
-->
   (<tr> ^<parg> <obj>)
}


sp {generate-grounded-predicate*elaborate*relation
   (state <s> ^name generate-grounded-predicate
   	      ^relevant-relation-definition <rel-def>
	      ^grounded-predicate <tr>
	      ^predicate-definition <p1>)
   (<tr> ^_instantiation-of <p1>)
-->
   (<tr> ^relation <ntr>)
   (<ntr> ^smem-rep <rel-def>)
}
