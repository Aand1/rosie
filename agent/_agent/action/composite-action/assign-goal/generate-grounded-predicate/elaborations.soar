## elaborate predicate
sp {generate-grounded-predicate*elaborate*predicate
   (state <s> ^name generate-grounded-predicate
   	      ^superstate.operator <sop>
	      ^smem.command <scomm>)
   (<sop> ^predicate-definition <pred>
   	  ^type <type>
	  ^superoperator <super>
	  ^desired <d>
	  ^procedural-id <pid>)
-->
   (<s> ^predicate-definition <pred>
   	^type <type>
	^indexed-action <super>
	^desired <d>
	^grounded-predicate <gp>
	^procedural-id <pid>)
   (<gp> ^_instantiation-of <pred>)
   (<scomm> ^retrieve <pred>)
}


### fill out relations if in the action-command
sp {generate-grounded-predicate*elaborate*relation*action-command
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <sop>)
   (<pid> ^<argument>.relation <rel-id>)
   (<pred> ^relation <rel-id>)
   (<sop> ^<argument>.relation.smem-rep <rel-def>)
-->
   (<s> ^relevant-relation-definition <rel-def>)
   (<s> ^generalized-with-attribute <argument>)
}

sp {generate-grounded-predicate*elaborate*relation*action-command*equals
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <sop>)
   (<pid> ^<argument>.relation <rel-id>)
   (<pred> ^relation.equals <rel-id>)
   (<sop> ^<argument>.relation.smem-rep <rel-def>)
-->
   (<s> ^relevant-relation-definition <rel-def>)
   (<s> ^generalized-with-attribute <argument>)
}

## else use the default value
sp {generate-grounded-predicate*elaborate*relation*default
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <sop>
	      -^generalized-with-attribute)
   (<pred> ^relation <rel-id>)
   (<rel-id> ^default <rel-def>)
-->
   (<s> ^relevant-relation-definition <rel-def>)
}

