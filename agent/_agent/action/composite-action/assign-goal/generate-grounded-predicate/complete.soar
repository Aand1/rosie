sp {generate-grounded-predicate*complete*relation
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^desired <des>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<pred> ^relation <any-rel>
   	   ^p1 <any-p1>
	   ^p2 <any-p2>)
-->
   (<des> ^track-relation <pred>)
}

sp {generate-grounded-predicate*complete*state
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^desired <des>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<pred> ^state <any-rel>
   	   ^p1 <any-p1>)
-->
   (<des> ^achieve-state <pred>)
}

### failure if the predicate relation is incomplete
sp {generate-grounded-predicate*failure*no-relation
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^type relation-predicate
	      ^predicate-definition.relation <rel-node>
	      ^topstate.interaction.status <stat>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<pred> -^relation <any-rel>)
-->
   (<stat> ^failure <f>)
   (<f> ^type failure
   	^information unknown-goal
	^details <d>)
   (<d> ^predicate <pred>
	^unknown relation
	^relation-node <rel-node>)
}
