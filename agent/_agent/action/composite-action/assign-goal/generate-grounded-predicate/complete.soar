sp {generate-grounded-predicate*operator-proposal-state*same*relation-predicate
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^action-operator-proposal-state.desired <asd>
	      ^desired <des>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<asd> ^track-relation <asd-tr>)
   (<asd-tr> ^relation.smem-rep <rel>
   	     ^p1.id <id1>
	     ^p2.id <id2>)
   (<pred> ^relation.smem-rep <rel>
   	   ^p1.id <id1>
	   ^p2.id <id2>)
-->
   (<des> ^track-relation <asd-tr>)
   (<s> ^matched-with-superstate <pred>)
}

 
sp {generate-grounded-predicate*complete*relation
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^desired <des>
	      -^matched-with-superstate <pred>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<pred> ^relation <any-rel>
   	   ^p1 <any-p1>
	   ^p2 <any-p2>)
-->
   (<des> ^track-relation <pred>)
}

sp {generate-grounded-predicate*operator-proposal-state*same*state-predicate
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^action-operator-proposal-state.desired <asd>
	      ^desired <des>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<asd> ^achieve-state <asd-tr>)
   (<asd-tr> ^state <srel>
   	     ^p1.id <id1>)
   (<pred> ^state <srel>
   	   ^p1.id <id1>)
-->
   (<des> ^achieve-state <asd-tr>)
   (<s> ^matched-with-superstate <pred>)
}

sp {generate-grounded-predicate*complete*state
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^desired <des>
	      -^matched-with-superstate <pred>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<pred> ^state <any-rel>
   	   ^p1 <any-p1>)
-->
   (<des> ^achieve-state <pred>)
}

### failure if the predicate relation is incomplete
sp {generate-grounded-predicate*failure*no-relation
   (state <s> ^name generate-grounded-predicate
   	      ^operator <op>
	      ^type relation-predicate
	      ^predicate-definition.relation <rel-node>
	      ^topstate.interaction.status <stat>)
   (<op> ^name complete-generate-grounded-predicate
   	 ^grounded-predicate <pred>)
   (<pred> -^relation <any-rel>)
-->
   (<stat> ^failure <f>)
   (<f> ^type failure
   	^information unknown-goal
	^details <d>)
   (<d> ^predicate <pred>
	^unknown relation
	^relation-node <rel-node>)
}
