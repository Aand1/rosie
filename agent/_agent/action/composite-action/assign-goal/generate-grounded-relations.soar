### elaborate desired
sp {assign-goal*elaborate*desired*state
   (state <s> ^name assign-goal
   	      ^desired <d>
	      ^goal-id <glid>)
   (<glid> ^relation-predicate <p1>)
-->
   (<d> ^track-relation <rel>)
   (<rel> ^_instantiation-of <p1>)
}

### connect desired to the objects in the indexed-operator
sp {assign-goal*elaborate*objects*relation*level1
   (state <s> ^name assign-goal
   	      ^desired.track-relation <tr>
	      ^goal-id <glid>
	      ^superoperator <iop>
	      ^procedural-id.<varg> <sobj>)
   (<glid> ^relation-predicate <p1>)
   (<tr> ^_instantiation-of <p1>)
   (<p1> ^<parg> <sobj>)
   (<iop> ^<varg> <obj>)
-->
   (<tr> ^<parg> <obj>)
   (<s> ^connected <sobj>)
}

sp {assign-goal*elaborate*objects*relation*level2
   (state <s> ^name assign-goal
   	      ^desired.track-relation <tr>
	      ^goal-id <glid>
	      ^superoperator <iop>
	      ^procedural-id.<narg>.<varg> <sobj>)
   (<glid> ^relation-predicate <p1>)
   (<tr> ^_instantiation-of <p1>)
   (<p1> ^<parg> <sobj>)
   (<iop> ^<narg>.<varg> <obj>)
-->
   (<tr> ^<parg> <obj>)
   (<s> ^connected <sobj>)
}


### default values for not connected objects
sp {assign-goal*elaborate*objects*relation*default
   (state <s> ^name assign-goal
   	      ^desired.track-relation <tr>
	      ^goal-id <glid>
	      -^connected <sobj>)
   (<glid> ^relation-predicate <p1>)
   (<tr> ^_instantiation-of <p1>)
   (<p1> ^<parg> <sobj>)
   (<sobj> ^default <defval>)
-->
   (<tr> ^<parg> <defval>)
}