### if pairing for an argument exists, ground that
sp {generate-grounded-relation-predicate*ground*paired-arguments*objects 
   (state <s> ^name generate-grounded-relation-predicate
   	      ^grounded-predicate <gp>
	      ^predicate-definition <pred>
	      ^pairing <pair>)
   (<pair> ^smem-object <sobj>
   	   ^wmem-object <wobj>)
   (<pred> ^{<arg> <> relation} <sobj>)
-->
   (<gp> ^<arg> <wobj>)
}

sp {generate-grounded-relation-predicate*ground*paired-arguments*relation
   (state <s> ^name generate-grounded-relation-predicate
   	      ^grounded-predicate <gp>
	      ^predicate-definition <pred>
	      ^pairing <pair>)
   (<pair> ^smem-object <sobj>
   	   ^wmem-object <wobj>)
   (<pred> ^relation <sobj>)
-->
   (<gp> ^relation <nrel>)
   (<nrel> ^smem-rep <wobj>)
}


### if pairing for an argument does-not exist, associate default
sp {generate-grounded-relation*ground*not-paired*relation
   (state <s> ^name generate-grounded-relation-predicate
   	      ^grounded-predicate <gp>
	      ^not-matched <pobj>
	      ^predicate-definition.relation <pobj>)
   (<pobj> ^default <rel-def>)
-->
   (<gp> ^relation <nrel>)
   (<nrel> ^smem-rep <rel-def>)
}

sp {generate-grounded-relation*ground*not-paired*objects
   (state <s> ^name generate-grounded-relation-predicate
   	      ^grounded-predicate <gp>
	      ^not-matched <pobj>
	      ^predicate-definition <pred>
	      ^object <obj>)
   (<pred> ^{<arg> <> relation} <pobj>)
   (<pobj> ^default <id>)
   (<obj> ^id <id>)
-->
   (<gp> ^<arg> <obj>)
}