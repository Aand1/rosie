#### elaborate state
## elaborate predicate
sp {generate-grounded-predicate*elaborate*predicate
   (state <s> ^name generate-grounded-relation-predicate
   	      ^superstate.operator <sop>
	      ^smem.command <scomm>)
   (<sop> ^predicate-definition <pred>
	  ^superoperator <super>
	  ^desired <d>
	  ^procedural-id <pid>)
-->
   (<s> ^predicate-definition <pred>
   	^type <type>
	^indexed-action <super>
	^desired <d>
	^grounded-predicate <gp>
	^procedural-id <pid>)
   (<gp> ^_instantiation-of <pred>)
   (<scomm> ^retrieve <pred>)
}

### elaborate arguments that can be filled in from the action-command
sp {generate-grounded-predicate*elaborate*arguments*action-command*level1
   (state <s> ^name generate-grounded-relation-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<ia> ^<parg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level1*equal
   (state <s> ^name generate-grounded-relation-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pobj>)
   (<pred> ^<pred-arg> <nobj>)
   (<nobj> ^equals <pobj>)
   (<ia> ^<parg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level2
   (state <s> ^name generate-grounded-relation-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pval>)
   (<pval> ^<pval-arg> <pobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<ia> ^<parg> <ival>)
   (<ival> ^<pval-arg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level2*equals
   (state <s> ^name generate-grounded-relation-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pval>)
   (<pval> ^<pval-arg> <nobj>)
   (<nobj> ^equals <pobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<ia> ^<parg> <ival>)
   (<ival> ^<pval-arg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

### elaborate objects that do not have pairings
sp {generate-grounded-predicate*elaborate*arguments*not*in*action-command
   (state <s> ^name generate-grounded-relation-predicate
   	      ^predicate-definition <pred>)
   (<pred> ^<parg> <pobj>)
  -{(<s> ^pairing <pair>)
    (<pair> ^smem-object <pobj>)}
-->
   (<s> ^not-matched <pobj>)
}

### elaborate retrievals for objects that do-not have pairings
sp {generate-grounded-predicate*retrieve*not-matched
   (state <s> ^name generate-grounded-predicate
   	      ^not-matched <pobj>)
   (<pobj> -^<arg>)
-->
   (<pobj> ^retrieve-lti <pobj>)
}


