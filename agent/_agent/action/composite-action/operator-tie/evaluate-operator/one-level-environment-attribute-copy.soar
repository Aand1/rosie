### one-level copy of environment attributes
sp {evaluate-operator*one-level-copy*environment-attributes*except*relation
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates.<val> <nval>
	      ^environment <env>)
   (<p> ^one-level-environment-attributes {<att> <> predicate <> not-predicate <> in-focus})
   (<superstate> ^environment.<att> <val>) 
-->
   (<env> ^<att> <nval>)
}

sp {evaluate-operator*one-level-copy*environment-attributes*relation
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates <d>
	      ^environment <env>)
   (<p> ^one-level-environment-attributes predicate)
   (<d> ^<val1> <nval1>
	^<val2> <nval2>)
   (<superstate> ^environment <senv>)
   (<senv> ^{<< predicate not-predicate >> <arg>} <pred>) 
   (<pred> ^a <val1>
   	   ^b <val2>
	   ^relation <rel>)
-->
   (<env> ^<arg> <npred>)
   (<npred> ^a <nval1>
   	    ^b <nval2>
	    ^relation <rel>)
}

sp {evaluate-operator*one-level-copy*environment-attributes*in-focus
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates.<val> <nval>
	      ^in-focus <if>)
   (<p> ^one-level-environment-attributes in-focus)
   (<superstate> ^in-focus.<f> <val>) 
-->
   (<if> ^<f> <nval>)
}