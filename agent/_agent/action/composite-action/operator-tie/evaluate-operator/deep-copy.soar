### copy action-space-operator
sp {duplicate-state*copy*action-space-operator
   (state <s> ^name evaluate-operator
   	      ^duplicate-of <ss>
	      ^problem-space.deep-copy action-space-operator
	      ^pairs <dup>)
   (<ss> ^action-space-operator <sop>)
   (<sop> ^name <name>
   	  ^_metadata <met>
	  ^action-type <type>
	  ^_instantiation-of <ins>)
-->
   (<s> ^action-space-operator <op>)
   (<op> ^name <name>
   	 ^_metadata <met>
	 ^action-type <type>
	 ^_instantiation-of <ins>)
   (<dup> ^<sop> <op>)
}

sp {duplicate-state*copy*action-space-operator*argument*one-level
   (state <s> ^name evaluate-operator
	      ^problem-space.deep-copy action-space-operator
	      ^duplicates <dup>
	      ^pairs <pair>
	      ^duplicate-of <ss>)
   (<ss> ^action-space-operator <sop>)
   (<pair> ^<sop> <op>)
   (<dup> ^<val> <obj>)
   (<sop> ^<arg> <val>)
   (<val> -^argument)
-->
   (<op> ^<arg> <obj>)
}

sp {duplicate-state*copy*action-space-operator*argument*two-level
   (state <s> ^name evaluate-operator
	      ^problem-space.deep-copy action-space-operator
	      ^pairs <pairs>
	      ^duplicates <dup>
	      ^duplicate-of <ss>)
   (<ss> ^action-space-operator <sop>)
   (<dup> ^<val> <obj>)
   (<pairs> ^<sop> <op>)
   (<sop> ^<arg> <aval>)
   (<aval> ^argument <val>)
-->
   (<op> ^<arg> <anew>)
   (<anew> ^argument <obj>)
   (<pairs> ^<aval> <anew>)
}

sp {duplicate-state*copy*action-space-operator*relation*two-level
   (state <s> ^name evaluate-operator
	      ^problem-space.deep-copy action-space-operator
	      ^pairs <pairs>
	      ^duplicates <dup>
	      ^action-space-operator <op>
	      ^duplicate-of <ss>)
   (<ss> ^action-space-operator <sop>)
   (<pairs> ^<sop> <op>
	  ^<aval> <anew>)
   (<sop> ^<arg> <aval>)
   (<aval> ^relation <rel>)
   (<op> ^<arg> <anew>)
-->
   (<anew> ^relation <rel>)
}

########### deep copy other put-down operator
sp {evaluate-operator*elaborate*operator*duplicate*argument*relation
   (state <s> ^superstate <ss>
   	      ^problem-space.deep-copy operator-argument
	      ^look-ahead-operator <o-copy>
	      ^duplicates <d>)
   (<o-copy> ^duplicate-of <o>)
   (<o> ^<arg> <sub-arg>)
   (<sub-arg> ^relation <relation>
   	      ^<arg2> <val2>)
   (<d> ^<val2> <obj2>)
-->
   (<d> ^<sub-arg> <nval>)
   (<nval> ^relation <relation>
   	   ^<arg2> <obj2>)
}

sp {evaluate-operator*elaborate*operator*duplicate*argument*no-relation
   (state <s> ^superstate <ss>
   	      ^problem-space.deep-copy operator-argument
	      ^look-ahead-operator <o-copy>
	      ^duplicates <d>)
   (<o-copy> ^duplicate-of <o>)
   (<o> ^<arg> <sub-arg>)
   (<sub-arg> -^relation <relation>
   	      ^<arg2> <val2>)
   (<d> ^<val2> <obj2>)
-->
   (<d> ^<sub-arg> <nval>)
   (<nval> ^<arg2> <obj2>)
}


sp {evaluate-operator*elaborate*operator*duplicate*metadata
   (state <s> ^superstate <ss>
   	      ^problem-space.deep-copy operator-argument
	      ^look-ahead-operator <o-copy>
	      ^duplicates <d>)
   (<o-copy> ^duplicate-of <o>)
   (<o> ^_metadata <met>)
-->
   (<o-copy> ^_metadata <met>)
}



	      