### deep copy
sp {action-space-operator*deep-copy*attributes*duplicate-state*relation
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates <d>)
   (<p> ^deep-copy action-space-operator)
   (<superstate> ^action-space-operator <val>)
   (<val> ^name <name>
   	  ^{<arg1> <> <arg2>} <val1>
	  ^<arg2> <val2>
	  ^action-type composite
	  ^_metadata <met>)
   (<val2> ^relation <rel>
   	   ^argument <val3>)
   (<d> ^<val3> <obj3>
   	^<val1> <obj1>)
-->
   (<s> ^action-space-operator <new>)
   (<new> ^name <name>
   	  ^<arg1> <obj1>
	  ^<arg2> <nval>
	      ^action-type composite
	      ^_metadata <met>)
   (<nval> ^relation <rel>
   	   ^argument <obj3>)
   (<d> ^<val2> <nval>)
}

sp {action-space-operator*deep-copy*attributes*duplicate-state
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates <d>)
   (<p> ^deep-copy action-space-operator)
   (<superstate> ^action-space-operator <val>)
   (<val> ^name <name>
   	  ^{<arg1> <> <arg2>} <val1>
	  ^<arg2> <val2>
	      ^action-type composite
	      ^_metadata <met>)
   (<val2> -^relation <rel>
   	   ^argument <val3>)
   (<d> ^<val3> <obj3>
   	^<val1> <obj1>)
-->
   (<s> ^action-space-operator <new>)
   (<new> ^name <name>
   	  ^<arg1> <obj1>
	  ^<arg2> <nval>
	      ^action-type composite
	      ^_metadata <met>)
   (<nval> ^argument <obj3>)
   (<d> ^<val2> <nval>)
}

## deep copy not-tracking relations
sp {evaluate-operator*deep-copy*track-relation
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates <d>)
   (<p> ^deep-copy track-relation)
   (<superstate> ^track-relation <val>)
   (<val> ^_instantiation-of <ins>
   	  ^p1 <val1>
	  ^p2 <val2> 
	  ^relation <rel>)
   (<d> ^<val1> <obj1>
   	^<val2> <obj2>)
-->
   (<s> ^track-relation <new>)
   (<new> ^_instantiation-of <ins>
   	  ^p1 <obj1>
	  ^p2 <obj2>
	  ^relation <rel>)
}

sp {evaluate-operator*deep-copy*not-achieved-state
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates <d>)
   (<p> ^deep-copy not-achieved-state)
   (<superstate> ^not-achieved-state <val>)
   (<val> ^_instantiation-of <ins>
   	  ^p1 <val1>
	  ^state <state>)
   (<d> ^<val1> <obj1>)
-->
   (<s> ^not-achieved-state <new>)
   (<new> ^_instantiation-of <ins>
   	  ^p1 <obj1>
	  ^state <rel>)
}


### deep copy operators
sp {evaluate-operator*elaborate*operator*duplicate*argument*relation
   (state <s> ^superstate <ss>
   	      ^problem-space.deep-copy operator-argument
	      ^look-ahead-operator <o-copy>
	      ^duplicates <d>)
   (<o-copy> ^duplicate-of <o>)
   (<o> ^<arg> <sub-arg>)
   (<sub-arg> ^relation <relation>
   	      ^<arg2> <val2>)
   (<d> ^<val2> <obj2>)
-->
   (<d> ^<sub-arg> <nval>)
   (<nval> ^relation <relation>
   	   ^<arg2> <obj2>)
}

sp {evaluate-operator*elaborate*operator*duplicate*argument*no-relation
   (state <s> ^superstate <ss>
   	      ^problem-space.deep-copy operator-argument
	      ^look-ahead-operator <o-copy>
	      ^duplicates <d>)
   (<o-copy> ^duplicate-of <o>)
   (<o> ^<arg> <sub-arg>)
   (<sub-arg> -^relation <relation>
   	      ^<arg2> <val2>)
   (<d> ^<val2> <obj2>)
-->
   (<d> ^<sub-arg> <nval>)
   (<nval> ^<arg2> <obj2>)
}
