sp {evaluate-operator*copy*problemspace
   (state <s> ^superstate.superstate.problemspace <sps>
   	      ^problemspace <ps>
	      ^name evaluate-operator)
   (<sps> ^<att> <val>)
-->
   (<ps> ^<att> <val>)
}


sp {evaluate-operator*create*environment
   (state <s> ^duplicate-of.environment <env>)
-->
   (<s> ^environment <env-new>)
}

   
### one-level copy of environment attributes
sp {evaluate-operator*one-level-copy*environment-attributes*except*relation
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates.<val> <nval>
	      ^environment <env>)
   (<p> ^one-level-environment-attributes {<att> <> relation})
   (<superstate> ^environment.<att> <val>) 
-->
   (<env> ^<att> <nval>)
}

sp {evaluate-operator*one-level-copy*environment-attributes*relation
   (state <s> ^duplicate-of <superstate>
   	      ^problem-space <p>
	      ^duplicates.<val1> <nval1>
	      ^duplicates.<val2> <nval2>
	      ^environment <env>)
   (<p> ^one-level-environment-attributes relation)
   (<superstate> ^environment.relation <pred>) 
   (<pred> ^a <val1>
   	   ^b <val2>
	   ^type <rel>)
-->
   (<env> ^relation <npred>)
   (<npred> ^a <nval1>
   	    ^b <nval2>
	    ^type <rel>)
}

