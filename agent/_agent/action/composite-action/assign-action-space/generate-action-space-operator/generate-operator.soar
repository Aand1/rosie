sp {elaborate*new*candidate*operator*argument*single-level
   (state <s> ^name generate-action-space-operator
   	      ^map-id.procedural <act>
	      ^object <co>
	      -^ignore-set.attribute <arg1>)
   (<act> ^<arg1> <val1>)
   (<val1> -^argument)
-->
   (<s> ^<arg1> <co>)
}

sp {elaborate*new*candidate*operator*argument*second*level
   (state <s> ^name generate-action-space-operator
   	      ^map-id.procedural <act>
	      ^object <co>
	      ^relation <rel>)
   (<act> ^<arg1> <arg1-val>)
   (<arg1-val> ^argument <val1>
   	       ^relation <rel1>)
-->
   (<s> ^<arg1> <argvalnew>)
   (<argvalnew> ^argument <co>
   		^relation <rel>)
}

sp {elaborate*new*candidate*operator*argument*second*level*no-relation
   (state <s> ^name generate-action-space-operator
   	      ^map-id.procedural <act>
	      ^object <co>
	      ^relation <rel>)
   (<act> ^<arg1> <arg1-val>)
   (<arg1-val> ^argument <val1>
   	       -^relation <rel1>)
-->
   (<s> ^<arg1> <argvalnew>)
   (<argvalnew> ^argument <co>)
}


### operator with one argument
sp {generate-action-space-operator*create*operator*one-argument
   (state <s> ^name generate-action-space-operator  
	      ^operator-template <act>
	      ^<arg1> <val1>
	      ^map-id <map>)
   (<act> ^<arg1> <id1>
   	  -^{<arg2> <> <arg1>}
	  ^op_name <name>)
   (<map> ^procedural <proc>)
-->
   (<s> ^candidate-operator <cop>)
   (<cop> ^<arg1> <val1>
   	  ^name <name>
	  ^_metadata.map-id <map>
	  ^action-type composite
	  ^_instantiation-of <proc>)
}

### operator with two arguments
sp {generate-action-space-operator*create*operator*two-argument
   (state <s> ^name generate-action-space-operator  
	      ^operator-template <act>
	      ^<arg1> <val1>
	      ^<arg2> <val2>
	      ^map-id <map>)
   (<act> ^<arg1> <id1>
   	  ^{<arg2> > <arg1>} <id2>
	  ^op_name <name>)
   (<val2> ^argument {<arg> <> <val1>})
   (<map> ^procedural <proc>)
-->
   (<s> ^candidate-operator <cop>)
   (<cop> ^<arg1> <val1>
   	  ^<arg2> <val2>
   	  ^name <name>
	  ^_metadata.map-id <map>
	  ^action-type composite
	  ^_instantiation-of <proc>)
}