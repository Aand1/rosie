######################### elaborate state ##############################
sp {generate-action-space-operator*elaborate*state
   (state <s> ^name generate-action-space-operator
   	      ^superstate.operator <sop>)
   (<sop> ^name generate-action-space-operator
   	  ^template <op-id>)
-->
   (<s> ^operator-template <op-id>)
}

######################### elaborate retrieve-id #########################
sp {generate-action-space-operator*retrieve*template-id
   (state <s> ^name generate-action-space-operator
   	      ^operator-template <op-id>)
   (<op-id> -^<any>)
-->
   (<s> ^nretrieve-lti <op-id>)
}

sp {generate-action-space-operator*retrieve*pp-object
   (state <s> ^name generate-action-space-operator
   	      ^operator-template <op-id>
	      -^retrieved-lti <pobj>)
   (<op-id> ^{<arg> <> op_name} <pobj>)
   (<pobj> -^<any>)
-->
   (<s> ^nretrieve-lti <pobj>)
}

###### elaborate candidate objects ########################################
sp {generate-action-space-operator*elaborate*candidate-operators*nfocus
   (state <s> ^name generate-action-space-operator
   	      ^object <obj>)
   (<obj> ^nfocus)
-->
   (<s> ^candidate-object <obj>)
}

sp {generate-action-space-operator*elaborate*candidate-operators*focus
   (state <s> ^name generate-action-space-operator
   	      ^object <obj>)
   (<obj> ^focus)
-->
   (<s> ^candidate-object <obj>)
}

sp {generate-action-space-operator*elaborate*arguments
   (state <s> ^name generate-action-space-operator
   	      ^operator-template <op-id>)
   (<op-id> ^<arg>)
-->
   (<s> ^argument <arg>)
}


##### query semantic memory
sp {generate-action-space-operator*query*map-id
   (state <s> ^name generate-action-space-operator
   	      ^operator-template <pid>)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^procedural <pid>)
}

sp {generate-action-space-operator*copy*map-id
   (state <s> ^name generate-action-space-operator
   	      ^operator-template <pid>
	      ^retrieved-lti-cue <rlc>)
   (<rlc> ^cue <cue>
   	  ^retrieved-lti <ret>)
-->
   (<s> ^map-id <ret>)
}


sp {generate-action-space-operator*elaborate*retrieved-action
   (state <s> ^name generate-action-space-operator  
   	      ^epmem.command.result <res>)
   (<rel> ^graph-match 1
   	  ^retrieved <ep>)
   (<ep> ^interaction.segment.purpose.parameters.indexable.result <res>)
-->
   (<s> ^retrieved-indexed-action <res>)
}
   	  