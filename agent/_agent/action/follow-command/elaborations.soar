
sp {follow-command*elaborate*problem-space
   (state <s> ^name follow-command)
-->
   (<s> ^problem-space <ps>)
   (<ps> ^action <action>
         ^state <state>)
   (<action> ^type execute
             ^propose-actions true)
   (<state> ^type current
            ^world-copy shallow)
}

# elaborate action-command
sp {follow-command*elaborate*action-command*superoperator
   (state <s> ^name follow-command
              ^superstate.operator.action-command <cmd>)
-->
   (<s> ^action-command <cmd>)
}

# elaborate action-command._metadata
sp {action*elaborate*action-command*metadata
   (state <s> ^name follow-command
              ^action-command <cmd>)
-->
   (<cmd> ^_metadata <meta>)
}

sp {action*elaborate*action-command*metadata*operator*names
   (state <s> ^name follow-command
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>
          ^id.procedural <proc>)
   (<proc> ^{ <op-type> << op_name execution-operator >>} <op-name>)
-->
   (<meta> ^<op-type> <op-name>)
}

sp {action*elaborate*action-command*metadata*arg-name
   (state <s> ^name follow-command
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>
          ^id.procedural.{<arg-name> <> op_name <> execution-operator} <arg>)
   (<arg> ^arg-type { <arg-type> <> until-clause })
-->
   (<meta> ^arg-name <arg-name>)
   (<meta> ^<arg-name> <arg-type>)
}

sp {action*elaborate*action-command*metadata*optional
   (state <s> ^name follow-command
              ^action-command <cmd>)
   (<cmd> ^_metadata <meta>
          ^id.procedural.<arg-name>.optional true)
   (<meta> ^arg-name <arg-name>)
-->
   (<meta> ^optional <arg-name>)
}
