sp {execute-pick-up*propose*complete-pick-up
   (state <s> ^name execute-pick-up
              ^current-action.command-status complete)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete-pick-up)
}

sp {execute-pick-up*apply*complete-pick-up*remove*moving
   (state <s> ^name execute-pick-up
              ^operator.name complete-pick-up
              ^target-object.predicates <preds>)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -)
}

sp {execute-pick-up*apply*complete-pick-up*add*grabbed
   (state <s> ^name execute-pick-up
              ^operator.name complete-pick-up
              ^target-object.predicates <preds>)
-->
   (<preds> ^arm-status grabbed)
}

#sp {execute-pick-up*apply*complete-pick-up*add*missing
#   (state <s> ^name execute-pick-up
#              ^operator.name complete-pick-up
#              ^world.robot.arm.action failure
#              ^target-object.predicates <preds>)
#-->
#   (<preds> ^status missing)
#}

sp {execute-pick-up*apply*complete-pick-up*mark*finished
   (state <s> ^name execute-pick-up
              ^operator.name complete-pick-up
              ^target-object.predicates.arm-status grabbed
              ^superstate.operator.action <a>)
-->
   (<a> ^status complete)
}

sp {execute-pick-up*apply*complete-pick-up*move*belief*to*arm
   (state <s> ^name execute-pick-up
              ^operator.name complete-pick-up
              ^target-object.svs-obj.id <id>
              ^io.input-link.self.pose <pose>
              ^topstate.svs.command <cmd>)
   (<pose> ^x <x> ^y <y> ^z <z>)
-->
   (<cmd> ^set_transform <xform>)
   (<xform> ^id <id>
            ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}