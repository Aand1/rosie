sp {action*elaborate*action-command-matches
   (state <s> ^name action
              ^action-command <cmd>)
-->
   (<s> ^action-command-matches <matches>)
}

sp {action*action-command-matches*elaborate*match-info*operator
   (state <s> ^name action
          ^operator <o> +
          ^action-command-matches <matches>)
   (<o> ^action-type << primitive composite >>)
-->
   (<matches> ^match-info.operator <o>)
}

sp {action*action-command-matches*elaborate*matched*name
   (state <s> ^name action
              ^action-command.id.procedural.op_name <op-name>
              ^action-command-matches <matches>)
   (<matches> ^match-info <info>)
   (<info> ^operator.name <op-name>)
-->
   (<info> ^matched name)
}

sp {action*action-command-matches*elaborate*matched*arg*level1
   (state <s> ^name action
              ^action-command <cmd>
              ^action-command-matches.match-info <info>)
   (<cmd> ^_metadata.level1-arg <arg-name>
          ^<arg-name> <arg>)
   (<info> ^operator.<arg-name> <arg>)
-->
   (<info> ^matched <arg-name>)
}

sp {action*action-command-matches*elaborate*matched*arg*level2
   (state <s> ^name action
              ^action-command <cmd>
              ^action-command-matches.match-info <info>)
   (<cmd> ^_metadata.level2-arg <arg-name>
          ^<arg-name> <arg>)
   (<arg> ^{ <arg2-name> <> relation } <arg2>
           ^relation <rel>)
   (<info> ^operator.<arg-name> <op-arg>)
   (<op-arg> ^<arg2-name> <arg2>
             ^relation <rel>)
-->
   (<info> ^matched <arg-name>)
}

sp {action*action-command-matches*elaborate*op-matched*no*name
   (state <s> ^name action
              ^action-command-matches.match-info <info>)
   (<info> -^matched name)
-->
   (<info> ^op-matched no)
}

sp {action*action-command-matches*elaborate*op-matched*no*arg
   (state <s> ^name action
              ^action-command._metadata.arg-name <arg-name>
              ^action-command-matches.match-info <info>)
   (<info> -^matched <arg-name>)
-->
   (<info> ^op-matched no)
}

sp {action*action-command-matches*elaborate*op-matched*yes
   (state <s> ^name action
              ^action-command-matches.match-info <info>)
   (<info> -^op-matched no)
-->
   (<info> ^op-matched yes)
}