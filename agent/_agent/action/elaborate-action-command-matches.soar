sp {action*elaborate*action-command-matches
   (state <s> ^name action
              ^action-command <cmd>)
-->
   (<s> ^action-command-matches <matches>)
}

sp {action*action-command-matches*elaborate*match-info*operator
   (state <s> ^name action
          ^operator <o> +
          ^action-command-matches <matches>)
   (<o> ^action-type << primitive composite >>)
-->
   (<matches> ^match-info.operator <o>)
}

sp {action*action-command-matches*elaborate*matched*name
   (state <s> ^name action
              ^action-command.id.procedural.initiation-operator <op-name>
              ^action-command-matches <matches>)
   (<matches> ^match-info <info>)
   (<info> ^operator.name <op-name>)
-->
   (<info> ^matched name)
}

sp {action*action-command-matches*elaborate*matched*object*arg
   (state <s> ^name action
              ^action-command <cmd>
              ^action-command-matches.match-info <info>)
   (<cmd> ^_metadata.arg-name <arg-name>
          ^_metadata.<arg-name> object
          ^<arg-name> <arg>)
   (<arg> ^arg-type object
          ^id <obj>)
   (<info> ^operator.<arg-name>.id <obj>)
-->
   (<info> ^matched <arg-name>)
}

sp {action*action-command-matches*elaborate*matched*concept*arg
   (state <s> ^name action
              ^action-command <cmd>
              ^action-command-matches.match-info <info>)
   (<cmd> ^_metadata.arg-name <arg-name>
          ^_metadata.<arg-name> concept
          ^<arg-name> <arg>)
   (<arg> ^arg-type concept
          ^id.name <concept-name>)
   (<info> ^operator.<arg-name>.id.name <concept-name>)
-->
   (<info> ^matched <arg-name>)
}

sp {action*action-command-matches*elaborate*matched*predicate*arg
   (state <s> ^name action
              ^action-command <cmd>
              ^action-command-matches.match-info <info>)
   (<cmd> ^_metadata.arg-name <arg-name>
          ^_metadata.<arg-name> predicate
          ^<arg-name> <arg>)
   (<arg> ^arg-type predicate
          ^id <arg2>)
   (<arg2> ^id.name <rel>
           ^{ <num> << 1 2 >> } <arg2>)
   (<info> ^operator.<arg-name> <op-arg>)
   (<op-arg> ^id.name <rel>
             ^<num> <arg2>)
-->
   (<info> ^matched <arg-name>)
}

sp {action*action-command-matches*elaborate*op-matched*no*name
   (state <s> ^name action
              ^action-command-matches.match-info <info>)
   (<info> -^matched name)
-->
   (<info> ^op-matched no)
}

sp {action*action-command-matches*elaborate*op-matched*no*arg
   (state <s> ^name action
              ^action-command._metadata <meta>
              ^action-command-matches.match-info <info>)
   (<meta> ^arg-name <arg-name>
          -^optional <arg-name>)
   (<info> -^matched <arg-name>)
-->
   (<info> ^op-matched no)
}

sp {action*action-command-matches*elaborate*op-matched*yes
   (state <s> ^name action
              ^action-command-matches.match-info <info>)
   (<info> -^op-matched no
            ^operator <op>)
-->
   (<info> ^op-matched yes)
   (<op> ^op-matched yes)
}