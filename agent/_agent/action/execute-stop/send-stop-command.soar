###############################################################################
#
# Inside the execute-stop1 substate, this will put a stop command on the output link
#
###############################################################################

sp {execute-stop1*propose*send-stop-command
   (state <s> ^name execute-stop1
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-stop-command)
}

sp {execute-stop1*apply*send-stop-command
   (state <s> ^name execute-stop1
              ^operator.name send-stop-command
              ^current-action <a>
              ^topstate.io.output-link <out>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name stop
          ^termination-condition.name none)
   (<a> ^sent-command <law>)
}

sp {send-stop-command*elaborations
   (state <s> ^name send-stop-command)
-->
   (<s> ^attend-to-all-scene-changes true
        ^allow-wait true)
}

sp {topstate*apply*execute-stop1*copy*command-status
   (state <s> ^topstate <s>
              ^operator.name execute-stop1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}