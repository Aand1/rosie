sp {reject*operators*with*different*name
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>)
   (<ia> ^name <ia-name>)
   (<op> -^name <ia-name>)
-->
   (<s> ^operator <op> -)
}

sp {mark*operators*with*same*arguments*relation
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>)
   (<ia> ^<arg> <val>)
   (<val> ^relation <rel>
   	  ^<arg2> <obj>)
   (<op> ^<arg> <nval>)
   (<nval> ^<arg2> <obj>)
-->
   (<op> ^matched <arg>)
}

sp {mark*operators*with*same*argument
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>)
   (<ia> ^<arg> <val>)
   (<val> -^relation <any>)
   (<op> ^<arg> <val>)
-->
   (<op> ^matched <arg>)
}


sp {elaborate*operator*match
   (state <s> ^operator <op> + 
   	      ^indexed-action <map-op> <> <op>)
   (<map-op> ^{<attr> <> applied <> information <> _relation <> _metadata <> actions <> name <> action-type} <val>)
   (<op> -^matched <attr>)
-->
   (<op> ^op-matched no)
   (<s> ^operator <op> -)
}


### augment spatial-relationship to operator
sp {augment*spatial-relationship
   (state <s> ^indexed-action <iop>
   	      ^operator <op> +)
   (<iop> ^<arg> <val>)
   (<val> ^relation <rel>
   	  ^<arg2> <obj>)
   (<op> ^<arg> <nval>)
   (<nval> ^<arg2> <obj>)
-->
   (<nval> ^relation <rel>)
}