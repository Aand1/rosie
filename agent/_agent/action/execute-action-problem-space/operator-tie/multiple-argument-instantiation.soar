sp {follow-command*operator-ti*multiple-argument-instantiations
   (state <s> ^superstate.name follow-command
   	      ^attribute operator
	      ^impasse tie
       ^item.op-matched yes)
-->
   (<s> ^name multiple-argument-instantiations)
}

sp {multiple-argument-instantiations*elaborate*action-command
   (state <s> ^name multiple-argument-instantiations
              ^superstate.action-command <cmd>)
-->
   (<s> ^action-command <cmd>)
}

sp {multiple-argument-instantiations*elaborate*arguments
   (state <s> ^name multiple-argument-instantiations
   	      ^action-command <cmd>)
   (<cmd> ^_metadata.arg-name <arg-name>
          ^<arg-name> <arg1> { <arg2> <> <arg1> })
-->
   (<s> ^multiple-instantiations-of <arg-name>)
}

sp {multiple-argument-instantiations*propose*report-multiple-arguments
   (state <s> ^name multiple-argument-instantiations)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-multiple-arguments)
}

sp {multiple-argument-instantiations*apply*report-multiple-arguments
   (state <s> ^name multiple-argument-instantiations
              ^operator.name report-multiple-arguments
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure.type multiple-arguments)
}