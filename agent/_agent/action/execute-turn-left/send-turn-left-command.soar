###############################################################################
#
# Inside the execute-turn-left1 substate, this will put a turn-left command on the output link
#
###############################################################################

sp {execute-turn-left1*propose*send-turn-left-command
   (state <s> ^name execute-turn-left1
             -^current-action.command-status)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-turn-left-command)
}

sp {execute-turn-left1*apply*send-turn-left-command
   (state <s> ^name execute-turn-left1
              ^operator.name send-turn-left-command
              ^current-action <a>
              ^topstate.io.output-link <out>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name turn
          ^parameters.direction 1
          ^termination-condition <cond>)
   (<cond> ^name rotation
           ^parameters.yaw 1.207) # Not = pi/2 due to overshooting
   (<a> ^sent-command <law>)
}

sp {send-turn-left-command*elaborations
   (state <s> ^name send-turn-left-command)
-->
   (<s> ^attend-to-all-scene-changes true
        ^allow-wait true)
}

sp {topstate*apply*execute-turn-left1*copy*command-status
   (state <s> ^topstate <s>
              ^operator.name execute-turn-left1
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<cmd> ^status { <status> << success failure >> })
-->
   (<a> ^sent-command <cmd> -)
   (<a> ^command-status <status>
        ^status complete)
   (<out> ^do-control-law <cmd> -)
}