sp {go-to-next-area*propose*go-to*area
   (state <s> ^name go-to-next-area
              ^superstate.operator.area <area>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name go-to
        ^destination <area>)
}

sp {go-to-next-area*apply*go-to*mark*previous-area
   (state <s> ^name go-to-next-area
              ^top-state.current-location { <cur-loc> <> <dest> }
              ^operator <o>)
   (<o> ^name go-to
        ^destination <dest>)
-->
   (<dest> ^previous-area <cur-loc>)
}

sp {go-to-next-area*apply*go-to*remove*old*previous-area
   (state <s> ^name go-to-next-area
              ^top-state.current-location { <cur-loc> <> <dest> }
              ^operator <o>)
   (<o> ^name go-to
        ^destination <dest>)
   (<dest> ^previous-area { <old-prev> <> <cur-loc> })
-->
   (<dest> ^previous-area <old-prev> -)
}

