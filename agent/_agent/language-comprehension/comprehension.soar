sp {mNL-Soar*propose*comprehension
   (state <s> ^name rosie
              ^io.input-link.language.sentence <sentence>)
   (<sentence> -^processed-sentence true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name comprehension
         ^sentence <sentence>)
}

sp {mNL-Soar*propose*comprehension*internal
   (state <s> ^world-usage internal
              ^current-sentence-number <csn>
             -^terminated-sentence <cs>)
-->
   (<s> ^operator <op> + =, <) # allow I/O
   (<op> ^name comprehension)
}
## For alternative structures that could be retried, keep the most recent.

sp {apply*comprehension*save-most-recent-alternative-structure
   (state <s> ^operator.name comprehension
              ^segment <seg>)
   (<seg> ^not-merged-receiver <nmr>)
   (<nmr> ^lt.alternative-structure)
-->
   (<seg> ^alternative-structure-receiver <nmr>)}


sp {mNL-Soar*propose*comprehension*stored
   (state <s> ^name rosie
              ^unprocessed-next nil
              -^top-state.dialog-context.learning-predicate.name
              ^stored-sentence <sentence>)
   (<sentence> -^processed-sentence true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name comprehension
         ^sentence <sentence>)
}


sp {rosie*store-sentence*remove-processed
    :o-support
   (state <s> ^name rosie
              -^unprocessed-next nil
              -^top-state.dialog-context.learning-predicate.name
              ^stored-sentence <st>)
  # (<o> ^name interpret-unknown-word)
   (<st> ^next <n1>)
   (<n1> ^next <n2> ^processed <p> ^first-word <fw>) 
-->
    (<n1> ^processed <p> -) 
    (<n1> ^first-word <fw> -) 
    (<s> ^unprocessed-next <n2>)
}

sp {rosie*interpret-unknown-word*store-sentence*remove-processed2
    :o-support
   (state <s> ^name rosie
              ^unprocessed-next <n1>)
  #            ^operator <o>)
  # (<o> ^name interpret-unknown-word)
   (<n1> ^next <n2> ^processed <p>) 
-->
    (<n1> ^processed <p> -)
    (<s> ^unprocessed-next <n1> -)
    (<s> ^unprocessed-next <n2>)
}
   

sp {apply*comprehension*save-most-recent-alternative-meaning
   (state <s> ^operator.name comprehension
              ^segment <seg>)
   (<seg> ^merged-receiver <nmr>)
   (<nmr> ^lt.alternative-meaning)
-->
   (<seg> ^alternative-meaning-receiver <nmr>)}
   
   
