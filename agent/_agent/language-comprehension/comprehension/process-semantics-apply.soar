## New version of process-semantics.
## Puts semantics on assigner.parent-receiver.semantics - need to create for every receiver.
##    So previously computed semantics for receiver structures are accessible via receiver.semantics 

##### APPLY

######### COPY Semantic substructure


sp {apply*process-semantics*process-semantics-copy
 #:interrupt
   (state <s> ^operator <o>)
   (<o> ^name process-semantics
        ^copy-semantics true
        ^assigner <ass>)
   (<ass> ^parent-receiver.semantics <sem>
          ^merged-receiver.semantics <sem2>)
   (<sem2> ^<att> <val>)
-->
   (<sem> ^<att> <val>)
   (<ass> ^processed-semantics *copy*)
   (write (crlf) |Copy semantics: | <sem> | ^| <att> | | <val>)
}
   
######### Embed Semantic substructure


sp {apply*process-semantics*process-semantics-embed
 #:interrupt
   (state <s> ^operator <o>)
   (<o> ^name process-semantics
        ^embed-semantics true
        ^embed-structure <embed>
        ^assigner <ass>)
   (<ass> ^parent-receiver.semantics <sem>
          ^merged-receiver.semantics <sem2>)
-->
   (<sem> ^<embed> <sem2>)
   (<ass> ^processed-semantics *embed*)
   (write (crlf) |Embed semantics: | <sem> | ^| <embed> | | <sem2>)
}

sp {apply*process-semantics*process-semantics-mark-processed
 #:interrupt
   (state <s> ^operator <o>)
   (<o> ^name process-semantics
        ^assigner <ass>
        ^semantics <sem>)
-->
   (write (crlf) |Mark | <ass> | with processed semantics for | <sem>)
   (<ass> ^processed-semantics <sem>)
}
   
##### APPLY Process-semantics

sp {apply*process-semantics*create-empty*semantics
   (state <s> ^operator <o>)
   (<o> ^name process-semantics
        ^assigner.parent-receiver <rec>)
  -(<rec> ^semantics <sem>)
-->
   (<rec> ^semantics <sem>)
}

sp {apply*process-semantics*simple
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
         ^assigner <ass>
         ^attribute <att>
         ^value <value>
        -^extra-attribute <of>
        -^relation <sr>)
   (<ass> ^parent-receiver.semantics <sem>)
-->
   (write (crlf) | Add to assigner: | <ass> | Semantics: | <sem> | ^|  <att> | | <value>)
   (<sem> ^<att> <value>)
   (<ass> ^processed-semantics <value>)
}

sp {apply*process-semantics*simple2
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
         ^assigner <ass>
         ^attribute2 <att>
         ^value2 <value>)
   (<ass> ^parent-receiver.semantics <sem>)
-->
   (write (crlf) | Add to assigner: | <ass> | Semantics: | <sem> | ^|  <att> | | <value>)
   (<sem> ^<att> <value>)
   (<ass> ^processed-semantics <value>)
}
            
sp {apply*process-semantics*unknown
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
         ^assigner <ass>
         ^attribute <att>
         ^value <value>
         ^extra-attribute <of>)   
   (<ass> ^parent-receiver.semantics <sem>)
-->
   (write (crlf) | Add to assigner: | <ass> | Semantics: | <sem> | ^|  <att> |.| <of> | unknown.|)
   (<sem> ^<att> <value>)
   (<value> ^<of> unknown
           ^item-type predicate)
   (<ass> ^processed-semantics <att>)
}

sp {apply*process-semantics*relation
   (state <s> ^operator <op>
              ^assigner <ass>)
   (<op> ^name process-semantics
         ^assigner <ass>
         ^attribute relation
         ^relation <sr>)
   (<sr> ^handle <sp>
         ^2 <sg>)
   (<ass> ^parent-receiver.semantics <sem>)
-->
    (write (crlf) | Add to assigner: | <ass> | Semantics: | <sem> | ^predicate | <sp> | (| <sg> |)|)
   (<sem> ^predicate <sr2>)
   (<sr2> ^handle <sp>         ## Added
          ^2 <sg>
          ^item-type predicate)
   (<ass> ^processed-semantics <sg>)}


sp {apply*process-semantics*relation*other
   (state <s> ^operator <op>
              ^assigner <ass>)
   (<op> ^name process-semantics
         ^assigner <ass>
         ^attribute { <> relation <att> }
         ^relation <sr>)
   (<sr> ^handle <sp>
         ^2 <sg>)
   (<ass> ^parent-receiver.semantics <sem>)
-->
    (write (crlf) | Add to assigner: | <ass> | Semantics: | <sem> | ^predicate | <sp> | (| <sg> |)|)
   (<sem> ^<att> <sr2>)
   (<sr2> ^handle <sp>         ## Added
          ^2 <sg>
          ^item-type predicate)
   (<ass> ^processed-semantics <sg>)}

##############################
### item-type

sp {apply*process-semantics*simple*item-type*predicate
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
         ^attribute << predicate relation >>
         ^value <value>
        -^extra-attribute <of>)
   (<value> -^item-type)
-->
   (write (crlf) | Add to item-type predicate to| <value>)
   (<value> ^item-type predicate)
}

sp {apply*process-semantics*simple*item-type*action
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
         ^attribute action 
         ^value <value>)
   (<value> -^item-type)
-->
   (write (crlf) | Add to item-type concept to| <value>)
   (<value> ^item-type action)
}

sp {apply*process-semantics*simple*item-type*concept
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
        -^attribute << action predicate relation >>
         ^value <value>
        -^extra-attribute <of>
        -^relation)
   (<value> -^item-type)
-->
   (write (crlf) | Add to item-type concept to| <value>)
   (<value> ^item-type concept)
}

##### attribute2

sp {apply*process-semantics*simple*item-type*predicate2
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
         ^attribute2 << predicate relation >>
         ^value2 <value>)
   (<value> -^item-type)
-->
   (write (crlf) | Add to item-type predicate to| <value>)
   (<value> ^item-type predicate)
}

sp {apply*process-semantics*simple*item-type*concept2
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
        -^attribute2 << predicate relation >>
         ^value2 <value>)
   (<value> -^item-type)
-->
   (write (crlf) | Add to item-type concept to| <value>)
   (<value> ^item-type concept)
}

##############################

sp {apply*process-semantics*remove-ungrounded
   #:interrupt
   (state <s> ^operator <op>)
   (<op> ^name process-semantics
         ^assigner <ass>
         ^attribute <att>)
   (<ass> ^parent-receiver.semantics <sem>)
   (<sem> ^<att> <val>)
   (<val> ^ungrounded <x>)
-->
   (write (crlf) | Removing ungrounded: | <sem> | ^| <att> | | <x>)
   (<sem> ^<att> <val> -)}

