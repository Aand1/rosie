sp {comprehend-word*propose*finish-comprehend*success
   (state <s> ^name << comprehend-word comprehend-construction >>
              ^comprehension-structure.not-merged-receiver.lt
              ^created-assigners <n>
              ^retrievals <m>)
-->
   (<s> ^operator <op> + =, <)
   (<op> ^name finish-comprehend)
}

sp {comprehend-word*propose*finish-comprehend*new-word
   (state <s> ^name << comprehend-word comprehend-construction >>
              ^new-word true)
-->
   (<s> ^operator <op> + =, <)
   (<op> ^name finish-comprehend)
}

sp {comprehend-word*compare*finish-comprehend
   (state <s> ^name << comprehend-word comprehend-construction >>
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name finish-comprehend)
   (<op2> ^name << remove-stale-receiver process-new-word create-assigner create-receiver
                   remove-stale-assigner >>)
-->
   (<s> ^operator <op1> < <op2>)
}

sp {comprehend-word*compare*finish-comprehend*better
   (state <s> ^name comprehend-word
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name finish-comprehend)
   (<op2> ^name << smem-lexical-retrieval-cw-pw smem-lexical-retrieval-cw-pw-type >>)
-->
   (<s> ^operator <op1> > <op2>)
}

## Can't chunk over because it is selected based on exhaustion.
sp {apply*finish-comprehend
   (state <s> ^operator <op>
              ^name comprehend-word
            #  ^quiescence t
              ^current-word <cw>
              ^segment <ss>
              ^comprehension-structure <cs>)
   (<op> ^name finish-comprehend)
-->
   (<ss> ^comprehension-structure <cs>)
   (<cs> ^processed true)
   (<cw> ^processed true)
}

## Can't chunk over because it is selected based on exhaustion.
sp {apply*finish-comprehend*construction
   (state <s> ^operator <op>
              ^name comprehend-construction
             # ^quiescence t
              ^segment <ss>
              ^comprehension-structure <cs>)
   (<cs> ^retrieved-lexical-item  <cw>)
   (<op> ^name finish-comprehend)
-->
   (<ss> ^comprehension-structure <cs>)
   (<cs> ^processed true)
   (<cw> ^processed true)
}

