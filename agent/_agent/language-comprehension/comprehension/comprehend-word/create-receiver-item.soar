sp {comprehend-word*propose*create-receiver-item
   (state <s> ^name comprehend-word
              ^segment <ss>
              ^current-word <cw>
              ^retrieved-lexical-item <id>)
  -{(<ss> ^comprehension-structure.unfilled-receiver-item <uri>)
    (<uri> ^lt-lexical-item <id>
           ^current-word <cw>)}
-->
   (<s> ^operator <op> + =)
   (<op> ^name create-receiver-item
         ^item <item>)
}
sp {comprehend-word*compare*create-receiver-item
   (state <s> ^name comprehend-word
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name create-receiver-item)
   (<op2> ^name << retrieve-semantic-substructure remove-stale-receive >>)
-->
   (<s> ^operator <op1> < <op2>)
}

sp {apply*create-receiver-item*create-item
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
              ^segment <ss>
             -^name comprehend-construction)
   (<ss> ^comprehension-structure <cs>)
   (<op> ^name create-receiver-item
         ^item <item>)
   (<id> ^structure-type <stype>
        -^constraint)
-->
   (<cs> ^unfilled-receiver-item <item>)
   (<item> ^current-word <cw>
           ^lt-lexical-item <id>
           ^structure-type <stype>
           ^decision-count (int (cmd stats -d)))}

sp {apply*create-receiver-item*create-item*2
   (state <s> ^operator <op>
              ^retrieved-lexical-item <id>
              ^segment <ss>
              ^current-word <cw>
             -^name comprehend-construction)
   (<op> ^name create-receiver-item
         ^item <item>)
   (<id> ^structure-type <stype>
         ^preset-semantic-structure.referent <sg>
         ^constraint <c>)
   (<ss> ^comprehension-structure <cs>)
-->
   (<cs> ^unfilled-receiver-item <item>)
   (<item> ^current-word <cw>
           ^lt-lexical-item <id>
           ^constraint <c>
           ^referent <sg>
           ^structure-type <stype>
           ^decision-count (int (cmd stats -d)))}

####

sp {apply*create-receiver-item*create-item*construction
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
              ^segment <ss>
              ^name comprehend-construction)
   (<ss> ^comprehension-structure <cs>)
   (<op> ^name create-receiver-item
         ^item <item>)
   (<id> ^structure-type <stype>
        -^constraint)
  -(<cs> ^remove-receiver)
-->
   (<cs> ^unfilled-receiver-item <item>)
   (<item> ^current-word <cw>
           ^lt-lexical-item <id>
           ^structure-type <stype>
           ^decision-count (int (cmd stats -d)))}

sp {apply*create-receiver-item*create-item*2*construction
   (state <s> ^operator <op>
              ^retrieved-lexical-item <id>
              ^segment <ss>
              ^current-word <cw>
              ^name comprehend-construction)
   (<op> ^name create-receiver-item
         ^item <item>)
   (<id> ^structure-type <stype>
         ^preset-semantic-structure.referent <sg>
         ^constraint <c>)
   (<ss> ^comprehension-structure <cs>)
  -(<cs> ^remove-receiver)
-->
   (<cs> ^unfilled-receiver-item <item>)
   (<item> ^current-word <cw>
           ^lt-lexical-item <id>
           ^constraint <c>
           ^referent <sg>
           ^structure-type <stype>
           ^decision-count (int (cmd stats -d)))}


#####
## Use old decision count so it stays older than others that
                           ##  have been created in the mean time.
sp {apply*create-receiver-item*create-item*construction*none
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
              ^segment <ss>
              ^name comprehend-construction)
   (<ss> ^comprehension-structure <cs>)
   (<op> ^name create-receiver-item
         ^item <item>)
   (<id> ^structure-type <stype>
        -^constraint)
   (<cs> ^remove-receiver.decision-count <dc>
        -^remove-receiver.decision-count < <dc>)
-->
   (write (crlf) | use old decision-count | <dc>)
   (<cs> ^unfilled-receiver-item <item>)
   (<item> ^current-word <cw>
           ^lt-lexical-item <id>
           ^structure-type <stype>
           ^decision-count <dc>)}

sp {apply*created-receiver-item*create-item*2*construction*none
   (state <s> ^operator <op>
              ^retrieved-lexical-item <id>
              ^segment <ss>
              ^current-word <cw>
              ^name comprehend-construction)
   (<op> ^name create-receiver-item
         ^item <item>)
   (<id> ^structure-type <stype>
         ^preset-semantic-structure.referent <sg>
         ^constraint <c>)
   (<ss> ^comprehension-structure <cs>)
   (<cs> ^remove-receiver.decision-count <dc>
        -^remove-receiver.decision-count < <dc>)
-->
   (write (crlf) | use old decision-count | <dc>)
   (<cs> ^unfilled-receiver-item <item>)
   (<item> ^current-word <cw>
           ^lt-lexical-item <id>
           ^constraint <c>
           ^referent <sg>
           ^structure-type <stype>
           ^decision-count <dc>)}
