sp {comprehend-word*propose*create-receiver
  # :interrupt
   (state <s> ^name comprehend-word
              ^current-word <cw>
              ^retrieved-lexical-item <id>)
  -{(<s> ^comprehension-structure.not-merged-receiver <uri>)
    (<uri> ^lt 
           ^current-word <cw>)}
   (<ss> -^retry)
-->
   (<s> ^operator <op> + =)
   (<op> ^name create-receiver
         ^item <item>)
}

sp {comprehend-word*compare*create-receiver
   (state <s> ^name comprehend-word
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name create-receiver)
   (<op2> ^name << retrieve-semantic-substructure remove-stale-receive >>)
-->
   (<s> ^operator <op1> < <op2>)
}

sp {apply*create-receiver*create-item
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
             -^name comprehend-construction
              ^comprehension-structure <cs>)
   (<op> ^name create-receiver
         ^item <item>)
   (<id> -^super-type)
-->
   (<cs> ^not-merged-receiver <item>)
   (<item> ^current-word <cw>
           ^lt <id>
           ^semantics <sem>)}
 #          ^decision-count (int (cmd stats -d)))}

############## Super-type ####### Must merge <id> structures (spelling and referent) with all other structures from <st>

sp {apply*create-receiver*create-item*super-type
 #  :interrupt
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
             -^name comprehend-construction
              ^comprehension-structure <cs>)
   (<op> ^name create-receiver
         ^item <item>)
   (<id> ^super-type <st>
         ^spelling <spell>)
-->
   (<cs> ^not-merged-receiver <item>)
   (<item> ^current-word <cw>
           ^super-type <st>
           ^proto-lt <proto-lt>
           ^semantics <sem>)
 #          ^decision-count (int (cmd stats -d)))
   (<proto-lt> ^spelling <spell>)}

sp {apply*create-receiver*create-item*super-type*copy-lt
   (state <s> ^operator <op>
              ^comprehension-structure.not-merged-receiver <item>)
   (<op> ^name create-receiver
         ^item <item>)
   (<item> ^super-type <st>
           ^proto-lt <plt>)
   (<st> ^<att> <val>)
-->
   (<plt> ^<att> <val>
          ^copied yes)}

sp {apply*create-receiver*create-item*super-type*copy-can-be-part-of
   (state <s> ^operator <op>
              ^retrieved-lexical-item <id>
              ^comprehension-structure.not-merged-receiver <item>)
   (<op> ^name create-receiver
         ^item <item>)
   (<item> ^proto-lt <proto>)
   (<id> ^{ << attachment can-be-part-of relation referent directional alternative-structure number tense specifier >> <att> } <cbpo>)
-->
   (<proto> ^<att> <cbpo>)}

sp {apply*create-receiver*create-item*super-type*finish
   (state <s> ^operator <op>
              ^comprehension-structure.not-merged-receiver <item>)
   (<op> ^name create-receiver
         ^item <item>)
   (<item> ^super-type <st>
           ^proto-lt <plt>)
   (<plt> ^copied yes)
-->
   (<item> ^lt <plt>)}


######### Constructions

sp {apply*create-receiver*create-item*construction
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
              ^name comprehend-construction
              ^comprehension-structure <cs>)
   (<op> ^name create-receiver
         ^item <item>)
  -(<cs> ^remove-receiver)
-->
   (<cs> ^not-merged-receiver <item>)
   (<item> ^current-word <cw>
           ^lt <id>)}
#           ^decision-count (int (cmd stats -d)))}

#####
## Use old decision count so it stays older than others that
                           ##  have been created in the mean time.
sp {apply*create-receiver*create-item*construction*none
   :interrupt
   (state <s> ^operator <op>
              ^current-word <cw>
              ^retrieved-lexical-item <id>
              ^name comprehend-construction
              ^comprehension-structure <cs>)
   (<op> ^name create-receiver
         ^item <item>)
   (<cs> ^remove-receiver.decision-count <dc>
        -^remove-receiver.decision-count < <dc>)
-->
   (<s> ^created-receiver true)
   (write (crlf) | use old decision-count | <dc>)
   (<cs> ^not-merged-receiver <item>)
   (<item> ^current-word <cw>
           ^lt <id>
           ^decision-count <dc>)}

#####
sp {apply*create-receiver*copy-structure-type
   (state <s> ^name << comprehend-word comprehend-construction >>
              ^comprehension-structure.not-merged-receiver <item>)
   (<item> ^lt.structure-type <stype>)
-->    
   (<item> ^structure-type <stype>)}

####
## Add first-word true from current-word

sp {apply*create-receiver*copy*first-word-true
   (state <s> ^operator <op>
              ^current-word.first-word true
             -^name comprehend-construction)
    (<op> ^name create-receiver
          ^item <item>)
-->
    (<item> ^first-word true)
}

