sp {comprehend-word*propose*process-preset-semantic-structure
   (state <s> ^name comprehend-word
              ^segment.semantics <sem>
              ^retrieved-lexical-item.preset-semantic-structure <pss>)
   (<pss> ^semantic-structure  <ss>
          ^referent.object <sg>
          ^spatial-relation <sp>)
  -(<sem> ^<ss>.object <sg>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name process-preset-semantic-structure
         ^preset-semantic-structure <pss>)
}

sp {apply*process-preset-semantic-structure*relation
   (state <s> ^operator <op>
              ^segment.semantics <sem>)
   (<op> ^name process-preset-semantic-structure
         ^preset-semantic-structure <pss>)
   (<pss> ^semantic-structure <ss>
          ^spatial-relation <sp>
          ^referent.object <sg>)
-->
   (<sem> ^<ss> <sr>)
   (<sr> ^relation <sp>
         ^object <sg>)
}

sp {comprehend-word*propose*process-preset-semantic-structure*not-relation
   (state <s> ^name comprehend-word
              ^segment.semantics <sem>
              ^retrieved-lexical-item.preset-semantic-structure <pss>)
   (<pss> ^semantic-structure << object agent assignment >>
          ^referent <sg>)
  -(<sem> ^ << agent object assignment >> <sg>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name process-preset-semantic-structure
         ^preset-semantic-structure <pss>)
}

sp {apply*process-preset-semantic-structure*infocus
   (state <s> ^operator <op>
              ^segment.semantics <sem>)
   (<op> ^name process-preset-semantic-structure
         ^preset-semantic-structure <pss>)
   (<pss> ^semantic-structure { <x> << object agent assignment >> }
          ^referent <sg>)
-->
   (<sem> ^<x> <sg>)
}


#####

