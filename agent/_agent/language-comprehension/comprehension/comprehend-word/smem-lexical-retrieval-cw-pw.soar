sp {comprehend-word*propose*smem-lexical-retrieval-cw-pw
   (state <s> ^name comprehend-word
              ^current-word.spelling <up>
              ^segment <seg>)
   (<s> -^retrieved-lexical-item)
  -{(<s> ^failed-retrieval <fr>)
    (<fr> ^current-word <up>
          ^prior-word <pick>)}
   (<seg> ^prior-word.spelling <pick>)
-->
   (<s> ^operator <o> +)
   (<o> ^name smem-lexical-retrieval-cw-pw
        ^current-word <up>
        ^prior-word <pick>
        ^prior-word-stale true)}

sp {comprehend-word*propose*smem-lexical-retrieval-cw-pw-type
   (state <s> ^name comprehend-word
              ^current-word.spelling <up>
              ^segment <seg>)
   (<s> -^retrieved-lexical-item)
  -{(<s> ^failed-retrieval <fr>)
    (<fr> ^current-word <up>
          ^prior-word <pick-type>)}
   (<seg> ^retrieved-stack.item.lt-lexical-item.structure-type <pick-type>)
-->
   (<s> ^operator <o> +)
   (<o> ^name smem-lexical-retrieval-cw-pw-type
        ^current-word <up>
        ^prior-word <pick-type>)}

sp {comprehend-word*compare*smem-lexical-retrieval-cw-pw*smem-lexical-retrieval
   (state <s> ^name comprehend-word
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name << smem-lexical-retrieval-cw-pw smem-lexical-retrieval-cw-pw-type >>)
   (<op2> ^name smem-lexical-retrieval)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {comprehend-word*compare*smem-lexical-retrieval-cw-pw*smem-lexical-retrieval-cw-pw-type
   (state <s> ^name comprehend-word
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name smem-lexical-retrieval-cw-pw)
   (<op2> ^name smem-lexical-retrieval-cw-pw-type)
-->
   (<s> ^operator <op1> > <op2>)
}

##### Apply

sp {comprehend-construction*apply*smem-lexical-retrieval-cw-pw
   (state <s> ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name << smem-lexical-retrieval-cw-pw smem-lexical-retrieval-cw-pw-type >>
        ^current-word <up>
        ^prior-word <pick>)
-->
   (<cmd> ^query <x>)
   (<x> ^current-word <up>
        ^prior-word <pick>)
}
############@@@@@@@@@@@@@@@@@##########
sp {comprehend-word*apply*smem-lexical-retrieval-assigner-prior-word-stale
   (state <s> ^operator <o>
              ^segment <seg>
              ^smem.result.retrieved <rli>)
   (<o> ^prior-word <pw>
        ^prior-word-stale true)
   (<seg> ^unfilled-assigner-item <uai>
          ^comprehension-structure <cs>)
   (<uai> ^lt-lexical-assigner.parent-lexical-item.spelling <pw>)
-->
   (write (crlf) |Remove assigner for prior word: | <pw>)
   (<cs> ^remove-assigner <uai>)}

sp {comprehend-word*apply*smem-lexical-retrieval-receiver-prior-word-stale
   (state <s> ^operator <o>
              ^segment <seg>
              ^smem.result.retrieved <rli>)
   (<o> ^prior-word <pw>
        ^prior-word-stale true)
   (<seg> ^unfilled-receiver-item <uai>
          ^comprehension-structure <cs>)
   (<uai> ^lt-lexical-item.spelling <pw>)
-->
   (write (crlf) |Remove receiver for prior word: | <pw>)
   (<cs> ^remove-receiver <uai>)}
