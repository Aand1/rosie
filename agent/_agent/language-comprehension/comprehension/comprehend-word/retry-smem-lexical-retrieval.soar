sp {comprehend-word*propose*retry-smem-lexical-retrieval
   (state <s> ^name comprehend-word
              ^current-word <cw>)
   (<s> -^retrieved-lexical-item
        -^failed-retrieval true
         ^segment.retry <retry>)
-->
   (<s> ^operator <o>)
   (<o> ^name retry-smem-lexical-retrieval
        ^word <cw>
        ^retry <retry>)}

sp {comprehend-word*apply*retry-smem-lexical-retrieval
   (state <s> ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name retry-smem-lexical-retrieval
        ^word.spelling <x>
        ^retry.lt.alternative-structure <as>)
-->   
   (write (crlf) |Attempt retrieval for: | <x> | as a | <as>)
   (<cmd> ^query <q>)
   (<q> ^spelling <x>
        ^structure-type <as>)
}

sp {comprehend-word*apply*retry-smem-lexical-retrieval*success
   (state <s> ^operator.name retry-smem-lexical-retrieval 
              ^segment.comprehension-structure <cs>
              ^smem <smem>)
   (<smem> ^result.retrieved <rli>
           ^command <c>)
   (<c> ^query <q>)
-->
   (<c> ^query <q> -)
   (<s> ^retrieved-lexical-item <rli>)
   (<cs> ^retrieved-lexical-item <rli>)
}

sp {comprehend-word*apply*retry-smem-lexical-retrieval*success*monitor
   (state <s> ^operator.name retry-smem-lexical-retrieval
              ^smem <smem>)
   (<smem> ^result.retrieved <rli>
           ^command <c>)
   (<c> ^query <q>)
   (<q> ^spelling <x>)
-->
   (write (crlf) |Retrieval Success: | <x>)
}

sp {comprehend-word*apply*retry-smem-lexical-retrieval*fail
   (state <s> ^superstate.operator <so>
              ^operator.name retry-smem-lexical-retrieval 
              ^smem <smem>)
   (<smem> ^result.failure <t>)
-->
   (write (crlf) |No lexical item found in semantic memory.|)
   (<s> ^failed-retrieval true)
}
