sp {comprehend-word*propose*smem-lexical-retrieval
   (state <s> ^name << comprehend-word comprehend-construction >>
              ^current-word <cw>)
   (<s> -^retrieved-lexical-item
        -^comprehension-structure.retrieved-lexical-item
        -^failed-retrieval true
        -^segment.retry
         ^segment <seg>)
  -{(<seg> ^prior-word.spelling |*|)
    (<cw> ^next.spelling |.|)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name smem-lexical-retrieval
        ^word <cw>)}


### Retrieval

sp {comprehend-word*apply*smem-lexical-retrieval*ignore-whether-first-word
   (state <s> ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name smem-lexical-retrieval
        ^word <word>)
   (<word> ^spelling <x>)
-->   
   (write (crlf) |Attempt retrieval for: | <x>)
   (<cmd> ^query <q> ^depth 9)
   (<q> ^spelling <x>)
}

#   (<p-item> ^current-word.first-word true) # Added JEL 4/39

### After retrieval
sp {comprehend-word*apply*smem-lexical-retrieval*success
   (state <s> ^operator.name << smem-lexical-retrieval smem-lexical-retrieval-cw-pw-type >>
              ^comprehension-structure <cs>
              ^smem <smem>)
   (<smem> ^result.retrieved <rli>
           ^command <c>)
   (<c> ^query <q>)
   (<q> ^spelling <x>)
   (<rli> ^spelling <x>)
-->
   (<c> ^query <q> -
        ^depth 9 -)
   (<cs> ^retrieved-lexical-item <rli>)
}

sp {comprehend-word*apply*smem-lexical-retrieval*success*monitor
   (state <s> ^operator.name smem-lexical-retrieval
              ^smem <smem>)
   (<smem> ^result.retrieved <rli>
           ^command <c>)
   (<c> ^query <q>)
   (<q> ^spelling <x>)
   (<rli> ^structure-type <st>
          ^spelling <x>)
-->
   (write (crlf) |Retrieval Success: | <x> |, | <st>)
}

sp {comprehend-word*apply*smem-lexical-retrieval*fail
   (state <s> ^superstate.operator <so>
              ^operator.name smem-lexical-retrieval 
              ^smem <smem>)
   (<smem> ^result.failure <t>)
-->
   (write (crlf) |No lexical item found in semantic memory.|)
   (<s> ^failed-retrieval true)
}
