sp {comprehension*propose*failed-comprehension
   (state <s> ^name comprehension
              ^segment.original-sentence <is>)
   (<is> ^processing-result failure
        -^processed-sentence true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name failed-comprehension)
}

sp {comprehension*apply*failed-comprehension*external*no-new-word
   (state <s> ^name comprehension
              ^top-state.world-usage external
              ^operator <op>
              ^segment <seg>)
   (<seg> ^original-sentence <is>
          ^semantics <sem>)
   (<op> ^name failed-comprehension)
  -(<sem> ^new-word)
-->
   (<sem> ^message-type failed-comprehension)
}

sp {comprehension*apply*failed-comprehension*external*new-word
   :interrupt
   (state <s> ^name comprehension
              ^operator <op>
              ^segment <seg>)
   (<seg> ^original-sentence <is>
          ^semantics <sem>)
   (<op> ^name failed-comprehension)
   (<sem> ^new-word)
-->
   (<sem> ^message-type unknown-word)
}

sp {comprehension*apply*failed-comprehension*internal
   (state <s> ^name comprehension
              ^operator <op>
              ^segment <seg>
              ^top-state <ts>)
   (<ts> ^world-usage internal
         ^current-sentence-number <csn>)
   (<seg> ^original-sentence <is>
          ^semantics <sem>
         -^not-merged-receiver.lt.alternative-structure)
   (<op> ^name failed-comprehension)
  -(<sem> ^new-word)
-->
   (<ts> ^terminated-sentence <csn>)
   (<sem> ^message-type failed-comprehension)
}


sp {apply*failed-comprehension
   (state <s> ^name comprehension
              ^operator.name failed-comprehension
              ^segment <seg>
              ^top-state.interaction.status <status>)
   (<seg> ^semantics <sem>
          ^original-sentence <is>)
   (<sem> ^message-type)
-->   
   (<is> ^processed-sentence true)
   (<status> ^new-message <sem>)
}

sp {comprehension*apply*failed-comprehension*internal*new-word
  # :interrupt
   (state <s> ^name comprehension
              ^operator <op>
              ^segment <seg>
              ^top-state <ts>)
   (<ts> ^world-usage internal
         ^current-sentence-number <csn>)
   (<seg> ^original-sentence <is>
          ^semantics <sem>)
   (<op> ^name failed-comprehension)
   (<sem> ^new-word)
-->
   (<ts> ^terminated-sentence <csn>)
   (<sem> ^message-type unknown-word)
}


sp {comprehension*apply*failed-comprehension*internal*new-word
   :interrupt
   (state <s> ^name comprehension
              ^operator <op>
              ^segment <seg>
              ^top-state <ts>)
   (<ts> ^world-usage internal
         ^current-sentence-number <csn>)
   (<seg> ^original-sentence <is>
         -^not-merged-receiver.lt.alternative-structure
          ^semantics <sem>)
   (<op> ^name failed-comprehension)
   (<sem> ^new-word)
-->
   (<ts> ^terminated-sentence <csn>)
   (<sem> ^message-type unknown-word)
}


sp {comprehension*apply*failed-comprehension*internal*alternative-structure
   :interrupt
   (state <s> ^name comprehension
              ^operator <op>
              ^segment <seg>
              ^top-state <ts>)
   (<ts> ^world-usage internal
         ^current-sentence-number <csn>)
   (<seg> ^original-sentence <is>
          ^not-merged-receiver <nmr> 
          ^semantics <sem>)
   (<op> ^name failed-comprehension)
   (<sem> -^new-word)
   (<nmr> ^lt.alternative-structure)
-->
   (<seg> ^parse-failed-receiver <nmr>)
}