sp {comprehension*propose*process-phrase-end
   (state <s> ^name comprehension
              ^segment <seg>)
   (<seg> ^retrieved-stack.item { <> <ps> <item> }
          ^last-merge.assigner.parent-structure <ps>
          ^last-merge.receiver <> <item>)
   (<ps> ^lt-lexical-item.converts-to-receiver-structure <nstruct>)
   (<item> ^structure-type <s-type>)
  -(<ps> ^structure-type <nstruct>)
  -{(<seg> ^unfilled-assigner-item <uai>)
    (<uai> ^parent-structure <ps>
           ^lt-lexical-assigner.required true)}
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name process-phrase-end
         ^assigner-parent <ps>)
}

sp {apply*process-phrase-end
   (state <s> ^operator <op>
              ^segment <seg>)
   (<op> ^name process-phrase-end
         ^assigner-parent <ps>)
   (<ps> ^structure-type { <> <nstruct> <oldstruct> }
         ^lt-lexical-item.converts-to-receiver-structure <nstruct>)
-->
   (write (crlf) |Convert | <ps> | from | <oldstruct> | to | <nstruct>)
   (<ps> ^structure-type <nstruct>
                         <oldstruct> -)
   (<seg> ^unfilled-receiver-item <ps>) #probably unnecessary
}
#
#sp {apply*process-phrase-end*backup-stack
#   (state <s> ^operator <op>
#              ^segment <seg>)
#   (<op> ^name process-phrase-end
#         ^assigner-parent <ps>)
#   (<seg> ^retrieved-stack <rs>)
#   (<rs> ^item <item>
#         ^prior <prior>)
#-->
#   (<seg> ^retrieved-stack <rs> -
#                           <prior>)
#   (<prior> ^next <rs>)
#}
#

sp {comprehension*compare*process-phrase-end*prefer-merge-before-process-end
   (state <s> ^name comprehension
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name process-phrase-end
          ^assigner-parent <ap>)
   (<op2> ^name merge
          ^assigner.parent-structure <ap>)
-->
   (<s> ^operator <op1> < <op2>)
}

sp {comprehension*compare*process-phrase-end*prefer-merge-before-process-end*not
   (state <s> ^name comprehension
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name process-phrase-end
          ^assigner-parent <ap>)
   (<op2> ^name merge
         -^assigner.parent-structure <ap>)
-->
   (<s> ^operator <op1> > <op2>)
}

