sp {elaborate*top-state*pass-down-evaluate
   (state <s> ^name evaluate-operator
              ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)}

                   ### NEEDS WORK
sp {evaluate-operator*elaborate*state*merge
   (state <s> ^name evaluate-operator
              ^superstate <ss>)
   (<ss> ^operator.superoperator <so>
         ^top-state.world <world>
         ^superstate <sss>)
   (<so> ^name merge
         ^assigner <assigner-item>
         ^receiver <receiver-item>)
   (<sss> ^segment.last-merge <lm>)
   (<lm> ^receiver <r1>
         ^assigner <a1>
         ^structure-type <st>)
   (<assigner-item> ^parent-structure <aps>)
-->
   (<s> ^world <world>
        ^id-mapping <im>
        ^assigner-parent-structure <apsi>
        ^segment <seg>)
   (<seg> ^last-merge <lmx>
          ^unfilled-receiver-item <ri>
          ^unfilled-assigner-item <ni>)
   (<lmx> ^receiver <r1>
          ^assigner <a1>
          ^structure-type <st>)
   (<im> ^<assigner-item> <ni>
         ^<receiver-item> <ri>
         ^<aps> <apsi>)
   (<ni> ^parent-structure <apsi>)
}

sp {evaluate-operator*elaborate*state*receiver
   (state <s> ^name evaluate-operator
              ^superstate <ss>
              ^id-mapping <im>)
   (<ss> ^operator.superoperator.name merge
         ^superstate.segment <sss>)
   (<sss> ^unfilled-receiver-item <receiver-item>)
   (<receiver-item> ^<att> <value>)
   (<im> ^<receiver-item> <ri>)
-->
   (<ri> ^<att> <value>)
}

sp {evaluate-operator*elaborate*state*assigner
   (state <s> ^name evaluate-operator
              ^superstate <ss>
              ^id-mapping <im>)
   (<ss> ^operator.superoperator.name merge
         ^superstate.segment <sss>)
   (<sss> ^unfilled-assigner-item <assigner-item>)
   (<im> ^<assigner-item> <ai>)
   (<assigner-item> ^{ <> parent-structure <att> } <value>)
-->
   (<ai> ^<att> <value>)
}

sp {evaluate-operator*elaborate*state*receiver*parent
   (state <s> ^name evaluate-operator
              ^superstate <ss>
              ^id-mapping <im>)
   (<ss> ^operator.superoperator.name merge
         ^superstate.segment <sss>)
   (<sss> ^unfilled-assigner-item <uai>)
   (<uai> ^parent-structure <aps>)
   (<aps> ^<att> <value>)
   (<im> ^<aps> <apsi>
         ^<uai> <nai>)
-->
   (<apsi> ^<att> <value>)
}

excise evaluate-operator*elaborate*state*look-ahead-operator*no-copy
sp {evaluate-operator*elaborate*state*look-ahead-operator*copy
   :default
   (state <s> ^name evaluate-operator
              ^superstate.operator <so>
              ^id-mapping <im>)
   (<so> ^name evaluate-operator
         ^superoperator <o>)
   (<o> ^name merge
        ^receiver <receiver-item>
        ^assigner <assigner-item>)
   (<im> ^<assigner-item> <ni>
         ^<receiver-item> <ri>)
-->
   (<s> ^look-ahead-operator <copy-o>)
   (<s> ^untried-tied-operator <copy-o>)
   (<copy-o> ^name merge
             ^receiver <ri>
             ^assigner <ni>)}

