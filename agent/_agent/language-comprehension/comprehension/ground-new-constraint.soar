sp {comprehension*propose*ground-new-constraint
   (state <s> ^name comprehension
              ^segment.new-constraint <nc>)
   (<nc> ^object <obj>)
  -(<obj> ^ground-tested <nc>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
}
sp {apply*ground-new-constraint*success
   (state <s> ^operator <op>
              ^segment <seg>
              ^world.prepositions <wsr>)
   (<seg> ^new-constraint <nc>)
   (<wsr> ^name <sr>
          ^pair <pwsr>)
   (<pwsr> ^a <r-1>
           ^b <r-2>)
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
   (<nc> ^object <pword>
         ^constraint <c>)
   (<c> ^preposition <prep>)
   (<pword> ^referent.object.id <r-1>)
   (<prep> ^lt-lexical-item.spatial-relation <sr>
           ^head.referent.object.id <r-2>)
-->
   (<seg> ^new-constraint <nc> -)
   (<pword> ^ground-tested <nc>)
   (write (crlf) |Successful relation grounding: | <r-1> | | <sr> | | <r-2>)
}

sp {apply*ground-new-constraint*removed-failed
   (state <s> ^operator <op>
              ^segment.new-constraint <nc>)
  -{(<s> ^world.prepositions <wsr>)
    (<wsr> ^name <sr>
           ^pair <pwsr>)
    (<pwsr> ^a <r-1>
            ^b <r-2>)}
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
   (<nc> ^object <pword>
         ^constraint <c>)
   (<c> ^preposition <prep>)
   (<pword> ^referent <obj1>)
   (<obj1> ^object.id <r-1>) # not sure it needs the DP
   (<prep> ^lt-lexical-item.spatial-relation <sr>
           ^head <head>)
   (<head> ^referent.object.id <r-2>)
-->
   (<pword> ^referent <obj1> -
            ^failed-constraint <fc>)
   (<fc> ^constraint <c>
         ^object <r-1>)
   (write (crlf) |Referent removed: | <r-1>)
}

sp {apply*ground-new-constraint*removed-semantics-failed
   (state <s> ^operator <op>
              ^segment <seg>)
   (<seg> ^semantics <sem>
          ^new-constraint <nc>)
   (<sem> ^<anything> <x>)
   (<x> ^object.id <r-1>)
  -{(<s> ^world.prepositions <wsr>)
   (<wsr> ^name <sr>
          ^pair <pwsr>)
   (<pwsr> ^a <r-1>
          ^b <r-2>)}
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
   (<nc> ^object <pword>
         ^constraint <c>)
   (<c> ^preposition <prep>)
   (<pword> ^referent.object.id <r-1>) # not sure it needs the DP
   (<prep> ^lt-lexical-item.spatial-relation <sr>
           ^head <head>)
   (<head> ^referent.object.id <r-2>)
-->
   (<sem> ^<anything> <x> -)
   (write (crlf) |Semantic referent removed: | <x>)
}

sp {apply*ground-new-constraint*failure
   (state <s> ^operator <op>
              ^segment <seg>)
   (<seg> ^new-constraint <nc>)
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
   (<nc> ^object <pword>
         ^constraint <c>)
   (<c> ^preposition <prep>)
   (<pword> ^referent <obj1>)
   (<obj1> ^object.id <r-1>)
   (<prep> ^lt-lexical-item.spatial-relation <sr>
           ^head <noun>)
   (<noun> ^referent.object.id <r-2>)
  -{(<s> ^world.prepositions <wsr>)
   (<wsr> ^name <sr>
          ^pair <pwsr>)
   (<pwsr> ^a <r-1>
          ^b <r-2>)}
   (<pword> ^lt-lexical-item.spelling <spelling>)
-->
   (<seg> ^new-constraint <nc> -)
   (<pword> ^ground-tested <nc>
            ^failed-constraint <fc>
            ^failed-grounding <fg>)
   (<fc> ^constraint <c>
         ^object <r-1>)
   (<fg> ^assigner-word <spelling>
         ^structure-type P
         ^spatial-relation <sr>
         ^lost-grounding <r-1>)
   (<pword> ^referent <obj1> -)
   (write (crlf) |Failed relation grounding: | <r-1> | | <sr> | | <r-2>)
   (write (crlf) |   Remove | <r-1> | as referent for | <pword> )
}

sp {apply*ground-new-constraint*failure*V
   (state <s> ^operator <op>
              ^segment <seg>)
   (<seg> ^new-constraint <nc>)
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
   (<nc> ^object.PP.DP <pword>
         ^constraint <c>)
   (<c> ^preposition <prep>)
   (<pword> ^referent.object.id <r-1>)
   (<prep> ^lt-lexical-item.spatial-relation <sr>
           ^head <noun>)
   (<noun> ^referent.object.id <r-2>)
  -{(<s> ^world.prepositions <wsr>)
   (<wsr> ^name <sr>
          ^pair <pwsr>)
   (<pwsr> ^a <r-1>
          ^b <r-2>)}
   (<pword> ^spelling <spelling>)
-->
   (<seg> ^new-constraint <nc> -)
   (<pword> ^ground-tested <nc>
            ^failed-constraint.object <r-1>
            ^failed-grounding <fg>)
   (<fg> ^assigner-word <spelling>
         ^structure-type P
         ^spatial-relation <sr>
         ^lost-grounding <r-1>)
   (<pword> ^referent <r-1> -)
   (write (crlf) |Failed relation grounding: | <r-1> | | <sr> | | <r-2>)
   (write (crlf) |   Remove | <r-1> | as referent for | <pword> )
}


############ No need to ground constraint to verb
#sp {apply*ground-new-constraint*verb-nothing
#   (state <s> ^operator <op>
#              ^new-constraint <nc>)
#   (<op> ^name ground-new-constraint
#         ^constraint <nc>)
#   (<nc> ^constraint <con>
#         ^object <obj>)
#   (<con> ^preposition.lt-lexical-item <prep>)
#   (<obj> ^structure-type V)
#-->
#   (<s> ^new-constraint <nc> -)
#   (<obj> ^ground-tested <nc>)
#   (write (crlf) |No need to test constraint on verb.|)
#}


sp {apply*ground-new-constraint*remove-no-referent
   (state <s> ^operator <op>
              ^segment <seg>)
   (<seg> ^last-merge.receiver <lmr>
          ^new-constraint <nc>)
   (<s> ^world.prepositions <wsr>)
   (<wsr> ^name <sr>
          ^pair <pwsr>)
   (<pwsr> ^a <r-1>
          ^b <r-2>)
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
   (<nc> ^object <pword>
         ^constraint <c>)
   (<c> ^preposition <prep>)
   (<prep> ^lt-lexical-item.spatial-relation <sr>
           ^head <head>)
  -{(<pword> ^referent.object.id <r-1>)
    (<head> ^referent.object.id <r-2>)}
-->
   (<seg> ^new-constraint <nc> -)
   (<pword> ^ground-tested <nc>
            ^ground-tested <lmr>)
   (write (crlf) |No grounding for | <nc>)
}


sp {apply*ground-new-constraint*remove-no-referent2
   (state <s> ^operator <op>
              ^segment <seg>)
   (<seg> ^last-merge.receiver <lmr>
          ^new-constraint <nc>)
  -{(<s> ^world.prepositions <wsr>)
   (<wsr> ^name <sr>
          ^pair <pwsr>)
   (<pwsr> ^a <r-1>
          ^b <r-2>)}
   (<op> ^name ground-new-constraint
         ^constraint <nc>)
   (<nc> ^object <pword>
         ^constraint <c>)
   (<c> ^preposition <prep>)
   (<prep> ^lt-lexical-item.spatial-relation <sr>
           ^head <head>)
   (<pword> ^referent.object.id <r-1>)
   (<head> ^referent.object.id <r-2>)
-->
   (<seg> ^new-constraint <nc> -)
   (<pword> ^ground-tested <nc>
            ^ground-tested <lmr>)
   (write (crlf) |No grounding for | <nc>)
}