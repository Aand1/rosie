
sp {comprehension*propose*failure-end-of-sentence
   (state <s> ^name comprehension
              ^segment <seg>)
   (<seg> ^parse-failed-end-of-sentence true
         -^original-sentence.processing-result failure
          ^not-merged-receiver <nmr>
         -^retry-once <nmr>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name failure-end-of-sentence)}

sp {comprehension*apply*failure-end-of-sentence2
   (state <s> ^operator.name failure-end-of-sentence
              ^segment <seg>)
   (<seg> ^not-merged-receiver <nmr>)
   (<nmr> ^lt.alternative-structure <structure-new>)
-->
   (write (crlf) |Failed hitting end of sentence without valid parse. Have an ambiguous word that didn't merge to retry.|)
   (<seg> ^retry <nmr>)}


sp {comprehension*apply*failure-end-of-sentence*saved-alternative
   (state <s> ^operator.name failure-end-of-sentence
              ^segment <seg>)
   (<seg> -^not-merged-receiver.lt.alternative-structure <structure-new>
           ^alternative-structure-receiver <asr>)
-->
   (write (crlf) |Failed hitting end of sentence without valid parse. Have an *ambiguous word that didn't merge to retry.|)
   (<seg> ^retry <asr>)}

sp {comprehension*apply*failure-end-of-sentence
   (state <s> ^operator.name failure-end-of-sentence
              ^segment <seg>)
   (<seg> -^not-merged-receiver.lt.alternative-structure
          -^alternative-structure-receiver
           ^original-sentence <os>
           ^semantics <sem>)
-->
   (write (crlf) |Failed hitting end of sentence without valid parse. No unmerged ambiguous word. Will not retry parsing sentence.|)
   (<os> ^processing-result failure)
   (<sem> ^interaction-status.failure <is>)
   (<is> ^type failed-parse
         ^word <spell>)}