sp {termination-processing*propose*create-new-adjective
   #:interrupt
   (state <s> ^name termination-processing
             -^stored-message <sem>
              ^segment.semantics <sem>)
   (<sem> ^message-type adjective-definition
          ^new-word <nw>
          ^object-feature <spell>
          ^predicate { <> shape <> name })
   (<nw> ^structure-type U
         ^id <id>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name create-new-adjective
         ^perceptual-feature (make-constant-symbol <spell>)
         ^spelling <spell>
         ^structure-type ADJ)
}

sp {termination-processing*propose*create-new-noun
 #  :interrupt
   (state <s> ^name termination-processing
             -^stored-message <sem>
              ^segment.semantics <sem>)
   (<sem> ^message-type adjective-definition
          ^new-word <nw>
          ^object-feature <spell>)
   (<sem> ^predicate << name shape >>)
   (<nw> ^structure-type U
         ^id <id>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name create-new-adjective
         ^perceptual-feature (make-constant-symbol <spell>)
         ^spelling <spell>
         ^structure-type N) 
}

sp {termination-processing*propose*create-new-adjective*seen-word-before-but-not-defined
  #:interrupt
   (state <s> ^name termination-processing
             -^stored-message <sem>
              ^segment.semantics <sem>)
   (<sem> ^message-type adjective-definition
          ^new-word.id <id> 
          ^object-feature <ofih>)
   (<ofih> ^spelling <spell>
          -^object-feature
          -^perceptual-feature
           ^structure-type <stype>)
 -->
   (<s> ^operator <op> + =, >)
   (<op> ^name create-new-adjective
         ^perceptual-feature <id> # (make-constant-symbol <spell>)
         ^spelling <spell>
         ^structure-type <stype>)  
}

sp {termination-processing*compare*create-new-adjective
   (state <s> ^name termination-processing
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name create-new-adjective)
   (<op2> ^name print-semantics)
-->
   (<s> ^operator <op1> > <op2>)
}


sp {termination-processing*apply*create-new-structure
   (state <s> ^operator <op>
              ^segment.semantics <sem>
              ^superstate.smem.command <cmd>)
   (<op> ^name create-new-adjective
         ^perceptual-feature <pf>         
         ^spelling <spell>
         ^structure-type <type>)
   (<sem> ^predicate <pred>
         -^new-word.structure-type U)
-->
   (<sem> ^feature-value <pf>
          ^feature-type <pred>)
   (<s> ^stored-message <sem>)
   (<cmd> ^store <new>)
   (<new> ^object-feature <pred>
          ^perceptual-feature <pf>
          ^structure-type <type>
          ^spelling <spell>)
   (write (crlf) |Create new | <type> |: | <pf> | of type | <pred>)
}

sp {termination-processing*apply*create-new-adjective*update-semantic-structure
   (state <s> ^operator <op>
              ^superstate.smem.command <cmd>
              ^segment.semantics <sem>)
   (<sem> ^new-word <nw>
          ^predicate <pred>)
   (<op> ^name create-new-adjective
         ^structure-type <type>)
   (<nw> ^structure-type U
         ^object-feature unknown)
-->
   (<nw> ^structure-type U -
                         <type>
         ^object-feature unknown -
                         <pred>)
  (<cmd> ^store <nw>)
}

sp {termination-processing*apply*create-new-adjective*update-semantic-memory
   (state <s> ^operator <op>
              ^superstate.smem.command <cmd>
              ^segment.semantics <sem>)
   (<sem> ^predicate <pred>
          ^object-feature <nw>)
   (<op> ^name create-new-adjective
         ^structure-type <type>)
   (<nw> ^structure-type U
         ^object-feature unknown)
-->
   (<nw> ^structure-type U -
                         <type>
         ^object-feature unknown -
                         <pred>)
   (<cmd> ^store <nw>)
}

### Remove index-word-failure because this sentence is defining it.
sp {termination-processing*apply*remove-interaction-status
 #  :interrupt
   (state <s> ^operator <op>
              ^segment.semantics <sem>)
   (<op> ^name create-new-adjective)
   (<sem> ^interaction-status <status>)
   (<status> ^type index-word-failure)
-->
   (<sem> ^interaction-status <status> -)
}


