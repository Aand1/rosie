sp {termination-processing*propose*create-new-verb
  # :interrupt
   (state <s> ^name termination-processing
              ^segment.new-verb <nv>
              ^segment.semantics <sem>
             -^new-verb <nv>)
   (<sem> ^message-type new-verb
          ^action <action>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name create-new-verb
         ^action <action>
         ^verb <nv>)
}
#   @T999  Move DP PP 
#   @T998  Pick DP (movable)
#   @T997  Clean (not movable)
#   @T996  Store DP (PP optional)
#   @T995  Point PP
#   @T994  Stop

smem --add {(@T999 ^att test)
            (@T998 ^att test)
            (@T996 ^att test)
            (@T995 ^att test)}

sp {termination-processing*apply*create-new-verb*moveable*arg1*998
   (state <s> ^operator <op>
              ^top-state.smem.command <cmd>
              ^segment.semantics <sem>)
   (<op> ^name create-new-verb
         ^action <action>
         ^verb <nv>)
   (<sem> ^arg1
         -^arg2)
   (<action> ^handle <name>)
   (<nv> ^spelling <nw>)
-->      
   (write (crlf) |Create new verb: | <action> |: type T996 (arg2 optional)|)
   (<s> ^new-verb <nv> 
        ^checked <nw>)   # so detect used but unknown word doesn't get selected.
   (<cmd> ^store <nv>)
   (<nv> ^new-action true
         ^super-type @T996)  #allow it to add later
}

sp {termination-processing*apply*create-new-verb*moveable*arg1*999
   (state <s> ^operator <op>
              ^top-state.smem.command <cmd>
              ^segment.semantics <sem>)
   (<op> ^name create-new-verb
         ^action <action>
         ^verb <nv>)
   (<sem> ^arg1
          ^arg2)
   (<action> ^handle <name>)
   (<nv> ^spelling <nw>)
-->      
   (write (crlf) |Create new verb: | <action> |: , type: T996 (arg2 optional)|)
   (<s> ^new-verb <nv>
        ^checked <nw>)   # so detect used but unknown word doesn't get selected.
   (<cmd> ^store <nv>)
   (<nv> ^new-action true
         ^super-type @T996)     ## Allow it to drop it later
}

sp {termination-processing*apply*create-new-verb*moveable*arg2*995
   (state <s> ^operator <op>
              ^top-state.smem.command <cmd>
              ^segment.semantics <sem>)
   (<op> ^name create-new-verb
         ^action <action>
         ^verb <nv>)
   (<nv> ^spelling <nw>)
   (<sem> -^arg1
           ^arg2)
-->      
   (write (crlf) |Create new verb: | <action> |: , type: T995|)
   (<s> ^new-verb <nv>
        ^checked <nw>)   # so detect used but unknown word doesn't get selected.
   (<cmd> ^store <nv>)
   (<nv> ^new-action true
         ^super-type @T995)
}
