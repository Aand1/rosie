sp {termination-processing*propose*check-unfilled-assigners
   (state <s> ^name termination-processing)
   (<s> -^checked-unfilled-assigners true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name check-unfilled-assigners)
}

sp {apply*check-unfilled-assigners
   (state <s> ^operator <op>
              ^segment <ss>)
   (<op> ^name check-unfilled-assigners)
  -(<ss> ^unfilled-assigner-item <item>)
-->
   (write (crlf) |No unfilled assigners.|)
   (<s> ^checked-unfilled-assigners true)
}

sp {apply*check-unfilled-assigners-optional
   (state <s> ^operator <op>
              ^segment <sp>)
   (<op> ^name check-unfilled-assigners)
   (<sp> ^unfilled-assigner-item <item>)
   (<item> ^structure-type <stype>
           ^parent-structure.lt-lexical-item.spelling <spelling>
           ^lt-lexical-assigner.optional true)
-->
   (<s> ^checked-unfilled-assigners true)
   (write (crlf) |Unfulfilled optional assigner: | <item> |: | <stype> | for | <spelling>)
}

sp {apply*check-unfilled-assigners-required
   (state <s> ^operator <op>
              ^segment <sp>)
   (<op> ^name check-unfilled-assigners)
   (<sp> ^unfilled-assigner-item <item>)
   (<item> ^structure-type <pos>
           ^lt-lexical-assigner.required true)
-->
   (<s> ^failed <item>)
   (<s> ^checked-unfilled-assigners true)
   (write (crlf) |Unfulfilled required assigner: | <item> | : | <pos>)
}


sp {apply*check-unfilled-assigners-neither
   (state <s> ^operator <op>
              ^segment <sp>)
   (<op> ^name check-unfilled-assigners)
   (<sp> ^unfilled-assigner-item <item>)
   (<item> ^structure-type <pos>
           ^lt-lexical-assigner <lla>)
   (<lla> -^required true
          -^optional true)
-->
   (<s> ^checked-unfilled-assigners true)
   (write (crlf) |Unfulfilled assigner: | <item> | : | <pos>)
}
