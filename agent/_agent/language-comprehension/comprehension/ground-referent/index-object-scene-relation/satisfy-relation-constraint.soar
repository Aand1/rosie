sp {index-object-scene-relation*propose*satisfy-relation-constraint
   (state <s> ^name index-object-scene-relation
              ^candidate-set <cands>
              ^relation-constraint <rc>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name satisfy-relation-constraint
        ^candidate-set <cands>
        ^relation-constraint <rc>)
}

sp {index-object-scene-relation*apply*satisfy-relation-constraint*mark*satisfied
   (state <s> ^name index-object-scene-relation
              ^operator <o>)
   (<o> ^name satisfy-relation-constraint
        ^candidate-set <cands>
        ^relation-constraint <rc>)
-->
   (<cands> ^satisfied-relation <rc>)
}
              
sp {index-object-scene-relation*apply*satisfy-relation-constraint*remove*candidate
   (state <s> ^name index-object-scene-relation
              ^operator <o>)
   (<o> ^name satisfy-relation-constraint
        ^candidate-set <cands>
        ^relation-constraint <rc>)
   (<cands> ^referent <cand>)
   (<cand> -^satisfies <rc>)
-->
   (<cands> ^referent <cand> -)
}
              
   
   
   