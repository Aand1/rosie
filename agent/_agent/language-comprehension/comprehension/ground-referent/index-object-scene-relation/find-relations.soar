sp {index-object-scene-relation*propose*find-relations
   (state <s> ^name index-object-scene-relation
              ^candidate-set.proto-referent <cand>
              ^relation-constraint <rc>
             -^found-relation.<prep> <cand>)
   (<cand> ^object.id <id>)
   (<rc> ^preposition <prep>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-relations
        ^prep <prep>
        ^obj <id>
        ^ind 1)
}

sp {index-object-scene-relation*apply*find-relations*create*relationships
   (state <s> ^name index-object-scene-relation
              ^operator <o>)
   (<o> ^name find-relations
        ^prep <prep>
        ^obj <id>)
-->
   (<s> ^relationships <rels>)
   (<rels> ^preposition <prep>
           ^with <id>)
}

sp {index-object-scene-relation*apply*find-relations*flag*copied
   (state <s> ^name index-object-scene-relation
              ^operator <o>
              ^relationships <rels>)
   (<o> ^name find-relations
        ^prep <prep>
        ^obj <id>)
   (<rels> ^preposition <prep>
           ^with <id>)
-->
   (<rels> ^copied-relations true)
}

sp {index-object-scene-relation*apply*find-relations*copy*relations
   (state <s> ^name index-object-scene-relation
              ^operator <o>
              ^relationships <rels>
              ^top-state.prepositions.prep <p>)
   (<o> ^name find-relations
        ^prep <prep>
        ^obj <id>)
   (<rels> ^preposition <prep>
           ^with <id>)
   (<p> ^name <prep>
        ^pair <pair>)
   (<pair> ^a <id>
           ^b <id2>)
-->
   (<rels> ^relations <r>)
   (<r> ^obj <id2> ^matched true)
}


sp {index-object-scene-relation*apply*find-relations*mark*found
   (state <s> ^name index-object-scene-relation
              ^operator <o>
              ^relationships <rels>
              ^candidate-set.proto-referent <cand>)
   (<o> ^name find-relations
        ^prep <prep>
        ^obj <id>)
   (<rels> ^preposition <prep>
           ^with <id>
           ^copied-relations true)
   (<cand> ^object.id <id>)
-->
   (<s> ^found-relation.<prep> <cand>)
}
  