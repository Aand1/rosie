sp {ground-referent*propose*failed-grounding
   (state <s> ^name ground-referent
              ^quiescence t                # no chunking failure              
              ^candidate-set <cs>)
   (<cs> ^generated-candidates true
        -^proto-referent.object <obj>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name failed-grounding)
}

sp {ground-referent*apply*failed-grounding
   (state <s> ^operator.name failed-grounding
             -^superstate.name evaluate-operator
              ^superstate.operator <so>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <c>)
-->
   (<c> ^failed-grounding <p>
        ^ground-tested <p>)
   (<p> ^failed-constraint <c>
        ^failed-grounding <p>)
   (write (crlf) |Nothing passed all constraints.|)
}

sp {ground-referent*apply*failed-grounding*evaluate
   (state <s> ^operator.name failed-grounding
              ^superstate.name evaluate-operator
              ^superstate.operator <so>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <c>)
-->
   (<c> ^ground-tested <p>)
   (write (crlf) |Nothing passed all constraints.|)
}
sp {apply*tested-referent*monitor*fail*remove-current-referent2
   (state <s> ^operator.name failed-grounding
              ^superstate.operator <so>
              ^constraints <cs>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <cs>)
   (<cs> ^referent <obj>)
-->
   (<cs> ^referent <obj> -)
   (write (crlf) |Remove prior referent | <obj> | from | <cs>)
}