sp {ground-referent*propose*failed-grounding
   (state <s> ^name ground-referent
              ^quiescence t                # no chunking failure              
              ^candidate-set <cs>)
   (<cs> ^generated-candidates true
        -^proto-referent.object <obj>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name failed-grounding)
}

sp {ground-referent*apply*failed-grounding
   (state <s> ^operator.name failed-grounding
             -^superstate.name evaluate-operator)
-->
   (<s> ^proto-referent <pr>)
   (<pr> ^predicates <pred>
         ^id (make-constant-symbol |new-object-id|))
   (write (crlf) |Nothing passed all constraints.|)
}

sp {ground-referent*apply*failed-grounding*property
   (state <s> ^operator.name failed-grounding
              ^proto-referent.predicates <pred>
             -^superstate.name evaluate-operator
              ^superstate.operator.<< double-constraints constraints >>.constraint <constraint>)
   (<constraint> ^name <value> ^property.name <prop>)
-->
   (<pred> ^<prop> <value>)
   (write (crlf) |Add predicate: ^| <prop> | | <value>)
}

sp {ground-referent*apply*failed-grounding*property2
   (state <s> ^operator.name failed-grounding
              ^proto-referent.predicates <pred>
             -^superstate.name evaluate-operator
              ^superstate.operator.<< double-constraints constraints >>.constraint <constraint>)
   (<constraint> ^{ <prop> << movable category type >> } <value>)
-->
   (<pred> ^<prop> <value>)
   (write (crlf) |Add predicate: ^| <prop> | | <value>)
}

sp {ground-referent*apply*failed-grounding*property3
   (state <s> ^operator.name failed-grounding
              ^proto-referent.predicates <pred>
             -^superstate.name evaluate-operator)
-->
   (<pred> ^visible false)
   (write (crlf) |Add predicate: ^visible false|)
}
sp {ground-referent*apply*failed-grounding*finish
   (state <s> ^operator.name failed-grounding
              ^superstate.operator <so>
              ^proto-referent <ref>)
   (<so> ^<< double-constraints constraints >> <c>)
   (<ref> ^predicates.visible false) 
-->
   (<c> ^ground-tested <c>
        ^referent <ref>)
}

sp {apply*tested-referent*monitor*fail*remove-current-referent2
   (state <s> ^operator.name failed-grounding
             -^proto-referent <obj>
              ^superstate.operator <so>
              ^constraints <cs>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <cs>)
   (<cs> ^referent <obj>)
-->
   (<cs> ^referent <obj> -)
   (write (crlf) |Remove prior referent | <obj> | from | <cs>)
}