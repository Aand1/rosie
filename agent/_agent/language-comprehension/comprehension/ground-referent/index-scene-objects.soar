sp {ground-referent*propose*index-scene-object
#:interrupt
  (state <s> ^name ground-referent
             ^candidate-set <cand>
             ^constraint-lt <lli>)
   (<lli> -^demonstrative true
           ^specifier <specifier>)
  -(<cand> ^generated-candidates true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-scene-objects
         ^specifier <specifier>)
}

sp {ground-referent*propose*index-scene-object*demonstrative
#:interrupt
   (state <s> ^name ground-referent
              ^candidate-set <cand>
              ^constraint-lt <lli>)
   (<lli> ^demonstrative true)
  -(<cand> ^generated-candidates true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-scene-objects
         ^specifier demonstrative)
}

sp {ground-referent*propose*index-scene-object*no-specifier
#:interrupt
   (state <s> ^name ground-referent
              ^candidate-set <cand>
              ^constraint-lt <lli>)
   (<lli> -^demonstrative true
          -^specifier)
  -(<cand> ^generated-candidates true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-scene-objects
         ^specifier definite)
}

############# APPLY #########

sp {ground-referent*apply*index-scene-objects*personal*create*robot
   (state <s> ^operator <o>
              ^constraint-count <cc>
              ^candidate-set <cand>
              ^top-state.world.robot <obj>)
   (<o> ^name index-scene-objects
        ^specifier personal)           
-->
   (<cand> ^proto-referent <pr>)
   (<pr> ^constraint-count <cc>
         ^object <obj>)
}

sp {ground-referent*apply*index-scene-objects*create*candidates
   (state <s> ^operator <o>
              ^candidate-set <cand>
              ^constraint-count <cc>
              ^top-state.world.objects.object <obj>)  
   (<o> ^name index-scene-objects
        ^specifier << definite universal >>)
-->
   (<cand> ^proto-referent <pr>)
   (<pr> ^constraint-count <cc>
         ^object <obj>)
}

sp {ground-referent*apply*index-scene-objects*create*current-location*object
   (state <s> ^operator <o>
              ^candidate-set <cand>
              ^constraint-count <cc>
              ^top-state.world.robot.current-location <obj>)  
   (<o> ^name index-scene-objects
        ^specifier << definite universal >>)
-->
   (<cand> ^proto-referent <pr>)
   (<pr> ^constraint-count <cc>
         ^object <obj>)
}
sp {ground-referent*apply*index-scene-objects*create*candidates*dialog
   (state <s> ^operator <o>
              ^candidate-set <cand>
              ^constraint-count <cc>
              ^top-state.dialog-world.objects.object <obj>)  
   (<o> ^name index-scene-objects
        ^specifier << definite universal >>)
-->
   (write (crlf) |Found | <pr> | from dialog-world.|)
   (<cand> ^proto-referent <pr>)
   (<pr> ^constraint-count <cc>
         ^object <obj>)
}

sp {ground-referent*apply*index-scene-objects*create*candidates*dialog*it
   (state <s> ^operator <o>
              ^candidate-set <cand>
              ^constraint-count <cc>
              ^segment.dialog-object-list-access.referent <obj>)  
   (<o> ^name index-scene-objects
        ^specifier it)
-->
   (write (crlf) |Found IT from dialog-list:| <obj>)
   (<cand> ^proto-referent <pr>)
   (<pr> ^constraint-count <cc>
         ^object <obj>)
}
sp {ground-referent*apply*index-scene-objects*create*candidate*indefinite
   (state <s> ^operator <o>
              ^candidate-set <cand>
              ^constraint-count <cc>)  
   (<o> ^name index-scene-objects
        ^specifier indefinite)
-->
   (write (crlf) |Indefinite referent. Constraint count: | <cc>)
   (<cand> ^proto-referent <pr>)
   (<pr> ^constraint-count <cc>)
}

sp {ground-referent*apply*index-scene-objects*create*candidates*parallel
   (state <s> ^operator <o>
              ^candidate-set <cand>
              ^constraint-count <cc>)  
   (<o> ^name index-scene-objects)
-->
   (<cand> ^generated-candidates true)
}
##########

sp {ground-referent*apply*index-scene-objects*demonstrative*create*candidates
   (state <s> ^operator <o>
              ^candidate-set <cand>
              ^constraint-count <cc>
              ^top-state.world.<< robot objects >>.object <obj>)
   (<obj> ^predicates.indicated pointed)              
   (<o> ^name index-scene-objects
        ^specifier demonstrative)           
-->
    (<cand> ^proto-referent <pr>)
   (<pr> ^constraint-count <cc>
         ^object <obj>)
}

sp {ground-referent*apply*index-scene-objects*demonstrative*create*candidates*failure
   (state <s> ^operator <o>
              ^candidate-set <cand>       
              ^superstate.operator <so>)
  -{(<s> ^top-state.world.<< robot objects >>.object <obj>)
    (<obj> ^predicates.indicated pointed)}            
    (<o> ^name index-scene-objects
         ^specifier demonstrative)
    (<so> ^parameter <p>
         ^constraints <c>)
-->
   (<c> ^failed-grounding <p>)   
   (write (crlf) |Nothing being pointed at: |)
   (<cand> ^generated-candidates true)
}
########
