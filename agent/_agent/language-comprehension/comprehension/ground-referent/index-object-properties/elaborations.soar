sp {index-object-properties*elaborate*candidate-set*copy
   (state <s> ^name index-object-properties
              ^superstate.operator.candidate-set <cand>)
-->
   (<s> ^candidate-set <cand>)
}

sp {index-object*properties*elaborate*property-constraint*copy
   (state <s> ^name index-object-properties
              ^superstate.operator.property-constraint <pc>)
-->
   (<s> ^property-constraint <pc>)
}

sp {index-object*properties*elaborate*candidate*violates*no-prop
   (state <s> ^name index-object-properties
              ^candidate-set.proto-referent <cand>
              ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<cand> ^object <obj>)
   (<obj> -^property.<< id name >> <prop-name>
          -^<< id name >> <prop-val>)
-->
   (<cand> ^violates <pc>)
}

sp {index-object*properties*elaborate*candidate*violates*no-value
   (state <s> ^name index-object-properties
              ^candidate-set.proto-referent <cand>
              ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<cand> ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^<< id name >> <prop-name>
          -^value.label
          -^label)
-->
   (<cand> ^violates <pc>)
}

sp {index-object*properties*elaborate*candidate*violates*diff-value
   (state <s> ^name index-object-properties
              ^candidate-set.proto-referent <cand>
              ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<cand> ^object.property <prop>)
   (<prop> ^<< id name >> <prop-name>
           ^value.label <> <prop-val>)
   -(<cand> ^object.<< id name >> <prop-val>)
-->
   (<cand> ^violates <pc>)
}


sp {index-object*properties*elaborate*candidate*violates*diff-value2
   (state <s> ^name index-object-properties
              ^candidate-set.proto-referent <cand>
              ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<cand> ^object.property <prop>)
   (<prop> ^<< id name >> <prop-name>
           ^label <> <prop-val>)
   -(<cand> ^object.<< id name >> <prop-val>)
-->
   (<cand> ^violates <pc>)
}