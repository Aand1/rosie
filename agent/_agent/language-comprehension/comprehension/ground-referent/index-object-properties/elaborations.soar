sp {index-object-properties*elaborate*candidate-set*copy
   (state <s> ^name index-object-properties
              ^superstate.operator.candidate-set <cand>)
-->
   (<s> ^candidate-set <cand>)
}

sp {index-object*properties*elaborate*property-constraint*copy
   (state <s> ^name index-object-properties
              ^superstate.operator.property-constraint <pc>)
-->
   (<s> ^property-constraint <pc>)
}

sp {index-object*properties*elaborate*candidate*violates*no-prop
   (state <s> ^name index-object-properties
              ^candidate-set.referent <cand>
              ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<cand> ^object <obj>)
   (<obj> -^property.name <prop-name>
          -^name <prop-val>)
-->
   (<cand> ^violates <pc>)
}

sp {index-object*properties*elaborate*candidate*violates*no-value
   (state <s> ^name index-object-properties
              ^candidate-set.referent <cand>
              ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<cand> ^object.property <prop>)
   (<prop> ^name <prop-name>
          -^label)
-->
   (<cand> ^violates <pc>)
}

sp {index-object*properties*elaborate*candidate*violates*diff-value
   (state <s> ^name index-object-properties
              ^candidate-set.referent <cand>
              ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<cand> ^object.property <prop>)
   (<prop> ^name <prop-name>
           ^label <> <prop-val>)
   -(<cand> ^object.name <prop-val>)
-->
   (<cand> ^violates <pc>)
}