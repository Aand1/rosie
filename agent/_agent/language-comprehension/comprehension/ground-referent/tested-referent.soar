sp {ground-referent*propose*tested-referent
   (state <s> ^name ground-referent
              ^candidate-set.proto-referent.constraint-count 0)
-->
   (<s> ^operator <op> + =)
   (<op> ^name tested-referent)
}
sp {apply*tested-referent
   (state <s> ^operator.name tested-referent
              ^superstate.operator <so>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <c>)
-->
   (<c> ^ground-tested <p>)
}

sp {apply*tested-referent*monitor
   (state <s> ^operator.name tested-referent
              ^candidate-set.proto-referent.object <obj>)
   (<obj> ^<< name id >> <id>)
-->
   (write (crlf) |Object | <obj> |, id:| <id> | passed all constraints.|)
}

sp {apply*tested-referent*copy-proto-referent
   (state <s> ^operator.name tested-referent
              ^candidate-set <cs>
              ^constraints <cons>)
   (<cs> ^proto-referent <pr>)
   (<pr> ^object <obj>
         ^constraint-count 0)
-->
   (<cons> ^referent <obj>)
}
sp {apply*tested-referent*monitor*fail*remove-current-referent
   (state <s> ^operator.name tested-referent
              ^superstate.operator <so>
              ^constraints <cs>
             -^candidate-set.proto-referent.object <obj>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <cs>)
   (<cs> ^referent <obj>)
-->
   (<cs> ^referent <obj> -)
   (write (crlf) |Remove prior referent | <obj> | from | <cs>)
}
