sp {ground-referent*propose*tested-referent
   (state <s> ^name ground-referent
              ^constraint-processing-finished true
              ^proto-referents one)
-->
   (<s> ^operator <op> + =)
   (<op> ^name tested-referent)
}

sp {ground-referent*propose*tested-referent*processed-multiple
   (state <s> ^name ground-referent
              ^constraint-processing-finished true
              ^processed-multiple true)
-->
   (<s> ^operator <op> + =)
   (<op> ^name tested-referent)
}


sp {apply*tested-referent
   (state <s> ^operator.name tested-referent
              ^superstate.operator <so>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <c>)
-->
   (<c> ^ground-tested <p>)
}

sp {apply*tested-referent*monitor
#:interrupt
   (state <s> ^operator.name tested-referent
              ^candidate-set.proto-referent.object <obj>)
   (<obj> ^handle <id>)
-->
   (write (crlf) |Object | <obj> |, id:| <id> | passed all constraints.|)
}

sp {apply*tested-referent*copy-proto-referent
   (state <s> ^operator.name tested-referent
              ^candidate-set.proto-referent.object <obj>
              ^constraints <cons>)
-->
   (<cons> ^referent <obj>)
}

sp {apply*tested-referent*add-referent-dialog-object-list
   (state <s> ^operator.name tested-referent
             -^proto-referents multiple
              ^top-state <ts>
              ^candidate-set.proto-referent.object <obj>)
   (<ts> ^dialog-object-list <dol>)
  -(<dol> ^referent <obj>)
-->
   (<ts> ^dialog-object-list <dol> -
                             <ndol>)
   (<ndol> ^referent <obj>
           ^next <dol>)
}

sp {apply*tested-referent*add-referent-dialog-object-list-access
   (state <s> ^operator.name tested-referent
             -^proto-referents multiple
              ^segment <ss>
              ^candidate-set.proto-referent.object <obj>)
   (<ss> ^dialog-object-list-access <dol>)
  -(<dol> ^referent <obj>)
-->
   (<ss> ^dialog-object-list-access <dol> -
                                   <ndol>)
   (<ndol> ^referent <obj>
           ^next <dol>)
}

####
sp {apply*tested-referent*monitor*fail*remove-current-referent
   (state <s> ^operator.name tested-referent
              ^superstate.operator <so>
              ^constraints <cs>
             -^candidate-set.proto-referent.object <obj>)
   (<so> ^parameter <p>
         ^<< double-constraints constraints >> <cs>)
   (<cs> ^referent <obj>)
-->
   (<cs> ^referent <obj> -)
   (write (crlf) |Remove prior referent | <obj> | from | <cs>)
}
