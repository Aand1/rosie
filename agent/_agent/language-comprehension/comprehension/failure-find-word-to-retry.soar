## When fail because of not-merged required assigner and the wrong word sense was retreived for an receiver
##   Many cases this won't handle, but this is the simplest.
##   Need to add handling unmerged receivers
## 1. Find a receiver that has right alternative structure if possible
##    otherwise pick most recent receiver (alternative-structure)
## 2. Undo 
#  ^parse-failed end-of-sentence
#sp {comprehension*propose*failure-find-word-to-retry*assigner
#   (state <s> ^name comprehension
#              ^segment <seg>)
#   (<seg> ^parse-failed-assigner <nma>
#          ^not-merged-receiver <nmr>)
#-->
#   (<s> ^operator <op> + =)
#   (<op> ^name failure-find-word-to-retry
#         ^not-merged-assigner <nma>
#         ^not-merged-receiver <nmr>)
#}

sp {comprehension*propose*failure-find-word-to-retry*receiver
   (state <s> ^name comprehension
              ^segment <seg>)
   (<seg> ^parse-failed-receiver <nmr>
          ^not-merged-assigner <nma>
         -^retry <nmr>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name failure-find-word-to-retry
         ^not-merged-receiver <nmr>
         ^not-merged-assigner <nma>)
}

sp {comprehension*propose*failure-find-word-to-retry*end-of-sentence
   (state <s> ^name comprehension
              ^segment <seg>)
   (<seg> ^parse-failed-end-of-sentence true
          ^not-merged-receiver <nmr>
         -^retry)
-->
   (<s> ^operator <op> + =)
   (<op> ^name failure-find-word-to-retry
         ^not-merged-receiver <nmr> 
         ^parse-failed-end-of-sentence true)
}

# Right alternative structure is best!
sp {comprehension*compare*failure-find-word-to-retry*prefer-alternative-structure-match
   (state <s> ^name comprehension
              ^operator <op1> +)
   (<op1> ^name failure-find-word-to-retry
          ^not-merged-receiver.lt.alternative-structure <structure>
          ^not-merged-assigner.lt.structure-type <structure>)
-->
   (<s> ^operator <op1> >)
}
## Prefer latest failed assigner as long as the one compared to does not have right alternative structure
sp {comprehension*compare*failure-find-word-to-retry*prefer-most-recent
   (state <s> ^name comprehension
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name failure-find-word-to-retry
          ^not-merged-receiver.decision-count > <dc>)
   (<op2> ^name failure-find-word-to-retry
          ^not-merged-receiver.decision-count <dc>)
 -{(<op2> ^name failure-find-word-to-retry
          ^not-merged-receiver.lt.alternative-structure <structure>
          ^not-merged-assigner.lt.structure-type <structure>)}
   
-->
   (<s> ^operator <op1> > <op2>)
}

## Prefer latest failed assigner to another one that also has right alternative structure
sp {comprehension*compare*failure-find-word-to-retry*prefer-most-recent2
   (state <s> ^name comprehension
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name failure-find-word-to-retry
          ^not-merged-receiver.decision-count > <dc>
          ^not-merged-receiver.lt.alternative-structure <structure1>
          ^not-merged-assigner.lt.structure-type <structure1>)
   (<op2> ^name failure-find-word-to-retry
          ^not-merged-receiver.decision-count <dc>
          ^not-merged-receiver.lt.alternative-structure <structure2>
          ^not-merged-assigner.lt.structure-type <structure2>)
   -->
   (<s> ^operator <op1> > <op2>)
}

sp {comprehension*apply*failure-find-word-to-retry
   (state <s> ^operator <op>
              ^segment <seg>)
   (<op> ^name failure-find-word-to-retry
         ^not-merged-receiver <nmr>)
   (<nmr> ^lt.structure-type <structure-old>
          ^lt.alternative-structure <structure-new>)
   (<seg> ^parse-failed-receiver <nmr>)
-->
   (write (crlf) |Will change type of | <nmr> | from | <structure-old> | to | <structure-new>)
   (<seg> ^retry <nmr>)}

