###############################################################################
#
# save-snapshot-command
# (<s> ^operator <o>)
# (<o> ^name save-snapshot-command
#      ^object-id <id>)
# 
# The id's given should be svs id's
#
# Returns ^save-snapshot-command.result << success failure >>
# (Failure case should only be if there was an unexpected error)
###############################################################################

sp {save-snapshot-command*elaborate*object-id
   (state <s> ^name save-snapshot-command
              ^superstate.operator.object-id <id>)
-->
   (<s> ^object-id <id>)
}

# Operator: perform-command
#   Creates the extract command and copies the result to the superstate
sp {save-snapshot-command*propose*perform-command
   (state <s> ^name save-snapshot-command
              ^object-id <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name perform-command
        ^category perception
        ^object-id <id>)
}

# Create the extract command
sp {save-snapshot-command*apply*perform-command
   (state <s> ^name save-snapshot-command
              ^operator <o>
              ^topstate.svs.command <cmd>)
   (<o> ^name perform-command
        ^object-id <id>)
-->
   (<cmd> ^save-snapshot <save>)
   (<save> ^id <id>)
   (<s> ^save-snapshot-command <save>)
}

# Remove the command from the svs.command link
sp {save-snapshot-command*apply*perform-command*cleanup
   (state <s> ^name save-snapshot-command
              ^operator.name perform-command
              ^save-snapshot-command <save>
              ^topstate.svs.command <cmd>)
   (<cmd> ^save-snapshot <save>)
   (<save> ^status <status>)
-->
   (<cmd> ^save-snapshot <save> -)
}

# Copy result of the successful command to the superstate
sp {save-snapshot-command*apply*perform-command*success
   (state <s> ^name save-snapshot-command
              ^operator.name perform-command
              ^save-snapshot-command.status success
              ^superstate <ss>)
-->
   (<ss> ^save-snapshot-command.result success)
}

# There was no valid result given, return none
sp {save-snapshot-command*apply*perform-command*failure
   (state <s> ^name save-snapshot-command
              ^operator.name perform-command
              ^save-snapshot-command.status <> success
              ^superstate <ss>)
-->
   (<ss> ^save-snapshot-command.result failure)
}
