sp {attend-to-new-object*propose*find-stale-candidates
   (state <s> ^name attend-to-new-object
              ^svs-object.id <id>
              -^find-stale-candidates.result)
-->
   (<s> ^operator <o> +)
   (<o> ^name find-stale-candidates
        ^category perception
        ^object-id <id>)
}

sp {find-stale-candidates*propose*create-intersect-filter
   (state <s> ^name find-stale-candidates
              ^superstate.operator.object-id <id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-intersect-filter
        ^category perception
        ^object-id <id>)
}

sp {find-stale-candidates*apply*create-intersect-filter
   (state <s> ^name find-stale-candidates
              ^operator <o>
              ^topstate.svs.command <cmd>)
   (<o> ^name create-intersect-filter
        ^object-id <id>)
-->
   (<cmd> ^extract_once <filter>)
   (<filter> ^type intersect
             ^a <obj>
             ^b <stale-objects>)
   (<obj> ^type node
          ^id <id>)
   (<stale-objects> ^type has-property
                    ^a.type all_nodes
                    ^property-name stale
                    ^property-value true)
   (<s> ^intersect-filter <filter>
        ^find-stale-candidates <f>)
}

sp {find-stale-candidates*apply*create-intersect-filter*cleanup
   (state <s> ^name find-stale-candidates
              ^operator.name create-intersect-filter
              ^find-stale-candidates.result <res>
              ^intersect-filter <filter>
              ^topstate.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
-->
   (<cmd> ^extract_once <filter> -)
}

sp {find-stale-candidates*apply*create-intersect-filter*copy*result
   (state <s> ^name find-stale-candidates
              ^operator.name create-intersect-filter
              ^find-stale-candidates <f>
              ^superstate <ss>)
   (<f> ^result <res>)
-->
   (<ss> ^find-stale-candidates <f>)
}

sp {find-stale-candidates*apply*create-intersect-filter*failure
   (state <s> ^name find-stale-candidates
              ^operator.name create-intersect-filter
              ^intersect-filter.result <res>
              ^find-stale-candidates <f>
              ^superstate <ss>)
   (<res> -^record.value t)
-->
   (<f> ^result none)
}

sp {find-stale-candidates*apply*create-intersect-filter*success
   (state <s> ^name find-stale-candidates
              ^operator.name create-intersect-filter
              ^intersect-filter.result.record <rec>
              ^find-stale-candidates <f>
              ^superstate <ss>)
   (<rec> ^value t
          ^params.b <id>)
-->
   (<f> ^result <id>)
}
   