sp {attend-to-new-object*propose*create-object
   (state <s> ^name attend-to-new-object
              ^svs-object <svs-obj>
              ^snapshot-saved true)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-object
        ^category perception
        ^svs-object <svs-obj>)
}

sp {attend-to-new-object*apply*create-object
   (state <s> ^name attend-to-new-object
              ^operator <o>
              ^io.input-link.time.seconds <t>
              ^topstate <top>)
   (<o> ^name create-object
        ^svs-object.id-int <id>)
   (<top> ^agent-params.stability-check-duration <dur>)
-->
   (<top> ^object <obj>)
   (<obj> ^id <id>
          ^status uncertain
          ^number-checks 0
          ^stability-check-time (+ <t> <dur>))
}
