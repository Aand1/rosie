sp {match-stale-object*propose*create-distance-filter
   (state <s> ^name match-stale-object
              ^svs-object <obja>
              ^closest-object.id <id>
              ^topstate.svs.spatial-scene.child <objb>
             -^centroid-distance)
   (<objb> ^id-int <id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-distance-filter
        ^category perception
        ^object-a <obja>
        ^object-b <objb>)
}

sp {match-stale-object*apply*create-distance-filter
   (state <s> ^name match-stale-object
              ^operator <o>
              ^topstate.svs.command <cmd>)
   (<o> ^name create-distance-filter
        ^object-a.id <obja>
        ^object-b.id <objb>)
-->
   (<cmd> ^extract_once <distance>)
   (<distance> ^type centroid_distance
              ^a <node-a>
              ^b <node-b>)
   (<node-a> ^type node
             ^id <obja>)
   (<node-b> ^type node
             ^id <objb>)
   (<s> ^distance-filter <distance>)
}

sp {match-stale-object*apply*create-distance-filter*success
   (state <s> ^name match-stale-object
              ^operator.name create-distance-filter
              ^distance-filter <filter>)
   (<filter> ^result.record.value <dist>)
-->
   (<s> ^centroid-distance <dist>)
   (write (crlf) |Closest Distance is: | <dist> )
}

sp {match-stale-object*apply*create-distance-filter*remove
   (state <s> ^name match-stale-object
              ^operator.name create-distance-filter
              ^distance-filter <filter>
              ^topstate.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
   (<filter> ^result)
-->
   (<cmd> ^extract_once <filter> -)
}