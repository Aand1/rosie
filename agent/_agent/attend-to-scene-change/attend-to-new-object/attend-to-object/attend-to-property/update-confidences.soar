sp {attend-to-object*propose*update-confidences
   (state <s> ^name attend-to-object
             -^updated-confidences)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-confidences
        ^category perception
        ^level confidence)
}

sp {attend-to-object*apply*update-confidences*change-name
	(state <s> ^name attend-to-object
		^operator.name update-confidences
		^object-pair <op>)
	(<op> ^svs.name <name>
		^ts <ts-obj>)
	(<ts-obj> ^category location
		^property <prop>)
	(<prop> ^name name
		^value <val>)
	(<val> ^word {<word> <> <name>})
-->
	(<val> ^word <word> -
        ^label <word> -
		^word <name>
  		^label <name>)
}

sp {attend-to-object*apply*update-confidences*add
   (state <s> ^name attend-to-object
              ^operator.name update-confidences
              ^object-pair <op>
              ^property-info.property <prop>)
   (<prop> ^name <prop-name>
           ^confidence <prop-conf>)
   (<op> ^svs <svs-obj>
         ^ts <ts-obj>)
   (<svs-obj> ^<prop-name> <label>
              ^<prop-conf> <conf>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
              ^value <val>)
   (<val> ^label <label>)
-->
   (<val> ^confidence-value <conf>)
}

sp {attend-to-object*apply*update-confidences*remove
   (state <s> ^name attend-to-object
              ^operator.name update-confidences
              ^object-pair <op>
              ^property-info.property <prop>)
   (<prop> ^name <prop-name>
           ^confidence <prop-conf>)
   (<op> ^svs <svs-obj>
         ^ts <ts-obj>)
   (<svs-obj> ^<prop-name> <label>
              ^<prop-conf> <conf>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
              ^value <val>)
   (<val> ^label <label>
          ^confidence-value <cv>)
-->
   (<val> ^confidence-value <cv> -)
}

sp {attend-to-object*apply*update-confidences*mark*updated
   (state <s> ^name attend-to-object
              ^operator.name update-confidences)
-->
   (<s> ^updated-confidences true)
}
