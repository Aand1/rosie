sp {attend-to-object*propose*delete-property
   (state <s> ^name attend-to-object
              ^object-pair <op>)
   (<op> ^svs <svs-obj>
         ^ts <ts-obj>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^type << visual state >> 
              ^name <prop-name>)
   (<svs-obj> -^<prop-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-property
        ^category perception
        ^level property
        ^property <ts-prop>
        ^object <ts-obj>)
}

sp {attend-to-object*reject*delete-property*not-specified
   (state <s> ^name attend-to-object
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name delete-property
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-object*apply*delete-property
   (state <s> ^name attend-to-object
              ^operator <o>)
   (<o> ^name delete-property
        ^property <prop>
        ^object <obj>)
-->
   (<obj> ^property <prop> -)
}