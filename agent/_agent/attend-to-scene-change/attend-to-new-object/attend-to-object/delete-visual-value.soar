sp {attend-to-object*propose*delete-visual-value
   (state <s> ^name attend-to-object
              ^object-pair <op>)
   (<op> ^svs <svs-obj>
         ^ts <ts-obj>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
              ^type visual
              ^value <val>)
   (<val> ^label <label>)
   (<svs-obj> -^<prop-name> <label>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-visual-value
        ^category perception
        ^level value
        ^property <ts-prop>
        ^value <val>)
}

sp {attend-to-object*reject*delete-visual-value*not-specified
   (state <s> ^name attend-to-object
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name delete-visual-value
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-object*apply*delete-visual-value
   (state <s> ^name attend-to-object
              ^operator <o>)
   (<o> ^name delete-visual-value
        ^property <ts-prop>
        ^value <val>)
-->
   (<ts-prop> ^value <val> -)
}