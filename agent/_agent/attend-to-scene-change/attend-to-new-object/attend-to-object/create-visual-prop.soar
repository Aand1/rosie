sp {attend-to-object*propose*create-visual-prop
   (state <s> ^name attend-to-object
              ^object-pair <op>
              ^property-info.property.name <prop-name>)
   (<op> ^svs.<prop-name> <label>
         ^ts <ts-obj>)
   (<ts-obj> -^property.name <prop-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-visual-prop
        ^category perception
        ^level property
        ^prop-name <prop-name>
        ^object <ts-obj>)
}

sp {attend-to-object*reject*create-visual-prop*not-specified
   (state <s> ^name attend-to-object
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name create-visual-prop
        ^prop-name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-object*apply*create-visual-prop
   (state <s> ^name attend-to-object
              ^operator <o>)
   (<o> ^name create-visual-prop
        ^prop-name <prop-name>
        ^object <obj>)
-->
   (<obj> ^property <prop-copy>)
   (<prop-copy> ^name <prop-name>
                ^type visual)
}