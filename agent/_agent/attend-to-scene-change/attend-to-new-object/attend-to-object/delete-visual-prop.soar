sp {attend-to-object*propose*delete-visual-prop
   (state <s> ^name attend-to-object
              ^object-pair <op>)
   (<op> ^svs <svs-obj>
         ^ts <ts-obj>)
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^type visual
              ^name <prop-name>)
   (<svs-obj> -^<prop-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-visual-prop
        ^category perception
        ^level property
        ^property <ts-prop>
        ^object <ts-obj>)
}

sp {attend-to-object*reject*delete-visual-prop*not-specified
   (state <s> ^name attend-to-object
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name delete-visual-prop
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-object*apply*delete-visual-prop
   (state <s> ^name attend-to-object
              ^operator <o>)
   (<o> ^name delete-visual-prop
        ^property <prop>
        ^object <obj>)
-->
   (<obj> ^property <prop> -)
}