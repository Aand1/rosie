sp {attend-to-object*propose*create-property
   (state <s> ^name attend-to-object
              ^object-pair <op>)
   (<op> ^svs.<prop-name> <prop>
         ^ts <ts-obj>)
   (<ts-obj> -^property.name <prop-name>)
   (<prop> ^type << visual state >>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-property
        ^level property
        ^property <prop>
        ^prop-name <prop-name>
        ^object <ts-obj>)
}

sp {attend-to-object*reject*create-property*not-specified
   (state <s> ^name attend-to-object
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name create-property
        ^prop-name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-object*apply*create-property
   (state <s> ^name attend-to-object
              ^operator <o>)
   (<o> ^name create-property
        ^property.type <prop-type>
        ^prop-name <prop-name>
        ^object <obj>)
-->
   (<obj> ^property <prop-copy>)
   (<prop-copy> ^name <prop-name>
                ^type <prop-type>)
}