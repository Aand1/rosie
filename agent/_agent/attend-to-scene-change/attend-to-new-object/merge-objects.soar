sp {attend-to-new-object*propose*setup-merge
   (state <s> ^name attend-to-new-object
              ^matches-known-object <> none
             -^merge-set-up true)
-->
   (<s> ^operator <o> +)
   (<o> ^name setup-merge)
}

sp {attend-to-new-object*apply*setup-merge
   (state <s> ^name attend-to-new-object
              ^operator.name setup-merge
              ^topstate <top>)
-->
   (<s> ^merge-set-up true)
   (<top> ^deleting-object true)
}


sp {attend-to-new-object*propose*merge-objects
   (state <s> ^name attend-to-new-object
              ^merge-set-up
              ^matches-known-object {<original> <> none}
              ^svs-object <copy>)
-->
   (<s> ^operator <o> +)
   (<o> ^name merge-objects
        ^category perception
        ^original <original>
        ^copy <copy>)
}

sp {attend-to-new-object*apply*merge-objects
   (state <s> ^name attend-to-new-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name merge-objects
        ^original.id <original-id>
        ^copy.id-int <copy-id>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type merge
         ^original-id <original-id>
         ^copy-id <copy-id>)
}

sp {attend-to-new-object*apply*merge-objects*monitor*perception
   (state <s> ^name attend-to-new-object
              ^operator <o>
              ^topstate.debug-perception true)
   (<o> ^name merge-objects
        ^original.id <orig>
        ^copy.id-int <copy>)
-->
   (write |Merging object | <copy> | into | <orig>)
}

sp {attend-to-new-object*apply*merge-objects*remove*deleting-flag
   (state <s> ^name attend-to-new-object
              ^operator.name merge-objects
              ^topstate <top>)
   (<top> ^deleting-object true)
-->
   (<top> ^deleting-object true -)
}

