sp {attend-to-new-object*propose*complete-attend-to-new-object
   (state <s> ^name attend-to-new-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-attend-to-new-object
        ^category perception)
}

sp {evaluate-stability*apply*mark-stable*mark*unidentified
   (state <s> ^name evaluate-stability
              ^operator <o>)
   (<o> ^name mark-stable
        ^monitor-obj <mon-obj>)
   (<mon-obj> ^status uncertain)
-->
   (<mon-obj> ^status uncertain -
              ^status unidentified)
}

sp {attend-to-new-object*apply*complete-attend-to-new-object*no-permanence*mark*unidentified
   (state <s> ^name attend-to-new-object
              ^operator.name complete-attend-to-new-object
              ^topstate.agent-params.eval-params.object-permanence false
              ^monitor-obj <obj>)
-->
   (<obj> ^status unidentified)
}

sp {attend-to-new-object*apply*complete-attend-to-new-object*add*to*object
   (state <s> ^name attend-to-new-object
              ^operator.name complete-attend-to-new-object
              ^topstate.agent-params.eval-params.object-permanence true
              ^io.input-link.time.seconds <t>
              ^monitor-obj <obj>
              ^topstate.agent-params.stability-check-duration <dur>)
-->
   (<obj> ^status uncertain
          ^stability-check-time (+ <t> <dur>)
          ^number-checks 0)
}

sp {attend-to-new-object*apply*complete-attend-to-new-object*create*filters
   (state <s> ^name attend-to-new-object
              ^operator.name complete-attend-to-new-object
              ^topstate.svs.command <cmd>
              ^monitor-obj <obj>)
   (<obj> ^perception-obj.id <id>)
-->
   (<cmd> ^extract <pos>)
   (<pos> ^type monitor_position
          ^a <pa>)
   (<pa> ^type node
         ^id <id>)
   (<obj> ^position-monitor <pos>)
   
   (<cmd> ^extract <vol>)
   (<vol> ^type monitor_volume
          ^a <va>)
   (<va> ^type node
         ^id <id>)
   (<obj> ^volume-monitor <vol>)
}
