sp {evaluate-grown-object*propose*internally-link-object
   (state <s> ^name evaluate-grown-object
              ^found-contained-object true
              ^monitor-obj.monitor-id <id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name internally-link-objects
        ^source-id <id>)
}

sp {evaluate-grown-object*elaborate*internally-link-objects*dest-id
   (state <s> ^name evaluate-grown-object
              ^operator <o> +
              ^contained-object <id>)
   (<o> ^name internally-link-objects)
-->
   (<o> ^dest-id <id>)
}

sp {evaluate-grown-object*apply*internally-link-objects*link
   (state <s> ^name evaluate-grown-object
              ^operator <o>
              ^topstate.scene-monitor.object <obj>)
   (<o> ^name internally-link-objects
        ^source-id <src-id>
        ^dest-id <dest-id>)
   (<obj> ^monitor-id <dest-id>)
-->
   (<obj> ^internal-link <src-id>)
}

sp {evaluate-grown-object*apply*internally-link-objects*monitor*perception
   (state <s> ^name evaluate-grown-object
              ^operator <o>
              ^topstate.agent-params.debug-mode on
              ^topstate.agent-params.debug-perception true)
   (<o> ^name internally-link-objects
        ^dest-obj.belief-obj.id <dest>
        ^source-id <source>)
-->
   (write |Internally linking object | <dest> | to | <source> (crlf))
}
        