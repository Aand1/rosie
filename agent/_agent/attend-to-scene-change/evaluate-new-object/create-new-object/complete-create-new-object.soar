sp {create-new-object*propose*complete-create-new-object
   (state <s> ^name create-new-object
              ^new-object <new-obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-create-new-object
        ^category perception
        ^object <new-obj>)
}

sp {create-new-object*apply*complete-create-new-object
   (state <s> ^name create-new-object
              ^operator <o>
              ^topstate <top>)
   (<o> ^name complete-create-new-object
        ^object <new-obj>)
-->
   (<top> ^object <new-obj>)
}

sp {create-new-object*apply*complete-create-new-object*create*belief*object
   (state <s> ^name create-new-object
              ^new-object.belief-id <bel-id>
              ^il-obj <il-obj>
              ^perception-obj.id <perc-id>
              ^topstate.svs.command <cmd>)
   (<il-obj> -^name << blocker-1 blocker-2 blocker-3 >>)
-->
   (<cmd> ^copy_node <copy>)
   (<copy> ^id <bel-id>
           ^source <perc-id>
           ^adjust true)
}
