sp {save-snapshot*propose*create-closest-filter
   (state <s> ^name save-snapshot
              ^svs-object.id <id>
             -^closest-object)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-closest-filter
        ^category perception
        ^object-id <id>)
}

sp {save-snapshot*apply*create-closest-filter
   (state <s> ^name save-snapshot
              ^operator <o>
              ^svs.command <cmd>)
   (<o> ^name create-closest-filter
        ^object-id <id>)
-->
   (<cmd> ^extract_once <closest>)
   (<closest> ^type closest
              ^a <new-node>
              ^b <all-nodes>)
   (<new-node> ^type node
               ^id <id>)
   (<all-nodes> ^type all_nodes)
   (<s> ^closest-filter <closest>)
}

sp {save-snapshot*apply*create-closest-filter*success
   (state <s> ^name save-snapshot
              ^operator.name create-closest-filter
              ^closest-filter.result.record.params.b <closest-id>)
-->
   (<s> ^closest-object <closest-id>)
}

sp {save-snapshot*apply*create-closest-filter*failure
   (state <s> ^name save-snapshot
              ^operator.name create-closest-filter
              ^closest-filter.result <res>)
   (<res> -^record)
-->
   (<s> ^closest-object none)
}

sp {save-snapshot*apply*create-closest-filter*clear*command
   (state <s> ^name save-snapshot
              ^operator.name create-occlusion-filter
              ^closest-filter <closest>
              ^svs.command <cmd>)
   (<closest> ^result <res>)
   (<cmd> ^extract_once <closest>)
-->
   (<cmd> ^extract_once <closest> -)
}
