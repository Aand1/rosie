sp {save-snapshot*propose*create-distance-filter
   (state <s> ^name save-snapshot
              ^svs-object.id <id>
              ^closest-object <closest-id>
             -^closest-object-distance)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-distance-filter
        ^category perception
        ^object-id <id>
        ^closest-id <closest-id>)
}

sp {save-snapshot*apply*create-distance-filter
   (state <s> ^name save-snapshot
              ^operator <o>
              ^svs.command <cmd>)
   (<o> ^name create-distance-filter
        ^object-id <id>
        ^closest-id <closest-id>)
-->
   (<cmd> ^extract_once <distance>)
   (<distance> ^type distance
               ^a <new-obj>
               ^b <closest-obj>)
   (<new-obj> ^type node
              ^id <id>)
   (<closest-obj> ^type node
                  ^id <closest-id>)
   (<s> ^distance-filter <distance>)
}

sp {save-snapshot*apply*create-distance-filter*copy*result
   (state <s> ^name save-snapshot
              ^operator.name create-distance-filter
              ^distance-filter <filter>)
   (<filter> ^result.record.value <dist>)
-->
   (<s> ^closest-object-distance <dist>)
}

sp {save-snapshot*apply*create-distance-filter*clear*command
   (state <s> ^name save-snapshot
              ^operator.name create-distance-filter
              ^distance-filter <distance>
              ^svs.command <cmd>)
   (<distance> ^result <res>)
   (<cmd> ^extract_once <distance>)
-->
   (<cmd> ^extract_once <distance> -)
}