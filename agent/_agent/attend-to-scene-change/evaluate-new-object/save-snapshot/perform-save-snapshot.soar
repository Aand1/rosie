sp {save-snapshot*propose*perform-save-snapshot
   (state <s> ^name save-snapshot
              ^svs-object.id <id>
              ^good-object-view true)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-save-snapshot
        ^category perception
        ^object-id <id>)
}

sp {save-snapshot*apply*perform-save-snapshot
   (state <s> ^name save-snapshot
              ^operator <o>
              ^svs.command <cmd>)
   (<o> ^name perform-save-snapshot
        ^object-id <id>)
-->
   (<cmd> ^save-snapshot <ss>)
   (<ss> ^id <id>)
   (<s> ^save-snapshot-command <ss>)
}

sp {save-snapshot*apply*perform-save-snapshot*mark*saved
   (state <s> ^name save-snapshot
              ^operator.name perform-save-snapshot
              ^save-snapshot-command.status success)
-->
   (<s> ^snapshot-saved true)
}

sp {save-snapshot*apply*perform-save-snapshot*remove*command
   (state <s> ^name save-snapshot
              ^operator.name perform-save-snapshot
              ^save-snapshot-command <save>
              ^svs.command <cmd>)
   (<cmd> ^save-snapshot <save>)
   (<save> ^status success)
-->
   (<cmd> ^save-snapshot <save> -)
}