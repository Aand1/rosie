sp {match-missing-object*propose*compare-volume
   (state <s> ^name match-missing-object
             -^volume-diff
              ^missing-object.belief-obj.id <bel-id>
              ^candidate.perception-obj.id <cand-id>)
-->
   (<s> ^operator <op> +)
   (<op> ^name compare-volume
         ^category perception
         ^missing-id <bel-id>
         ^candidate-id <cand-id>)
}

sp {match-missing-object*apply*compare-volume*create*filter
   (state <s> ^name match-missing-object
              ^operator <op>
              ^topstate.svs.command <cmd>)
   (<op> ^name compare-volume
         ^missing-id <bel-id>
         ^candidate-id <cand-id>)
-->
   (<cmd> ^extract_once <bel-vol> <cand-vol>)
   (<bel-vol> ^type volume
              ^a <ba>
              ^volume_type scale)
   (<ba> ^type node
         ^id <bel-id>)
   (<cand-vol> ^type volume
               ^a <ca>
               ^volume_type scale)
   (<ca> ^type node
         ^id <cand-id>)
   (<s> ^candidate-volume-filter <cand-vol>
        ^belief-volume-filter <bel-vol>)
}

sp {match-missing-object*apply*compare-volume*copy*result
   (state <s> ^name match-missing-object
              ^operator.name compare-volume
              ^candidate-volume-filter.result.record.value <cand-vol>
              ^belief-volume-filter.result.record.value <bel-vol>)
-->
   (<s> ^volume-diff (/ (+ <cand-vol> 0.000000001) (+ <bel-vol> 0.000000001)))
}

sp {match-missing-object*apply*compare-volume*no*result*belief
   (state <s> ^name match-missing-object
              ^operator.name compare-volume
              ^belief-volume-filter.result <res>)
   (<res> -^record.value <val>)
-->
   (<s> ^volume-diff 0)
}

sp {match-missing-object*apply*compare-volume*no*result*candidate
   (state <s> ^name match-missing-object
              ^operator.name compare-volume
              ^candidate-volume-filter.result <res>)
   (<res> -^record.value <val>)
-->
   (<s> ^volume-diff 0)
}

sp {match-missing-object*apply*compare-volume*cleanup
   (state <s> ^name match-missing-object
              ^operator.name compare-volume
              ^candidate-volume-filter <cand-vol>
              ^belief-volume-filter <bel-vol>
              ^topstate.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
   (<filter> ^result <res>)
-->
   (<cmd> ^extract_once <cand-vol> -
          ^extract_once <bel-vol> -)
   (<s> ^candidate-volume-filter <cand-vol> -
        ^belief-volume-filter <bel-vol> -)
}
