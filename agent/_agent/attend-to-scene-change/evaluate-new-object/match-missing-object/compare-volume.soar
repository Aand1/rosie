sp {match-missing-object*propose*compare-volume
   (state <s> ^name match-missing-object
             -^volume-diff
              ^missing-object.belief-obj.id <bel-id>
              ^candidate.perception-obj.id <cand-id>)
-->
   (<s> ^operator <op> +)
   (<op> ^name compare-volume
         ^category perception
         ^missing-id <bel-id>
         ^candidate-id <cand-id>)
}

sp {match-missing-object*apply*compare-volume*create*filter
   (state <s> ^name match-missing-object
              ^operator <op>
              ^topstate.svs.command <cmd>)
   (<op> ^name compare-volume
         ^missing-id <bel-id>
         ^candidate-id <cand-id>)
-->
   (<cmd> ^extract_once <compare-vol>)
   (<compare-vol> ^type compare-nodes
                  ^a <a>
                  ^b <b>
                  ^compare volume)
   (<a> ^type node ^id <bel-id>)
   (<b> ^type node ^id <cand-id>)
   (<s> ^compare-volume-filter <compare-vol>)
}

sp {match-missing-object*apply*compare-volume*copy*result
   (state <s> ^name match-missing-object
              ^operator.name compare-volume
              ^compare-volume-filter.result.record.value <val>)
-->
   (<s> ^volume-diff <val>)
}

sp {match-missing-object*apply*compare-volume*no*result
   (state <s> ^name match-missing-object
              ^operator.name compare-volume
              ^compare-volume-filter.result <res>)
   (<res> -^record.value <val>)
-->
   (<s> ^volume-diff 0)
}

sp {match-missing-object*apply*compare-volume*cleanup
   (state <s> ^name match-missing-object
              ^operator.name compare-volume
              ^compare-volume-filter <filter>
              ^topstate.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
   (<filter> ^result <res>)
-->
   (<cmd> ^extract_once <filter> -)
   (<s> ^compare-volume-filter <filter> -)
}
