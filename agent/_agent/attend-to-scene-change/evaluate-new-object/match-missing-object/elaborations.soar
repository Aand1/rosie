sp {match-missing-object*elaborate*missing-object
   (state <s> ^name match-missing-object
              ^superstate.operator.missing-object <obj>)
-->
   (<s> ^missing-object <obj>)
}

sp {match-missing-object*elaborate*candidate
   (state <s> ^name match-missing-object
              ^superstate.operator.monitor-obj <obj>)
-->
   (<s> ^candidate <obj>)
}

#sp {match-missing-object*interrupt*enter-substate
#   (state <s> ^name match-missing-object
#              ^topstate.agent-params.enable-interrupts true)
#-->
#   (interrupt)
#}

# Volume match, object must be close in volume to be a match
sp {match-missing-object*elaborate*volume-match*true
   (state <s> ^name match-missing-object
              ^topstate.agent-params.vol-low-diff-threshold <low>
              ^topstate.agent-params.vol-high-diff-threshold <high>
              ^volume-diff >= <low> <= <high>)
-->
   (<s> ^volume-match true)
}
   
sp {match-missing-object*elaborate*volume-match*false*too-small
   (state <s> ^name match-missing-object
              ^topstate.agent-params.vol-low-diff-threshold <low>
              ^volume-diff < <low>)
-->
   (<s> ^volume-match false)
}

sp {match-missing-object*elaborate*volume-match*false*too-large
   (state <s> ^name match-missing-object
              ^topstate.agent-params.vol-high-diff-threshold <high>
              ^volume-diff < <high>)
-->
   (<s> ^volume-match false)
}

# Properties match, object must not have any conflicting visual properties
sp {match-missing-object*elaborate*property-conflict*true*no*property
   (state <s> ^name match-missing-object
              ^missing-object.wm-obj.property <wm-prop>
              ^candidate <cand>)
   (<wm-prop> ^name <prop-name>
              ^type visual
              ^prev-case certain)
   (<cand> -^property.name <prop-name>)
-->
   (<s> ^property-conflict true)
}

sp {match-missing-object*elaborate*property-conflict*true*diff*value
   (state <s> ^name match-missing-object
              ^missing-object.wm-obj.property <wm-prop>
              ^candidate.property <cand-prop>)
   (<wm-prop> ^name <prop-name>
              ^type visual
              ^prev-case certain
              ^label <label>)
   (<cand-prop> ^name <prop-name>)
   -{(<cand-prop> ^case certain
                  ^hypothesis <best-hyp>)
     (<best-hyp> ^best true
                 ^label <label>)}
-->
   (<s> ^property-conflict true)
}

sp {match-missing-object*elaborate*property-conflict*false
   (state <s> ^name match-missing-object
             -^property-conflict true)
-->
   (<s> ^property-conflict false)
}