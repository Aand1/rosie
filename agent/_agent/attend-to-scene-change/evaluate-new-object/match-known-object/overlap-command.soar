sp {match-known-object*propose*overlap-command
   (state <s> ^name match-known-object
              ^monitor-obj.perception-obj.id <new-id>
              ^intersect-command.result {<bel-id> <> none}
             -^overlap-results.result.object-b <bel-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name overlap-command
        ^category perception
        ^object-a <new-id>
        ^object-b <bel-id>)
}

# EVAL_RULE
# If not doing segmentation reasoning, don't do overlap tests for existing objects
# (Only link to stale objects)
sp {match-known-object*reject*overlap-command*existing-object
   (state <s> ^name match-known-object
              ^topstate.agent-params.eval-params.segmentation-reasoning false
              ^operator <o> +
              ^topstate.object <obj>)
   (<o> ^name overlap-command
        ^object-b <bel-id>)
   (<obj> ^belief-obj.id <bel-id>
          ^perception-obj)
-->
   (<s> ^operator <o> -)
}

sp {match-known-object*apply*overlap-command*copy-result
   (state <s> ^name match-known-object
              ^operator.name overlap-command
              ^overlap-command <cmd>
              ^overlap-results <results>)
   (<cmd> ^result <res>)
   (<res> ^overlap <val>)
-->
   (<s> ^overlap-command <cmd> -)
   (<results> ^result <res>)
}

sp {match-known-object*apply*overlap-command*debug-perception*report*overlap
   (state <s> ^name match-known-object
              ^operator.name overlap-command
              ^overlap-command.result <res>
              ^topstate.agent-params.debug-mode on
              ^topstate.agent-params.debug-perception true)
   (<res> ^object-a <a> ^object-b <b> ^overlap <val>)
-->
   (write |Overlap: | <val> | of | <a> | is contained by | <b> (crlf))
} 