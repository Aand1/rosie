sp {match-stale-object*propose*merge-objects
   (state <s> ^name match-stale-object
              ^matched-stale-object <original>
              ^wm-object <copy>)
-->
   (<s> ^operator <o> +)
   (<o> ^name merge-objects
        ^category perception
        ^original <original>
        ^copy <copy>)
}

sp {match-stale-object*apply*merge-objects
   (state <s> ^name match-stale-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name merge-objects
        ^original.id <original-id>
        ^copy.id <copy-id>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type merge
         ^original-id <original-id>
         ^copy-id <copy-id>)
}


sp {match-stale-object*apply*merge-objects*delete
   (state <s> ^name match-stale-object
              ^operator.name merge-objects
              ^wm-object <obj>
              ^topstate <top>)
   (<top> ^object <obj>)
-->
   (<top> ^object <obj> -)
}

sp {match-stale-object*apply*merge-objects*monitor*perception
   (state <s> ^name match-stale-object
              ^operator <o>
              ^topstate.agent-params.debug-perception true)
   (<o> ^name merge-objects
        ^original.id <orig>
        ^copy.id-int <copy>)
-->
   (write |Merging object | <copy> | into | <orig>)
}

sp {match-stale-object*apply*merge-objects*remove*deleting-flag
   (state <s> ^name match-stale-object
              ^operator.name merge-objects
              ^topstate <top>)
   (<top> ^deleting-object true)
-->
   (<top> ^deleting-object true -)
}
