sp {evaluate-new-object*propose*match-known-object
   (state <s> ^name evaluate-new-object
              ^svs-object <svs-obj>
             -^matches-known-object )
-->
   (<s> ^operator <o> +)
   (<o> ^name match-known-object
        ^category perception
        ^svs-object <svs-obj>)
}

sp {evaluate-new-object*propose*create-new-object
   (state <s> ^name evaluate-new-object
              ^matches-known-object none
             -^created-new-object
              ^wm-object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-new-object
        ^category perception
        ^wm-object <obj>)
}

sp {evaluate-new-object*propose*check-clear-view
   (state <s> ^name evaluate-new-object
              ^created-new-object
              ^svs-object.id <id>
             -^clear-object-view)
-->
   (<s> ^operator <o> +)
   (<o> ^name check-clear-view
        ^category perception
        ^object-id <id>)
}