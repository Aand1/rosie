sp {attend-to-stale-object*propose*delete-object
   (state <s> ^name attend-to-stale-object
              ^stale-object <obj>)
   (<obj> ^marked-for-deletion true)
-->
   (<s> ^operator <o> +)
   (<o> ^name delete-object
        ^object <obj>)
}


sp {attend-to-stale-object*apply*delete-object*delete-from-svs
   (state <s> ^name attend-to-stale-object
              ^operator.name delete-object
              ^stale-object.id <id>
              ^io.output-link <ol>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type delete
         ^id <id>)
}

sp {attend-to-stale-object*apply*delete-object*delete-from-wm
   (state <s> ^name attend-to-stale-object
              ^operator.name delete-object
              ^stale-object <obj>
              ^topstate <top>)
   (<top> ^object <obj>)
-->
   (<top> ^object <obj> -)
}

sp {attend-to-stale-object*apply*delete-object*remove*deleting-object
   (state <s> ^name attend-to-stale-object
              ^operator.name delete-object
              ^topstate <top>)
   (<top> ^deleting-object true)
-->
   (<top> ^deleting-object true -)
}