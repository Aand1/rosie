# EVAL_RULES
# if we are not doing segmentation reasoning, only link 1 object
sp {attend-to-stale-object*propose*link-objects*one-pair
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.eval-params.segmentation-reasoning false
              ^monitor-obj.belief-obj.id <stale-id>
              ^contained-fragment <a>
              ^overlap-results.result <res>)
   (<res> ^object-a <a>
          ^object-b <stale-id>
          ^overlap <overlap>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name link-objects
        ^category perception
        ^dest-id <stale-id>
        ^source-id <a>
        ^overlap <overlap>)
}

sp {attend-to-stale-object*prefer*link-objects*higher*overlap
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.eval-params.segmentation-reasoning false
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name link-objects
         ^overlap <overlap>)
   (<o2> ^name link-objects
         ^overlap < <overlap>)
-->
   (<s> ^operator <o1> > <o2>)
}
              
# If we are doing segmentation reasoning, link multiple fragments
sp {attend-to-stale-object*propose*link-objects
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.eval-params.segmentation-reasoning true
              ^found-contained-fragment true
              ^monitor-obj.belief-obj.id <dest-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name link-objects
        ^category perception
        ^dest-id <dest-id>)
}

sp {attend-to-stale-object*elaborate*link-objects*source-id
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.eval-params.segmentation-reasoning true
              ^operator <o> +
              ^contained-fragment <src-id>)
   (<o> ^name link-objects)
-->
   (<o> ^source-id <src-id>)
}

# Need to do all at once, otherwise only 1 fragment will be linked
sp {attend-to-stale-object*apply*link-objects
   (state <s> ^name attend-to-stale-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type link
         ^source-id <source>
         ^dest-id <dest>)
}

sp {attend-to-stale-object*apply*link-objects*monitor*perception
   (state <s> ^name attend-to-stale-object
              ^operator <o>
              ^topstate.agent-params.debug-mode on
              ^topstate.agent-params.debug-perception true)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (write |Linking object | <source> | to | <dest> (crlf))
}
        