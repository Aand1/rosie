sp {attend-to-stale-object*propose*occlusion-command
   (state <s> ^name attend-to-stale-object
              ^stale-object <obj>
              ^svs-object.id <id>)
   (<obj> -^marked-for-deletion)
-->
   (<s> ^operator <o> +)
   (<o> ^name occlusion-command
        ^category perception
        ^object-id <id>)
}

sp {attend-to-stale-object*elaborate*object-occluded*true
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.occlusion-threshold <occlusion-threshold>
              ^occlusion-command.result >= <occlusion-threshold>)
-->
   (<s> ^object-occluded true)
}

sp {attend-to-stale-object*elaborate*object-occluded*false*not-occluded
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.occlusion-threshold <occlusion-threshold>
              ^occlusion-command.result < <occlusion-threshold>)
-->
   (<s> ^object-occluded false)
}

sp {attend-to-stale-object*elaborate*object-occluded*false*no-result
   (state <s> ^name attend-to-stale-object
              ^occlusion-command.result none)
-->
   (<s> ^object-occluded false)
}

sp {attend-to-stale-object*apply*occlusion-command*occluded
   (state <s> ^name attend-to-stale-object
              ^operator.name occlusion-command
              ^stale-object <wm-obj>
              ^object-occluded true
              ^topstate.agent-params.occlusion-check-period <check-period>
              ^io.input-link.time.seconds <cur-time>)
   (<wm-obj> ^stale-time <time>)
-->
   (<wm-obj> ^stale-time <time> -
             ^stale-time (+ <cur-time> <check-period>))
}

sp {attend-to-stale-object*apply*occlusion-command*not-occluded*mark-for-deletion
   (state <s> ^name attend-to-stale-object
              ^operator.name occlusion-command
              ^stale-object <obj>
              ^object-occluded false
              ^topstate <top>)
-->
   (<obj> ^marked-for-deletion true)
   (<top> ^deleting-object true)
}

sp {attend-to-stale-object*apply*occlusion-command*not-occluded*report
   (state <s> ^name attend-to-stale-object
              ^operator.name occlusion-command
              ^stale-object.id <id>
              ^object-occluded false
              ^topstate.agent-params.debug-perception true)
-->
   (write |Deleting Object | <id>)
}