sp {attend-to-scene-change*apply*attend-to-arm-grab*add*grabbed
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm-grab
              ^io.input-link.self.grabbed-object <id>
              ^topstate.object <obj>
              ^topstate.robot-state.arm <arm>)
   (<obj> ^id <id>)
-->
   (<arm> ^grabbed <obj>)
}

sp {attend-to-scene-change*apply*attend-to-arm-grab*move*belief*object
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm-grab
              ^io.input-link.self <self>
              ^topstate.object <obj>
              ^topstate.svs.command <cmd>)
   (<self> ^grabbed-object <id>
           ^pose <pose>)
   (<pose> ^x <x> ^y <y> ^z <z>)
   (<obj> ^id <id>
          ^belief-obj.id <bel-id>)
-->
   (<cmd> ^property <xp> <yp> <zp>)
   (<xp> ^id <bel-id>
         ^property px
         ^value <x>)
   (<yp> ^id <bel-id>
         ^property py
         ^value <y>)
   (<zp> ^id <bel-id>
         ^property pz
         ^value <z>)
}
