sp {attend-to-scene-change*apply*attend-to-arm*action*add*current
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm
              ^io.input-link.self.action <action>
              ^topstate.robot-state.arm <arm>)
-->
   (<arm> ^action <action>)
}

sp {attend-to-scene-change*apply*attend-to-arm*stopped*remove*moving-object*flag
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm
              ^io.input-link.self.action << wait failure >>
              ^topstate.object <obj>)
   (<obj> ^moving-object true)
-->
   (<obj> ^moving-object true -)
}

sp {attend-to-scene-change*apply*attend-to-arm*action*remove*old
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm
              ^io.input-link.self.action <action>
              ^topstate.robot-state.arm <arm>)
   (<arm> ^action {<old-action> <> <action>})
-->
   (<arm> ^action <old-action> -)
}
sp {attend-to-scene-change*apply*attend-to-arm*release*remove*grabbed
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm
              -^io.input-link.self.grabbed-object <id>
              ^topstate.robot-state.arm <arm>)
   (<arm> ^grabbed <obj>)
   (<obj> ^id <id>)
-->
   (<arm> ^grabbed <obj> -)
}

sp {attend-to-scene-change*apply*attend-to-arm*grab*add*grabbed
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm
              ^io.input-link.self.grabbed-object <id>
              ^topstate.object <obj>
              ^topstate.robot-state.arm <arm>)
   (<obj> ^id <id>)
-->
   (<arm> ^grabbed <obj>)
}

sp {attend-to-scene-change*apply*attend-to-arm*grab*move*belief*object
   (state <s> ^name attend-to-scene-change
              ^operator <o>
              ^io.input-link.self <self>
              ^topstate.robot-state.arm <arm>
              ^topstate.object <obj>
              ^topstate.svs.command <cmd>)
   (<self> ^grabbed-object <id>
           ^pose <pose>)
   (<pose> ^x <x> ^y <y> ^z <z>)
   (<obj> ^id <id>
          ^belief-obj.id <bel-id>)
   (<arm> -^grabbed <obj>)
-->
   (<cmd> ^property <xp> <yp> <zp>)
   (<xp> ^id <bel-id>
         ^property px
         ^value <x>)
   (<yp> ^id <bel-id>
         ^property py
         ^value <y>)
   (<zp> ^id <bel-id>
         ^property pz
         ^value <z>)
}
