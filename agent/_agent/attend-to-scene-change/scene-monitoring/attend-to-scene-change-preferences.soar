sp {anystate*scene-change*prefer*new*over*stale
   (state <s> ^operator <o1> +
               ^operator <o2> +)
   (<o1> ^name attend-to-scene-change
         ^change.type new-object)
   (<o2> ^name attend-to-scene-change
         ^change.type stale-object)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {anystate*scene-change*prefer*stale*over*recheck-occlusion
   (state <s> ^operator <o1> +
               ^operator <o2> +)
   (<o1> ^name attend-to-scene-change
         ^change <c1>)
   (<c1> ^type stale-object
         ^object <obj>)
   (<o2> ^name attend-to-scene-change
         ^change <c2>)
   (<c2> ^type recheck-occlusion
         ^object <obj>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {anystate*scene-change*reject*if*held
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.object.wm-obj.held true)
-->
   (<s> ^operator <o> -)
}

sp {anystate*scene-change*prefer*new-object*over*object-changes
   (state <s> ^operator <o1> +
               ^operator <o2> +)
   (<o1> ^name attend-to-scene-change
         ^change.type new-object)
   (<o2> ^name attend-to-scene-change
         ^change.type << grown-object shrunken-object moved-object >> )
-->
   (<s> ^operator <o1> > <o2>)
}
