##################################################################
#
# Monitoring change in volumes
#
##################################################################
sp {topstate*scene-monitor*elaborate*svs-command*compare-volume
   (state <s> ^topstate <s>
              ^scene-monitor.object <obj>
              ^svs.command <cmd>)
   (<obj> ^perception-obj.id <perc-id>
          ^belief-obj.id <bel-id>
          ^differences <diffs>)
-->
   (<cmd> ^extract <perc-vol> <bel-vol>)
   (<perc-vol> ^type volume
               ^a <pa>
               ^volume_type scale)
   (<pa> ^type node
         ^id <perc-id>)
   (<bel-vol> ^type volume
              ^a <ba>
              ^volume_type scale)
   (<ba> ^type node
         ^id <bel-id>)
   (<diffs> ^belief-vol <bel-vol>
            ^perception-vol <perc-vol>)
}

sp {topstate*scene-monitor*elaborate*vol-diff
   (state <s> ^topstate <s> 
              ^scene-monitor.object.differences <diffs>)
   (<diffs> ^belief-vol.result.record.value <bel-vol>
            ^perception-vol.result.record.value <perc-vol>)
-->
   # Add a cubic mm to each to avoid divide by 0
   (<diffs> ^vol-diff (/ (+ <perc-vol> 0.000000001) (+ <bel-vol> 0.000000001)))
}

sp {topstate*scene-monitor*elaborate*has-shrunk
   (state <s> ^topstate <s>
              ^scene-monitor.object.differences <diffs>
              ^agent-params.vol-low-diff-threshold <low>)
   (<diffs> ^vol-diff < <low>)
-->
   (<diffs> ^has-shrunk true)
}

sp {topstate*scene-monitor*elaborate*has-grown
   (state <s> ^topstate <s>
              ^scene-monitor.object.differences <diffs>
              ^agent-params.vol-high-diff-threshold <high>)
   (<diffs> ^vol-diff > <high>)
-->
   (<diffs> ^has-grown true)
}

sp {topstate*scene-monitor*elaborate*growth-wait*true
   (state <s> ^topstate <s>
              ^scene-monitor.object.differences <diffs>
              ^io.input-link.time.seconds <t>)
   (<diffs> ^has-grown true
            ^recheck-growth-at > <t>)
-->
   (<diffs> ^growth-wait true)
}
    
###########################################################################
#
# Change: shrunken-object
#   If an object's volume drastrically lowers, investigate
#
##########################################################################
sp {monitor-scene-changes*elaborate*scene-changes*shurnken-object
   (state <s> ^topstate <s>
              ^scene-monitor.scene-changes <sc>
              ^scene-monitor.object <obj>)
   (<obj> ^differences <diffs>)
   (<diffs> ^has-shrunk true
            ^is-occluded false)
-->
   (<sc> ^change <change>)
   (<change> ^type shrunken-object
             ^object <obj>)
}

###########################################################################
#
# Change: grown-object
#   If an object's volume drastrically increases, investigate
#
##########################################################################
sp {monitor-scene-changes*elaborate*scene-changes*grown-object
   (state <s> ^topstate <s>
              ^io.input-link.self.action wait
              ^scene-monitor.scene-changes <sc>
              ^scene-monitor.object <obj>)
   (<obj> ^differences <diffs>)
   (<diffs> ^has-grown true
           -^growth-wait true)
-->
   (<sc> ^change <change>)
   (<change> ^type grown-object
             ^object <obj>)
}

sp {topstate*elaborate*scene-changes*change-info*volume-changes
   (state <s> ^topstate <s>
              ^scene-monitor.scene-changes.change-info <info>)
-->
   (<info> ^change <grown-obj> <shrunk-obj>)
   (<shrunk-obj> ^name shrunken-object
                    ^op-name evaluate-shrunken-object)
   (<grown-obj> ^name grown-object
                   ^op-name evaluate-grown-object)
}
