#stupid pair.c make more general paremeters
sp {extract-set-set*elaborate*pairs
   (state <s> ^name extract-set-set
              ^superstate.operator <o>
              ^linkstate.prepositions.prep <p>)
   (<p> ^name <name>)
   (<o> ^prep <name>
        ^a <r>
        ^negative false
        ^b <r2>)
   (<r> ^result.<set>.<pair2> <pa> ^parameter.result.link.link.<set>.<pair2>.<elem> holder)
   (<pa> ^<elem> <e>)
  -{(<e> ^object.id <ida>)
   -{(<p> ^pair <pair>)
     (<r2> ^result.<set2>.<object2>.<elem2>.id <idb> ^parameter.result.link.link.<set2>.<object2>.<elem2> holder)
     (<pair> ^a <ida> ^b <idb>)}}
-->
   (<s> ^pair <pa>)
}
sp {extract-set-set*elaborate*pairs*negative
   (state <s> ^name extract-set-set
              ^superstate.operator <o>
              ^linkstate.prepositions.prep <p>)
   (<p> ^name <name>)
   (<o> ^prep <name>
        ^a <r>
        ^negative true
        ^b <r2>)
   (<r> ^result.<set>.<pair2> <pa> ^parameter.result.link.link.<set>.<pair2>.<elem> holder)
   (<pa> ^<elem> <e>)
  -{(<e> ^object.id <ida>)
    (<p> ^pair <pair>)
    (<r2> ^result.<set2>.<object2>.<elem2>.id <idb> ^parameter.result.link.link.<set2>.<object2>.<elem2> holder)
    (<pair> ^a <ida> ^b <idb>)}
-->
   (<s> ^pair <pa>)
}

sp {extract-set-set*elaborate*result
   (state <s> ^name extract-set-set)
-->
   (<s> ^result.set <res>)
}

sp {extract-set-set*elaborate*result-pairs
   (state <s> ^name extract-set-set
              ^result.set <res>
              ^pair <p>)
-->
   (<res> ^pair <p>)
}

sp {extract-set-set*propose*complete
   (state <s> ^name extract-set-set
              ^result.set <res>)
   -{(<s> ^pair <p>)
     (<res> -^pair <p>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete)
}

sp {extract-set-set*apply*complete
   (state <s> ^name extract-set-set
              ^result <res>
              ^superstate <ss>
              ^operator.name complete)
-->
   (<ss> ^result <res>)
}