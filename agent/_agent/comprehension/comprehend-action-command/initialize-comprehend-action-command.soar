### elaborate direct-object
sp {elaborate*direct-object*comprehend-action-command
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <i>
   	      ^operator.name initialize-comprehend-action-command)
   (<i> ^lexical <info>)
   (<info> ^verb.direct-object.object <do>)
-->
   (<i> ^direct-object <ido>)
   (<ido> ^type object
   	  ^lexical <do>)
}

### delete existing direct-object
sp {delete*direct-object*comprehend-action-command
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <i>
   	      ^operator.name initialize-comprehend-action-command)
   (<i> ^direct-object <do>)
   (<do> -^result)
 -->
   (<i> ^direct-object <do> -)
}


### elaborate pp-object
sp {elaborate*pp-object*comprehend-action-command
   (state <s> ^name comprehend-action-command
   	      ^operator.name initialize-comprehend-action-command
   	      ^indexable-parameter <i>)
   (<i> ^lexical <info>)
   (<info> ^verb.preposition <do>)
   (<do> ^word <preposition>
   	 ^object <np>)
-->
   (<i> ^pp-object <ido>)
   (<ido> ^preposition <prep>
   	  ^type pp-object
	  ^object <object>
	  ^lexical <do>)
   (<object> ^type object
   	     ^lexical <np>)
   (<prep> ^type preposition
   	   ^lexical <preposition>)
}

### 
sp {delete*pp-object*comprehend-action-command
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <i>
   	      ^operator.name initialize-comprehend-action-command)
   (<i> ^pp-object <do>)
   (<do> -^result)
 -->
   (<i> ^pp-object <do> -)
}



### elaborate verb
sp {elaborate*verb*comprehend-action-command
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <i>
   	      ^operator.name initialize-comprehend-action-command)
   (<i> ^lexical.verb <info>)
   (<info> ^word <verbword>)
-->
   (<i> ^verb <verb>)
   (<verb> ^lexical <verbword>
   	   ^type verb)
}

sp {delete*verb*comprehend-action-command
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <i>
   	      ^operator.name initialize-comprehend-action-command)
   (<i> ^verb <do>)
   (<do> -^result)
 -->
   (<i> ^verb <do> -)
}

sp {initialize-comprehend-action-command*mark
   (state <s> ^name comprehend-action-command
   	      ^operator.name initialize-comprehend-action-command)
-->
   (<s> ^initialized yes)
}
