### elaborate indexable parameter
sp {elaborate*lexical-information
   (state <s> ^name comprehend-action-command
   	      ^superstate.operator <sop>)
   (<sop> ^name comprehend-action-command
   	  ^parameter <i>)
-->
   (<s> ^indexable-parameter <i>)
}


#### elaborate _metadata
sp {comprehend-action-command*elaborate*metadata
   (state <s> ^name comprehend-action-command)
-->
   (<s> ^_metadata <met>)
   (<met> ^argument-attributes <arg-attr>)
}

#### elaborate attributes
sp {comprehend-action-command*elaborate*argument-attributes*do
   (state <s> ^name comprehend-action-command
   	      ^_metadata <met>)
   (<met> ^map-id.linguistic.direct-object <so>
   	  ^map-id.procedural.<arg> <so>
	  ^argument-attributes <ft>)
-->
   (<ft> ^<arg> <fnew>)
}

sp {comprehend-action-command*elaborate*argument-attributes*ppo
   (state <s> ^name comprehend-action-command
   	      ^_metadata <met>)
   (<met> ^map-id.linguistic.pp-object <ppo>
   	  ^map-id.procedural.<arg> <proco>
	  ^argument-attributes <ft>)
   (<ppo> ^object <so>)
   (<proco> ^argument <so>)
-->
   (<ft> ^<arg> <fnew>)
   (<fnew> ^argument <o>)
}

sp {comprehend-action-command*copy*object*attributes*level1
   (state <s> ^name comprehend-action-command	
   	      ^_metadata <met>
	      ^indexable-parameter <param>)
   (<met> ^map-id <map>
   	  ^argument-attributes <ft>)
   (<map> ^linguistic.direct-object <dobj>
   	  ^procedural.<arg> <dobj>)
   (<param> ^direct-object.indexable.result.sense.label <label>)
   (<ft> ^<arg> <val>)
-->
   (<val> ^attribute <label>)
}

sp {comprehend-action-command*copy*object*attributes*level2
   (state <s> ^name comprehend-action-command	
   	      ^_metadata <met>
	      ^indexable-parameter <param>)
   (<met> ^map-id <map>
   	  ^argument-attributes <ft>)
   (<map> ^linguistic.pp-object.object <dobj>
   	  ^procedural.<arg>.<arg2> <dobj>)
   (<param> ^pp-object.object.indexable.result.sense.label <label>)
   (<ft> ^<arg>.<arg2> <val>)
-->
   (<val> ^attribute <label>)
}
   

### elaborate indexable parameter
sp {elaborate*initialized
   (state <s> ^name comprehend-action-command
   	      ^superstate.operator <sop>)
   (<sop> ^name comprehend-action-command
   	  ^initialized <i>)
-->
   (<s> ^initialized <i>)
}


