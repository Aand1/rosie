############## elaborate state ##################################
sp {get-linguistic-id*elaborate*state
   (state <s> ^name get-linguistic-id
   	      ^superstate.operator <sop>)
   (<sop> ^name get-linguistic-id
   	  ^cue <cue>)
-->
   (<s> ^cue <cue>)
}

#################################################################
### elaborate retrieval ids
sp {get-linguistic-id*query*procedural-id
   (state <s> ^name get-linguistic-id
   	      ^cue <cue>)
-->
   (<s> ^smem-query-cue <cue>)
}


sp {get-linguistic-id*write*to*superstate*no-failure
   (state <s> ^name get-linguistic-id
   	      ^cue <cue>
	      ^retrieved-lti-cue <rl>)
   (<rl> ^cue <cue>
   	 ^retrieved-lti <lti>)
-->
   (<s> ^linguistic <lti>)
}

### retrieve pp-object
sp {propose*pp-object*get-linguistic-id
   (state <s> ^name get-linguistic-id
   	      ^linguistic <ling>)
   (<ling> ^pp-object <ppobj>)
   (<ppobj> -^<any>)
-->
   (<s> ^nretrieve-lti <ppobj>)
}

################################## elaborate failures #######################
### elaborate failures: no map for the verb
sp {get-linguistic-id*failure*no-map
   (state <s> ^name get-linguistic-id
   	      ^linguistic failure)
-->
   (<s> ^failure <f>)
   (<f> ^type no-map)
}

### elaborate failure: an argument is missing
sp {get-linguistic-id*elaborate*failure*missing-argument
   (state <s> ^name get-linguistic-id
   	      ^linguistic {<lid> <> failure}
	      ^cue <cue>)
   (<cue> ^<arg>)
   (<lid> -^<arg>)
-->
   (<s> ^failure <f>)
   (<f> ^type missing-argument
   	^argument <arg>
	^smem-pointer <lid>)
}

### elaborate failure: argument structure is missing
sp {get-linguistic-id*elaborate*failure*missing-argument-substructure
   (state <s> ^name get-linguistic-id
   	      ^linguistic {<lid> <> failure}
	      ^retrieved <any>
	      ^cue <cue>)
   (<cue> ^<arg> <val>)
   (<val> ^<arg1> <val1>)
   (<lid> ^<arg> <sval>)
   (<sval> -^<arg1>)
-->
   (<s> ^failure <f>)
   (<f> ^type missing-argument-substructure
   	^argument <arg1>
	^smem-pointer <sval>)
}