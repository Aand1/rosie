sp {get-linguistic-id*elaborate*state
   (state <s> ^name get-linguistic-id
   	      ^superstate.operator <sop>)
   (<sop> ^name get-linguistic-id
   	  ^cue <cue>
	  -^failure specific-relation)
-->
   (<s> ^cue <cue>)
}

sp {get-linguistic-id*write*to*superstate*no-failure
   (state <s> ^name get-linguistic-id
   	      ^smem.result <res>
	      ^superstate <ss>)
   (<res> ^retrieved <ret>)
-->
   (<s> ^linguistic <ret>)
}


sp {get-linguistic-id*bit*flip
   (state <s> ^name get-linguistic-id)
-->
   (<s> ^bit flip)
}


### elaborate failures: no map for the verb
sp {get-linguistic-id*failure*no-map
   (state <s> ^name get-linguistic-id
   	      ^smem.result <res>)
   (<res> ^failure <any>)
-->
   (<s> ^failure <f>)
   (<f> ^type no-map)
}

### elaborate failure: an argument is missing
sp {get-linguistic-id*elaborate*failure*missing-argument
   (state <s> ^name get-linguistic-id
   	      ^linguistic <lid>
	      ^retrieved <any>
	      ^cue <cue>)
   (<cue> ^<arg>)
   (<lid> -^<arg>)
-->
   (<s> ^failure <f>)
   (<f> ^type missing-argument
   	^argument <arg>
	^smem-pointer <lid>)
}

### elaborate failure: argument structure is missing
sp {get-linguistic-id*elaborate*failure*missing-argument-substructure
   (state <s> ^name get-linguistic-id
   	      ^linguistic <lid>
	      ^retrieved <any>
	      ^cue <cue>)
   (<cue> ^<arg> <val>)
   (<val> ^<arg1> <val1>)
   (<lid> ^<arg> <sval>)
   (<sval> -^<arg1>)
-->
   (<s> ^failure <f>)
   (<f> ^type missing-argument-substructure
   	^argument <arg1>
	^smem-pointer <sval>)
}