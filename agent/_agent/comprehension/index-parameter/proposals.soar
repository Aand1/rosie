# This is a recursive call of index-parameter to any children of the current parameter that should be indexed
sp {index-parameter*propose*index-parameter*recurse
	(state <s> ^name index-parameter
           ^parameter.indexable <i>)
	(<i> ^lexical
	    -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-parameter
        ^parameter <i>)
}

sp {index-parameter*propose*index-property
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type property
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-property
        ^property-param <p>)
}

sp {index-parameter*propose*index-word
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type word
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-word
        ^word-param <p>)
}

#JK
sp {index-parameter*propose*lookup-preposition
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type preposition
        ^lexical <prep>
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-preposition
   	 ^type <prep>
	 ^interaction true)
}
sp {index-parameter*elaborate*lookup-preposition
   (state <s> ^name index-parameter
              ^found-prep.type <prep>
              ^parameter <p>)
   (<p> ^type preposition
        ^lexical <prep>)
-->
   (<p> ^result <prep>)
}

sp {index-parameter*propose*index-object
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type object
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^object-param <p>)
}