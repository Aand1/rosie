#################################################
#
# return-no-matches
#   No objects have matched, either ask or return failure
#
##################################################

sp {index-object*propose*return-no-matches
   (state <s> ^name index-object
              ^object-to-index.matches <m>)
   (<m> ^finished true
        ^no-matches true)
-->
   (<s> ^operator <o> +)
   (<o> ^name return-no-matches)
}

################################################
# failure
#   Cannot do an interaction, just create an empty structure
#################################################
sp {index-object*apply*return-no-matches*no-teaching*return-failure
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^index-info.use-interaction false
              ^object-to-index.object <obj>)
-->
   (<obj> ^result none)
}

################################################
# teaching-request
#   Ask the mentor to provide more information
#################################################
sp {index-object*apply*return-no-matches*teaching-request*create-message
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^index-info.use-interaction true)
-->
   (<s> ^teaching-request <req>)
   (<req> ^type teaching-request
          ^originator agent
          ^fields.description <desc>)
   (<desc> ^determiner the)
}

sp {index-object*apply*return-no-matches*teaching-request*copy*adjective*non-shape
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^teaching-request.fields.description <desc>
              ^object-to-index.word <w>)
   (<w> ^word <word>
        ^sense.property <prop>)
   (<prop> ^type visual
           ^name <> shape)
-->
   (<desc> ^adjective <word>)
}

sp {index-object*apply*return-no-matches*teaching-request*copy*adjective*shape
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^teaching-request.fields.description <desc>
              ^object-to-index <oti>)
   (<oti> ^word.sense.property.name name
          ^word <w>)
   (<w> ^word <word>
        ^sense.property.name shape)
-->
   (<desc> ^adjective <word>)
}

sp {index-object*apply*return-no-matches*teaching-request*copy*word*shape
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^teaching-request.fields.description <desc>
              ^object-to-index <oti>)
   (<oti> -^word.sense.property.name name
          ^word <w>)
   (<w> ^word <word>
        ^sense.property.name shape)
-->
   (<desc> ^word <word>)
}

sp {index-object*apply*return-no-matches*teaching-request*copy*word*name
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^teaching-request.fields.description <desc>
              ^object-to-index.word <w>)
   (<w> ^word <word>
        ^sense.property.name name)
-->
   (<desc> ^word <word>)
}

sp {index-object*apply*return-no-matches*teaching-request*copy*word*object
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^teaching-request.fields.description <desc>
              ^object-to-index <oti>)
   (<oti> -^word.sense.property.name name
          -^word.sense.property.name shape)
-->
   (<desc> ^word object)
}

sp {index-object*apply*return-no-matches*teaching-request*mark*message-ready
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^teaching-request.fields.description.word)
-->
   (<s> ^message-ready true)
}

sp {index-object*apply*return-no-matches*teaching-request*create*new-segment
   (state <s> ^name index-object
              ^operator.name return-no-matches
              ^message-ready true
              ^teaching-request <msg>
              ^object-to-index.object <obj>
              ^topstate.interaction.status <status>)
-->
   (<status> ^new-segment-info <info>)
   (<info> ^purpose <p>
        ^message <msg>
        ^originator agent)
   (<p> ^type teaching-request
        ^target-object <obj>
        ^satisfaction.dialog-event.type single-word-response)
}
        