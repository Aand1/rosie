sp {topstate*propose*link-missing-object
   (state <s> ^topstate <s>
              ^interaction.stack.segment.purpose.parameters <p>)
   (<p> ^indexable <i>)
   (<i> ^type object
        ^matches-missing-object <old-obj>
        ^result <new-obj>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name link-missing-object
        ^parameters <p>
        ^missing-object <old-obj>
        ^object-match <new-obj>)
}

sp {topstate*apply*link-missing-object*clean*monitor-obj
   (state <s> ^topstate <s>
              ^operator <o>
              ^scene-monitor.object <monitor-obj>)
   (<o> ^name link-missing-object
        ^missing-object <obj>)
   (<monitor-obj> ^wm-obj <obj>
                  ^searching-for-object true)
-->
   (<monitor-obj> ^searching-for-object true -)
}

sp {topstate*apply*link-missing-object*clean*parameters
   (state <s> ^topstate <s>
              ^operator <o>)
   (<o> ^name link-missing-object
        ^parameters <p>
        ^missing-object.perception-obj  # Wait till the object is live
        ^object-match <obj>)
   (<p> ^indexable <i>)
   (<i> ^result <obj>)
-->
   (<p> ^indexable <i> -)
}

sp {topstate*apply*link-missing-object*link*objects
   (state <s> ^topstate <s>
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name link-missing-object
        ^object-match.perception-obj.id <src-id>
        ^missing-object.belief-obj.id <dest-id>)
-->
   (<out> ^modify-scene <ms>)
   (<ms> ^type link
         ^source-id <src-id>
         ^dest-id <dest-id>)
}

sp {topstate*apply*link-missing-object*copy*objects
   (state <s> ^topstate <s>
              ^operator <o>
              ^scene-monitor.object <monitor-obj>)
   (<o> ^name link-missing-object
        ^missing-object.belief-obj <obj>
        ^missing-object.perception-obj)
   (<monitor-obj> ^belief-obj <obj>)
-->
   (<monitor-obj> ^mark-for-copying true)
}

sp {topstate*apply*link-missing-object*delete*object
   (state <s> ^topstate <s>
              ^operator <o>
              ^scene-monitor.object <monitor-obj>)
   (<o> ^name link-missing-object
        ^object-match.perception-obj <perc>
        ^missing-object.belief-obj.id <dest-id>)
   (<monitor-obj> ^perception-obj <perc>)
-->
   (<monitor-obj> ^mark-for-deletion true)
}



