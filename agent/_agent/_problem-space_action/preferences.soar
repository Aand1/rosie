sp {action*reject*non-matched*operators
   (state <s> ^problem-space.name action
              ^operator <o> +
              ^operator-matches.match-info <info>)
   (<info> ^operator <o>
           ^op-matched no)
-->
   (<s> ^operator <o> -)
}

sp {reject*wait*if*until-clause*satisfied
   (state <s> ^problem-space.name action
              ^operator <o> +)
   (<o> ^name op_wait1
        ^until-clause <until>)
   (<until> ^satisfied true)
-->
   (<s> ^operator <o> -)
}

sp {action*best*preference*evaluate-operator*matched
   (state <s> ^problem-space.name action
              ^operator-matches.match-info <info>
              ^operator <o> +)
   (<info> ^op-matched yes
           ^operator <op>)
   (<o> ^name evaluate-operator
        ^superoperator <op>)
-->
   (<s> ^operator <o> >)
}

sp {action*reject*remember*if*no*effect
   (state <s> ^problem-space.name action
              ^operator <o> +
              ^world.objects.object { <obj> <> <obj2> })
   (<o> ^name op_remember1
        ^arg2.id <obj2>)
   (<obj> ^predicates <p1>)
   (<obj2> ^predicates <p2>)
   -{(<p2> ^{ <att> << property category >> } <val>)
     (<p1> -^<att> <val>)}
-->
   (<s> ^operator <o> -)
}
 