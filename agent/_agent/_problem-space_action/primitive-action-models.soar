### implement action-model for pick-up 
sp {action*apply*pickup*do*action-model
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
          ^operator <op>)
   (<op> ^name initiate-pick-up1
     ^arg1.id.predicates <preds>)
   (<preds> ^arm-status not-grabbed)
-->
   (<preds> ^arm-status grabbed
            ^arm-status not-grabbed -)
}

sp {action*apply*pickup*do*action-model*remove*in*relation
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
              ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name initiate-pick-up1
        ^arg1.id <obj>)
   (<pred> ^handle in1
           ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
-->
   (<pred> ^instance <i> -)
}


# action model for close
sp {action*apply*putdown*do*action-model*arm-status
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
               ^operator <o>)
   (<o> ^name initiate-put-down1
        ^arg1.id.predicates <obj1-preds>)
   (<obj1-preds> ^arm-status grabbed)
-->
   (<obj1-preds> ^arm-status grabbed -)
   (<obj1-preds> ^arm-status not-grabbed)
}

sp {action*apply*putdown*do*action-model*on
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
               ^operator <o>
               ^world.predicates.predicate <pred>)
   (<o> ^name initiate-put-down1
        ^arg1.id <obj1>
        ^arg2.{2} <obj2>
        ^arg2.handle <pred-handle>)
   (<pred> ^handle <pred-handle>)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
}

sp {action*apply*put-down*do*action-model*add*in*relation
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
              ^operator <o>
              ^world.robot.current-location <loc>
              ^world.predicates.predicate <pred>)
   (<o> ^name initiate-put-down1
        ^arg1.id <obj>)
   (<pred> ^handle in1)
   (<obj> ^predicates.arm-status grabbed)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

# action model for go-to-location
sp {action*apply*go-to-location*do*action-model*add*in*relation
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
              ^operator <o>
              ^world.robot <robot>)
   (<o> ^name initiate-go-to-location1
        ^arg2.2 <new-loc>)
   (<robot> ^current-location { <old-loc> <> <new-loc> })
-->
   (<robot> ^current-location <old-loc> -
            ^current-location <new-loc>)
}
           
# action model for open
sp {action*apply*open*do*action-model
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
          ^operator <op>)
   (<op> ^name intiate-open1
     ^arg1.id.predicates <preds>)
-->
   (<preds> ^door closed -
            ^door open)
}

# action model for close
sp {action*apply*close*do*action-model
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
          ^operator <op>)
   (<op> ^name intiate-close1
     ^arg1.id.predicates <preds>)
-->
   (<preds> ^door open -
            ^door closed)
}

# action model for turn-on
sp {action*apply*turn-on*do*action-model
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
          ^operator <op>)
   (<op> ^name intiate-turn-on1
     ^arg1.id.predicates <preds>)
   (<preds> ^<pred> off)
-->
   (<preds> ^<pred> off -
            ^<pred> on)
}

# action model for turn-off
sp {action*apply*turn-off*do*action-model
   (state <s> ^problem-space.name action
              ^problem-space.action.type simulate
          ^operator <op>)
   (<op> ^name intiate-turn-off1
     ^arg1.id.predicates <preds>)
   (<preds> ^<pred> on)
-->
   (<preds> ^<pred> on -
            ^<pred> off)
}
