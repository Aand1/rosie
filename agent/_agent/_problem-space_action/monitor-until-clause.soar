sp {execute-primitive-action*elaborate*until-clause*state*predicate*satisfied
   (state <s> ^problem-space.name action
              ^until-clause <until>
              ^world.objects.object <obj>)
   (<until> ^predicate <pred>)
   (<pred> ^type state
           ^handle <pred-handle>
           ^property-handle <prop-handle>
           ^{1} <obj>)
   (<obj> ^predicates.<prop-handle> <pred-handle>)
-->
   (<s> ^satisfied <pred>)
}

sp {execute-primitive-action*elaborate*until-clause*state*relation*satisfied
   (state <s> ^problem-space.name action
              ^until-clause <until>
              ^world <world>)
   (<until> ^predicate <until-pred>)
   (<until-pred> ^type relation
           ^handle <pred-handle>
           ^1 <obj1>
           ^2 <obj2>)
   (<world> ^predicates.predicate <pred>
            ^objects.object <obj1>
            ^objects.object <obj2>)
   (<pred> ^handle <pred-handle>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<s> ^satisfied <until-pred>)
}

sp {execute-primitive-action*elaborate*until-clause*predicate*satisfied*false
   (state <s> ^problem-space.name action
              ^until-clause <until>
             -^satisfied <pred>)
   (<until> ^predicate <pred>)
-->
   (<s> ^unsatisfied <pred>)
}

sp {execute-primitive-action*elaborate*until-clause*satisfied*false
   (state <s> ^problem-space.name action
              ^until-clause <until>
              ^unsatisfied <pred>)
   (<until> ^predicate <pred>)
-->
   (<s> ^unsatisfied <until>)
}

sp {execute-primitive-action*elaborate*until-clause*satisfied*true
   (state <s> ^problem-space.name action
              ^until-clause <until>
              ^satisfied <pred>)
   (<until> ^predicate <pred>)
   -{(<until> ^predicate <other>)
     (<s> ^unsatisfied <other>)}
-->
   (<s> ^satisfied <until>)
}

 
