sp {generate-grounded-predicate*elaborate*argument-to-match*from*superstate*1*args
   (state <s> ^name generate-grounded-predicate
              ^procedural-id <proc>
              ^superstate.action-context <action>)
   (<action> ^<arg1-name> <arg1>)
   (<proc> ^<arg1-name>)
   -{(<action> ^{<arg2-name> <> <arg1-name>} <arg2>)
     (<proc> ^<arg2-name> <parg2>)}
-->
   (<s> ^argument-to-match <atm1>)
   (<atm1> ^name <arg1-name>
           ^argument <arg1>)
}

sp {generate-grounded-predicate*elaborate*argument-to-match*from*superstate*2*args
   (state <s> ^name generate-grounded-predicate
              ^procedural-id <proc>
              ^superstate.action-context <action>)
   (<action> ^<arg1-name> <arg1>
             ^{<arg2-name> > <arg1-name>} <arg2>)
   (<proc> ^<arg1-name>
           ^<arg2-name>)
   -{(<action> ^{<arg3-name> <> <arg1-name> <> <arg2-name>} <arg3>)
     (<proc> ^<arg3-name> <parg3>)}
-->
   (<s> ^argument-to-match <atm1> <atm2>)
   (<atm1> ^name <arg1-name>
           ^argument <arg1>)
   (<atm2> ^name <arg2-name>
           ^argument <arg2>)
}

sp {generate-grounded-predicate*elaborate*procedural-id*from*superstate
   (state <s> ^name generate-grounded-predicate
               ^superstate.procedural-id <proc>)
-->
  (<s> ^procedural-id <proc>)
}

sp {generate-grounded-predicate*elaborate*predicate*from*superoperator
   (state <s> ^name generate-grounded-predicate
              ^superstate.operator.predicate <pred>)
-->
   (<s> ^predicate <pred>)
}

sp {generate-grounded-predicate*elaborate*grounding-rep*state*complete*true
   (state <s> ^name generate-grounded-predicate
              ^grounded-rep <rep>)
   (<rep> ^type state
          ^handle <handle>
          ^1 <arg1>)
-->
   (<s> ^complete <rep>)
}

sp {generate-grounded-predicate*elaborate*grounding-rep*relation*complete*true
   (state <s> ^name generate-grounded-predicate
              ^grounded-rep <rep>)
   (<rep> ^type relation
          ^handle <handle>
          ^1 <arg1>
          ^2 <arg2>)
-->
   (<s> ^complete <rep>)
}
