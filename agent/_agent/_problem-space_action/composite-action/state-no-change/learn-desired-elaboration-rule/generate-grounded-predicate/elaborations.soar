sp {generate-grounded-predicate*elaborate*action-context*from*superstate*1*args
   (state <s> ^name generate-grounded-predicate
              ^action-id.procedural <proc>
              ^superstate.action-context <action>)
   (<action> ^<arg1-name> <arg1>)
   (<proc> ^<arg1-name>)
   -{(<action> ^{<arg2-name> <> <arg1-name>} <arg2>)
     (<proc> ^<arg2-name> <parg2>)}
-->
   (<s> ^action-context <action>)
}

sp {generate-grounded-predicate*elaborate*action-context*from*superstate*2*args
   (state <s> ^name generate-grounded-predicate
              ^action-id.procedural <proc>
              ^superstate.action-context <action>)
   (<action> ^<arg1-name> <arg1>
             ^{<arg2-name> <> <arg1-name>} <arg2>)
   (<proc> ^<arg1-name>
           ^<arg2-name>)
   -{(<action> ^{<arg3-name> <> <arg1-name> <> <arg2-name>} <arg3>)
     (<proc> ^<arg3-name> <parg3>)}
-->
   (<s> ^action-context <action>)
}

sp {generate-grounded-predicate*elaborate*action-id*from*superstate
   (state <s> ^name generate-grounded-predicate
              ^superstate.action-id <id>)
-->
   (<s> ^action-id <id>)
}

sp {generate-grounded-predicate*elaborate*predicate*from*superoperator
   (state <s> ^name generate-grounded-predicate
              ^superstate.operator.predicate <pred>)
-->
   (<s> ^predicate <pred>)
}

sp {generate-grounded-predicate*elaborate*world
   (state <s> ^name generate-grounded-predicate
              ^superstate.world <world>)
-->
   (<s> ^world <world>)
}

sp {generate-grounded-predicate*elaborate*grounding-rep*missing
   (state <s> ^name generate-grounded-predicate
              ^grounded-rep <rep>
              ^grounding-info <info>)
   (<info> ^attribute <att>
           ^no-grounding true)
-->
   (<rep> ^missing <att>)
}
