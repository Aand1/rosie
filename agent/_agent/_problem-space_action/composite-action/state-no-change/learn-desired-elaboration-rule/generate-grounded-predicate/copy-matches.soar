sp {generate-grounded-predicate*propose*copy-grounding*match
   (state <s> ^name generate-grounded-predicate
              ^grounding-info <info>
              ^grounded-rep <rep>)
   (<info> ^matches <g>
           ^attribute <att>)
   (<rep> -^<att>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-grounding
        ^match-type exact
        ^grounding <g>
        ^attribute <att>)
}

sp {generate-grounded-predicate*propose*copy-grounding*default-match
   (state <s> ^name generate-grounded-predicate
              ^grounding-info <info>
              ^grounded-rep <rep>)
   (<info> ^matches-default <g>
           ^attribute <att>)
   (<rep> -^<att>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-grounding
        ^match-type default
        ^grounding <g>
        ^attribute <att>)
}

sp {generate-grounded-predicate*prefer*copy-grounding*exact
   (state <s> ^name generate-grounded-predicate
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name copy-grounding
         ^match-type exact)
   (<o2> ^name copy-grounding
         ^match-type default)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {generate-grounded-predicate*apply*copy-grounding
   (state <s> ^name generate-grounded-predicate
              ^operator <o>
              ^grounded-rep <rep>)
   (<o> ^name copy-grounding
        ^grounding <g>
        ^attribute <att>)
-->
   (<rep> ^<att> <g>)
}