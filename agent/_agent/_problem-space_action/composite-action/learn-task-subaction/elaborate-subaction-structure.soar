# Elaborate the argument info onto the subaction generalized structure

sp {learn-task-subaction*elaborate*subaction-structure
   (state <s> ^name learn-task-subaction)
-->
   (<s> ^subaction-structure <struct>)
}

sp {learn-task-subaction*elaborate*perform*once*internal*subaction
   (state <s> ^name learn-task-subaction
              ^subaction-id.internal true
              ^subaction-structure <struct>)
-->
   (<struct> ^perform once)
}

sp {learn-task-subaction*elaborate*perform*multiple*not*internal
   (state <s> ^name learn-task-subaction
              ^subaction-id <sub-id>
              ^subaction-structure <struct>)
   (<sub-id> -^internal true)
-->
   (<struct> ^perform multiple)
}

sp {learn-task-subaction*elaborate*op_name*onto*subaction-structure
   (state <s> ^name learn-task-subaction
              ^subaction.name <op-name>
              ^subaction-structure <struct>)
-->
   (<struct> ^op_name <op-name>)
}

sp {learn-task-subaction*elaborate*action-handle*onto*subaction-structure
   (state <s> ^name learn-task-subaction
              ^subaction.action-handle <action>
              ^subaction-structure <struct>)
-->
   (<struct> ^action-handle <action>
             ^handle (make-constant-symbol |subaction|))
}

sp {learn-task-subaction*elaborate*action-type*onto*subaction-structure
   (state <s> ^name learn-task-subaction
              ^subaction.action-type <type>
              ^subaction-structure <struct>)
-->
   (<struct> ^action-type <type>)
}

sp {learn-task-subaction*elaborate*argument-info*onto*subaction-structure
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>
              ^subaction-structure <struct>)
   (<arg-info> ^arg-name <arg-name>
               ^generalized-rep <gen>)
-->
   (<struct> ^<arg-name> <gen>)
}

sp {learn-task-subaction*elaborate*argument-info*object-type*onto*subaction-structure
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>
              ^subaction-structure <struct>)
   (<arg-info> ^arg-name <arg-name>
               ^object-type <type>)
   (<struct> ^<arg-name> <struct-arg>)
-->
   (<struct-arg> ^object-type <type>)
}


