sp {action*composite-action*elaborate*smem-query
   (state <s> ^problem-space <ps>
              ^action-operator.action-handle <action-handle>
             -^action-id)
   (<ps> ^name action
         ^action-type composite)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 8)
   (<cue> ^handle <action-handle>)
}

sp {action*composite-action*elaborate*action-id*from*query*result
   :o-support
   (state <s> ^problem-space <ps>
              ^smem-query <query>
              ^action-operator.action-handle <action-handle>)
   (<query> ^cue.handle <action-handle>
            ^result <id>)
-->
   (<s> ^action-id <id>)
}

sp {execute-composite-action*elaborate*smem-retrieve*subaction
   :o-support
   (state <s> ^problem-space.name action
              ^action-id.procedural.subaction <sub>)
   (<sub> -^op_name)
-->
   (<s> ^smem-retrieve <ret>)
   (<ret> ^identifier <sub>
          ^depth 5)
}