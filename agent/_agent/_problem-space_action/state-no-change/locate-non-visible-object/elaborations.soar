sp {locate-non-visible-object*elaborate*primitive-action*superstate
   (state <s> ^name locate-non-visible-object
              ^superstate.action-to-match <atm>)
   (<atm> ^action-type primitive)
-->
   (<s> ^primitive-action <atm>)
}

sp {locate-non-visible-object*elaborate*composite-action-operator*from*interaction-stack
   (state <s> ^name locate-non-visible-object
              ^top-state.interaction.stack.segment.next.purpose.parameters <params>)
   (<params> ^action-operator <action>)
   (<action> ^action-type composite)
-->
   (<s> ^composite-action-operator <action>)
}

sp {locate-non-visible-object*elaborate*world*superstate
   (state <s> ^name locate-non-visible-object
              ^superstate.world <world>)
-->
   (<s> ^world <world>)
}

sp {locate-non-visible-object*elaborate*non-visible-object*depth*1
   (state <s> ^name locate-non-visible-object
              ^primitive-action.<arg-name> <arg>)
   (<arg> ^arg-type object
          ^id <obj>)
   (<obj> -^predicates.visibility visible1)
-->
   (<s> ^non-visible-object <obj>)
}

sp {locate-non-visible-object*elaborate*non-visible-object*depth*2
   (state <s> ^name locate-non-visible-object
              ^primitive-action.<arg-name>.<num> <arg>)
   (<arg> ^arg-type object
          ^id <obj>)
   (<obj> -^predicates.visibility visible1)
-->
   (<s> ^non-visible-object <obj>)
}
