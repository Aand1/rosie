sp {locate-non-visible-object*propose*merge-objects
   (state <s> ^name locate-non-visible-object
              ^match-info <info>)
   (<info> ^object <obj2>
           ^matches <obj1>)
-->
   (<s> ^operator <o> + )
   (<o> ^name merge-objects
        ^object1 <obj1>
        ^object2 <obj2>)
}

sp {locate-non-visible-object*apply*merge-objects*mark*source
   (state <s> ^name locate-non-visible-object
              ^operator <o>)
   (<o> ^name merge-objects
        ^object1 <obj1>
        ^object2 <obj2>)
-->
   (<obj2> ^source <obj1>)
}

sp {locate-non-visible-object*apply*merge-objects*copy*predicates
   (state <s> ^name locate-non-visible-object
              ^operator <o>)
   (<o> ^name merge-objects
        ^object2.predicates.{ <att> << property category >> } <val>
        ^object1.predicates <preds>)
-->
   (<preds> ^<att> <val>)
}

sp {locate-non-visible-object*apply*merge-objects*remove*object2*from*world
   (state <s> ^name locate-non-visible-object
              ^operator <o>
              ^top-state.world.objects <objs>)
   (<o> ^name merge-objects
        ^object2 <obj2>)
   (<objs> ^object <obj2>)
-->
   (<objs> ^object <obj2> -)
}

sp {locate-non-visible-object*apply*merge-objects*replace*object2*on*primitive-action*depth*1
   (state <s> ^name locate-non-visible-object
              ^operator <o>
              ^primitive-action <action>)
   (<o> ^name merge-objects
        ^object1 <obj1>
        ^object2 <obj2>)
   (<action> ^<arg-name> <arg>)
   (<arg> ^id <obj2>)
-->
   (<arg> ^id <obj2> -
          ^id <obj1>)
}
  
sp {locate-non-visible-object*apply*merge-objects*replace*object2*on*primitive-action*depth*2
   (state <s> ^name locate-non-visible-object
              ^operator <o>
              ^primitive-action <action>)
   (<o> ^name merge-objects
        ^object1 <obj1>
        ^object2 <obj2>)
   (<action> ^<arg-name>.<num> <arg>)
   (<arg> ^id <obj2>)
-->
   (<arg> ^id <obj2> -
          ^id <obj1>)
}

sp {locate-non-visible-object*apply*merge-objects*mark*composite-action-context*merged
   (state <s> ^name locate-non-visible-object
              ^operator <o>
              ^composite-action-context <action>)
   (<o> ^name merge-objects
        ^object1 <obj1>
        ^object2 <obj2>)
-->
   (<action> ^merged <m>)
   (<m> ^object1 <obj1>
        ^object2 <obj2>)
}