# Create an argument-info structure used when generalizing the argument

sp {learn-task-subaction*elaborate*argument-info*object
   (state <s> ^name learn-task-subaction
              ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type { <arg-type> << object location >> }
          ^id <obj>)
-->
   (<s> ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type <arg-type>
               ^instance <arg>
               ^id <gen>)
   (<s> ^generalize-argument <gen>)
   (<gen> ^arg-type object
          ^instance <obj>)
}

sp {learn-task-subaction*elaborate*argument-info*concept
   (state <s> ^name learn-task-subaction
              ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type concept
          ^id <concept>)
-->
   (<s> ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type concept
               ^instance <arg>
               ^id <gen>)
   (<s> ^generalize-argument <gen>)
   (<gen> ^arg-type concept
          ^instance <concept>)
}

sp {learn-task-subaction*elaborate*argument-info*predicate
   (state <s> ^name learn-task-subaction
              ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type predicate
          ^arg-status <status>
          ^id <pred>
          ^2 <obj>)
-->
   (<s> ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type predicate
               ^instance <arg>
               ^id <gen-pred>
               ^2 <gen-obj>)
   (<s> ^generalize-argument <gen-pred> <gen-obj>)
   (<gen-pred> ^arg-type predicate 
               ^instance <pred>)
   (<gen-obj> ^arg-type object
              ^instance <obj>)
}

sp {learn-task-subaction*elaborate*argument-info*until
   (state <s> ^name learn-task-subaction
              ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type until-clause)
-->
   (<s> ^argument-info <arg-info>)
   (<arg-info> ^arg-name <arg-name>
               ^arg-type until-clause
               ^instance <arg>)
}

sp {learn-task-subaction*elaborate*argument-info*until*predicate
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause 
               ^instance.predicate <until-pred>)
-->
   (<arg-info> ^predicate <gen>)
   (<s> ^generalize-argument <gen>)
   (<gen> ^arg-type until-predicate
          ^instance <until-pred>)
}

# Create the generalized-rep on the argument-info #

sp {learn-task-subaction*elaborate*argument-info*generalized-rep*object
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type { <arg-type> << object location >> }
               ^id.generalized-rep <obj-gen>
               ^instance <i>)
   (<i> ^object-type <type>
        ^object-status <status>)
-->
   (<arg-info> ^generalized-rep <gen-rep>)
   (<gen-rep> ^arg-type <arg-type>
              ^object-type <type>
              ^object-status <status>
              ^id <obj-gen>)
}

sp {learn-task-subaction*elaborate*argument-info*generalized-rep*concept
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type concept
               ^id.generalized-rep <conc-gen>)
-->
   (<arg-info> ^generalized-rep <gen-rep>)
   (<gen-rep> ^arg-type concept
              ^id <conc-gen>)
}

sp {learn-task-subaction*elaborate*argument-info*generalized-rep*predicate
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type predicate
               ^id.generalized-rep <pred-gen>
               ^{2}.generalized-rep <obj2-gen>)
-->
   (<arg-info> ^generalized-rep <gen-rep>)
   (<gen-rep> ^arg-type predicate
              ^id <pred-gen>
              ^2 <obj2-gen>)
}

sp {learn-task-subaction*elaborate*argument-info*generalized-rep*until-clause
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause)
-->
   (<arg-info> ^generalized-rep <gen-rep>)
   (<gen-rep> ^arg-type until-clause)
}

sp {learn-task-subaction*elaborate*argument-info*generalized-rep*until-clause*predicate
   (state <s> ^name learn-task-subaction
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause
               ^generalized-rep <gen-rep>
               ^predicate.generalized-rep <pred-rep>)
-->
   (<gen-rep> ^predicate <pred-rep>)
}

