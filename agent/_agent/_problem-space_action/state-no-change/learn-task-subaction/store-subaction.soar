sp {learn-task-subaction*propose*store-subaction
   (state <s> ^name learn-task-subaction
              ^subaction-structure <sub>
             -^stored <sub>)
-->
   (<s> ^operator <o> +)
   (<o> ^name store-subaction
        ^subaction <sub>)
}

sp {learn-task-subaction*apply*store-subaction
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^action-id.procedural <proc>
              ^smem.command <cmd>
              ^superstate <ss>)
   (<o> ^name store-subaction
        ^subaction <sub>)
-->
   (<proc> ^subaction <sub>)
   (<cmd> ^store <sub> <proc>)
   (<s> ^stored <sub>)
}

sp {learn-task-subaction*apply*store-subaction*argument
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name> <arg>)
   (<arg> ^arg-type <any>)
-->
   (<cmd> ^store <arg>)
}

sp {learn-task-subaction*apply*store-subaction*arg*slots
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 predicate >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {learn-task-subaction*apply*store-subaction*predicate*arg*default
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}

sp {learn-task-subaction*apply*store-subaction*arg*predicate*slot
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> } <slot>)
-->
   (<cmd> ^store <slot>)
}

sp {learn-task-subaction*apply*store-subaction*predicate*arg*predicate*default
   (state <s> ^name learn-task-subaction
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-subaction
        ^subaction.<arg-name>.predicate.{ << id 1 2 3 4 5 >> }.{ << default imagined >> } <def>)
-->
   (<cmd> ^store <def>)
}