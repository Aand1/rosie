sp {generate-grounded-predicate*propose*create-grounded-rep*state
   (state <s> ^name generate-grounded-predicate
              ^predicate-info <info>
             -^grounded-rep)
   (<info> ^type state
           ^id.instance
           ^{1}.instance)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-grounded-rep
        ^predicate-info <info>)
}

sp {generate-grounded-predicate*propose*create-grounded-rep*relation
   (state <s> ^name generate-grounded-predicate
              ^predicate-info <info>
             -^grounded-rep)
   (<info> ^type relation
           ^id.instance
           ^{1}.instance
           ^{2}.instance)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-grounded-rep
        ^predicate-info <info>)
}

sp {generate-grounded-predicate*apply*create-grounded-rep*state
   (state <s> ^name generate-grounded-predicate
              ^operator <o>)
   (<o> ^name create-grounded-rep
        ^predicate-info <info>)
   (<info> ^type state
           ^id.instance <handle>
           ^id.property-handle <prop-handle>
           ^{1}.instance <obj1>)
-->
   (<s> ^grounded-rep <rep>)
   (<rep> ^type state
          ^handle <handle>
          ^property-handle <prop-handle>
          ^1 <obj1>)
}

sp {generate-grounded-predicate*apply*create-grounded-rep*relation
   (state <s> ^name generate-grounded-predicate
              ^operator <o>)
   (<o> ^name create-grounded-rep
        ^predicate-info <info>)
   (<info> ^type relation
           ^id.instance <handle>
           ^{1}.instance <obj1>
           ^{2}.instance <obj2>)
-->
   (<s> ^grounded-rep <rep>)
   (<rep> ^type relation
          ^handle <handle>
          ^1 <obj1>
          ^2 <obj2>)
}
