
# Create the argument-info structures, depending on the number of arguments
sp {learn-subaction-proposal*elaborate*argument-info*1*argument
   (state <s> ^name learn-subaction-proposal
              ^argument-map <map>)
   (<map> ^<arg1-name> <arg1>)
   -{(<map> ^{ <arg2-name> <> <arg1-name> } <arg2>)}
-->
   (<s> ^argument-info <info1>)
   (<info1> ^arg-name <arg1-name> ^generalized-rep <arg1>)
}

sp {learn-subaction-proposal*elaborate*argument-info*2*arguments
   (state <s> ^name learn-subaction-proposal
              ^argument-map <map>)
   (<map> ^<arg1-name> <arg1>
          ^{ <arg2-name> > <arg1-name> } <arg2>)
   -{(<map> ^{ <arg3-name> <> <arg1-name> <> <arg2-name> } <arg3>)}
-->
   (<s> ^argument-info <info1> <info2>)
   (<info1> ^arg-name <arg1-name> ^generalized-rep <arg1>)
   (<info2> ^arg-name <arg2-name> ^generalized-rep <arg2>)
}

sp {learn-subaction-proposal*elaborate*argument-info*3*arguments
   (state <s> ^name learn-subaction-proposal
              ^argument-map <map>)
   (<map> ^<arg1-name> <arg1>
          ^{ <arg2-name> > <arg1-name> } <arg2>
          ^{ <arg3-name> > <arg2-name> } <arg3>)
   -{(<map> ^{ <arg4-name> <> <arg1-name> <> <arg2-name> <> <arg3-name> } <arg4>)}
-->
   (<s> ^argument-info <info1> <info2> <info3>)
   (<info1> ^arg-name <arg1-name> ^generalized-rep <arg1>)
   (<info2> ^arg-name <arg2-name> ^generalized-rep <arg2>)
   (<info3> ^arg-name <arg3-name> ^generalized-rep <arg3>)
}

sp {learn-subaction-proposal*elaborate*argument-info*4*arguments
   (state <s> ^name learn-subaction-proposal
              ^argument-map <map>)
   (<map> ^<arg1-name> <arg1>
          ^{ <arg2-name> > <arg1-name> } <arg2>
          ^{ <arg3-name> > <arg2-name> } <arg3>
          ^{ <arg4-name> > <arg3-name> } <arg4>)
   -{(<map> ^{ <arg5-name> <> <arg1-name> <> <arg2-name> <> <arg3-name> <> <arg4-name> } <arg5>)}
-->
   (<s> ^argument-info <info1> <info2> <info3> <info4>)
   (<info1> ^arg-name <arg1-name> ^generalized-rep <arg1>)
   (<info2> ^arg-name <arg2-name> ^generalized-rep <arg2>)
   (<info3> ^arg-name <arg3-name> ^generalized-rep <arg3>)
   (<info4> ^arg-name <arg4-name> ^generalized-rep <arg4>)
}

# Elaborate the type of the argument

sp {learn-subaction-proposal*elaborate*argument-info*info
   (state <s> ^name learn-subaction-proposal
              ^argument-info <info>)
   (<info> ^generalized-rep.arg-type <arg-type>)
-->
   (<info> ^arg-type <arg-type>)
}

# Elaborate the arg-status

sp {learn-subaction-proposal*elaborate*argument-info*arg-status
  (state <s> ^name learn-subaction-proposal
              ^argument-info <arg-info>)
  (<arg-info> ^generalized-rep.arg-status <arg-status>)
-->
  (<arg-info> ^arg-status <arg-status>)
}

# Create ground-argument structures to ground the parts of the argument
#   SEE: _problem-space_action/elaborate-ground-argument.soar

sp {learn-subaction-proposal*elaborate*argument-info*object
   (state <s> ^name learn-subaction-proposal
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type << object location >>
               ^generalized-rep <arg-rep>)
   (<arg-rep> ^id <obj-rep>)
-->
   (<arg-info> ^id <ground-obj>)
   (<s> ^ground-argument <ground-obj>)
   (<ground-obj> ^arg-type object
                 ^generalized-rep <obj-rep>)
}

sp {learn-subaction-proposal*elaborate*argument-info*concept
   (state <s> ^name learn-subaction-proposal
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type concept
               ^generalized-rep <arg-rep>)
   (<arg-rep> ^id <concept-rep>)
-->
   (<arg-info> ^id <ground-conc>)
   (<s> ^ground-argument <ground-conc>)
   (<ground-conc> ^arg-type concept
                  ^generalized-rep <concept-rep>)
}

sp {learn-subaction-proposal*elaborate*argument-info*predicate
   (state <s> ^name learn-subaction-proposal
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type predicate
               ^generalized-rep <arg-rep>)
   (<arg-rep> ^id <pred-rep>
              ^2 <obj-rep>)
-->
   (<arg-info> ^id <ground-pred>
               ^2 <ground-obj>)
   (<s> ^ground-argument <ground-pred> <ground-obj>)
   (<ground-pred> ^arg-type predicate 
                  ^generalized-rep <pred-rep>)
   (<ground-obj> ^arg-type object
                 ^generalized-rep <obj-rep>)
}

sp {learn-subaction-proposal*elaborate*argument-info*until*predicate
   (state <s> ^name learn-subaction-proposal
              ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause 
               ^generalized-rep <arg-rep>)
   (<arg-rep> ^predicate <until-pred>)
-->
   (<arg-info> ^predicate <ground-pred>)
   (<s> ^ground-argument <ground-pred>)
   (<ground-pred> ^arg-type until-predicate
                  ^generalized-rep <until-pred>)
}
