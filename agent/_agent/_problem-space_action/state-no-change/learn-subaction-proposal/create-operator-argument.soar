sp {learn-subaction-proposal*propose*create-operator-argument
   (state <s> ^name learn-subaction-proposal
              ^argument-info <arg-info>)
   (<arg-info> -^created true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
}

sp {learn-subaction-proposal*apply*create-operator-argument*object
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^subaction-operator <sub-op>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type { <arg-type> << object location >> }
               ^arg-name <arg-name>
               ^arg-status <status>
               ^id.instance <obj>)
-->
   (<sub-op> ^<arg-name> <new-arg>)
   (<new-arg> ^arg-type <arg-type>
              ^arg-status <status>
              ^id <obj>)
   (<arg-info> ^created true)
}

sp {learn-subaction-proposal*apply*create-operator-argument*concept
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^subaction-operator <sub-op>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type concept
               ^arg-status <status>
               ^arg-name <arg-name>
               ^id.instance <concept>)
-->
   (<sub-op> ^<arg-name> <new-arg>)
   (<new-arg> ^arg-type concept
              ^arg-status <status>
              ^id <concept>)
   (<arg-info> ^created true)
}

sp {learn-subaction-proposal*apply*create-operator-argument*predicate
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^subaction-operator <sub-op>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type predicate
               ^arg-name <arg-name>
               ^arg-status <status>
               ^id.instance <pred>
               ^{2}.instance <obj2>)
-->
   (<sub-op> ^<arg-name> <new-arg>)
   (<new-arg> ^arg-type predicate
              ^arg-status <status>
              ^handle <pred>
              ^2 <obj2>)
   (<arg-info> ^created true)
}

sp {learn-subaction-proposal*apply*create-operator-argument*until-clause
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^subaction-operator <sub-op>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause
               ^arg-name <arg-name>)
-->
   (<sub-op> ^<arg-name> <new-arg>)
   (<new-arg> ^arg-type until-clause)
}

sp {learn-subaction-proposal*apply*create-operator-argument*until-clause*state*predicate
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^subaction-operator.<arg-name> <sub-arg>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause
               ^arg-name <arg-name>
               ^predicate.instance <pred>)
   (<pred> ^type state
           ^handle <handle>
           ^property-handle <prop-handle>
           ^1 <obj1>)
-->
   (<sub-arg> ^predicate <new-pred>)
   (<new-pred> ^type state
               ^handle <handle>
               ^property-handle <prop-handle>
               ^1 <obj1>)
   (<arg-info> ^created true)
}

sp {learn-subaction-proposal*apply*create-operator-argument*until-clause*relation*predicate
   (state <s> ^name learn-subaction-proposal
              ^operator <o>
              ^subaction-operator.<arg-name> <sub-arg>)
   (<o> ^name create-operator-argument
        ^argument-info <arg-info>)
   (<arg-info> ^arg-type until-clause
               ^arg-name <arg-name>
               ^predicate.instance <pred>)
   (<pred> ^type relation
           ^handle <handle>
           ^1 <obj1>
           ^2 <obj2>)
-->
   (<sub-arg> ^predicate <new-pred>)
   (<new-pred> ^type relation
               ^handle <handle>
               ^1 <obj1>
               ^2 <obj2>)
   (<arg-info> ^created true)
}
