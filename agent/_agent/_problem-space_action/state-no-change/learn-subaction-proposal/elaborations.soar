sp {learn-subaction-proposal*elaborate*procedural-id
   (state <s> ^name learn-subaction-proposal
              ^superstate.operator.procedural-id <proc>)
-->
   (<s> ^procedural-id <proc>)
}

sp {learn-subaction-proposal*elaborate*action-context
   (state <s> ^name learn-subaction-proposal
              ^superstate.operator.action-context <action>)
-->
   (<s> ^action-context <action>)
}

sp {learn-subaction-proposal*elaborate*subaction-id
   (state <s> ^name learn-subaction-proposal
              ^superstate.operator.subaction-id <sub>)
-->
   (<s> ^subaction-id <sub>)
}

sp {learn-subaction-proposal*elaborate*world*superstate
   (state <s> ^name learn-subaction-proposal
              ^superstate.superstate.world <world>)
-->
   (<s> ^world <world>)
}


sp {learn-subaction-proposal*elaborate*argument
   (state <s> ^name learn-subaction-proposal
              ^subaction-id <sub>)
   (<sub> ^<arg-name> <arg>)
   (<arg> ^arg-type <any>)
-->
   (<s> ^argument <arg>)
}

sp {learn-subaction-proposal*elaborate*argument-map*1*argument
   (state <s> ^name learn-subaction-proposal
              ^argument <arg1>)
   -{(<s> ^argument { <arg2> <> <arg1> })}
-->
   (<s> ^argument-map <map>)
   (<map> ^argument.source <arg1>)
}

sp {learn-subaction-proposal*elaborate*argument-map*2*arguments
   (state <s> ^name learn-subaction-proposal
              ^argument <arg1>
              ^argument { <arg2> > <arg1> })
   -{(<s> ^argument { <arg3> <> <arg1> <> <arg2> })}
-->
   (<s> ^argument-map <map>)
   (<map> ^argument.source <arg1>
          ^argument.source <arg2>)
}

sp {learn-subaction-proposal*elaborate*argument-map*3*arguments
   (state <s> ^name learn-subaction-proposal
              ^argument <arg1>
              ^argument { <arg2> > <arg1> }
              ^argument { <arg3> > <arg2> })
   -{(<s> ^argument { <arg4> <> <arg1> <> <arg2> <> <arg3> })}
-->
   (<s> ^argument-map <map>)
   (<map> ^argument.source <arg1>
          ^argument.source <arg2>
          ^argument.source <arg3>)
}

sp {learn-subaction-proposal*elaborate*argument-map*4*arguments
   (state <s> ^name learn-subaction-proposal
              ^argument <arg1>
              ^argument { <arg2> > <arg1> }
              ^argument { <arg3> > <arg2> }
              ^argument { <arg4> > <arg3> })
   -{(<s> ^argument { <arg5> <> <arg1> <> <arg2> <> <arg3> <> <arg4> })}
-->
   (<s> ^argument-map <map>)
   (<map> ^argument.source <arg1>
          ^argument.source <arg2>
          ^argument.source <arg3>
          ^argument.source <arg4>)
}

sp {learn-subaction-proposal*elaborate*all-arguments-created*true
   (state <s> ^name learn-subaction-proposal
              ^argument-map <map>)
   -{(<map> ^argument <arg>)
     (<arg> ^source <src>
           -^created true)}
-->
   (<s> ^all-arguments-created true)
}