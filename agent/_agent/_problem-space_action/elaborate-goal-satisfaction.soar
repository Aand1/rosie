sp {action*elaborate*desired*state*predicate*satisfied
   (state <s> ^problem-space.name action
              ^desired.predicate <p>)
   (<p> ^type state
        ^predicate <pred-name>
        ^1 <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^<pred-type> <pred-name>)
-->
   (<p> ^satisfied true)
}

sp {action*elaborate*desired*relation*predicate*satisfied
   (state <s> ^problem-space.name action
              ^desired.predicate <p>
              ^world.predicates <preds>)
   (<p> ^type relation
        ^handle <rel-handle>
        ^1 <obj1>
        ^2 <obj2>)
   (<preds> ^predicate <pred>)
  (<pred> ^handle <rel-handle>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<p> ^satisfied true)
}

sp {action*elaborate*predicate*not*satisfied
   (state <s> ^problem-space.name action
              ^desired.predicate <p>)
   (<p> -^satisfied true)
-->
   (<p> ^satisfied false)
}

sp {action*elaborate*desired*satisfied
   (state <s> ^problem-space.name action
              ^desired <d>)
   (<d> ^predicate.satisfied true
       -^predicate.satisfied false)
-->
   (<d> ^satisfied true)
}
