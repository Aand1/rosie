sp {push-action*elaborate*action-stack
   (state <s> ^name push-action
              ^superstate.action-stack <stack>)
-->
   (<s> ^action-stack <stack>)
}

sp {push-action*elaborate*action-to-push
   (state <s> ^name push-action
              ^superstate.operator.action <action>)
-->
   (<s> ^action-to-push <action>)
}

sp {push-action*elaborate*smem-retrieve*action-handle
   (state <s> ^name push-action
              ^action-to-push.execution-operator.action-handle <handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue.handle <handle>
        ^depth 8)
}

sp {push-action*elaborate*action-id*from*smem-query
   (state <s> ^name push-action
              ^smem-query <q>
              ^action-to-push.execution-operator.action-handle <handle>)
   (<q> ^cue.handle <handle>
        ^result <res>)
-->
   (<s> ^action-id <res>)
}

sp {push-action*elaborate*smem-query*predicate-handle
   (state <s> ^name push-action
              ^action-to-push.execution-operator.<arg-name> <arg>)
   (<arg> ^arg-type predicate
          ^handle <handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue.handle <handle>
        ^depth 4)
}

sp {push-action*elaborate*predicate-lti*from*smem-query
   (state <s> ^name push-action
              ^action-to-push.execution-operator.<arg-name> <arg>
              ^smem-query.result <res>)
   (<arg> ^arg-type predicate
          ^handle <handle>)
   (<res> ^handle <handle>)
-->
   (<s> ^predicate-lti <res>)
}