
# Add real objects/relations to world if not there

sp {push-action*propose*add-argument-to-world*object*depth1
   (state <s> ^name push-action
              ^action-to-push.execution-operator.<arg-name> <arg>
              ^action-id.procedural.<arg-name> <parg>
              ^top-state.world.objects <objs>)
   (<parg> ^object-type concrete)
   (<arg> ^arg-type << object location >>
          ^id <obj>)
   (<objs> -^object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-world
        ^arg-type object
        ^id <obj>)
}

sp {push-action*propose*add-argument-to-world*object*depth2
   (state <s> ^name push-action
              ^action-to-push.execution-operator.<arg-name> <arg>
              ^action-id.procedural.<arg-name> <parg>
              ^top-state.world.objects <objs>)
   (<parg> ^arg-status << visible imagined >>)
   (<arg> ^arg-type predicate
          ^{ << 1 2 3 4 5 >> } <obj>)
   (<objs> -^object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-world
        ^arg-type object
        ^id <obj>)
}

sp {push-action*propose*add-argument-to-world*predicate
   (state <s> ^name push-action
              ^action-to-push.execution-operator <ex-op>
              ^action-id.procedural.<arg-name2> <parg>
              ^top-state.world.predicates.predicate <ts-pred>
              ^predicate-lti <pred-lti>)
   (<ex-op> ^<arg-name1> <obj-arg>
            ^<arg-name2> <pred-arg>)
   (<pred-arg> ^arg-type predicate
              ^handle <p-handle>
              ^2 <obj2>)
   (<obj-arg> ^arg-type object
              ^id <obj1>)
   (<parg> ^arg-status << visible imagined >>)
   (<ts-pred> ^handle <p-handle>)
   -{(<ts-pred> ^instance <i>)
     (<i> ^1 <obj1> ^2 <obj2>)}
   (<pred-lti> ^handle <p-handle>
              -^spatial-relation)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-world
        ^arg-type predicate
        ^predicate-handle <p-handle>
        ^1 <obj1>
        ^2 <obj2>)
}

sp {push-action*propose*add-argument-to-world*predicate*spatial-relation
   (state <s> ^name push-action
              ^action-to-push.execution-operator <ex-op>
              ^action-id.procedural.<arg-name2> <parg>
              ^top-state.world.predicates.predicate <ts-pred>
              ^predicate-lti <pred-lti>)
   (<ex-op> ^<arg-name1> <obj-arg>
            ^<arg-name2> <pred-arg>)
   (<pred-arg> ^arg-type predicate
              ^handle <p-handle>
              ^2 <obj2>)
   (<obj-arg> ^arg-type object
              ^id <obj1>)
   (<parg> ^arg-status << visible imagined >>)
   (<ts-pred> ^handle <rel-handle>)
   -{(<ts-pred> ^instance <i>)
     (<i> ^1 <obj1> ^2 <obj2>)}
   (<pred-lti> ^handle <p-handle>
               ^spatial-relation.handle <rel-handle>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-world
        ^arg-type predicate
        ^predicate-handle <rel-handle>
        ^1 <obj1>
        ^2 <obj2>)
}

sp {push-action*apply*add-argument-to-world*object
   (state <s> ^name push-action
              ^operator <o>
              ^top-state.world.objects <objs>)
   (<o> ^name add-argument-to-world
        ^arg-type object
        ^id <obj>)
-->
   (<objs> ^object <obj>)
}

sp {push-action*apply*add-argument-to-world*predicate
   (state <s> ^name push-action
              ^operator <o>
              ^top-state.world.predicates.predicate <ts-pred>)
   (<o> ^name add-argument-to-world
        ^arg-type predicate
        ^predicate-handle <pred-handle>
        ^1 <obj1>
        ^2 <obj2>)
   (<ts-pred> ^handle <pred-handle>)
-->
   (<ts-pred> ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
}
              