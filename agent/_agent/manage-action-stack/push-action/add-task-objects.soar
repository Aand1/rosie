sp {push-action*propose*add-task-objects
  (state <s> ^name push-action
             ^action-to-push <action>)
  (<action> -^task-objects none
            -^task-objects.object)
-->
  (<s> ^operator <o> + >)
  (<o> ^name add-task-objects)
}

sp {push-action*elaborate*add-task-objects*object
  (state <s> ^name push-action
             ^operator <o> +
             ^action-to-push.execution-operator.<arg-name> <arg>)
  (<o> ^name add-task-objects)
  (<arg> ^arg-type object
         ^id <obj>)
-->
  (<o> ^object <obj>)
}

sp {push-action*elaborate*add-task-objects*object*from*predicate
   (state <s> ^name push-action
              ^operator <o> +
              ^action-to-push.execution-operator.<arg-name> <arg>)
   (<o> ^name add-task-objects)
   (<arg> ^arg-type predicate
          ^2 <obj>)
-->
   (<o> ^object <obj>)
}

sp {push-action*elaborate*add-taks-objects*object*from*until-clause
   (state <s> ^name push-action
              ^operator <o> +
              ^action-to-push.execution-operator.until-clause <until>)
   (<o> ^name add-task-objects)
   (<until> ^{ <num> << 1 2 >> } <obj>)
-->
   (<o> ^object <obj>)
}

sp {push-action*elaborate*add-task-objects*has-objects*true
  (state <s> ^name push-action
             ^operator <o> +)
  (<o> ^name add-task-objects
       ^object <obj>)
-->
  (<o> ^has-objects true)
}

sp {push-action*apply*add-task-objects*no*objects
  (state <s> ^name push-action
             ^operator <o>
             ^action-to-push <action>)
  (<o> ^name add-task-objects
       -^has-objects)
-->
  (<action> ^task-objects none)
}

sp {push-action*apply*add-task-objects*create*task-objects
  (state <s> ^name push-action
             ^operator <o>
             ^action-to-push <action>)
  (<o> ^name add-task-objects
       ^has-objects true)
-->
  (<action> ^task-objects <objs>)
}

sp {push-action*apply*add-task-objects*copy*object
  (state <s> ^name push-action
             ^operator <o>
             ^action-to-push.task-objects <objs>)
  (<o> ^name add-task-objects
       ^object <obj>)
-->
  (<objs> ^object <obj>)
}

