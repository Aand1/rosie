# Elaborate bottom action onto set of actions
sp {top-state*elaborate*actions*bottom*action
   (state <s> ^superstate nil
              ^action-stack <stack>)
   (<stack> ^bottom { <action> <> none }
            ^actions <a>)
-->
   (<a> ^action <action>)
}

# Elaborate other actions onto the set
sp {top-state*elaborate*actions*next*action
   (state <s> ^superstate nil
              ^action-stack.actions <a>)
   (<a> ^action <action>)
   (<action> ^next { <next> <> none })
-->
   (<a> ^action <next>)
}

# Elaborate prev links between actions
sp {top-state*elaborate*action-stack*prev*links
   (state <s> ^superstate nil
              ^action-stack.actions.action <a>)
   (<a> ^next { <next> <> none })
-->
   (<next> ^prev <a>)
}

# Elaborate prev link on bottom action
sp {top-state*elaborate*action-stack*bottom*prev
   (state <s> ^superstate nil
              ^action-stack.bottom <bot>)
-->
   (<bot> ^prev none)
}

sp {top-state*elaborate*action-stack*object-set*object*argument*world
   (state <s> ^superstate nil
              ^action-stack.object-set <obj-set>
              ^world.objects.object <obj>)
   (<obj> ^predicates.category object)
-->
   (<obj-set> ^object <obj>)
}

sp {top-state*elaborate*action-stack*object-set*object*argument
   (state <s> ^superstate nil
              ^action-stack <stack>)
   (<stack> ^actions.action.execution-operator.<arg-name> <arg>
            ^object-set <obj-set>)
   (<arg> ^arg-type object
          ^id <obj>)
-->
   (<obj-set> ^object <obj>)
}

sp {top-state*elaborate*action-stack*object-set*predicate*argument
   (state <s> ^superstate nil
              ^action-stack <stack>)
   (<stack> ^actions.action.execution-operator.<arg-name> <arg>
            ^object-set <obj-set>)
   (<arg> ^arg-type predicate
          ^2 <obj>)
-->
   (<obj-set> ^object <obj>)
}

sp {top-state*elaborate*action-stack*object-set*until-clause*argument
   (state <s> ^superstate nil
              ^action-stack <stack>)
   (<stack> ^actions.action.execution-operator.until-clause <until>
            ^object-set <obj-set>)
   (<until> ^{ <num> << 1 2 >> } <obj>)
-->
   (<obj-set> ^object <obj>)
}
