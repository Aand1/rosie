sp {top-state*elaborate*actions*bottom*action
   (state <s> ^superstate nil
              ^action-stack <stack>)
   (<stack> ^bottom { <action> <> none }
            ^actions <a>)
-->
   (<a> ^action <action>)
}

sp {top-state*elaborate*actions*next*action
   (state <s> ^superstate nil
              ^action-stack.actions <a>)
   (<a> ^action <action>)
   (<action> ^next { <next> <> none })
-->
   (<a> ^action <next>)
}

sp {top-state*elaborate*action-stack*prev*links
   (state <s> ^superstate nil
              ^action-stack.actions.action <a>)
   (<a> ^next { <next> <> none })
-->
   (<next> ^prev <a>)
}

sp {top-state*elaborate*action-stack*bottom*prev
   (state <s> ^superstate nil
              ^action-stack.bottom <bot>)
-->
   (<bot> ^prev none)
}
