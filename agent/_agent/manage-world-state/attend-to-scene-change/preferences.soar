# Reject an attend-to-scene-change operator if attending to an object being moved

#sp {anystate*reject*attend-to-scene-change*moving*object
#   (state <s> ^operator <o> +)
#   (<o> ^name attend-to-scene-change
#        ^change.object-info.wm-obj.predicates.arm-status << moving grabbed >>
#        ^change.type <> changed-object-status)
#-->
#   (<s> ^operator <o> -)
#}



sp {anystate*reject*delete-object*task*object
   (state <s> ^operator <o> +
              ^top-state.action-stack.object-set.object <obj>)
   (<o> ^name attend-to-scene-change
        ^change.type << missing-object delete-object >>
        ^change.object-info.wm-obj <obj>)
-->
   (<s> ^operator <o> -)
}

# Ordering of scene change operators

sp {anystate*attend-to-scene-change*prefer*higher*preference-order
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change
         ^preference-order <pref1>)
   (<o2> ^name attend-to-scene-change
         ^preference-order < <pref1>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {anystate*attend-to-scene-change*prefer*equal*same*preference-order
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change
         ^preference-order <pref>)
   (<o2> ^name attend-to-scene-change
         ^preference-order <pref>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {anystate*attend-to-scene-change*elaborate*default*preference-order
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
       -^preference-order > 0)
-->
   (<o> ^preference-order 0)
}


# Preference Orders:

# 200 update-pose
# 200 delete-object 
# 200 create-belief-object
# 190 new-object
# 180 changed-waypoint 
# 160 changed-moving-state
# 120 moved-object
# 100 newly-visible-object
# 100 newly-not-visible-object
# 100 new-object-predicate
#  80 missing-object

# 200 update-pose
# 200 delete-object 
sp {anystate*scene-change*attend*preference*200
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << update-pose delete-object create-belief-object >>)
-->
   (<o> ^preference-order 200)
}

# 190 new-object
sp {anystate*scene-change*attend*preference*190
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << new-object >>)
-->
   (<o> ^preference-order 190)
}

# 180 attend-to-changed-waypoint 
sp {anystate*scene-change*attend*preference*180
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << changed-waypoint >>)
-->
   (<o> ^preference-order 180)
}

# 160 changed-moving-state
sp {anystate*scene-change*attend*preference*160
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << changed-moving-state >>)
-->
   (<o> ^preference-order 160)
}

# 120 moved-object
sp {anystate*scene-change*attend*preference*120
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << moved-object >>)
-->
   (<o> ^preference-order 120)
}

# 100 newly-visible-object
# 100 newly-not-visible-object
# 100 new-object-predicate
sp {anystate*scene-change*attend*preference*100
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << newly-visible-object newly-not-visible-object new-object-predicate >>)
-->
   (<o> ^preference-order 100)
}

#  80 missing-object
sp {anystate*scene-change*attend*preference*80
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << missing-object >>)
-->
   (<o> ^preference-order 80)
}
