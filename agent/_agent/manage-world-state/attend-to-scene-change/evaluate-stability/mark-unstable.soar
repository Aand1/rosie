sp {evaluate-stability*propose*mark-unstable
   (state <s> ^name evaluate-stability
              ^obj-is-stable false
              ^object-info <obj-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-unstable
        ^object-info <obj-info>)
}

sp {evaluate-stability*apply*mark-unstable*reset*timers
   (state <s> ^name evaluate-stability
              ^operator <o>
              ^io.input-link.time.seconds <cur-t>
              ^top-state.agent-params.stability-check-duration <dur>)
   (<o> ^name mark-unstable
        ^object-info <obj-info>)
   (<obj-info> ^stability-check-time <old-t>
               ^number-checks <num>)
-->
   (<obj-info> ^stability-check-time <old-t> -
               ^stability-check-time (+ <cur-t> <dur>)
               ^number-checks <num> -
               ^number-checks (+ <num> 1))
}

sp {evaluate-stability*apply*mark-unstable*remove*filters
   (state <s> ^name evaluate-stability
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name mark-unstable
         ^object-info <obj-info>)
   (<obj-info> ^perception-obj.id <id>
               ^position-monitor <old-pos>
               ^volume-monitor <old-vol>)
-->
   (<cmd> ^extract <old-pos> -
          ^extract <old-vol> -)
   (<obj-info> ^position-monitor <old-pos> -
               ^volume-monitor <old-vol> -)   
}

sp {evaluate-stability*apply*mark-unstable*reset*filters
   (state <s> ^name evaluate-stability
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name mark-unstable
        ^object-info <obj-info>)
   (<obj-info> ^perception-obj.id <id>)
-->
   (<cmd> ^extract <pos>)
   (<pos> ^type monitor_position
          ^a <pa>)
   (<pa> ^type node
         ^id <id>)
   (<obj-info> ^position-monitor <pos>)
   
   (<cmd> ^extract <vol>)
   (<vol> ^type monitor_volume
          ^a <va>)
   (<va> ^type node
         ^id <id>)
   (<obj-info> ^volume-monitor <vol>)
}
