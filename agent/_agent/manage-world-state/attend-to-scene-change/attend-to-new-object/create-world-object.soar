sp {attend-to-new-object*propose*create-world-object
   (state <s> ^name attend-to-new-object
             -^wm-obj)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-world-object)
}

# Create the bare version as world-obj
sp {attend-to-new-object*apply*create-world-object*create*world-obj
   (state <s> ^name attend-to-new-object
              ^operator.name create-world-object
              ^object-handle <handle>)
-->
   (<s> ^world-obj <obj>)
   (<obj> ^handle <handle>
          ^item-type object
          ^predicates <preds>)
}

# Copy world-obj as wm-obj (will retract operator)
sp {attend-to-new-object*apply*create-world-object*copy*as*wm-obj
   (state <s> ^name attend-to-new-object
              ^operator.name create-world-object
              ^world-obj <obj>)
-->
   (<s> ^wm-obj <obj>)
}

# Copy predicates onto object
sp {attend-to-new-object*apply*create-world-object*copy*predicates*level2
   (state <s> ^name attend-to-new-object
              ^operator.name create-world-object
              ^world-obj.predicates <preds>
              ^predicates.{ <prop> <> from-perception <> from-smem <> from-epmem } <pred>)
-->
   (<preds> ^<prop> <pred>)
}

# Copy properties onto the object from smem
sp {attend-to-new-object*apply*create-world-object*copy*predicates*level1
   (state <s> ^name attend-to-new-object
              ^operator.name create-world-object
              ^smem-obj.<prop> <pred>
              ^world-obj <obj>
              ^top-state.agent-params.object-rep-info.copy-level1 <prop>)
-->
   (<obj> ^<prop> <pred>)
}

# Mark the object as in-smem
sp {attend-to-new-object*apply*create-world-object*mark*in-smem
   (state <s> ^name attend-to-new-object
              ^operator.name create-world-object
              ^smem-obj <smem>
              ^world-obj <obj>)
-->
   (<obj> ^in-smem true)
}

#### Visibility/object-status 

# mark confirmed
sp {attend-to-new-object*apply*create-world-object*mark*object-status*confirmed
   (state <s> ^name attend-to-new-object
              ^operator.name create-world-object
              ^visible true
              ^world-obj.predicates <preds>)
-->
   (<preds> ^object-status confirmed
            ^object-status unconfirmed -
            ^visible true
            ^visible false -)
}

# mark unconfirmed
sp {attend-to-new-object*apply*create-world-object*mark*object-status*unconfirmed
   (state <s> ^name attend-to-new-object
              ^operator.name create-world-object
              ^visible false
              ^world-obj.predicates <preds>)
-->
   (<preds> ^object-status unconfirmed
            ^object-status confirmed -
            ^visible false
            ^visible true -)
}

