# If the perception-obj matches an existing world object, 
#   simply update the perception-id on that object
sp {attend-to-new-object*propose*link-to-existing-object*matches-world-object
   (state <s> ^name attend-to-new-object
              ^source-type perception-obj
              ^world-match-result { <obj-info> <> none })
-->
   (<s> ^operator <o> +)
   (<o> ^name link-to-existing-object
        ^object <obj-info>)
}

# If the perception-obj matches a smem object in the world that is not visible, 
#    update the perception-id on that object
sp {attend-to-new-object*propose*link-to-existing-object*matches*smem-obj
   (state <s> ^name attend-to-new-object
              ^source-type perception-obj
              ^smem-obj.handle <handle>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^object-handle <handle>
               ^status.visible false)
-->
   (<s> ^operator <o> + >)
   (<o> ^name link-to-existing-object
        ^object <obj-info>)
}

# Change the perception-id on the object-info
sp {attend-to-new-object*apply*link-to-existing-object*change*perception-id
   (state <s> ^name attend-to-new-object
              ^operator <o>
              ^perception-id <id>)
   (<o> ^name link-to-existing-object
        ^object <obj-info>)
   (<obj-info> ^perception-id { <old-id> <> <id> } )
-->
   (<obj-info> ^perception-id <old-id> -
               ^perception-id <id>)
}
   
   