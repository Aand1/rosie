sp {match-world-object*elaborate*candidate-set
   (state <s> ^name match-world-object)
-->
   (<s> ^candidate-set <set>)
}

sp {match-world-object*elaborate*candidate-set*candidate
   (state <s> ^name match-world-object
              ^candidate-set <set>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> -^perception-obj)
-->
   (<set> ^candidate <cand>)
   (<cand> ^object-info <obj-info>)
}

sp {match-world-object*elaborate*predicate-conflict*1
   (state <s> ^name match-world-object
              ^candidate-set.candidate <cand>
              ^input-link-obj.classifications <cfs>)
   (<cand> ^object-info.wm-obj.predicates <preds>)
   (<cfs> ^<prop> <pred>)
   (<preds> ^<prop> <> <pred>)
-->
   (<cand> ^predicate-conflict true)
}

sp {match-world-object*elaborate*predicate-conflict*2
   (state <s> ^name match-world-object
              ^candidate-set.candidate <cand>
              ^input-link-obj.classifications <cfs>)
   (<cand> ^object-info.wm-obj.predicates <preds>)
   (<preds> ^<prop> <pred>)
   (<cfs> ^<prop> <> <pred>)
-->
   (<cand> ^predicate-conflict true)
}

sp {match-world-object*elaborate*type-conflict
   (state <s> ^name match-world-object
              ^candidate-set.candidate <cand>)
   (<cand> ^object-info.wm-obj.item-type <> object)
-->
   (<cand> ^type-conflict true)
}
