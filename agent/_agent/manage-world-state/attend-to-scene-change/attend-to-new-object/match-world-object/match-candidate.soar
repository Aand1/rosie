sp {match-world-object*propose*match-candidate
   (state <s> ^name match-world-object
              ^candidate-set.candidate <cand>)
   (<cand> -^predicate-conflict true
           -^type-conflict true
            ^distance <d>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name match-candidate
        ^candidate <cand>)
}

sp {match-world-object*reject*match-candidate*too*far
   (state <s> ^name match-world-object
              ^operator <o> +)
   (<o> ^name match-candidate
         ^candidate.distance > .1)
-->
   (<s> ^operator <o> -)
}

sp {match-world-object*match-candidate*better*prefrence*lower*distance
   (state <s> ^name match-world-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name match-candidate
         ^candidate.distance <d>)
   (<o2> ^name match-candidate
         ^candidate.distance > <d>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {match-world-object*apply*match-candidate*report*to*superstate
   (state <s> ^name match-world-object
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name match-candidate
        ^candidate.object-info <obj-info>)
-->
   (<ss> ^matches-world-object <obj-info>)
}