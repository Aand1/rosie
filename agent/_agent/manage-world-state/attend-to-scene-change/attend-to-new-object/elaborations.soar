sp {attend-to-new-object*elaborate*objects*from*superoperator
   (state <s> ^name attend-to-new-object
              ^superstate.operator.{ <type> << perception-obj smem-obj epmem-obj imagined-obj >> } <obj>)
-->
   (<s> ^<type> <obj>)
}

sp {attend-to-new-object*elaborate*input-link-obj*matching*perception-obj
   (state <s> ^name attend-to-new-object
              ^perception-obj.id <obj-handle>
              ^top-state.io.input-link.objects.object <obj>)
   (<obj> ^handle <obj-handle>)
-->
   (<s> ^input-link-obj <obj>)
}

sp {attend-to-new-object*elaborate*world-obj*from*imagined-obj
   (state <s> ^name attend-to-new-object
              ^imagined-obj <obj>)
-->
   (<s> ^world-obj <obj>
        ^created-world true)
}
