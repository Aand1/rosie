# We matches a new perception-obj to an existing world object
# We tell the perception system to link the perception object to a different id
# This should cause the substate to retract

sp {update-existing-object*propose*link-perception-object
   (state <s> ^name update-existing-object
              ^source-type perception-obj
              ^source-obj.id <perc-id>
              ^existing-obj.perception-id <obj-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name link-perception-object
        ^perception-obj-id <perc-id>
        ^existing-obj-id <obj-id>)
}

sp {update-existing-object*apply*link-perception-object
   (state <s> ^name update-existing-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-perception-object
        ^perception-obj-id <perc-id>
        ^existing-obj-id <obj-id>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type link
         ^source-handle <perc-id>
         ^destination-handle <obj-id>)
}

sp {update-existing-object*apply*link-perception-object*monitor*perception
   (state <s> ^name update-existing-object
              ^operator <o>
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<o> ^name link-perception-object
        ^perception-obj-id <perc-id>
        ^existing-obj-id <obj-id>)
-->
   (write |Linking object | <perc-id> | to | <obj-id>)
}
        
