##################################################################
#
# Monitoring for need to copy an object
#
##################################################################

sp {anystate*apply*attend-to-scene-change*update-object*svs*copy_node
   (state <s> ^operator <o> +
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-scene-change
        ^change.type update-object
        ^change.object-info <obj-info>)
   (<obj-info> ^perception-obj.id <perc-id>
               ^belief-obj.id <bel-id>)
-->
   (<cmd> ^copy_transform <copy>)
   (<copy> ^source <perc-id>
           ^destination <bel-id>
           ^position yes
           ^rotation yes
           ^scale yes
           ^adjust true)
   (<obj-info> ^copy-command <copy>)
}

sp {anystate*apply*attend-to-scene-change*update-object*cleanup
   (state <s> ^operator <o> +
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-scene-change
        ^change.type update-object
        ^change.object-info <obj-info>)
   (<obj-info> ^mark-for-copying true
               ^copy-command <copy>)
   (<copy> ^status success)
-->
   (<obj-info> ^mark-for-copying true -
               ^copy-command <copy> -)
}
   
sp {anystate*apply*attend-to-scene-change*update-object*report
   (state <s> ^operator <o> +
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<o> ^name attend-to-scene-change
        ^change.type update-object
        ^change.object-info.object-handle <obj-handle>)
-->
   (write |Updating Object | <obj-handle>)
}
   
