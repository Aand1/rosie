sp {delete-objet*propose*retrieve-smem-object*from*epmem
   (state <s> ^name delete-objet
              ^object-handle <obj-handle>
              ^world-obj.in-smem true
             -^smem-obj)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
   (<cue> ^handle <obj-handle>)
}

sp {delete-objet*apply*retrieve-smem-object*create*query
   (state <s> ^name delete-objet
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
-->
   (<cmd> ^query <cue>)
}

sp {delete-objet*apply*retrieve-smem-object*success*copy*object
   (state <s> ^name delete-objet
              ^operator <o>
              ^smem.result <res>
              ^smem.command <cmd>)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
   (<res> ^success <cue>
          ^retrieved <obj-lti>)
-->
   (<s> ^smem-obj <obj-lti>)
   (<cmd> ^query <cue> -)
}
        
sp {delete-objet*apply*retrieve-smem-object*failure*copy*object
   (state <s> ^name delete-objet
              ^operator <o>
              ^smem.result.failure <cue>
              ^smem.command <cmd>)
   (<o> ^name retrieve-smem-object
        ^cue <cue>)
-->
   (<s> ^smem-obj none)
   (<cmd> ^query <cue> -)
}