sp {delete-object*propose*update-smem-object
   (state <s> ^name delete-object
              ^smem-obj <> none
             -^updated-smem-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name update-smem-object)
}

sp {delete-object*apply*update-smem-object*store*smem
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem.command <cmd>
              ^smem-obj <obj>)
-->
   (<cmd> ^store <obj>)
}

sp {delete-object*apply*update-smem-object*mark*updated
   (state <s> ^name delete-object
              ^operator.name update-smem-object)
-->
   (<s> ^updated-smem-object true)
}

sp {delete-object*apply*update-smem-object*add*level1*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
              ^world-obj.{ <prop> << waypoint >> } <pred>)
-->
   (<obj> ^<prop> <pred>)
}

sp {delete-object*apply*update-smem-object*add*level2*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
              ^world-obj.predicates.{ <prop> <> object-status <> arm-status <> visibility } <pred>)
-->
   (<obj> ^<prop> <pred>)
}

sp {delete-object*apply*update-smem-object*remove*level1*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
             -^world-obj.<prop> <pred>)
   (<obj> ^{ <prop> << waypoint >> } <pred>)
-->
   (<obj> ^<prop> <pred> -)
}

sp {delete-object*apply*update-smem-object*remove*level2*predicates
   (state <s> ^name delete-object
              ^operator.name update-smem-object
              ^smem-obj <obj>
             -^world-obj.predicates.<prop> <pred>)
   (<obj> ^{ <prop> <> handle <> item-type <> object-instance <> waypoint } <pred>)
-->
   (<obj> ^<prop> <pred> -)
}
        