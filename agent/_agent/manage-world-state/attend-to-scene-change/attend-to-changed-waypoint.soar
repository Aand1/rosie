# Updating world.robot.current-waypoint
sp {anystate*apply*attend-to-scene-change*changed-waypoint*add*new
   (state <s> ^operator <o>
              ^top-state.world.robot <robot>
              ^top-state.maps.map.waypoint <wp>)
   (<o> ^name attend-to-scene-change
        ^change.type changed-waypoint
        ^change.new-waypoint-handle <wp-handle>)
   (<wp> ^handle <wp-handle>)
-->
   (<robot> ^current-waypoint <wp>)
}
   
sp {anystate*apply*attend-to-scene-change*changed-waypoint*remove*old
   (state <s> ^operator <o>
              ^top-state.world.robot <robot>
              ^top-state.maps.map.waypoint <wp>)
   (<o> ^name attend-to-scene-change
        ^change.type changed-waypoint
        ^change.new-waypoint-handle <wp-handle>)
   (<wp> ^handle <wp-handle>)
   (<robot> ^current-waypoint { <old-wp> <> <wp> })
-->
   (<robot> ^current-waypoint <old-wp> -)
}

sp {anystate*apply*attend-to-scene-change*monitor*changed-waypoint
   (state <s> ^operator <o>
              ^top-state.agent-params.debug-mode on)
   (<o> ^name attend-to-scene-change
        ^change.type changed-waypoint
        ^change.new-waypoint-handle <wp-handle>)
-->
   (write |current-waypoint: | <wp-handle> (crlf))
}
              