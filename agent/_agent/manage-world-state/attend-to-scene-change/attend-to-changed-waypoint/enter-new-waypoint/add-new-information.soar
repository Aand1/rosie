sp {enter-new-waypoint*propose*add-new-information
   (state <s> ^name enter-new-waypoint)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-new-information)
}

sp {enter-new-waypoint*apply*add-new-information*current-waypoint
   (state <s> ^name enter-new-waypoint
              ^operator.name add-new-information
              ^new-waypoint <wp>
              ^top-state.world.robot <rob>)
-->
   (<rob> ^current-waypoint <wp>)
}

sp {enter-new-waypoint*apply*add-new-information*current-location
   (state <s> ^name enter-new-waypoint
              ^operator.name add-new-information
              ^new-location { <loc> <> none }
              ^top-state.world.robot <rob>)
-->
   (<rob> ^current-location <loc>)
}

sp {enter-new-waypoint*apply*add-new-information*havent-scanned
   (state <s> ^name enter-new-waypoint
              ^operator.name add-new-information
              ^new-location { <loc> <> none }
              ^top-state.world.robot <rob>)
-->
   (<rob> ^havent-scanned <loc>)
}

sp {enter-new-waypoint*apply*add-new-information*add*epmem-obj
   (state <s> ^name enter-new-waypoint
              ^operator.name add-new-information
              ^episode-result.world.objects.object <obj>
              ^episode-result.world.predicates.predicate <in-pred>
              ^new-location.handle <loc-handle>
             -^top-state.world.objects.object.handle <obj-handle>
              ^top-state.perception-monitor.changes <chgs>)
   (<obj> ^handle <obj-handle>
          ^predicates.object-status confirmed)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^{2}.handle <loc-handle>)
-->
   (<chgs> ^change <c>)
   (<c> ^type new-object
        ^epmem-obj <obj>)
}

sp {enter-new-waypoint*add*in*predicates*for*visible*objects
   (state <s> ^name enter-new-waypoint
              ^operator.name add-new-information
              ^new-location { <loc> <> none }
              ^top-state.world.predicates.predicate <in-pred>
              ^top-state.world.objects.object <obj>)
   (<in-pred> ^handle in1)
   (<obj> ^predicates.visible true)
   -{(<in-pred> ^instance <curi>)
     (<cur-i> ^1 <obj> ^2 <loc>)}
-->
   (<in-pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

sp {enter-new-waypoint*apply*add-new-information*set*location*visible
   (state <s> ^name enter-new-waypoint
              ^operator.name add-new-information
              ^new-location.predicates <preds>)
-->
   (<preds> ^visible true 
            ^visible false -
            ^object-status confirmed
            ^object-status unconfirmed -)
}
