sp {enter-new-waypoint*propose*retrieve-previous-episode
   (state <s> ^name enter-new-waypoint
              ^new-location <loc>
             -^episode-result)
   (<loc> ^handle <h>)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-previous-episode
        ^location <loc>)
}

sp {enter-new-waypoint*apply*retrieve-previous-episode*query
   (state <s> ^name enter-new-waypoint
              ^operator <o>
              ^epmem.command <cmd>)
   (<o> ^name retrieve-previous-episode
        ^location.handle <loc-handle>)
-->
   (<cmd> ^query <q>)
   (<q> ^world.robot.current-location.handle <loc-handle>)
}

sp {enter-new-waypoint*apply*retrieve-previous-episode*success*copy*result
   (state <s> ^name enter-new-waypoint
              ^operator.name retrieve-previous-episode
              ^epmem.result <res>)
   (<res> ^success <q>
          ^retrieved <episode>
          ^normalized-match-score 1.0)
-->
   (<s> ^episode-result <episode>)
}

sp {enter-new-waypoint*apply*retrieve-previous-episode*failure*copy*result
   (state <s> ^name enter-new-waypoint
              ^operator.name retrieve-previous-episode
              ^epmem.result.failure <q>)
-->
   (<s> ^episode-result failure)
}

sp {enter-new-waypoint*apply*retrieve-previous-episode*failure*copy*result*bad*match
   (state <s> ^name enter-new-waypoint
              ^operator.name retrieve-previous-episode
              ^epmem.result <res>)
   (<res> ^success <q>
          ^normalized-match-score < 1.0)
-->
   (<s> ^episode-result failure)
}