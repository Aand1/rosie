sp {leave-old-waypoint*elaborate*old-waypoint
   (state <s> ^name leave-old-waypoint
              ^superstate.operator.old-waypoint <wp>)
-->
   (<s> ^old-waypoint <wp>)
}

sp {leave-old-waypoint*elaborate*old-location
   (state <s> ^name leave-old-waypoint
              ^top-state.world.robot.current-location <old-loc>)
-->
   (<s> ^old-location <old-loc>)
}

# An unconfirmed object not relevant to the current task
sp {leave-old-waypoint*elaborate*unconfirmed-object
   (state <s> ^name leave-old-waypoint
              ^top-state.world.objects.object <obj>
             -^top-state.action-stack.object-set.object <obj>)
   (<obj> ^predicates.object-status unconfirmed)
-->
   (<s> ^unconfirmed-object <obj>)
}

sp {leave-old-waypoint*elaborate*unconfirmed-objects*true
   (state <s> ^name leave-waypoint
              ^unconfirmed-object <any>)
-->
   (<s> ^unconfirmed-objects true)
}

sp {leave-old-waypoint*elaborate*confirmed-objects
   (state <s> ^name leave-old-waypoint
              ^top-state.world.objects.object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^object-status confirmed)
-->
   (<s> ^confirmed-object <obj>)
}

sp {leave-old-waypoint*elaborate*svs*filter*object*info
   (state <s> ^name leave-old-waypoint
              ^confirmed-object.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
-->
   (<cmd> ^extract_once <pos> <rot> <scale>)
   (<pos> ^type node_position
          ^a <node>)
   (<rot> ^type node_rotation
          ^a <node>)
   (<scale> ^type node_scale
            ^a <node>)
   (<node> ^type node
           ^id <id>)
   (<s> ^position-filter <pos>
        ^rotation-filter <rot>
        ^scale-filter <scale>)
}

sp {leave-old-waypoint*elaborate*object-info-available
   (state <s> ^name leave-old-waypoint
              ^position-filter.result.record <any>)
-->
   (<s> ^object-info-available true)
}

sp {leave-old-waypoint*elaborate*waiting-for-filters*true
   (state <s> ^name leave-old-waypoint
              ^{ << position-filter rotation-filter scale-filter >> } <any>)
   (<any> -^result)
-->
   (<s> ^waiting-for-filters true)
}
              