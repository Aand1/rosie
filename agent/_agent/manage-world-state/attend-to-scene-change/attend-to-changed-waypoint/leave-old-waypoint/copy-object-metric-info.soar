sp {leave-old-waypoint*propose*copy-object-metric-info
   (state <s> ^name leave-old-waypoint
              ^object-info-available true)
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-object-metric-info)
}

sp {leave-old-waypoint*apply*copy-object-metric-info*copy*position
   (state <s> ^name leave-old-waypoint
              ^operator.name copy-object-metric-info
              ^confirmed-object <obj>
              ^position-filter.result.record <r>)
   (<r> ^params.a <id> ^value <pos>)
   (<obj> ^svs-obj.id <id>)
-->
   (<obj> ^position <pos>)
}

sp {leave-old-waypoint*apply*copy-object-metric-info*copy*rotation
   (state <s> ^name leave-old-waypoint
              ^operator.name copy-object-metric-info
              ^confirmed-object <obj>
              ^rotation-filter.result.record <r>)
   (<r> ^params.a <id> ^value <rot>)
   (<obj> ^svs-obj.id <id>)
-->
   (<obj> ^rotation <rot>)
}

sp {leave-old-waypoint*apply*copy-object-metric-info*copy*scale
   (state <s> ^name leave-old-waypoint
              ^operator.name copy-object-metric-info
              ^confirmed-object <obj>
              ^scale-filter.result.record <r>)
   (<r> ^params.a <id> ^value <scale>)
   (<obj> ^svs-obj.id <id>)
-->
   (<obj> ^scale <scale>)
}
   
sp {leave-old-waypoint*apply*copy-object-metric-info*remove*filters
   (state <s> ^name leave-old-waypoint
              ^operator.name copy-object-metric-info
              ^{ <type> << position-filter rotation-filter scale-filter >> } <f>
              ^top-state.svs.command <cmd>)
   (<cmd> ^extract_once <f>)
-->
   (<cmd> ^extract_once <f> -)
   (<s> ^<type> <f> -)
}
   