sp {attend-to-stale-object*propose*internally-link-objects
   (state <s> ^name attend-to-stale-object
              ^contained-by <src-handle>
              ^object-info <obj-info>
              ^top-state.perception-monitor.object-monitor.object-info <src-obj-info>)
   (<src-obj-info> ^object-handle <src-handle>
                   ^perception-obj.id <src-perc-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name internally-link-objects
        ^category perception
        ^source-handle <src-handle>
        ^source-perception-id <src-perc-id>
        ^dest-object-info <obj-info>)
}

sp {attend-to-stale-object*prefer*internally-link-objects*containment
   (state <s> ^name attend-to-stale-object
              ^operator <o1> +
              ^operator <o2> +
              ^overlap-results.result <r1> <r2>)
   (<o1> ^name internally-link-objects
         ^source-perception-id <id1>)
   (<o2> ^name internally-link-objects
         ^source-perception-id <id2>)
   (<r1> ^object-b <id1>
         ^overlap <overlap>)
   (<r2> ^object-b <id2>
         ^overlap < <overlap>)
-->
   (<s> ^operator <o1> > <o2>)
}
   
sp {attend-to-stale-object*apply*internally-link-objects
   (state <s> ^name attend-to-stale-object
              ^operator <o>)
   (<o> ^name internally-link-objects
        ^source-handle <src-handle>
        ^dest-object-info <obj-info>)
-->
   (<obj-info> ^internal-link <src-handle>)
}

sp {attend-to-stale-object*apply*internally-link-objects*monitor*perception
   (state <s> ^name attend-to-stale-object
              ^operator <o>
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<o> ^name internally-link-objects
        ^dest-object-info.belief-obj.id <dest-id>
        ^source-handle <source-handle>)
-->
   (write |Internally linking object | <dest-id> | to | <source-handle> (crlf))
}
        
