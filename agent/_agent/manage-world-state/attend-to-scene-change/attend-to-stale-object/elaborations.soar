sp {attend-to-stale-object*elaborate*missing-object
   (state <s> ^name attend-to-stale-object
              ^superstate.operator.missing-object true)
-->
   (<s> ^missing-object true)
}

sp {attend-to-stale-object*elaborate*object-info
   (state <s> ^name attend-to-stale-object
              ^superstate.operator.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

#sp {attend-to-stale-object*elaboate*aggressive-match 
#   (state <s> ^name attend-to-stale-object
#              ^superstate.superstate.operator.aggressive-match <m>)
#-->
#   (<s> ^aggressive-match <m>)
#}

sp {attend-to-stale-object*elaborate*agent-params*occlusion-check-period
   (state <s> ^name attend-to-stale-object
              ^top-state.agent-params.occlusion-check-period <p>)
-->
   (<s> ^occlusion-check-period <p>)
}

sp {attend-to-stale-object*elaborate*agent-params*contained-threshold
   (state <s> ^name attend-to-stale-object
              ^top-state.agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

sp {attend-to-stale-object*elaborate*agent-params*contained-by-threshold
   (state <s> ^name attend-to-stale-object
              ^top-state.agent-params.contained-by-threshold <t>)
-->
   (<s> ^contained-by-threshold <t>)
}

sp {attend-to-stale-object*elaborate*overlap-results
   (state <s> ^name attend-to-stale-object)
-->
   (<s> ^overlap-results <res>)
}

sp {attend-to-stale-object*elaborate*overlap-results*object-info*1
   (state <s> ^name attend-to-stale-object
              ^overlap-results.result <res>
              ^object-info <a-info>
              ^top-state.perception-monitor.object-monitor.object-info <b-info>)
   (<res> ^object-a <a-id>
          ^object-b <b-id>)
   (<a-info> ^belief-obj.id <a-id>)
   (<b-info> ^perception-obj.id <b-id>)
-->
   (<res> ^object-a-info <a-info>
          ^object-b-info <b-info>)
}

sp {attend-to-stale-object*elaborate*overlap-results*object-info*2
   (state <s> ^name attend-to-stale-object
              ^overlap-results.result <res>
              ^object-info <b-info>
              ^top-state.perception-monitor.object-monitor.object-info <a-info>)
   (<res> ^object-a <a-id>
          ^object-b <b-id>)
   (<a-info> ^perception-obj.id <a-id>)
   (<b-info> ^belief-obj.id <b-id>)
-->
   (<res> ^object-a-info <a-info>
          ^object-b-info <b-info>)
}

sp {attend-to-stale-object*elaborate*contained-fragment
   (state <s> ^name attend-to-stale-object
              ^object-info.belief-obj.id <stale-id>
              ^overlap-results.result <res>
              ^contained-threshold <t>)
   (<res> ^object-a <a>
          ^object-b <stale-id>
          ^overlap > <t>)
-->
   (<s> ^contained-fragment <a>)
}

sp {attend-to-stale-object*elaborate*contained-by
   (state <s> ^name attend-to-stale-object
              ^object-info.belief-obj.id <stale-id>
              ^overlap-results.result <res>
              ^contained-by-threshold <t>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<res> ^object-a <stale-id>
          ^object-b <b>
          ^overlap > <t>)
   (<obj-info> ^object-handle <obj-handle>
               ^perception-obj.id <b>)
-->
   (<s> ^contained-by <obj-handle>)
}

sp {attend-to-stale-object*elaborate*found-contained-fragment
   (state <s> ^name attend-to-stale-object
              ^contained-fragment <f>)
-->
   (<s> ^found-contained-fragment true)
}
