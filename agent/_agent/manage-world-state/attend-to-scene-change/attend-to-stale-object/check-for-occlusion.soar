# See attend-to-scene-change.svs-command-operators.check-for-occlusion

sp {attend-to-stale-object*apply*mark-for-deletion
   (state <s> ^name attend-to-stale-object
              ^operator <o>)
   (<o> ^name mark-for-deletion
        ^object-info <obj-info>)
-->
   (<obj> ^mark-for-deletion true)
}

# If we have object permanence, we check for occlusion
sp {attend-to-stale-object*propose*check-for-occlusion
   (state <s> ^name attend-to-stale-object
              ^object-info <obj-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name check-for-occlusion
        ^object-info <obj-info>
        ^report-to-superstate true
        ^report-to-top-state false)
}

sp {attend-to-stale-object*apply*check-for-occlusion*not-occluded*mark-for-deletion
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
             -^missing-object
              ^object-occluded false
              ^object-info <obj-info>)
-->
   (<obj-info> ^mark-for-deletion true)
}

# If the object is missing and we need it, ask the user for help
sp {attend-to-stale-object*apply*check-for-occlusion*not-occluded*missing-object*interaction
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
              ^missing-object true
              ^object-occluded false
              ^object-info <obj-info>
              ^top-state.interaction.status <status>)
-->
   (<status> ^failure <f>)
   (<f> ^type missing-object
        ^missing-object <obj-info>)
   (<obj-info> ^searching-for-object true)
}

sp {attend-to-stale-object*apply*check-for-occlusion*occluded*add*info
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
              ^object-occluded true
              ^object-info.differences <diffs>
              ^io.input-link.time.seconds <cur-t>
              ^occlusion-check-period <period>)
-->
   (<diffs> ^is-occluded true
            ^recheck-occlusion-time (+ <cur-t> <period>))
}

sp {attend-to-stale-object*apply*check-for-occlusion*occluded*remove*old*info
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
              ^object-occluded true
              ^object-info.differences <diffs>)
   (<diffs> ^recheck-occlusion-time <t>)
-->
   (<diffs> ^recheck-occlusion-time <t> -)
}

