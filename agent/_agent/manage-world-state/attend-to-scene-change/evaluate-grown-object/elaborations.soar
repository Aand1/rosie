sp {evaluate-grown-object*elaborate*monitor-obj
   (state <s> ^name evaluate-grown-object
              ^superstate.operator.object <obj>)
-->
   (<s> ^monitor-obj <obj>)
}

sp {evaluate-grown-object*elaborate*agent-params*contained-threshold
   (state <s> ^name evaluate-grown-object
              ^top-state.agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

sp {evaluate-grown-object*elaborate*overlap-results
   (state <s> ^name evaluate-grown-object)
-->
   (<s> ^overlap-results <res>)
}

sp {evaluate-grown-object*elaborate*contained-object
   (state <s> ^name evaluate-grown-object
              ^monitor-obj.perception-obj.id <grown-id>
              ^overlap-results.result <res>
              ^contained-threshold <t>
              ^top-state.scene-monitor.object <obj>)
   (<res> ^object-a <a>
          ^object-b <grown-id>
          ^overlap > <t>)
   (<obj> ^id <id>
          ^belief-obj.id <a>)
-->
   (<s> ^contained-object <id>)
}

sp {evaluate-grown-object*elaborate*found-contained-object
   (state <s> ^name evaluate-grown-object
              ^contained-object <f>)
-->
   (<s> ^found-contained-object true)
}
