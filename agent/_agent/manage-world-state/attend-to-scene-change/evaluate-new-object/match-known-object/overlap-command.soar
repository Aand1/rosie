sp {match-known-object*propose*overlap-command
   (state <s> ^name match-known-object
              ^object-info.perception-obj.id <new-id>
              ^intersect-command.result { <int-id> <> none }
             -^overlap-results.result.object-b <int-id>
              ^top-state.perception-monitor.object-monitor.object-info <int-info>)
   (<int-info> ^belief-obj.id <int-id>
              -^wm-obj.predicates.category location) # Hack so we don't merge with locations
-->
   (<s> ^operator <o> + =)
   (<o> ^name overlap-command
        ^object-a <new-id>
        ^object-b <int-id>)
}

sp {match-known-object*apply*overlap-command*copy-result
   (state <s> ^name match-known-object
              ^operator.name overlap-command
              ^overlap-command <cmd>
              ^overlap-results <results>)
   (<cmd> ^result <res>)
   (<res> ^overlap <val>)
-->
   (<s> ^overlap-command <cmd> -)
   (<results> ^result <res>)
}

sp {match-known-object*apply*overlap-command*debug-perception*report*overlap
   (state <s> ^name match-known-object
              ^operator.name overlap-command
              ^overlap-command.result <res>
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<res> ^object-a <a> ^object-b <b> ^overlap <val>)
-->
   (write |Overlap: | <val> | of | <a> | is contained by | <b> (crlf))
} 
