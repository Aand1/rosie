sp {match-missing-object*propose*copy-object
   (state <s> ^name match-missing-object
              ^volume-match true
              ^property-conflict false
              ^missing-object.belief-obj.id <dest>
              ^candidate.perception-obj.id <src>
             -^copied)
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-object
        ^source-id <src>
        ^dest-id <dest>)
}

sp {match-missing-object*apply*copy-object
   (state <s> ^name match-missing-object
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name copy-object
        ^source-id <src>
        ^dest-id <dest>)
-->
   (<cmd> ^copy_transform <copy>)
   (<copy> ^source <src>
           ^destination <dest>
           ^position yes
           ^rotation yes
           ^scale yes
           ^adjust true)
   (<s> ^copied true)
}

sp {match-missing-object*propose*link-objects
   (state <s> ^name match-missing-object
              ^volume-match true
              ^property-conflict false
              ^missing-object.belief-obj.id <dest>
              ^candidate.perception-obj.id <src>)
-->
   (<s> ^operator <o> +)
   (<o> ^name link-objects
        ^category perception
        ^dest-id <dest>
        ^source-id <src>)
}

sp {match-missing-object*apply*link-objects
   (state <s> ^name match-missing-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type link
         ^source-id <source>
         ^dest-id <dest>)
}

sp {match-missing-object*apply*link-objects*monitor*perception
   (state <s> ^name match-missing-object
              ^operator <o>
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (write |Linking object | <source> | to | <dest>)
}
        