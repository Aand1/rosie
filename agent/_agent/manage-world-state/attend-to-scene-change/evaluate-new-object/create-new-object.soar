sp {evaluate-new-object*elaborate*create-new-object*belief-id
   (state <s> ^name evaluate-new-object
              ^operator <o> +)
   (<o> ^name create-new-object
        ^object-handle <obj-handle>)
-->
   (<o> ^belief-id (concat |bel-| <obj-handle>))
}

sp {evaluate-new-object*apply*create-new-object*create*object-info
   (state <s> ^name evaluate-new-object
              ^operator <o>
              ^top-state.perception-monitor.object-monitor <obj-mon>)
   (<o> ^name create-new-object
        ^object-handle <obj-handle>
        ^belief-id <bel-id>
        ^perception-obj.id <perc-id>)
-->
   (<obj-mon> ^object-info <new-obj-info>)
   (<new-obj-info> ^object-handle <obj-handle>
                   ^belief-id <bel-id>
                   ^perception-id <perc-id>
                   ^differences <diffs>)
}

sp {evaluate-new-object*apply*create-new-object*create*belief-object
   (state <s> ^name evaluate-new-object
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name create-new-object
        ^belief-id <bel-id>
        ^perception-obj.id <perc-id>)
-->
   (<cmd> ^copy_node <copy>)
   (<copy> ^id <bel-id>
           ^source <perc-id>
           ^adjust true)
}
