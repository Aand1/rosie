sp {match-known-object*propose*match-candidate
   (state <s> ^name match-known-object
              ^candidate-set.candidate <cand>)
   (<cand> -^predicate-conflict true
           -^type-conflict true
            ^distance <d>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name match-candidate
        ^candidate <cand>)
}

sp {match-known-object*reject*match-candidate*too*far
   (state <s> ^name match-known-object
              ^operator <o> +)
   (<o> ^name match-candidate
         ^candidate.distance > .1)
-->
   (<s> ^operator <o> -)
}

sp {match-known-object*match-candidate*better*prefrence*lower*distance
   (state <s> ^name match-known-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name match-candidate
         ^candidate.distance <d>)
   (<o2> ^name match-candidate
         ^candidate.distance > <d>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {match-known-object*match-candidate*change*perception-id
   (state <s> ^name match-known-object
              ^operator <o>
              ^object-info.perception-id <perc-id>)
   (<o> ^name match-candidate
        ^candidate.object-info <obj-info>)
   (<obj-info> ^perception-id { <old-id> <> <perc-id> })
-->
   (<obj-info> ^perception-id <old-id> -
               ^perception-id <perc-id>)
}

sp {match-known-object*match-candidate*delete*new*object
   (state <s> ^name match-known-object
              ^operator.name match-candidate
              ^object-info <obj-info>
              ^top-state.perception-monitor.object-monitor <obj-mon>)
   (<obj-mon> ^object-info <obj-info>)
-->
   (<obj-mon> ^object-info <obj-info> -)
}