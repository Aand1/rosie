
sp {match-known-object*elaborate*candidate-set
   (state <s> ^name match-known-object)
-->
   (<s> ^candidate-set <set>)
}

sp {match-known-object*elaborate*candidate-set*candidate
   (state <s> ^name match-known-object
              ^candidate-set <set>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^status.visible false)
-->
   (<set> ^candidate <cand>)
   (<cand> ^object-info <obj-info>)
}

sp {match-known-object*elaborate*predicate-conflict*1
   (state <s> ^name match-known-object
              ^candidate-set.candidate <cand>
              ^object-info.input-link-obj.classifications <cfs>)
   (<cand> ^object-info.wm-obj.predicates <preds>)
   (<cfs> ^<prop> <pred>)
   (<preds> ^<prop> <> <pred>
           -^<prop> <pred>)
-->
   (<cand> ^predicate-conflict true)
}

sp {match-known-object*elaborate*predicate-conflict*2
   (state <s> ^name match-known-object
              ^candidate-set.candidate <cand>
              ^object-info.input-link-obj.classifications <cfs>)
   (<cand> ^object-info.wm-obj.predicates <preds>)
   (<preds> ^{ <prop> <> category } <pred>)
   (<cfs> ^<prop> <> <pred>)
-->
   (<cand> ^predicate-conflict true)
}

sp {match-known-object*elaborate*type-conflict
   (state <s> ^name match-known-object
              ^candidate-set.candidate <cand>)
   (<cand> ^object-info.wm-obj.item-type <> object)
-->
   (<cand> ^type-conflict true)
}
