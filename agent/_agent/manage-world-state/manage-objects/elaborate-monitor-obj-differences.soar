##################################################################
#
# is-stale << true false >>
#   true if there is a belief-obj w/ no perception-obj
#
##################################################################

sp {top-state*scene-monitor*elaborate*wm-object*stale*true
   (state <s> ^top-state <s>
              ^scene-monitor.object <obj>)
   (<obj> -^perception-obj
          ^differences <diff>)
-->
   (<diff> ^is-stale true)
}

sp {top-state*scene-monitor*elaborate*wm-object*stale*false
   (state <s> ^top-state <s>
              ^scene-monitor.object <obj>)
   (<obj> ^perception-obj <perc-obj>
          ^differences <diff>)
-->
   (<diff> ^is-stale false)
}

###################################################
#
# stability-timer-satisfied true
#   created when the stability-check-time is passed
#
###################################################

sp {top-state*elaborate*scene-monitor*new-object*stability-timer*satisfied
   (state <s> ^top-state <s>
              ^scene-monitor.new-object <obj>
              ^io.input-link.time.seconds <cur-t>)
   (<obj> ^stability-check-time <= <cur-t>)
-->
   (<obj> ^stability-timer-satisfied true)
}


##################################################################
#
# Monitoring occluded objects
#   is-occluded << true false >> - based on the last time occlusion was checked
#   occlusion-time-expired true - if recheck-occlusion-time is passed
#
##################################################################

sp {top-state*scene-monitor*elaborate*is-occluded*false
   (state <s> ^top-state <s>
              ^scene-monitor.object.differences <diffs>)
   (<diffs> -^is-occluded true)
-->
   (<diffs> ^is-occluded false)
}

sp {top-state*scene-monitor*elaborate*occlusion-time-expired
   (state <s> ^top-state <s>
              ^scene-monitor.object.differences <diffs>
              ^io.input-link.time.seconds <t>)
   (<diffs> ^is-occluded true
            ^recheck-occlusion-time < <t>)
-->
   (<diffs> ^occlusion-time-expired true)
}

###################################################
#
# position differences
#   compare-position <compare> - svs filter
#   pos-diff <diff> - svs filter result 
#   has-moved true - true if pos-diff > threshold
#
###################################################

sp {top-state*scene-monitor*elaborate*svs-command*compare-position
   (state <s> ^top-state <s>
              ^scene-monitor.object <obj>
              ^svs.command <cmd>)
   (<obj> ^perception-obj.id <perc-id>
          ^belief-obj.id <bel-id>
          ^differences <diffs>)
-->
   (<cmd> ^extract <compare>)
   (<compare> ^type distance 
              ^a <a>
              ^b <b>
              ^distance_type centroid)
   (<a> ^type node
        ^id <perc-id>)
   (<b> ^type node
        ^id <bel-id>)
   (<diffs> ^compare-position <compare>)
}

sp {top-state*scene-monitor*elaborate*pos-diff
   (state <s> ^top-state <s>
              ^scene-monitor.object.differences <diffs>)
   (<diffs> ^compare-position.result.record.value <val>)
-->
   (<diffs> ^pos-diff <val>)
}

sp {top-state*scene-monitor*elaborate*has-moved
   (state <s> ^top-state <s>
              ^scene-monitor.object.differences <diffs>
              ^agent-params.pos-diff-threshold <thresh>)
   (<diffs> ^pos-diff > <thresh>)
-->
   (<diffs> ^has-moved true)
}

##################################################################
#
# Monitoring change in volumes
#   belief-vol <vol> - filter to extract belief-obj volume
#   perception-vol <vol> - filter to extract perception-obj volume
#   vol-diff <diff> - perception-vol/belief-vol
#   has-grown true - true if vol-diff > vol-high-diff-threshold
#   has-shrunk true - true if vol-diff < vol-low-diff-threshold
#   growth-wait true - true if not yet time recheck-growth-at <t> 
#
##################################################################
sp {top-state*scene-monitor*elaborate*svs-command*compare-volume
   (state <s> ^top-state <s>
              ^scene-monitor.object <obj>
              ^svs.command <cmd>)
   (<obj> ^perception-obj.id <perc-id>
          ^belief-obj.id <bel-id>
          ^differences <diffs>)
-->
   (<cmd> ^extract <perc-vol> <bel-vol>)
   (<perc-vol> ^type volume
               ^a <pa>
               ^volume_type scale)
   (<pa> ^type node
         ^id <perc-id>)
   (<bel-vol> ^type volume
              ^a <ba>
              ^volume_type scale)
   (<ba> ^type node
         ^id <bel-id>)
   (<diffs> ^belief-vol <bel-vol>
            ^perception-vol <perc-vol>)
}

sp {top-state*scene-monitor*elaborate*vol-diff
   (state <s> ^top-state <s> 
              ^scene-monitor.object.differences <diffs>)
   (<diffs> ^belief-vol.result.record.value <bel-vol>
            ^perception-vol.result.record.value <perc-vol>)
-->
   # Add a cubic mm to each to avoid divide by 0
   (<diffs> ^vol-diff (/ (+ <perc-vol> 0.000000001) (+ <bel-vol> 0.000000001)))
}

sp {top-state*scene-monitor*elaborate*has-shrunk
   (state <s> ^top-state <s>
              ^scene-monitor.object.differences <diffs>
              ^agent-params.vol-low-diff-threshold <low>)
   (<diffs> ^vol-diff < <low>)
-->
   (<diffs> ^has-shrunk true)
}

sp {top-state*scene-monitor*elaborate*has-grown
   (state <s> ^top-state <s>
              ^scene-monitor.object.differences <diffs>
              ^agent-params.vol-high-diff-threshold <high>)
   (<diffs> ^vol-diff > <high>)
-->
   (<diffs> ^has-grown true)
}

sp {top-state*scene-monitor*elaborate*growth-wait*true
   (state <s> ^top-state <s>
              ^scene-monitor.object.differences <diffs>
              ^io.input-link.time.seconds <t>)
   (<diffs> ^has-grown true
            ^recheck-growth-at > <t>)
-->
   (<diffs> ^growth-wait true)
}
