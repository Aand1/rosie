##################################################################
#
# Monitoring for need to copy an object
#
##################################################################
sp {top-state*scene-monitor*mark-for-copying*apply*any-op*copy_node
   (state <s> ^top-state <s>
              ^operator <o>
              ^scene-monitor.object <obj>
              ^svs.command <cmd>)
   (<obj> ^mark-for-copying true
          ^perception-obj.id <perc-id>
          ^belief-obj.id <bel-id>)
-->
   (<cmd> ^copy_transform <copy>)
   (<copy> ^source <perc-id>
           ^destination <bel-id>
           ^position yes
           ^rotation yes
           ^scale yes
           ^adjust true)
   (<obj> ^copy-command <copy>)
}

sp {top-state*scene-monitor*mark-for-copying*apply*any-op*debug-report
   (state <s> ^top-state <s>
              ^operator <o>
              ^scene-monitor.object <obj>
              ^svs.command <cmd>
              ^agent-params.debug-mode on
              ^agent-params.debug-perception true)
   (<obj> ^mark-for-copying true
          ^id <id>
          ^perception-obj)
-->
   (write |Updating Object | <id>)
}
   
sp {top-state*scene-monitor*mark-for-copying*apply*any-op*cleanup
   (state <s> ^top-state <s>
              ^operator <o>
              ^scene-monitor.object <obj>
              ^svs.command <cmd>)
   (<obj> ^mark-for-copying true
          ^copy-command <copy>)
   (<copy> ^status success)
-->
   (<obj> ^mark-for-copying true -
          ^copy-command <copy> -)
}


##################################################################
#
# Monitoring for an object that needs deleted
#
##################################################################
sp {top-state*scene-monitor*mark-for-deletion*apply*any-op*delete*from*svs
   (state <s> ^top-state <s>
              ^operator <o>
              ^scene-monitor.object <obj>
              ^svs.command <cmd>)
   (<obj> ^mark-for-deletion true
          ^belief-obj.id <id>)
-->
   (<cmd> ^delete_node.id <id>)
}

sp {top-state*scene-monitor*mark-for-deletion*apply*any-op*delete*from*wm
  (state <s> ^top-state <s>
              ^operator <o>
              ^scene-monitor <sm>)
   (<sm> ^object <obj>)
   (<obj> ^mark-for-deletion true)
-->
   (<sm> ^object <obj> -)
}

sp {top-state*scene-monitor*mark-for-deletion*apply*any-op*report
   (state <s> ^top-state <s>
              ^operator <o>
              ^scene-monitor.object <obj>
              ^agent-params.debug-mode on
              ^agent-params.debug-perception true)
   (<obj> ^mark-for-deletion true
          ^id <id>)
-->
   (write |Deleting Object | <id>)
}
