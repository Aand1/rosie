sp {evaluate-new-object*propose*match-known-object
   (state <s> ^name evaluate-new-object
              ^monitor-obj <obj>
             -^matches-known-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name match-known-object
        ^category perception
        ^monitor-obj <obj>)
}

sp {evaluate-new-object*propose*match-missing-object
   (state <s> ^name evaluate-new-object
              ^monitor-obj <obj>
              ^match-missing-object <missing>
             -^matches-missing-object)
   (<missing> -^wm-obj.predicates.status live) # The object may be no longer missing
-->
   (<s> ^operator <o> +)
   (<o> ^name match-missing-object
        ^category perception
        ^monitor-obj <obj>
        ^missing-object <missing>)
}

sp {evaluate-new-object*propose*create-new-object
   (state <s> ^name evaluate-new-object
              ^monitor-obj.perception-obj <obj>
              ^monitor-obj.id <id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-new-object
        ^category perception
        ^perception-obj <obj>
        ^object-id <id>)
}
