sp {attend-to-stale-object*propose*internally-link-objects
   (state <s> ^name attend-to-stale-object
              ^contained-by <src-id>
              ^monitor-obj <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name internally-link-objects
        ^category perception
        ^source-id <src-id>
        ^dest-obj <obj>)
}

sp {attend-to-stale-object*prefer*internally-link-objects*containment
   (state <s> ^name attend-to-stale-object
              ^operator <o1> +
              ^operator <o2> +
              ^overlap-results.result <r1> <r2>)
   (<o1> ^name internally-link-objects
         ^source-id <id1>)
   (<o2> ^name internally-link-objects
         ^source-id <id2>)
   (<r1> ^object-b <id1>
         ^overlap <overlap>)
   (<r2> ^object-b <id2>
         ^overlap < <overlap>)
-->
   (<s> ^operator <o1> > <o2>)
}
   
sp {attend-to-stale-object*apply*internally-link-objects
   (state <s> ^name attend-to-stale-object
              ^operator <o>)
   (<o> ^name internally-link-objects
        ^source-id <id>
        ^dest-obj <obj>)
-->
   (<obj> ^internal-link <id>)
}

sp {attend-to-stale-object*apply*internally-link-objects*monitor*perception
   (state <s> ^name attend-to-stale-object
              ^operator <o>
              ^topstate.agent-params.debug-mode on
              ^topstate.agent-params.debug-perception true)
   (<o> ^name internally-link-objects
        ^dest-obj.belief-obj.id <dest>
        ^source-id <source>)
-->
   (write |Internally linking object | <dest> | to | <source> (crlf))
}
        
