sp {anystate*elaborate*attend-to-scene-change*missing-object
   (state <s> ^attend-to-all-scene-changes true
              ^operator <o> +
              ^attend-to-missing-object-id <id>)
   (<o> ^name attend-to-scene-change
        ^change <change>)
   (<change> ^type stale-object
             ^object.id <id>)
-->
   (<change> ^missing-object true)
}

sp {attend-to-stale-object*elaborate*missing-object
   (state <s> ^name attend-to-stale-object
              ^superstate.operator.missing-object true)
-->
   (<s> ^missing-object true)
}

sp {attend-to-stale-object*elaborate*monitor-obj
   (state <s> ^name attend-to-stale-object
              ^superstate.operator.object <obj>)
-->
   (<s> ^monitor-obj <obj>)
}

#sp {attend-to-stale-object*elaboate*aggressive-match 
#   (state <s> ^name attend-to-stale-object
#              ^superstate.superstate.operator.aggressive-match <m>)
#-->
#   (<s> ^aggressive-match <m>)
#}

sp {attend-to-stale-object*elaborate*agent-params*occlusion-check-period
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.occlusion-check-period <p>)
-->
   (<s> ^occlusion-check-period <p>)
}

sp {attend-to-stale-object*elaborate*agent-params*contained-threshold
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

sp {attend-to-stale-object*elaborate*agent-params*contained-by-threshold
   (state <s> ^name attend-to-stale-object
              ^topstate.agent-params.contained-by-threshold <t>)
-->
   (<s> ^contained-by-threshold <t>)
}

sp {attend-to-stale-object*elaborate*overlap-results
   (state <s> ^name attend-to-stale-object)
-->
   (<s> ^overlap-results <res>)
}

sp {attend-to-stale-object*elaborate*contained-fragment
   (state <s> ^name attend-to-stale-object
              ^monitor-obj.belief-obj.id <stale-id>
              ^overlap-results.result <res>
              ^contained-threshold <t>)
   (<res> ^object-a <a>
          ^object-b <stale-id>
          ^overlap > <t>)
-->
   (<s> ^contained-fragment <a>)
}

sp {attend-to-stale-object*elaborate*contained-by
   (state <s> ^name attend-to-stale-object
              ^monitor-obj.belief-obj.id <stale-id>
              ^overlap-results.result <res>
              ^contained-by-threshold <t>
              ^topstate.scene-monitor.object <obj>)
   (<res> ^object-a <stale-id>
          ^object-b <b>
          ^overlap > <t>)
   (<obj> ^id <id>
          ^perception-obj.id <b>)
-->
   (<s> ^contained-by <id>)
}

sp {attend-to-stale-object*elaborate*found-contained-fragment
   (state <s> ^name attend-to-stale-object
              ^contained-fragment <f>)
-->
   (<s> ^found-contained-fragment true)
}
