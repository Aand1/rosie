# See attend-to-scene-change.svs-command-operators.check-for-occlusion

sp {attend-to-stale-object*apply*mark-for-deletion
   (state <s> ^name attend-to-stale-object
              ^operator <o>)
   (<o> ^name mark-for-deletion
        ^monitor-obj <obj>)
-->
   (<obj> ^mark-for-deletion true)
}

# If we have object permanence, we check for occlusion
sp {attend-to-stale-object*propose*check-for-occlusion
   (state <s> ^name attend-to-stale-object
              ^monitor-obj <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name check-for-occlusion
        ^category perception
        ^monitor-obj <obj>
        ^report-to-superstate true
        ^report-to-topstate false)
}

sp {attend-to-stale-object*apply*check-for-occlusion*not-occluded*mark-for-deletion
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
             -^missing-object
              ^object-occluded false
              ^monitor-obj <obj>)
-->
   (<obj> ^mark-for-deletion true)
}

# If the object is missing and we need it, ask the user for help
sp {attend-to-stale-object*apply*check-for-occlusion*not-occluded*missing-object*interaction
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
              ^missing-object true
              ^object-occluded false
              ^monitor-obj <monitor-obj>
              ^topstate.interaction.status <status>)
-->
   (<status> ^failure <f>)
   (<f> ^type missing-object
        ^missing-object <monitor-obj>)
   (<monitor-obj> ^searching-for-object true)
}

sp {attend-to-stale-object*apply*check-for-occlusion*occluded*add*info
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
              ^object-occluded true
              ^monitor-obj.differences <diffs>
              ^io.input-link.time.seconds <cur-t>
              ^occlusion-check-period <period>)
-->
   (<diffs> ^is-occluded true
            ^recheck-occlusion-time (+ <cur-t> <period>))
}

sp {attend-to-stale-object*apply*check-for-occlusion*occluded*remove*old*info
   (state <s> ^name attend-to-stale-object
              ^operator.name check-for-occlusion
              ^object-occluded true
              ^monitor-obj.differences <diffs>)
   (<diffs> ^recheck-occlusion-time <t>)
-->
   (<diffs> ^recheck-occlusion-time <t> -)
}

