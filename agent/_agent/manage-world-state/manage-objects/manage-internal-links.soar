# If an object is being internally linked to, link it to itself
sp {top-state*scene-monitor*elaborate*internal-link
   (state <s> ^top-state <s>
              ^scene-monitor <m>)
   (<m> ^object <obj>
        ^object.internal-link <id>)
   (<obj> ^id <id>)
-->
   (<obj> ^internal-link <id>)
   (write |Linking | <id> | to itself| (crlf)) 
}

# If an internally-linked object appears again, remove the link
sp {top-state*scene-monitor*remove*internal-link*not*stale
   :o-support
   (state <s> ^top-state <s>
              ^scene-monitor.object <obj>)
   (<obj> ^id <id>
          ^internal-link {<id2> <> <id>}
          ^perception-obj <p>)
-->
   (<obj> ^internal-link <id2> -)
   (write |Removing link | <id2> | from | <id> |: visible again| (crlf))
}

sp {top-state*scene-monitor*remove*internal-link*not*stale*remove*occlusion
   :o-support
   (state <s> ^top-state <s>
              ^scene-monitor.object <obj>)
   (<obj> ^id <id>
          ^internal-link {<id2> <> <id>}
          ^perception-obj <p>
          ^differences <diffs>)
   (<diffs> ^is-occluded true
            ^recheck-occlusion-time <t>)
-->
   (<diffs> ^is-occluded true -
            ^recheck-occlusion-time <t> -)
}

sp {top-state*scene-monitor*remove*internal-link*stale*object
   :o-support
   (state <s> ^top-state <s>
              ^scene-monitor <mon>)
   (<mon> ^object <src>
          ^object <dest>)
   (<dest> ^id <id>
           ^internal-link {<id2> <> <id>})
   (<src> ^id <id2>
         -^perception-obj)
-->
   (<dest> ^internal-link <id2> -)
}

# Don't attend to a stale object which is being linked to another one
sp {anystate*reject*attend-to-scene-change*stale-object*internally-linked
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type stale-object
        ^object <obj>)
   (<obj> ^id <id>
          ^internal-link {<id2> <> <id>})
-->
   (<s> ^operator <o> -)
   (<c> ^rejected true)
   (write |Reject attend to stale object | <id> |: linked to | <id2> (crlf))
}

# Don't attend to linked object properties, size, or position changes (can't trust)
sp {anystate*reject*attend-to-scene-change*pos*size*changes*internally-linked
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type { <type> << grown-object shrunken-object moved-object >> }
        ^object <obj>)
   (<obj> ^id <id>
          ^internal-link <id2>)
-->
   (<s> ^operator <o> -)
   (<c> ^rejected true)
   (write |Reject attend to | <type> | for object | <id> | linked to | <id2> (crlf))
}

sp {anystate*reject*attend-to-scene-change*property*changes*internally-linked
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type { <type> << changed-property different-best higher-confidence new-property different-feature-val >> }
        ^object <obj>)
   (<obj> ^id <id>
          ^internal-link <id2>)
-->
   (<s> ^operator <o> -)
   (<c> ^rejected true)
   (write |Reject attend to | <type> | for object | <id> | linked to | <id2> (crlf))
}

# Internally linked objects should periodically check occlusion
#   If not occluded, then remove the link (should be able to see)
sp {top-state*scene-monitor*elaborate*occlusion-time-expired*internal-link
   (state <s> ^top-state <s>
              ^scene-monitor.object <obj>)
   (<obj> ^id <id>
          ^internal-link {<id2> <> <id>}
          ^differences <diffs>)
   (<diffs> -^recheck-occlusion-time)
-->
   (<diffs> ^occlusion-time-expired true)
   (write |Start checking occlusion for object | <id> | linked to | <id2> (crlf))
}

sp {check-for-occlusion*apply*occlusion-command*is-occluded*false*remove*internal-link
   (state <s> ^name check-for-occlusion
              ^operator.name occlusion-command
              ^report-to-top-state true
              ^object-occluded false
              ^monitor-obj <obj>)
   (<obj> ^id <id>
          ^internal-link {<id2> <> <id>})
-->
   (<obj> ^internal-link <id2> -)
   (write |Object | <id> | no longer occluded. Remove link to | <id2> (crlf))
}

