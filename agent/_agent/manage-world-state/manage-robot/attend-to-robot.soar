# Update world.robot.moving-state

sp {anystate*apply*attend-to-scene-change*different-moving-state
   (state <s> ^operator <o>
              ^topstate.world.robot <robot>)
   (<o> ^name attend-to-scene-change
        ^change.type different-moving-state
        ^change.self-info.moving-state <new-state>)
   (<robot> ^moving-state { <old-state> <> <new-state> })
-->
   (<robot> ^moving-state <old-state> -
            ^moving-state <new-state>)
}

sp {anystate*apply*attend-to-scene-change*monitor*moving-state
   (state <s> ^operator <o>
              ^topstate.agent-params.debug-mode on
              ^topstate.world.robot <robot>)
   (<o> ^name attend-to-scene-change
        ^change.type different-moving-state
        ^change.self-info.moving-state <new-state>)
   (<robot> ^moving-state { <old-state> <> <new-state> })
-->
   (write |robot.moving-state changed from | <old-state> | to | <new-state> (crlf))
}

# Updating world.robot.current-waypoint
sp {anystate*apply*attend-to-scene-change*new-waypoint*add*new
   (state <s> ^operator <o>
              ^topstate.world.robot <robot>
              ^topstate.map.waypoints.waypoint <wp>)
   (<o> ^name attend-to-scene-change
        ^change.type new-waypoint
        ^change.waypoint-id <wp-id>)
   (<wp> ^id <wp-id>)
-->
   (<robot> ^current-waypoint <wp>)
}
   
sp {anystate*apply*attend-to-scene-change*new-waypoint*remove*old
   (state <s> ^operator <o>
              ^topstate.world.robot <robot>
              ^topstate.map.waypoints.waypoint <wp>)
   (<o> ^name attend-to-scene-change
        ^change.type new-waypoint
        ^change.waypoint-id <wp-id>)
   (<wp> ^id <wp-id>)
   (<robot> ^current-waypoint { <old-wp> <> <wp> })
-->
   (<robot> ^current-waypoint <old-wp> -)
}

sp {anystate*apply*attend-to-scene-change*monitor*new-waypoint
   (state <s> ^operator <o>
              ^topstate.agent-params.debug-mode on)
   (<o> ^name attend-to-scene-change
        ^change.type new-waypoint
        ^change.waypoint-id <wp-id>)
-->
   (write |current-waypoint: | <wp-id> (crlf))
}
              