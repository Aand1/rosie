
sp {anystate*reject*attend-to-scene-change*new-stable-object*moving*candidate
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type new-stable-object
        ^change.object.candidate-for-missing-object.moving-object true)
-->
   (<s> ^operator <o> -)
}

# Dont attend to a stale object if another has been flagged as a potential match
sp {anystate*reject*attend-to-scene-change*stale-object*still*candidates
   (state <s> ^operator <o> +
              ^topstate.scene-monitor.new-object.candidate-for-missing-object <obj>)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type stale-object
        ^object <obj>)
-->
   (<s> ^operator <o> -)
   (<c> ^rejected true)
}

# If we're searching for the object, don't delete it yet
sp {anystate*reject*attend-to-scene-change*stale-object*searching-for-object
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type stale-object
        ^object.searching-for-object true)
-->
   (<s> ^operator <o> -)
   (<c> ^rejected true)
}



# When we move an object, remove all internal links associated with it
sp {attend-to-scene-change*apply*attend-to-arm*stopped*remove*moved-object*internal-links
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm
              ^io.input-link.self.action << wait failure >>
              ^topstate.scene-monitor.object <obj>)
   (<obj> ^wm-obj.moving-object true
          ^id <id>
          ^internal-link <id2>)
-->
   (<obj> ^internal-link <id2> -)
   (write |Moved object | <id> | and removed link to | <id2> (crlf))
}

sp {attend-to-scene-change*apply*attend-to-arm*stopped*remove*other*internal-links
   (state <s> ^name attend-to-scene-change
              ^operator.name attend-to-arm
              ^io.input-link.self.action << wait failure >>
              ^topstate.scene-monitor <mon>)
   (<mon> ^object <src-obj>
          ^object <dest-obj>)
   (<src-obj> ^wm-obj.moving-object true
              ^id <id>)
   (<dest-obj> ^internal-link <id>
               ^id <id2>)
-->
   (<dest-obj> ^internal-link <id> -)
   (write |Moved object | <id> | and removed link from | <id2> (crlf))
}

sp {anystate*reject*attend-to-scene-change*moving-object
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^object.wm-obj.moving-object true)
-->
   (<s> ^operator <o> -)
   (<c> ^rejected true)
}

sp {anystate*reject*attend-to-scene-change*grabbed-object
   (state <s> ^operator <o> +
              ^robot-state.arm.grabbed <obj>)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^object.wm-obj <obj>)
-->
   (<s> ^operator <o> -)
   (<c> ^rejected true)
}

