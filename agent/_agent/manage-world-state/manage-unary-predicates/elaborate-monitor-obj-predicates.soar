sp {topstate*elaborate*monitor-obj*predicates
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type> <obj>)
   (<obj> ^id <id>)
-->
   (<obj> ^predicates <preds>)
}

sp {topstate*elaborate*monitor-obj*predicate*name
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type> <obj>)
   (<obj> ^input-link-obj.property <prop>
          ^predicates <preds>)
   (<prop> ^name name
           ^values.<obj-name> <any>)
-->
   (<preds> ^predicate <pred>)
   (<pred> ^predicate-category name
           ^predicate-type linguistic
           ^predicate-value <obj-name>)
}

sp {topstate*elaborate*monitor-obj*predicate*visual*state
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type> <obj>)
   (<obj> ^input-link-obj.property <prop>
          ^predicates <preds>)
   (<prop> ^name <pred-cat>
           ^type {<type> << visual state >> })
-->
   (<preds> ^predicate <pred>)
   (<pred> ^il-predicate <prop>
           ^predicate-category <pred-cat>
           ^predicate-type <type>)
}

sp {topstate*elaborate*scene-monitor*visual*predicate*hypothesis
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type>.predicates.predicate <pred>)
   (<pred> ^predicate-type visual
           ^il-predicate.values.<pred-value> {<conf> > .1})
-->
   (<pred> ^hypothesis <h>)
   (<h> ^value <pred-value>
        ^conf <conf>)
}

sp {topstate*elaborate*scene-monitor*visual*predicate*best*hypothesis
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type>.predicates.predicate <pred>)
   (<pred> ^predicate-type visual
           ^hypothesis <h1>
          -^hypothesis.conf > <best>)
   (<h1> ^conf <best>)
-->
   (<h1> ^best true)
}

sp {topstate*elaborate*scene-monitor*visual*predicate*hypothesis*lower-threshold
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type>.predicates.predicate <pred>)
   (<pred> ^predicate-type visual 
           ^hypothesis <h>)
   (<h> ^best true
        ^conf <conf>)
-->
   (<h> ^lower-threshold (* <conf> .8))
}

sp {topstate*elaborate*scene-monitor*visual*predicate*value
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type>.predicates.predicate <pred>)
   (<pred> ^predicate-type visual
           ^hypothesis <h>)
   (<h> ^best true
        ^value <pred-value>
        ^lower-threshold <low-thresh>)
   -{(<pred> ^hypothesis {<h2> <> <h>})
     (<h2> ^conf > <low-thresh>)}
-->
   (<pred> ^predicate-value <pred-value>)
}

sp {topstate*elaborate*scene-monitor*state*predicate*value
   (state <s> ^topstate <s>
              ^scene-monitor.<obj-type>.predicates.predicate <pred>)
   (<pred> ^predicate-type state
           ^il-predicate.value <pred-value>)
-->
   (<pred> ^predicate-value <pred-value>)
}

