##########################################################################
#
# perception-monitor.object-monitor: 
#   A top-state set of elaborations that monitor objects 
#   and compare new perceptual information with current representations
#   in working memory. If discepancies are reported they are recorded
#
##########################################################################

# The structure will look as follows
#(state <s> ^superstate nil
#           ^perception-monitor <perc-mon>)
#  (<perc-mon> ^object-monitor <obj-mon>)
#    (<obj-mon> ^object-info <info>)
#      (<info> ^object-handle <handle>
#              ^belief-id <id>            # The id of the belief object in svs
#              ^perception-obj <perc-obj> # Reference to the perceptual object in svs
#              ^belief-obj <bel-obj>      # Reference to the belief object in svs
#              ^wm-obj <wm-obj>           # Reference to the object representation in wm (top-state.world object)
#              ^input-link-obj <il-obj>   # Reference to the object representation on the input-link
#              ^status <status>           # Elaborations recording the current status of the object
#              ^differences <diffs>       # Elaborations used to record informatino about discrepancies
#              ^properties <props>)       # Elaborations used to monitor properties (e.g. color, door-state, etc.)
                 

##############################################################
#
# Monitoring objects
#
##############################################################

sp {top-state*perception-monitor*object-monitor*object-info*elaborate*substructure
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>)
-->
   (<obj-info> ^status <status>
               ^differences <diffs>
               ^properties <props>)
}

sp {top-state*perception-monitor*object-monitor*object-info*elaboratee*belief-obj
  (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^svs.spatial-scene.child <belief-obj>)
  (<obj-info> ^belief-id <bel-id>)
  (<belief-obj> ^id <bel-id>)
-->
  (<obj-info> ^belief-obj <belief-obj>)
}

sp {top-state*perception-monitor*object-monitor*object-info*elaborate*perception-obj
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^svs.spatial-scene.child <perc-obj>)
   (<obj-info> ^perception-id <perc-id>)
   (<perc-obj> ^id <perc-id>)
-->
   (<obj-info> ^perception-obj <perc-obj>)
}

sp {top-state*perception-monitor*object-monitor*object-info*elaborate*input-link-obj
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^io.input-link.objects.object <il-obj>)
   (<obj-info> ^perception-id <perc-id>)
   (<il-obj> ^handle <perc-id>)
-->
   (<obj-info> ^input-link-obj <il-obj>)
}
