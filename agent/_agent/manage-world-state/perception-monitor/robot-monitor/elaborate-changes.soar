################################################################
# Change: changed-arm-status
#   Occurs when the arm status on the input-link changes
################################################################

sp {top-state*perception-monitor*change-info*elaborate*changed-arm-status
   (state <s> ^top-state <s>
              ^perception-monitor.change-info <info>)
-->
   (<info> ^change <changed-arm-status>)
   (<changed-arm-status> ^type changed-arm-status
                         ^category robot
                         ^op-name attend-to-changed-arm-status)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*changed-arm-status*grabbed
   (state <s> ^top-state <s>
              ^perception-monitor.changes <changes>
              ^io.input-link.self <self>
              ^world.robot.arm <arm>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<self> ^held-object <handle>)
   (<obj-info> ^perception-id <handle>
               ^wm-obj.predicates.arm-status not-grabbed)
   (<arm> ^state external)
-->
   (<changes> ^change <change>)
   (<change> ^type changed-arm-status
             ^self-info <self>)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*changed-arm-status*not*grabbed
   (state <s> ^top-state <s>
              ^perception-monitor.changes <changes>
              ^io.input-link.self <self>
              ^world.robot.arm <arm>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> -^wm-obj.predicates.arm-status not-grabbed
               ^perception-id <handle>)
   (<self> -^held-object <handle>)
   (<arm> ^state external)
-->
   (<changes> ^change <change>)
   (<change> ^type changed-arm-status
             ^self-info <self>)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*changed-arm-status*not*grabbed*internal
   (state <s> ^top-state <s>
              ^perception-monitor.changes <changes>
              ^world.robot.arm <arm>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> -^wm-obj.predicates.arm-status)
   (<arm> ^state internal)
-->
   (<changes> ^change <change>)
   (<change> ^type changed-arm-status
             ^self-info <self>)
}

#########################################################
# Change: changed-moving-state
#   Occurs when the robot moving-state changes on the input-link
#########################################################

sp {top-state*perception-monitor*change-info*elaborate*changed-moving-state
   (state <s> ^top-state <s>
              ^perception-monitor.change-info <info>)
-->
   (<info> ^change <changed-moving-state>)
   (<changed-moving-state> ^type changed-moving-state
                           ^category robot
                           ^op-name attend-to-changed-moving-state)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*changed-moving-state
   (state <s> ^top-state <s>
              ^world.robot.moving-state <cur-state>
              ^perception-monitor.changes <changes>
              ^io.input-link.self <self>)
   (<self> ^moving-state { <new-state> <> <cur-state> })
-->
   (<changes> ^change <change>)
   (<change> ^type changed-moving-state
             ^self-info <self>)
}

########################################################
# Change: changed-waypoint
########################################################

sp {top-state*perception-monitor*change-info*elaborate*changed-waypoint
   (state <s> ^top-state <s>
              ^perception-monitor.change-info <info>)
-->
   (<info> ^change <changed-waypoint>)
   (<changed-waypoint> ^type changed-waypoint
                       ^category robot
                       ^op-name attend-to-changed-waypoint)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*new-waypoint
   (state <s> ^top-state <s>
              ^world.robot <robot>
              ^io.input-link.self.current-waypoint.waypoint-handle <wp-handle>
              ^perception-monitor.changes <changes>)
   (<robot> -^current-waypoint.handle <wp-handle>)
-->
   (<changes> ^change <change>)
   (<change> ^type changed-waypoint
             ^new-waypoint-handle <wp-handle>)
}
