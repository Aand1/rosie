sp {top-state*perception-monitor*predicate-monitor*propose*retrieve-predicate
   (state <s> ^superstate nil
              ^perception-monitor.predicate-monitor.predicate-info <info>)
   (<info> ^predicate-handle <pred-handle>
          -^smem-info)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name retrieve-predicate
        ^predicate-info <info>)
}

sp {retrieve-predicate*elaborate*predicate-info*superoperator
   (state <s> ^name retrieve-predicate
              ^superstate.operator.predicate-info <info>)
-->
   (<s> ^predicate-info <info>)
}

sp {retrieve-predicate*elaborate*smem-query-cue
   (state <s> ^name retrieve-predicate
              ^predicate-info.predicate-handle <pred-handle>)
-->
   (<s> ^smem-query-cue <cue>)
   (<cue> ^handle <pred-handle>)
}

sp {retrieve-predicate*elaborate*smem-info*from*retrieved-lti-cue
   (state <s> ^name retrieve-predicate
              ^retrieved-lti-cue <ret>
              ^predicate-info.predicate-handle <pred-handle>)
   (<ret> ^retrieved-lti <lti>)
   (<lti> ^handle <pred-handle>)
-->
   (<s> ^smem-info <lti>)
}

sp {retrieve-predicate*elaborate*retrieve-lti*relation
   (state <s> ^name retrieve-predicate
              ^smem-info.relation <rel>)
-->
   (<s> ^retrieve-lti <rel>)
}

sp {retrieve-predicate*propose*complete-retrieve-predicate
   (state <s> ^name retrieve-predicate
              ^smem-info <smem-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-retrieve-predicate)
}

sp {retrieve-predicate*prefer*retrieve*over*complete
   (state <s> ^name retrieve-predicate
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name smem-retrieve-new)
   (<o2> ^name complete-retrieve-predicate)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {retrieve-predicate*apply*complete-retrieve-predicate
   (state <s> ^name retrieve-predicate
              ^operator.name complete-retrieve-predicate
              ^smem-info <lti>
              ^predicate-info <pred-info>)
-->
   (<pred-info> ^smem-info <lti>)
}
   
