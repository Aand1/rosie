sp {match-missing-object*propose*copy-object
   (state <s> ^name match-missing-object
              ^volume-match true
              ^property-conflict false
              ^missing-object-info.belief-obj.id <dest>
              ^candidate-info.perception-obj.id <src>
             -^copied)
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-object
        ^source-id <src>
        ^dest-id <dest>)
}

sp {match-missing-object*apply*copy-object
   (state <s> ^name match-missing-object
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name copy-object
        ^source-id <src>
        ^dest-id <dest>)
-->
   (<cmd> ^copy_transform <copy>)
   (<copy> ^source <src>
           ^destination <dest>
           ^position yes
           ^rotation yes
           ^scale yes
           ^adjust true)
   (<s> ^copied true)
}

sp {match-missing-object*propose*link-objects
   (state <s> ^name match-missing-object
              ^volume-match true
              ^property-conflict false
              ^missing-object-info <dest-obj-info>
              ^candidate-info <src-obj-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name link-objects
        ^src-info <src-obj-info>
        ^dest-info <dest-obj-info>)
}

sp {match-missing-object*apply*link-objects
   (state <s> ^name match-missing-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-objects
        ^src-info.perception-obj.id <src-handle>
        ^dest-info.object-handle <dest-handle>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type link
         ^source-handle <src-handle> 
         ^destination-handle <dest-handle>)
}

sp {match-missing-object*apply*link-objects*monitor*perception
   (state <s> ^name match-missing-object
              ^operator <o>
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<o> ^name link-objects
        ^src-info.perception-obj.id <source>
        ^dest-info.perception-obj.id <dest>)
-->
   (write |Linking object | <source> | to | <dest>)
}
        
