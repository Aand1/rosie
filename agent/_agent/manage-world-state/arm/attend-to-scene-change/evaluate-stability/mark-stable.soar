sp {evaluate-stability*propose*mark-stable
   (state <s> ^name evaluate-stability
              ^obj-is-stable true
              ^object-info <obj-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-stable
        ^object-info <obj-info>)
}

sp {evaluate-stability*apply*mark-stable*mark*confirmed
   (state <s> ^name evaluate-stability
              ^operator <o>)
   (<o> ^name mark-stable
        ^object-info <obj-info>)
   (<obj-info> ^confirmed false)
-->
   (<obj-info> ^confirmed false -
               ^confirmed true)
}

sp {evaluate-stability*apply*mark-stable*cleanup*object
   (state <s> ^name evaluate-stability
              ^operator <o>)
   (<o> ^name mark-stable
		    ^object-info <obj-info>)
   (<obj-info> ^number-checks <num>
               ^check-stability-time <t>)
-->
   (<obj-info> ^number-checks <num> -
               ^check-stability-time <t> -)
}

sp {evaluate-stability*apply*mark-stable*remove*filters
   (state <s> ^name evaluate-stability
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name mark-stable
        ^object-info <obj-info>)
   (<obj-info> ^perception-obj.id <id>
               ^position-monitor <old-pos>
               ^volume-monitor <old-vol>)
-->
   (<cmd> ^extract <old-pos> -
          ^extract <old-vol> -)
   (<obj-info> ^position-monitor <old-pos> -
               ^volume-monitor <old-vol> -)   
}
