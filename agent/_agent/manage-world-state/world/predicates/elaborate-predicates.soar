sp {top-state*world*predicates*elaborate*predicate
   (state <s> ^superstate nil
              ^world.predicates <preds>
              ^perception-monitor.predicate-monitor.predicate-info <pred-info>)
   (<pred-info> ^predicate-handle <pred-handle>)
-->
   (<preds> ^predicate <pred>)
   (<pred> ^handle <pred-handle>)
}

sp {top-state*world*predicates*elaborate*predicate-monitor*object-test*relations
   (state <s> ^superstate nil
              ^world.objects.object <obj>
              ^perception-monitor.predicate-monitor.predicate-info <pred-info>)
   (<pred-info> ^smem-info.item-type relation)
-->
   (<pred-info> ^object-test <test>)
   (<test> ^object <obj>)
}

sp {top-state*world*predicates*elaborate*instance*from*predicate-monitor
   (state <s> ^superstate nil
              ^perception-monitor.predicate-monitor.predicate-info <pred-info>
              ^world.objects.object <obj2>
              ^world.predicates.predicate <pred>)
   (<pred-info> ^predicate-handle <pred-handle>
                ^object-test <test>)
   (<test> ^object <obj1>
           ^x.result.record.params.b <id2>)
   (<obj2> ^svs-obj.id <id2>)
   (<pred> ^handle <pred-handle>)
-->
   (<pred> ^instance <args>)
   (<args> ^1 <obj1> ^2 <obj2>)
}