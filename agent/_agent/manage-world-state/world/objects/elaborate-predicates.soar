## Elaborate predicates from the object-monitor
#sp {top-state*world*object*predicates*elaborate*object-monitor*property
#   (state <s> ^superstate nil
#              ^world.objects.object <obj>
#              ^perception-monitor.object-monitor.object-info <obj-info>)
#   (<obj> ^handle <obj-handle>
#          ^predicates <preds>)
#   (<obj-info> ^object-handle <obj-handle>
#               ^properties.property-info <prop-info>)
#   (<prop-info> ^property-handle <prop-handle>
#                ^predicate-handle <pred-handle>)
#-->
#   (<preds> ^<prop-handle> <pred-handle>)
#}
#   

# Elaborate object category, either location or block

sp {top-state*world*object*predicates*elaborate*category*from*agent-info
   (state <s> ^top-state <s>
              ^world.objects.object.predicates <preds>
              ^agent-info.category-info.<name> <cat>)
   (<preds> ^name <name>)
-->
   (<preds> ^category <cat>)
}

sp {top-state*world*object*predicates*elaborate*category*default*block
   (state <s> ^top-state <s>
              ^world.objects.object.predicates <preds>)
   (<preds> -^category <> block1)
-->
   (<preds> ^category block1
            ^movable true)
}

sp {top-state*world*object*predicates*elaborate*property*current
   (state <s> ^top-state <s>
              ^world.robot.current-location <loc>)
   (<loc> ^predicates <preds>)
-->
   (<preds> ^property current)
}


# Elaborate pointed predicate if the object is being pointed to
   
sp {top-state*world*object*predicates*elaborate*indicated*pointed
   (state <s> ^top-state <s>
              ^io.input-link.pointed-object <handle>
              ^world.objects.object <obj>)
   (<obj> ^handle <handle>
          ^predicates <preds>)
-->
   (<preds> ^indicated pointed)
}

# Elaborate default arm-status of not-grabbed

sp {top-state*world*object*predicates*elaborate*arm-status*default*not-grabbed
   (state <s> ^superstate nil
              ^world.objects.object.predicates <preds>)
   (<preds> -^arm-status << moving grabbed >> )
-->
   (<preds> ^arm-status not-grabbed)
}

sp {top-state*world*object*predicates*elaborate*visible*in*location
   (state <s> ^superstate nil
              ^agent-params.domain mobile
              ^world-usage external
              ^world <world>)
   (<world> ^robot.current-location <cur-loc>
            ^predicates.predicate <in-pred>
            ^objects.object <obj>)
   (<obj> ^predicates <preds>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <cur-loc>)
-->
   (<preds> ^visible true)
}

sp {top-state*world*object*predicates*elaborate*not*visible*not*in*location
   (state <s> ^superstate nil
              ^agent-params.domain mobile
              ^world-usage external
              ^world <world>)
   (<world> ^robot.current-location <cur-loc>
            ^predicates.predicate <in-pred>
            ^objects.object <obj>)
   (<obj> ^predicates <preds>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 { <loc> <> <cur-loc> })
-->
   (<preds> ^visible false)
}