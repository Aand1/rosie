# Elaborate predicates from the object-monitor
sp {top-state*world*object*predicates*elaborate*object-monitor*property
   (state <s> ^superstate nil
              ^world.objects.object <obj>
              ^perception-monitor.object-monitor.object-info <obj-info>)
   (<obj> ^handle <obj-handle>
          ^predicates <preds>)
   (<obj-info> ^object-handle <obj-handle>
               ^properties.property-info <prop-info>)
   (<prop-info> ^property-handle <prop-handle>
                ^predicate-handle <pred-handle>)
-->
   (<preds> ^<prop-handle> <pred-handle>)
}
   

# Elaborate object category, either location or block

sp {top-state*world*object*predicates*elaborate*category*from*agent-info
   (state <s> ^top-state <s>
              ^world.objects.object.predicates <preds>
              ^agent-info.category-info.<name> <cat>)
   (<preds> ^name <name>)
-->
   (<preds> ^category <cat>)
}

sp {top-state*world*object*predicates*elaborate*category*default*block
   (state <s> ^top-state <s>
              ^world.objects.object.predicates <preds>)
   (<preds> -^category <> block1)
-->
   (<preds> ^category block1
            ^movable true)
}

# Elaborate pointed predicate if the object is being pointed to
   
sp {top-state*world*object*predicates*elaborate*indicated*pointed
   (state <s> ^top-state <s>
              ^io.input-link.pointed-object <handle>
              ^world.objects.object <obj>)
   (<obj> ^handle <handle>
          ^predicates <preds>)
-->
   (<preds> ^indicated pointed)
}

# Elaborate default arm-status of not-grabbed

sp {top-state*world*object*predicates*elaborate*arm-status*default*not-grabbed
   (state <s> ^superstate nil
              ^world.objects.object.predicates <preds>)
   (<preds> -^arm-status << moving grabbed >> )
-->
   (<preds> ^arm-status not-grabbed)
}