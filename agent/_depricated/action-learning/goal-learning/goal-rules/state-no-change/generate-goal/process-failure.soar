### if failure occured because Rosie does not know the goal definition, report status to top-state$

sp {generate-goal*report-status*unknown-goal
   (state <s> ^name generate-goal
   	      ^operator <op>
	      ^failure <det>
	      ^goal-id <gid>
	      ^top-state.interaction.status <stat>)
   (<op> ^name process-failure
   	 -^type unmatched-constraints)
 -->
   (<stat> ^failure <f>)
   (<f> ^type failure
   	^information unknown-goal
	^details <det>)
}

sp {generate-goal*report-status*unknown-goal*unmateched
   (state <s> ^name generate-goal
   	      ^operator <op>
	      ^failure <det>
	      ^goal-id <gid>
	      ^top-state.interaction.status <stat>
	      ^deleted unmatched-objects)
   (<op> ^name process-failure
   	 ^type unmatched-constraints)
 -->
   (<stat> ^failure <f>)
   (<f> ^type failure
   	^information unknown-goal
	^details <det>)
}

sp {generate-goal*report-status*unmatched-constraints*delete
   (state <s> ^name generate-goal
   	      ^operator <op>
	      ^failure.predicate-grounding-failure.partial-grounds <pg>)
   (<op> ^name process-failure
   	 ^type unmatched-constraints
	 ^wmem-object <wobj>)
   (<pg> ^<arg> <wobj>)
-->
   (<pg> ^<arg> <wobj> -)
   (<s> ^deleted unmatched-objects)
}
	 


sp {generate-goal*report-status*unmatched-constraints*state-predicate
   (state <s> ^name generate-goal
	      ^failure <f>
	      ^desired <d>
	      ^goal-id <gid>
	      ^operator <op>)
   (<op> ^name process-failure
   	 ^type unmatched-constraints)
   (<d> ^achieve-state <pred>)
   (<gid> ^state-predicate <pred-def>)
-->
   (<f> ^predicate-grounding-failure <pgf>)   
   (<pgf> ^type overspecific-definition
   	  ^predicate-definition <pred-def>
	  ^predicate-type state-predicate
	  ^partial-grounds <pred>)
}

sp {generate-goal*report-status*unmatched-constraints*relation-predicate
   (state <s> ^name generate-goal
	      ^failure <f>
	      ^desired <d>
	      ^goal-id <gid>
	      ^operator <op>)
   (<op> ^name process-failure
   	 ^type unmatched-constraints)
   (<d> ^track-relation <pred>)
   (<gid> ^relation-predicate <pred-def>)
-->
   (<f> ^predicate-grounding-failure <pgf>)   
   (<pgf> ^type overspecific-definition
   	  ^predicate-definition <pred-def>
	  ^predicate-type relation-predicate
	  ^partial-grounds <pred>)
}