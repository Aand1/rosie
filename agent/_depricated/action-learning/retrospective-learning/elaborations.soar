### elaborate problemspce
sp {elaborate*action-policy*problemspace
   (state <s> ^name retrospective-learning
   	      ^problemspace <ps>)
-->
   (<ps> ^state <st>
   	 ^learning <l>
	 ^action <act>)
   (<st> ^dont-copy io
   	 ^dont-copy object
	 ^dont-copy environment
	 ^type retrospective
	 ^elaborate closed-world)
   (<l> ^type exploitation)
   (<act> ^type project)
}

### elaborate segment
sp {elaborate*retrospective-learning*segment
   (state <s> ^name retrospective-learning
   	      ^superstate.operator <op>)
   (<op> ^segment <seg>)
-->
   (<s> ^segment <seg>
	^name action
	^action-matches <am>)
}

sp {retrospective-learning*copy*superstate*indexed-action
   (state <s> ^name retrospective-learning)
-->
   (<s> ^indexed-action <ia>)
}
   	      

### elaborate indexed-action
sp {retrospective-learning-learning*elaborate*segment-information
   (state <s> ^name retrospective-learning
   	      ^segment.purpose.parameters.indexable <pi>)
   (<pi> ^result <ia>
   	 ^lexical <lex>)
   (<ia> ^_metadata.map-id <map-id>)
-->
   (<s> ^top-state-indexed-action <ia>
   	^lexical-information <lex>
	^relevant-map-id <map-id>)
}

######################### generate a copy of the indexed-action ########

sp {retrospective-learning-learning*elaborate*indexed-action
   (state <s> ^name retrospective-learning
   	      ^top-state-indexed-action <ia>
	      ^superstate <ss>
	      ^indexed-action <iop>)
   (<ia> ^name <name>
   	 ^_metadata <met>)
-->
   (<iop> ^name <name>
   	     ^_metadata <met>
   	     ^action-type composite)
}

### elaborate composite action arguments level 1
sp {retrospective-learning-learning*elaborate*composite-action-operator*level1
   (state <s> ^name retrospective-learning
   	      ^top-state-indexed-action <ia>
	      ^indexed-action <op> 
	      ^object <obj>
	      ^duplicates.<val> <obj>)
   (<op> ^name <name>)
   (<ia> ^name <name>
   	 ^<arg> <val>)
-->
   (<op> ^<arg> <obj>)
}

sp {retrospective-learning-learning*elaborate*composite-action-operator*level2*object
   (state <s> ^name retrospective-learning
   	      ^top-state-indexed-action <ia>
	      ^indexed-action <op> 
	      ^object <obj>
	      ^duplicates.<val> <obj>)
   (<op> ^name <name>)
   (<ia> ^name <name>
   	 ^<arg>.argument <val>)
-->
   (<op> ^<arg>.argument <obj>)
}

sp {retrospective-learning-learning*elaborate*composite-action-operator*level2*relation
   (state <s> ^name retrospective-learning
   	      ^top-state-indexed-action <ia>
	      ^indexed-action <op> 
	      ^object <obj>
	      ^duplicates.<valy> <valx>)
   (<op> ^name <name>
   	 ^<arg> <karg>)
   (<karg> ^argument <valx>)
   (<ia> ^name <name>
   	 ^<arg> <nval>)
   (<nval> ^argument <valy>
   	   ^relation <rel>)
-->
   (<karg> ^relation <rel>)
}

sp {retrospective-learning*copy*indexed-action*superstate-indexed-action
   (state <s> ^name retrospective-learning
   	      ^indexed-action <ia>)
-->
   (<s> ^superstate-indexed-action <ia>)
}
	      




