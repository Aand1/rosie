sp {propose*series-of-points
   (state <s> ^name pointer)
-->
   (<s> ^operator <o> +)
   (<o> ^name series-of-points)
}

sp {propose*begin-points
   (state <s> ^name series-of-points
             -^current <any>)
-->
   (<s> ^operator <o> +)
   (<o> ^name begin-points)
}

sp {apply*begin-points
   (state <s> ^operator.name begin-points
              ^topstate.point-list.begin <first>)
-->
   (<s> ^current <first>)
}

sp {propose*do-current-point
   (state <s> ^name series-of-points
              ^current.pose <pos>
              ^io.output-link <out>)
   -{(<out> ^<command> <com>)
   	 (<com> ^status complete)}
-->
   (<s> ^operator <o> + <)
   (<o> ^name do-current-point
        ^actions.point <point>)
   (<point> ^pose <pos>)
}

sp {propose*update-current
   (state <s> ^name series-of-points
              ^current <cur>
              ^io.output-link <out>)
   (<out> ^<command> <com>)
   (<com> ^status complete
   		  ^pose <pos>)	
   (<cur> ^pose <pos>)
   (<cur> ^next <> nil)
   -->
   (<s> ^operator <o> +)
   (<o> ^name update-current)
}

sp {apply*update-current
   (state <s> ^operator.name update-current
              ^current <cur>
              ^io.output-link <out>)
   (<cur> ^next <next>)
   (<out> ^<command> <com>)
   (<com> ^pose.x <x>
          ^pose.y <y>)
-->
   (<s> ^current <cur> -)
   (<out> ^<command> <com> -)
   (write (crlf) |Taking away | <x> |, | <y>)
   (<s> ^current <next>)
}

sp {monitor*command*status
  (state <s> ^name series-of-points 
			 ^io.output-link <ol>)
   (<ol> ^<command> <com>)
   (<com> ^pose.x <x>
          ^pose.y <y>)
   (<com> ^status complete)
-->
   (write (crlf) <x> |, | <y> | was completed.|)   
}