sp {pointer*propose*series-of-points
   (state <s> ^name pointer
             -^done true)
-->
   (<s> ^operator <o> +)
   (<o> ^name series-of-points)
}

sp {series-of-points*propose*begin-points
   (state <s> ^name series-of-points
             -^topstate.current <any>)
-->
   (<s> ^operator <o> +)
   (<o> ^name begin-points)
}

sp {series-of-points*apply*begin-points
   (state <s> ^operator.name begin-points
              ^topstate <ts>)
   (<ts> ^point-list.begin <first>)
-->
   (<ts> ^current <first>)
}

sp {series-of-points*propose*do-current-point
   (state <s> ^name series-of-points
              ^topstate.current.pose <pos>
              ^io.output-link <out>)
   -{(<out> ^<command> <com>)
   	 (<com> ^status complete)}
-->
   (<s> ^operator <o> + <)
   (<o> ^name do-current-point
        ^actions.point <point>)
   (<point> ^pose <pos>)
}

sp {series-of-points*propose*update-current
   (state <s> ^name series-of-points
              ^topstate.current <cur>
              ^io.output-link <out>)
   (<out> ^<command> <com>)
   (<com> ^status complete
   		  ^pose <pos>)	
   (<cur> ^pose <pos>)
   (<cur> ^next <> nil)
-->	
   (<s> ^operator <o> +)
   (<o> ^name update-current)
}

sp {series-of-points*apply*update-current
   (state <s> ^operator.name update-current
              ^topstate <ts>
              ^io.output-link <out>)
   (<out> ^<command> <com>)
   (<ts> ^current <cur>)
   (<cur> ^next <next>)
-->
   (<ts> ^current <cur> -)
   (<ts> ^current <next>)
   (<out> ^<command> <com> -)
}

sp {series-of-points*propose*end-points
   (state <s> ^name series-of-points
              ^topstate.current.next nil)
-->
   (<s> ^operator <o> +)
   (<o> ^name end-points)
}

sp {series-of-points*apply*end-points
   (state <s> ^operator.name end-points
              ^superstate <ss>)
-->
   (<ss> ^done true)
}